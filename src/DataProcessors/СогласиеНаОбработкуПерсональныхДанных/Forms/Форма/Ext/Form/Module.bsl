
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ДатаСогласия", ДатаСогласия) Тогда
		ДатаСогласия = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("Организация", Организация) Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Организации") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации");
			Организация = Модуль.ОрганизацияПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	ЗаполнитьДанныеОрганизации();
	
	Если ЗначениеЗаполнено(Параметры.Субъекты) Тогда
		ВариантПечатиСогласия = "ВыводитьПоСубъектам";
		Для Каждого Субъект Из Параметры.Субъекты Цикл
			НоваяСтрока = СубъектыПерсональныхДанных.Добавить();
			НоваяСтрока.Субъект = Субъект;
		КонецЦикла;
		ЗаполнитьДанныеСубъектовПерсональныхДанных();
	Иначе
		ВариантПечатиСогласия = "ВыводитьБланк";
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЭтоLinuxКлиент() Тогда
		Элементы.ФормаПечатьMSWord.Видимость = Ложь;
		Элементы.ФормаПечатьOOWritter.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьСпискаВыбораСубъектов();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ВариантПечатиСогласияПриИзменении(Элемент)
	
	УстановитьДоступностьСпискаВыбораСубъектов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ЗаполнитьДанныеОрганизации();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД

&НаКлиенте
Процедура Печать(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеФормы(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	Если СубъектыПерсональныхДанных.Количество() > 0 И ЗначениеЗаполнено(СубъектыПерсональныхДанных[0].Субъект) Тогда
		ПараметрКоманды.Добавить(СубъектыПерсональныхДанных[0].Субъект);
	Иначе
		ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка"));
	КонецЕсли;
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Обработка.СогласиеНаОбработкуПерсональныхДанных", "СогласиеНаОбработкуПерсональныхДанных",
		ПараметрКоманды, ЭтотОбъект, Новый Структура("ДанныеПечатиСогласия", ДанныеПечатиСогласия()));

КонецПроцедуры

&НаКлиенте
Процедура ПечатьMSWord(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеФормы(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПечатьСогласияНаОбработкуПерсональныхДанных("Обработка.СогласиеНаОбработкуПерсональныхДанных", "СогласиеНаОбработкуПерсональныхДанных(MSWord)", ДанныеПечатиСогласия(), ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьOOWritter(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеФормы(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ПечатьСогласияНаОбработкуПерсональныхДанных("Обработка.СогласиеНаОбработкуПерсональныхДанных", "СогласиеНаОбработкуПерсональныхДанных(ODT)", ДанныеПечатиСогласия(), ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьСпискаВыбораСубъектов()
	
	Элементы.ГруппаДанныеПечатиСогласия.ТекущаяСтраница = Элементы[ВариантПечатиСогласия];
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеПечатиСогласия()
	
	ОбщиеДанные = Новый Структура;
	ОбщиеДанные.Вставить("ДатаСогласия", 		?(ЗначениеЗаполнено(ДатаСогласия), Формат(ДатаСогласия, "ДЛФ=DD"), НСтр("ru = '«____»_______________20___г.'")));
	ОбщиеДанные.Вставить("Организация", 		?(ЗначениеЗаполнено(Организация), Организация, НСтр("ru = '<Организация>'")));
	ОбщиеДанные.Вставить("АдресОрганизации", 	?(ЗначениеЗаполнено(АдресОрганизации), АдресОрганизации, НСтр("ru = '<Адрес организации>'")));
	ОбщиеДанные.Вставить("ОтветственныйЗаОбработкуПерсональныхДанных", ?(ЗначениеЗаполнено(ОтветственныйЗаОбработкуПерсональныхДанных), ОтветственныйЗаОбработкуПерсональныхДанных, НСтр("ru = '<ФИО ответственного лица>'")));
	
	Субъекты = Новый Массив;
	Если ВариантПечатиСогласия = "ВыводитьБланк" Тогда
		Субъекты.Добавить(Новый Структура("ФИО, Адрес, ПаспортныеДанные", НСтр("ru = '<ФИО субъекта>'"), НСтр("ru = '<Адрес субъекта>'"), НСтр("ru = '<Паспортные данные субъекта>'")));
	Иначе
		Для Каждого Субъект Из СубъектыПерсональныхДанных Цикл
			ДанныеСубъекта = Новый Структура("ФИО, Адрес, ПаспортныеДанные");
			ЗаполнитьЗначенияСвойств(ДанныеСубъекта, Субъект);
			Субъекты.Добавить(ДанныеСубъекта);
		КонецЦикла;
	КонецЕсли;
	
	// Структура по каждому субъекту дополняется общими данными
	Для Каждого Субъект Из Субъекты Цикл
		Для Каждого КлючИЗначение Из ОбщиеДанные Цикл
			Субъект.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Субъекты;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеОрганизации()
	
	ДанныеОрганизацииОператора = Новый Структура("АдресОрганизации, ОтветственныйЗаОбработкуПерсональныхДанных");
	
	ЗащитаПерсональныхДанныхПереопределяемый.ДополнитьДанныеОрганизацииОператораПерсональныхДанных(Организация, ДанныеОрганизацииОператора, ДатаСогласия);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеОрганизацииОператора);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеСубъектовПерсональныхДанных()
	
	// заполнение данных субъектов
	ЗащитаПерсональныхДанныхПереопределяемый.ДополнитьДанныеСубъектовПерсональныхДанных(СубъектыПерсональныхДанных, ДатаСогласия);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСогласияНаОбработкуПерсональныхДанных(ИмяМенеджераПечати, ИмяМакета, Субъекты, ФормаИсточник)
	
	// Проверим количество объектов
	Если Субъекты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = ?(Субъекты.Количество() > 1, 
		НСтр("ru = 'Выполняется формирование печатных форм...'"),
		НСтр("ru = 'Выполняется формирование печатной формы...'"));
	Состояние(ТекстСообщения);
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.МакетыИДанныеОбъектовДляПечати(ИмяМенеджераПечати, ИмяМакета, Субъекты);
	
	Для Каждого Субъект Из Субъекты Цикл
		НапечататьСогласиеНаОбработкуПерсональныхДанныхСубъекта(Строка(Субъект.ФИО) + Строка(Субъект.Адрес) + Строка(Субъект.ПаспортныеДанные), МакетИДанныеОбъекта, ИмяМакета, МакетИДанныеОбъекта.ЛокальныйКаталогФайловПечати);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НапечататьСогласиеНаОбработкуПерсональныхДанныхСубъекта(СубъектКлюч, МакетИДанныеОбъекта, ИмяМакета, ЛокальныйКаталогФайловПечати)
	
	ТипМакета				= МакетИДанныеОбъекта.Макеты.ТипыМакетов[ИмяМакета];
	ДвоичныеДанныеМакетов	= МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	Области					= МакетИДанныеОбъекта.Макеты.ОписаниеОбластей;
	ДанныеОбъекта = МакетИДанныеОбъекта.Данные[СубъектКлюч][ИмяМакета];
	
	Макет = УправлениеПечатьюКлиент.ИнициализироватьМакетОфисногоДокумента(ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета, ИмяМакета);
	Если Макет = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗакрытьОкноПечатнойФормы = Ложь;
	Попытка
		ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета, Макет.НастройкиСтраницыМакета);
		Если ПечатнаяФорма = Неопределено Тогда
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			Возврат;
		КонецЕсли;
		
		// Вывод обычных областей с параметрами
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["Заголовок"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["НомерДата"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["Преамбула"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["ОсновнойТекст"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["РеквизитыОператора"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["РеквизитыСубъекта"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		Область = УправлениеПечатьюКлиент.ОбластьМакета(Макет, Области[ИмяМакета]["Подпись"]);
		УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, Область, ДанныеОбъекта, Ложь);
		
		УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗакрытьОкноПечатнойФормы = Истина;
	КонецПопытки;
	
	УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, ЗакрытьОкноПечатнойФормы);
	УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеФормы(Отказ)
	
	Если ВариантПечатиСогласия = "ВыводитьПоСубъектам" И СубъектыПерсональныхДанных.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Субъекты персональных данных не выбраны.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СубъектыПерсональныхДанных", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
