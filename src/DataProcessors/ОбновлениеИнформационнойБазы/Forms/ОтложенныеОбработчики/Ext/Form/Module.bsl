
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	СведенияОбОбновлении = ОбновлениеИнформационнойБазыСлужебный.СведенияОбОбновленииИнформационнойБазы();
	ВремяНачалаОтложенногоОбновления = СведенияОбОбновлении.ВремяНачалаОтложенногоОбновления;
	ВремяОкончанияОтложенногоОбновления = СведенияОбОбновлении.ВремяОкончаниеОтложенногоОбновления;
	НомерТекущегоСеанса = СведенияОбОбновлении.НомерСеанса;
	ИБФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	Если Не Пользователи.ЭтоПолноправныйПользователь(, Истина) Тогда
		Элементы.ГруппаПовторныйЗапуск.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ИБФайловая Тогда
		ОбновлениеВыполняется = (СведенияОбОбновлении.ОтложенноеОбновлениеЗавершеноУспешно = Неопределено);
	КонецЕсли;
	
	Если Не Пользователи.РолиДоступны("ПросмотрЖурналаРегистрации") Тогда
		Элементы.ГиперссылкаОтложенноеОбновление.Видимость = Ложь;
	КонецЕсли;
	
	Статус = "ВсеПроцедуры";
	
	ОбновитьТаблицуОтложенныхОбработчиков();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОбновлениеВыполняется Тогда
		ПодключитьОбработчикОжидания("ОбновитьТаблицуОбработчиков", 15);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПовторно(Команда)
	Оповестить("ОтложенноеОбновление");
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОтложенноеОбновлениеНажатие(Элемент)
	
	Если ЗначениеЗаполнено(ВремяНачалаОтложенногоОбновления) И ЗначениеЗаполнено(ВремяОкончанияОтложенногоОбновления) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДатаНачала", ВремяНачалаОтложенногоОбновления);
		ПараметрыФормы.Вставить("ДатаОкончания", ВремяОкончанияОтложенногоОбновления);
		ПараметрыФормы.Вставить("Сеанс", НомерТекущегоСеанса);
		
		ОткрытьФорму("Обработка.ЖурналРегистрации.Форма.ЖурналРегистрации", ПараметрыФормы);
	Иначе
		
		Если ЗначениеЗаполнено(ВремяНачалаОтложенногоОбновления) Тогда
			ТекстПредупреждения = НСтр("ru = 'Обработка данных еще не завершилась.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Обработка данных еще не выполнялась.'");
		КонецЕсли;
		
		ПоказатьПредупреждение(,ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	ОбновитьТаблицуОтложенныхОбработчиков();
	Если ОтложенныеОбработчики.Количество() > 0 Тогда
		Элементы.ОтложенныеОбработчики.ТекущаяСтрока = ОтложенныеОбработчики[0].ПолучитьИдентификатор();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	ОбновитьТаблицуОтложенныхОбработчиков();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьТаблицуОбработчиков()
	
	ВыполненыВсеОбработчики = Истина;
	ОбновитьТаблицуОтложенныхОбработчиков(ВыполненыВсеОбработчики);
	Если ВыполненыВсеОбработчики Тогда
		ОтключитьОбработчикОжидания("ОбновитьТаблицуОбработчиков");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуОтложенныхОбработчиков(ВыполненыВсеОбработчики = Истина)
	
	ОтложенныеОбработчики.Очистить();
	ОбработчикиНеВыполнялись = Истина;
	СведенияОбОбновлении = ОбновлениеИнформационнойБазыСлужебный.СведенияОбОбновленииИнформационнойБазы();
	
	Для Каждого СтрокаДереваБиблиотека Из СведенияОбОбновлении.ДеревоОбработчиков.Строки Цикл
		Для Каждого СтрокаДереваВерсия Из СтрокаДереваБиблиотека.Строки Цикл
			Для Каждого СтрокаОбработчика Из СтрокаДереваВерсия.Строки Цикл
				
				Если Статус <> "ВсеПроцедуры"
					И Не СтрокаОбработчика.Статус = Статус Тогда
					Продолжить;
				КонецЕсли;
				
				Если Не ПустаяСтрока(СтрокаПоиска) Тогда
					Если Не ПустаяСтрока(СтрокаОбработчика.Комментарий) Тогда
						Если Найти(ВРег(СтрокаОбработчика.Комментарий), ВРег(СтрокаПоиска)) = 0 Тогда
							Продолжить;
						КонецЕсли;
					Иначе
						Если Найти(ВРег(СтрокаОбработчика.ИмяОбработчика), ВРег(СтрокаПоиска)) = 0 Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				СтрокаСписка = ОтложенныеОбработчики.Добавить();
				Если Не ПустаяСтрока(СтрокаОбработчика.Комментарий) Тогда
					СтрокаСписка.Обработчик = СтрокаОбработчика.Комментарий;
				Иначе
					СтрокаСписка.Обработчик = СтрокаОбработчика.ИмяОбработчика;
				КонецЕсли;
				
				Если СтрокаОбработчика.Статус = "Выполнено" Тогда
					ОбработчикиНеВыполнялись = Ложь;
					СтрокаСписка.ИнформацияОПроцедуреОбновления = 
						НСтр("ru = 'Процедура ""%1"" обработки данных завершилась успешно.'");
					СтрокаСписка.СтатусОбработчика = НСтр("ru = 'Выполнена'");
					СтрокаСписка.Вес = 1;
					СтрокаСписка.СтатусКартинка = БиблиотекаКартинок.Успешно;
				ИначеЕсли СтрокаОбработчика.Статус = "Выполняется" Тогда
					ОбработчикиНеВыполнялись = Ложь;
					СтрокаСписка.ИнформацияОПроцедуреОбновления = 
						НСтр("ru = 'Процедура ""%1"" обработки данных в данный момент выполняется.'");
					СтрокаСписка.СтатусОбработчика = НСтр("ru = 'Выполняется'");
					СтрокаСписка.Вес = 3;
				ИначеЕсли СтрокаОбработчика.Статус = "Ошибка" Тогда
					ОбработчикиНеВыполнялись = Ложь;
					ВыполненыВсеОбработчики = Ложь;
					СтрокаСписка.ИнформацияОПроцедуреОбновления = СтрокаОбработчика.ИнформацияОбОшибке;
					СтрокаСписка.СтатусОбработчика = НСтр("ru = 'Ошибка'");
					СтрокаСписка.Вес = 4;
					СтрокаСписка.СтатусКартинка = БиблиотекаКартинок.Остановить;
				Иначе
					ВыполненыВсеОбработчики = Ложь;
					СтрокаСписка.СтатусОбработчика = НСтр("ru = 'Не выполнялась'");
					СтрокаСписка.Вес = 2;
					СтрокаСписка.ИнформацияОПроцедуреОбновления = НСтр("ru = 'Процедура ""%1"" обработки данных еще не выполнялась.'");
				КонецЕсли;
				
				СтрокаСписка.ИнформацияОПроцедуреОбновления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					СтрокаСписка.ИнформацияОПроцедуреОбновления, СтрокаОбработчика.ИмяОбработчика);
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если ВыполненыВсеОбработчики Или ОбновлениеВыполняется Тогда
		Элементы.ГруппаПовторныйЗапуск.Видимость = Ложь;
	КонецЕсли;
	
	Если ОбработчикиНеВыполнялись Тогда
		Элементы.ТекстПояснения.Заголовок = НСтр("ru = 'Рекомендуется запустить невыполненные процедуры обработки данных.'");
	Иначе
		Элементы.ТекстПояснения.Заголовок = НСтр("ru = 'Невыполненные процедуры рекомендуется запустить повторно.'");
	КонецЕсли;
	
	ОтложенныеОбработчики.Сортировать("Вес Убыв");
	
	НомерЭлемента = 1;
	Для Каждого СтрокаТаблицы Из ОтложенныеОбработчики Цикл
		СтрокаТаблицы.Номер = НомерЭлемента;
		НомерЭлемента = НомерЭлемента + 1;
	КонецЦикла;
	
	Элементы.ОбновлениеВыполняется.Видимость = ОбновлениеВыполняется;
	
КонецПроцедуры

#КонецОбласти
