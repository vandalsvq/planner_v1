
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗапуститьИЗавершить = Параметры.ЗапуститьИЗавершить;
	КаталогВыгрузкиМодулей = Параметры.КаталогВыгрузкиМодулей;
	
	ИмяАдминистратораИБ = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();
	
	Если Не ЗапуститьИЗавершить Тогда
		
		ФайловаяБаза = Найти(СтрокаСоединения, "File=");
		Если ФайловаяБаза = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru ='Данная обработка предназначена для использования с файловой базой'"));
		КонецЕсли;
		
		ПервыйСимволПути = ФайловаяБаза + 6;
		СтрокаСоединения = Сред(СтрокаСоединения,ПервыйСимволПути);
		ПоследнийСимволПути = Найти(СтрокаСоединения,";");
		СтрокаСоединения = Лев(СтрокаСоединения,ПоследнийСимволПути - 2);
		
		ПутьКИБ = СтрокаСоединения;
		ИмяКаталога  = КаталогВременныхФайлов() + "ВыгрузкаМодулей";
		СоздатьКаталог(ИмяКаталога);
		КаталогВыгрузкиМодулей = ИмяКаталога;
		
	КонецЕсли;
	
	// Список метаданных, которые необходимо пропускать при проверке прав.
	СписокИсключений = Новый СписокЗначений;
	СписокИсключений.Добавить(Метаданные.Справочники.ДополнительныеОтчетыИОбработки.Имя);
	СписокИсключений.Добавить(Метаданные.РегистрыСведений.НазначениеДополнительныхОбработок.Имя);
	СписокИсключений.Добавить(Метаданные.РегистрыСведений.ПользовательскиеНастройкиДоступаКОбработкам.Имя);
	СписокИсключений.Добавить(Метаданные.Константы.КлючОбластиДанных.Имя);
	
	// Блок описания текущих проверок
	
	// В этом месте необходимо добавить описание любой проверки в формате:
	// ДобавитьПроверку(<Текстовое представление проверки для пользователя>, <Наименование функции проверки>);
	ДобавитьПроверку(НСтр("ru = 'Модули обычного и управляемого приложения должны совпадать'"), "Подключаемый_ПроверитьСовпадениеМодулейУправляемогоИОбычногоПриложений()", НСтр("ru = 'Требуется закрытие конфигуратора!'"));
	ДобавитьПроверку(НСтр("ru = 'Каждый объект должен принадлежать хотя бы одной подсистеме'"), "Подключаемый_ПроверитьПринадлежностьОбъектовПодсистемам()");
	ДобавитьПроверку(НСтр("ru = 'Демонстрационные объекты не должны включаться в поставляемые подсистемы'"), "Подключаемый_ПроверитьПринадлежностьДемонстрационныхОбъектов()");
	ДобавитьПроверку(НСтр("ru = 'Демонстрационные примеры должны быть правильно закомментированы'"), "Подключаемый_ПроверитьКорректностьКомментированияДемоПримеров()", НСтр("ru = 'Требуется закрытие конфигуратора!'"));
	ДобавитьПроверку(НСтр("ru = 'Демонстрационные объекты должны быть правильно названы'"), "Подключаемый_ПроверитьПравильностьИменованияДемоОбъектов()");
	ДобавитьПроверку(НСтр("ru = 'Общие модули должны иметь корректные настройки'"), "Подключаемый_ПроверитьУстановкуФлажковОбщихМодулей()");
	ДобавитьПроверку(НСтр("ru = 'Некорректны вызовы процедур и функций общего модуля из него самого, содержащие имя модуля.'"), "Подключаемый_ПроверитьВызовыОбщихМодулейИзНихСамих()", НСтр("ru = 'Требуется закрытие конфигуратора!'"));
	ДобавитьПроверку(НСтр("ru = 'Дополнительные команды открытия списков должны иметь в ролях те же права, что и объекты метаданных.'"), "Подключаемый_ПроверитьПравоПросмотраКомандОткрытияСписковВРолях()");
	ДобавитьПроверку(НСтр("ru = 'Роли разделенных пользователей не должны содержать прав на изменение общих данных'"), "Подключаемый_ПроверитьРолиНаНаличиеНедопустимыхПрав()");
	ДобавитьПроверку(НСтр("ru = 'Комментарии блоков кода, относящихся к той или иной подсистемы должны быть корректными'"), "Подключаемый_ПроверитьКомментарииДляБлоковКодаПодсистем()", НСтр("ru = 'Требуется закрытие конфигуратора!'"));
	ДобавитьПроверку(НСтр("ru = 'Недопустимо наличие права ""Интерактивное удаление""'"), "Подключаемый_ПроверитьНаличиеПраваИнтерактивноеУдаление()");
	ДобавитьПроверку(НСтр("ru = 'Роль ПолныеПрава должна давать все права на разделенные объекты метаданных'"), "Подключаемый_ПроверитьРольПолныеПраваДляРазделяемыхОбъектовМетаданных()");
	ДобавитьПроверку(НСтр("ru = 'Роль ""Администратор системы"" не должна давать права на разделенные объекты метаданных'"), "Подключаемый_ПроверитьРольАдминистраторСистемыДляРазделяемыхОбъектовМетаданных()");
	ДобавитьПроверку(НСтр("ru = 'Роль ""Администратор системы"" должна давать все права на неразделенные объекты метаданных'"), "Подключаемый_ПроверитьРольАдминистраторСистемыДляНеразделяемыхОбъектовМетаданных()");
	ДобавитьПроверку(НСтр("ru = 'Использование функции проверки существования подсистем'"), "Подключаемый_ПроверитьПравильностьУсловныхВызовов()", НСтр("ru = 'Требуется закрытие конфигуратора!'"));
	ДобавитьПроверку(НСтр("ru = 'Недопустимо наличие недокументированных жестких связей между подсистемами'"), "Подключаемый_НекорректныеСвязиМеждуПодсистемами()", НСтр("ru = 'Требуется закрытие конфигуратора!'"));
	ДобавитьПроверку(НСтр("ru = 'Недопустимо наличие ""TODO"" в текстах модулей'"), "Подключаемый_ВыполнитьПоискЗапрещенныхКомментариев()", НСтр("ru = 'Требуется закрытие конфигуратора!'"));
	ДобавитьПроверку(НСтр("ru = 'У поставляемых подсистем должен быть снят флаг ""Включать в содержание справки""'"), "Подключаемый_ПроверитьЗначениеФлагаВключатьВСодержаниеСправки()");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗапуститьИЗавершить Тогда
		
		Для Каждого Проверка Из Объект.Проверки Цикл
			Проверка.ПроводитьПроверку = Истина;
		КонецЦикла;
		
		КомандаВыполнить(Неопределено);
	КонецЕсли;
	
#Если ТолстыйКлиентУправляемоеПриложение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда
	Элементы.СравнитьФайлыПриложений.Видимость = Истина;
#КонецЕсли
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
#Если Не ВебКлиент Тогда
	Если Не ЗапуститьИЗавершить Тогда
		ОчиститьКаталогДляВыгрузки(КаталогВыгрузкиМодулей);
	КонецЕсли;
	ВыполнитьПроверку();
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКонфигуратор(Команда)
#Если Не ВебКлиент Тогда
	
	Состояние(НСтр("ru = 'Запуск конфигуратора'"));
	СтрокаЗапускаПлатформы = КаталогПрограммы() + "1cv8.exe";
	КаталогКонфигурации = ПутьКИБ;
	Пользователь = ИмяАдминистратораИБ;
	Пароль = ПарольАдминистратораИБ;
	КоманднаяСтрока = СтрокаЗапускаПлатформы + " DESIGNER /F"""
		+ КаталогКонфигурации + """ /N"""
		+ Пользователь + """ /P""" + Пароль + """";
	ЗапуститьПриложение(КоманднаяСтрока,,Ложь);				  
	
#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьВсеПроверки(Команда)
	Для Каждого Проверка Из Объект.Проверки Цикл
		Проверка.ПроводитьПроверку = Истина;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсеПроверки(Команда)
	Для Каждого Проверка Из Объект.Проверки Цикл
		Проверка.ПроводитьПроверку = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрошлыеРезультатыПроверки(Команда)
	РезультатыОтчета.Показать();
КонецПроцедуры

&НаКлиенте
Процедура СравнитьФайлыПриложений(Команда)
#Если ТолстыйКлиентУправляемоеПриложение ИЛИ ТолстыйКлиентОбычноеПриложение Тогда
	Сравнение = Новый Сравнениефайлов;	
	
	Если НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt").Количество() = 0 Тогда
		ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей);
	КонецЕсли;
	
	ПервыйФайл = НайтиФайлы(КаталогВыгрузкиМодулей, "*Конфигурация.МодульОбычногоПриложения.txt");
	ВторойФайл = НайтиФайлы(КаталогВыгрузкиМодулей, "*Конфигурация.МодульУправляемогоПриложения.txt");
	
	Если ПервыйФайл.Количество() = 0 Или ВторойФайл.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru ='Не найдены файлы приложений'"));
		Возврат;
	КонецЕсли;
	
	Сравнение.ПервыйФайл = ПервыйФайл[0].ПолноеИмя;
	Сравнение.ВторойФайл = ВторойФайл[0].ПолноеИмя;
	
	Сравнение.ИгнорироватьПустоеПространство = Истина;
	Сравнение.УчитыватьРазделителиСтрок = Ложь;
	Сравнение.УчитыватьРегистр = Ложь;
	Сравнение.СпособСравнения = СпособСравненияФайлов.ТекстовыйДокумент;
	Сравнение.ПоказатьРазличия();
	
#КонецЕсли
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции

&НаКлиенте
Процедура ОчиститьКаталогДляВыгрузки(КаталогВыгрузкиМодулей)
	МассивФайлов = НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt");
	Для Каждого Файл Из МассивФайлов Цикл
		УдалитьФайлы(Файл.ПолноеИмя);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверку()
	СписокВывода = Новый СписокЗначений;
	Для Каждого Проверка Из Объект.Проверки Цикл
		Если Проверка.ПроводитьПроверку Тогда
			Попытка
				Состояние(НСтр("ru = 'Проверка правила:'") + " " + Проверка.НаименованиеПроверки);
				МассивПроверок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Проверка.НаименованиеПроцедурыПроверки);
				Для Каждого ПромежуточнаяПроверка Из МассивПроверок Цикл
					СтруктураОшибок = Вычислить(СокрЛП(ПромежуточнаяПроверка));
					Если СтруктураОшибок <> Неопределено Тогда		
						Если ТипЗнч(СтруктураОшибок) = Тип("Массив") Тогда
							Для каждого ЭлементМассива Из СтруктураОшибок Цикл
								СписокВывода.Добавить(ЭлементМассива);
							КонецЦикла;
						Иначе
							СписокВывода.Добавить(СтруктураОшибок);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			Исключение
				ПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(НСтр("ru = 'Проверка перед сборкой'"), "Ошибка", ПредставлениеОшибки);
				ТекстСообщения = НСтр("ru = 'Не удалось выполнить проверку %1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,Проверка.НаименованиеПроверки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	ТабДок = ВывестиОшибки(СписокВывода);
	ТабДок.ОтображатьГруппировки = Истина;
	ТабДок.ОтображатьСетку = Ложь;
	ТабДок.ОтображатьЗаголовки = Ложь;
	
	Если ЗапуститьИЗавершить Тогда
		ТабДок.Записать(КаталогВыгрузкиМодулей + "\ПроверкаБСППередСборкой\ПроверкаБСППередСборкой.mxl");
		ПрекратитьРаботуСистемы(Ложь);
	КонецЕсли;
	
	ТабДок.Показать();
	
	ОбновитьГиперссылку();
	РезультатыОтчета = ТабДок;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьГиперссылку()
	ШаблонЗаголовка = НСтр("ru = 'Открыть результаты проверки на: %1'"); 
	Элементы.ОткрытьРезультатыПроверки.Заголовок = 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, Строка(ДатаСеанса()));
	Элементы.ОткрытьРезультатыПроверки.Доступность = Истина;
КонецПроцедуры

&НаСервере
Функция ДатаСеанса()
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции	

&НаКлиенте
Процедура ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей)
	
	Если ЗапуститьИЗавершить Тогда
		Возврат;
	КонецЕсли;
	
#Если Не ВебКлиент Тогда
	Состояние(НСтр("ru = 'Выгрузка модулей в текстовые файлы'"));
	
	СтрокаЗапускаПлатформы = КаталогПрограммы() + "1cv8.exe";
	
	КаталогКонфигурации = ПутьКИБ;
	
	Пользователь = ИмяАдминистратораИБ;
	
	Пароль = ПарольАдминистратораИБ;
	
	ИмяБатФайла = ПолучитьИмяВременногоФайла("bat");
	БатФайл = Новый ЗаписьТекста(ИмяБатФайла);
	КоманднаяСтрока = СтрокаЗапускаПлатформы + " DESIGNER /F"""
	+ КаталогКонфигурации + """ /N"""
	+ Пользователь + """ /P""" + Пароль
	+ """ /DumpConfigFiles """ + КаталогВыгрузкиМодулей
	+ """ -Module";
	
	БатФайл.ЗаписатьСтроку(КоманднаяСтрока);
	БатФайл.Закрыть();
	
	ЗапуститьПриложение(КоманднаяСтрока,,Истина);
#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьПроверку(ИмяПроверки,ИмяПроцедурыПроверки, ДопУсловие = Неопределено)
	НоваяСтрокаПроверки = Объект.Проверки.Добавить();
	НоваяСтрокаПроверки.ПроводитьПроверку = Истина;
	НоваяСтрокаПроверки.НаименованиеПроверки = ИмяПроверки;
	НоваяСтрокаПроверки.НаименованиеПроцедурыПроверки = ИмяПроцедурыПроверки;
	НоваяСтрокаПроверки.ДополнительноеУсловие  = ДопУсловие;
КонецПроцедуры

&НаСервере
Функция ВывестиОшибки(СписокОшибок)
	
	ТабДок = Новый ТабличныйДокумент;
	ТабДок.НачатьАвтогруппировкуСтрок();
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("МакетОтчетаСОшибками");
	
	ЕстьОшибки = Ложь;
	Для Каждого ЭлементСтруктураОшибок Из СписокОшибок Цикл
		СтруктураОшибок = ЭлементСтруктураОшибок.Значение;
		ОшибочныеДанные = СтруктураОшибок.ОшибочныеДанные;
		
		Если ОшибочныеДанные = Неопределено
			Или ОшибочныеДанные.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьПравила = Макет.ПолучитьОбласть("ИмяПравила");
		ОбластьПравила.Параметры.ИмяПравила = СтруктураОшибок.ПредставлениеОшибки;
		ОбластьПравила.Параметры.КоличествоОшибок = ?(ОшибочныеДанные = Неопределено, 0, ОшибочныеДанные.Количество());
		ТабДок.Вывести(ОбластьПравила, 0);
		
		ОбластьИсправления = Макет.ПолучитьОбласть("ОписаниеИсправления");
		ОбластьИсправления.Параметры.МетодикаИсправления = СтруктураОшибок.ОписаниеИсправления;
		ТабДок.Вывести(ОбластьИсправления,1);
		
		Если ТипЗнч(ОшибочныеДанные) = Тип("СписокЗначений") Тогда
			Для Каждого Ошибка Из ОшибочныеДанные Цикл
				ОбластьОшибки = Макет.ПолучитьОбласть("Объект");
				ОбластьОшибки.Параметры.ИмяОбъекта = Ошибка.Значение;
				ТабДок.Вывести(ОбластьОшибки,2);
			КонецЦикла;
		КонецЕсли;
		
		ЕстьОшибки = Истина;
	КонецЦикла;
	
	Если Не ЕстьОшибки Тогда
		ОбластьПравила = Макет.ПолучитьОбласть("ИмяПравила");
		ОбластьПравила.Параметры.ИмяПравила = НСтр("ru = 'Ошибок не обнаружено'");
		ОбластьПравила.Параметры.КоличествоОшибок = 0;
		ТабДок.Вывести(ОбластьПравила, 0);
	КонецЕсли;
	
	ТабДок.ЗакончитьАвтогруппировкуСтрок();
	Возврат ТабДок;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокТиповМетаданныхДляПроверки()
	СписокТипов = Новый СписокЗначений;
	СписокТипов.Добавить("WebСервисы");	
	СписокТипов.Добавить("WSСсылки");	
	СписокТипов.Добавить("БизнесПроцессы");	
	СписокТипов.Добавить("ГруппыКоманд");
	СписокТипов.Добавить("Документы");	
	СписокТипов.Добавить("ЖурналыДокументов");	
	СписокТипов.Добавить("Задачи");	
	СписокТипов.Добавить("Интерфейсы");	
	СписокТипов.Добавить("Константы");	
	СписокТипов.Добавить("КритерииОтбора");
	СписокТипов.Добавить("НумераторыДокументов");
	СписокТипов.Добавить("Обработки");
	СписокТипов.Добавить("ОбщиеКартинки");
	СписокТипов.Добавить("ОбщиеКоманды");
	СписокТипов.Добавить("ОбщиеМакеты");
	СписокТипов.Добавить("ОбщиеМодули");
	СписокТипов.Добавить("ОбщиеФормы");
	СписокТипов.Добавить("Отчеты");
	СписокТипов.Добавить("ПакетыXDTO");
	СписокТипов.Добавить("ПараметрыСеанса");
	СписокТипов.Добавить("ПараметрыФункциональныхОпций");
	СписокТипов.Добавить("Перечисления");
	СписокТипов.Добавить("ПланыВидовРасчета");
	СписокТипов.Добавить("ПланыВидовХарактеристик");
	СписокТипов.Добавить("ПланыОбмена");
	СписокТипов.Добавить("ПланыСчетов");
	СписокТипов.Добавить("ПодпискиНаСобытия");
	СписокТипов.Добавить("Последовательности");
	СписокТипов.Добавить("РегистрыБухгалтерии");
	СписокТипов.Добавить("РегистрыНакопления");
	СписокТипов.Добавить("РегистрыРасчета");
	СписокТипов.Добавить("РегистрыСведений");
	СписокТипов.Добавить("РегламентныеЗадания");
	СписокТипов.Добавить("Роли");
	СписокТипов.Добавить("Справочники");
	СписокТипов.Добавить("Стили");
	СписокТипов.Добавить("ФункциональныеОпции");
	СписокТипов.Добавить("ХранилищаНастроек");
	СписокТипов.Добавить("ЭлементыСтиля");
	СписокТипов.Добавить("Языки");
	Возврат СписокТипов;
	
КонецФункции

&НаСервере
Функция ПолучитьСписокОбъектовПодсистем()
	
	СоответствиеОбъектовПодсистемам = Новый ТаблицаЗначений;
	СоответствиеОбъектовПодсистемам.Колонки.Добавить("Подсистема");
	СоответствиеОбъектовПодсистемам.Колонки.Добавить("Объект");
	
	// проведем проверку аналогично АПК - до второго уровня вложенности подсистем
	
	Для Каждого ПодсистемаПервогоУровня Из Метаданные.Подсистемы Цикл
		Для Каждого ПодсистемаВторогоУровня Из ПодсистемаПервогоУровня.Подсистемы Цикл
			Для Каждого ПодсистемаТретьегоУровня Из ПодсистемаВторогоУровня.Подсистемы Цикл
				ДополнитьСоответствиеОбъектамиПодсистемы(СоответствиеОбъектовПодсистемам, ПодсистемаТретьегоУровня);
			КонецЦикла;
			ДополнитьСоответствиеОбъектамиПодсистемы(СоответствиеОбъектовПодсистемам, ПодсистемаВторогоУровня);
		КонецЦикла;
		ДополнитьСоответствиеОбъектамиПодсистемы(СоответствиеОбъектовПодсистемам, ПодсистемаПервогоУровня);
	КонецЦикла;
	
	Возврат СоответствиеОбъектовПодсистемам;
	
КонецФункции

&НаСервере
Процедура ДополнитьСоответствиеОбъектамиПодсистемы(СоответствиеОбъектовПодсистемам, ИсследуемаяПодсистема)
	Для Каждого ОбъектСостава Из ИсследуемаяПодсистема.Состав Цикл
		НовоеСоответствие = СоответствиеОбъектовПодсистемам.Добавить();
		НовоеСоответствие.Подсистема = ИсследуемаяПодсистема;
		НовоеСоответствие.Объект = ОбъектСостава;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьТекстовыйФайлПоПути(КаталогОбмена, ИмяФайла)
	ФайлПоиска = НайтиФайлы(КаталогОбмена,ИмяФайла);
	Если ФайлПоиска.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	ТекстФайла = Новый ТекстовыйДокумент;
	ТекстФайла.Прочитать(ФайлПоиска[0].ПолноеИмя);
	Возврат ТекстФайла;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Подключаемые функции

// Описание функции проверки: 
// Функция проверки может проводиться как на клиенте, так и на сервере
// Возвращаемое значение : Неопределено, если ошибок не обнаружено, либо
//
// Структура ("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
//  <Пользовательское представление ошибки - строка>, <Пользовательское описание метода исправления - строка>
//  <Список текстовых представлений ошибок - список значений>).
//
// Если для описания ошибки достаточно только пользовательского представления (например, если
// надо сообщить, что модули не совпадают без уточнения места), тогда в качестве второго параметра
// структуры необходимо указывать Неопределено.
//
&НаСервере
Функция Подключаемый_ПроверитьПравильностьИменованияДемоОбъектов()
	
	СписокТиповМетаданных = ПолучитьСписокТиповМетаданныхДляПроверки();
	СписокНеПодчиненныхОбъектов = Новый СписокЗначений;
	
	Для Каждого ТипМетаданныхВСписке Из СписокТиповМетаданных Цикл
		ТипМетаданных = ТипМетаданныхВСписке.Значение;
		Для Каждого ОбъектМетаданныхЗаданногоТипа Из Метаданные[ТипМетаданных] Цикл
			Если ТипМетаданных = "ПараметрыСеанса" Тогда 
				Продолжить;
			КонецЕсли;
			
			Если Найти(ОбъектМетаданныхЗаданногоТипа.Имя, "_Демо") > 0 И Лев (ОбъектМетаданныхЗаданногоТипа.Имя, 5) <> "_Демо" Тогда
				СтрокаНаименования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 : %2", Строка(ТипМетаданных), ОбъектМетаданныхЗаданногоТипа.Имя);
				СписокНеПодчиненныхОбъектов.Добавить(СтрокаНаименования);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
	Возврат Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		НСтр("ru = 'Неправильно названные демо-объекты'"), 
		НСтр("ru = 'Демо-объекты именуются с префиксом ""_Демо"" в начале имени. Исключение - объекты типа ПараметрыСеанса, где данный префикс 
			|может находиться в середине имени.'"), СписокНеПодчиненныхОбъектов);
КонецФункции

&НаСервере
Функция Подключаемый_ПроверитьПринадлежностьОбъектовПодсистемам()
	
	Результат = Новый Массив();
	ОшибочныеОбъекты = ВыполнитьПроверкуПоПринадлежностиОбъектов();
	
	ПредставлениеОшибки = НСтр("ru = 'Следующие объекты не принадлежат ни одной поставляемой подсистеме.'");
	ОписаниеИсправления = НСтр("ru = 'Все объекты метаданных в конфигурации должны быть подчинены одной поставляемой подсистеме.
		|Исключение составляют только те объекты, которые не могут быть подчинены подсистемам в текущей версии платформы.'");
	СтруктураОшибок =  Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
		ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеОбъекты.НеПодчиненныеОбъекты);
	Результат.Добавить(СтруктураОшибок);
	
	ПредставлениеОшибки = НСтр("ru = 'Следующие объекты принадлежат более чем одной поставляемой подсистеме.'");
	СтруктураОшибок =  Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
		ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеОбъекты.ИзбыточноПодчиненныеОбъекты);
	Результат.Добавить(СтруктураОшибок);
		
	Возврат Результат;
КонецФункции

&НаСервере
Функция Подключаемый_ПроверитьПринадлежностьДемонстрационныхОбъектов()

	СписокОшибочныхОбъектов = Новый СписокЗначений;
	СписокОбъектовПодсистем = ПолучитьСписокОбъектовПодсистем();
	
	Для Каждого ПодчинениеОбъектаПодсистеме Из СписокОбъектовПодсистем Цикл		
		
		Если ЭтоПоставляемаяПодсистема(ПодчинениеОбъектаПодсистеме.Подсистема) И
			Лев(ПодчинениеОбъектаПодсистеме.Объект.Имя,5) = "_Демо" Тогда
			СписокОшибочныхОбъектов.Добавить(Строка(ПодчинениеОбъектаПодсистеме.Объект));
		КонецЕсли;
		
	КонецЦикла;
	
	ОписаниеИсправления = НСтр("ru = 'Демонстрационные объекты не должны быть включены в поставляемые подсистемы'");
	ПредставлениеОшибки = НСтр("ru = 'Следующие демонстрационные объекты включены в поставляемые подсистемы'");
	СтруктураОшибок =  Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьКорректностьКомментированияДемоПримеров()

	ТекстФайла = Новый ТекстовыйДокумент;
	СписокОшибочныхОбъектов  = Новый СписокЗначений;
	
	Если НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt").Количество() = 0 Тогда
		ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей);
	КонецЕсли;
	
	МассивМодулей = НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt");
	
	Для Каждого ФайлМодуля Из МассивМодулей Цикл
		
		НомерТекущейСтроки = 0;
		ТекстФайла.Прочитать(ФайлМодуля.ПолноеИмя);
		КоличествоСтрок = ТекстФайла.КоличествоСтрок();
		
		Пока НомерТекущейСтроки < КоличествоСтрок Цикл
			
			СтрокаПроверки = ТекстФайла.ПолучитьСтроку(НомерТекущейСтроки);
			Если Найти(СтрокаПроверки, "Демо ") = 0 Тогда
				НомерТекущейСтроки = НомерТекущейСтроки + 1;
				Продолжить;
			Иначе
				Если (Найти(СтрокаПроверки, "_Демо начало примера") = 0 И Найти(СтрокаПроверки, "_Демо конец примера") = 0) 
						И Найти(СтрокаПроверки, "//")> 0 Тогда
					СтрокаОшибки = "%1 : %2";
					СтрокаОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаОшибки,
					ФайлМодуля.ИмяБезРасширения,
					"Строка " + НомерТекущейСтроки);
					СписокОшибочныхОбъектов.Добавить(СтрокаОшибки);
				КонецЕсли;
			КонецЕсли;
			НомерТекущейСтроки = НомерТекущейСтроки + 1;
		КонецЦикла;
	КонецЦикла;
	
	ОписаниеИсправления = НСтр("ru = 'Демонстрационные примеры выделяются в коде с помощью комментариев вида 
		|//_Демо начало примера
		|<Код примера>
		|//_Демо конец примера
		|Другие представления демонстрационных примеров в коде запрещены.'");
	ПредставлениеОшибки = НСтр("ru = 'Возможно неправильное комментирование демонстрационных примеров.'");
	СтруктураОшибок =  Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьСовпадениеМодулейУправляемогоИОбычногоПриложений()
	
	ТекстФайлаОбычногоПриложения = Новый ТекстовыйДокумент;
	ТекстФайлаУправляемогоПриложения = Новый ТекстовыйДокумент;
	СписокОшибочныхОбъектов  = Новый СписокЗначений;
	
	Если НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt").Количество() = 0 Тогда
		ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей);
	КонецЕсли;
	
	ТекстФайлаОбычногоПриложения = ПолучитьТекстовыйФайлПоПути(КаталогВыгрузкиМодулей, "Конфигурация.МодульОбычногоПриложения.txt");
	ТекстФайлаУправляемогоПриложения = ПолучитьТекстовыйФайлПоПути(КаталогВыгрузкиМодулей, "Конфигурация.МодульУправляемогоПриложения.txt");
	
	Список = Новый СписокЗначений;
	
	// если не найден один из файлов - ошибка
	Если ТекстФайлаОбычногоПриложения = Неопределено Или ТекстФайлаУправляемогоПриложения = Неопределено Тогда
		Список.Добавить("Не удалось найти файлы модулей приложения!");
		Возврат Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",НСтр("ru = 'Не удалось найти файлы модулей приложения!'"),"", Список) ;
	КонецЕсли;
	
	ПредставлениеОшибки = Нстр("ru = 'Модули обычного и управляемого приложений различны!'");
	ОписаниеИсправления = НСтр("ru = 'БСП поддерживает работу как в управляемом, так и в обычном приложении.
		|Соответсвенно, модули обычного и управляемого приложений должны совпадать за исключением специально предусмотренных случаев'");
	
	// если количество строк различно - ошибка
	Если ТекстФайлаОбычногоПриложения.КоличествоСтрок() <> ТекстФайлаУправляемогоПриложения.КоличествоСтрок() Тогда
		Список.Добавить(ПредставлениеОшибки);
		Возврат Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
			ПредставлениеОшибки, ОписаниеИсправления, Список);
	КонецЕсли;
	
	НомерПроверяемойСтроки = 0;
	
	Пока НомерПроверяемойСтроки < ТекстФайлаОбычногоПриложения.КоличествоСтрок() Цикл
		
		Если СокрЛП(ТекстФайлаОбычногоПриложения.ПолучитьСтроку(НомерПроверяемойСтроки)) <> 
			СокрЛП(ТекстФайлаУправляемогоПриложения.ПолучитьСтроку(НомерПроверяемойСтроки)) Тогда
			Список.Добавить(ПредставлениеОшибки);
			Возврат Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
				ПредставлениеОшибки, ОписаниеИсправления, Список);
		КонецЕсли;
		НомерПроверяемойСтроки = НомерПроверяемойСтроки + 1;
	КонецЦикла;
	
	Возврат Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
		ПредставлениеОшибки, ОписаниеИсправления, Список);
	
КонецФункции

&НаСервере
Функция Подключаемый_ПроверитьУстановкуФлажковОбщихМодулей()
	СписокОшибочныхМодулей = Новый СписокЗначений;
	Для Каждого ОбщийМодуль Из Метаданные.ОбщиеМодули Цикл
		Если ОбщийМодуль.ВызовСервера И Не ОбщийМодуль.Сервер Тогда
			СписокОшибочныхМодулей.Добавить(ОбщийМодуль.Имя);
		КонецЕсли;
		
		Если ОбщийМодуль.ВызовСервера И ОбщийМодуль.КлиентУправляемоеПриложение Тогда
			Если СписокОшибочныхМодулей.НайтиПоЗначению(ОбщийМодуль.Имя) = Неопределено Тогда
				СписокОшибочныхМодулей.Добавить(ОбщийМодуль.Имя);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПредставлениеОшибки = НСтр("ru = 'Неправильно настроенные общие модули'");
	ОписаниеИсправления = НСтр("ru = 'Следующие комбинации настроек общих модулей недопустимы:
		|1) Флаг ""Вызов сервера"" установлен, но сброшен флаг ""Сервер"";
		|2) Установлены Флаги ""Вызов сервера"" и "" Клиент(управляемое приложение)""'");
	СтруктураОшибок =  Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхМодулей);
	Возврат СтруктураОшибок;
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьВызовыОбщихМодулейИзНихСамих()
	
	СписокОшибочныхОбъектов  = Новый СписокЗначений;
	
	Если НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt").Количество() = 0 Тогда
		ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей);
	КонецЕсли;
	
	ТекстФайла = Новый ТекстовыйДокумент;
	
	ПостфиксПовтИсп = "ПовтИсп.";
	
	МассивМодулей = НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt");
	Для Каждого ФайлМодуля Из МассивМодулей Цикл
		Если Найти(ФайлМодуля.Имя, "ОбщийМодуль") = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НомерТекущейСтроки = 0;
		ТекстФайла.Прочитать(ФайлМодуля.ПолноеИмя);
		КоличествоСтрок = ТекстФайла.КоличествоСтрок();
		
		ИмяОбщегоМодуля = СтрЗаменить(ФайлМодуля.ИмяБезРасширения,"ОбщийМодуль.","");
		ИмяОбщегоМодуля = СтрЗаменить(ИмяОбщегоМодуля,".Модуль",".");
		
		Если ВРег(Прав(ИмяОбщегоМодуля, СтрДлина(ПостфиксПовтИсп))) = ВРег(ПостфиксПовтИсп) Тогда
			Продолжить;
		КонецЕсли;
		
		Пока НомерТекущейСтроки < КоличествоСтрок Цикл
			
			СтрокаПроверки = ТекстФайла.ПолучитьСтроку(НомерТекущейСтроки);
			
			// Поиск строки вида "ИмяОбщегоМодуля."
			НайденВызов = Найти(СтрокаПроверки, ИмяОбщегоМодуля);
			Если НайденВызов = 0
				Или Найти(СтрокаПроверки, "//") > 0 Тогда
				НомерТекущейСтроки = НомерТекущейСтроки + 1;
				Продолжить;
			КонецЕсли;
			
			// Поиск открывающей скобки после строки "ИмяОбщегоМодуля."
			НайденаСкобка = Найти(Прав(СтрокаПроверки, СтрДлина(СтрокаПроверки)-НайденВызов), "(");
			Если НайденаСкобка = 0 Тогда
				НомерТекущейСтроки = НомерТекущейСтроки + 1;
				Продолжить;
			КонецЕсли;
			
			// Проверка первого символа перед вызовом.
			СимволПередВызовом = СокрЛП(Сред(СтрокаПроверки, НайденВызов-1, 1));
			Если (ПустаяСтрока(СимволПередВызовом) Или СимволПередВызовом = "(")
				// Проверка того, что между строкой "ИмяОбщегоМодуля." и открывающей скобкой нет пробелов.
				И Найти(Сред(СтрокаПроверки, НайденВызов, НайденаСкобка-НайденВызов), " ") = 0 Тогда
				
				// Расчет количества кавычек в строке.
				НачальныйСимвол = 1;
				КоличествоКавычек = 0;
				Пока Истина Цикл
					
					ЕстьКавычка = Найти(Сред(СтрокаПроверки, НачальныйСимвол, НайденВызов), """");
					Если ЕстьКавычка <> 0 Тогда
						КоличествоКавычек = КоличествоКавычек + 1;
						НачальныйСимвол = НачальныйСимвол + ЕстьКавычка;
					Иначе
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
				// Если количество кавычек нечетное - пропускаем строку.
				Если КоличествоКавычек <> 0 Тогда
					ЧетноеКоличество = (КоличествоКавычек/2 = Цел(КоличествоКавычек/2));
					Если Не ЧетноеКоличество Тогда
						НомерТекущейСтроки = НомерТекущейСтроки + 1;
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				// это не запрос
				НайденСимвол = Найти(СтрокаПроверки, "|");
				Если НайденСимвол > 0
					И НайденСимвол < НайденВызов Тогда
					НомерТекущейСтроки = НомерТекущейСтроки + 1;
					Продолжить;
				КонецЕсли;
				
				СтрокаОшибки = "%1 : %2";
				СтрокаОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаОшибки,
				ФайлМодуля.ИмяБезРасширения,
				"Строка " + НомерТекущейСтроки);
				СписокОшибочныхОбъектов.Добавить(СтрокаОшибки);
			КонецЕсли;
			
			НомерТекущейСтроки = НомерТекущейСтроки + 1;
		КонецЦикла;		
		
	КонецЦикла;
	
	ОписаниеИсправления = НСтр("ru = 'Возможен некорректный вызов процедуры или функции общего модуля из него самого.
		|В этом случае в строке вызова излишне писать имя модуля.'");
	ПредставлениеОшибки = НСтр("ru = 'Название общего модуля в его коде'");
	СтруктураОшибок =  Новый Структура("ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные", 
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаСервере
Функция Подключаемый_ПроверитьПравоПросмотраКомандОткрытияСписковВРолях()
	
	КоллекцииОбъектомМетаданных = Новый Массив;
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.Справочники);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.Документы);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.ЖурналыДокументов);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.ПланыВидовХарактеристик);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.ПланыСчетов);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.ПланыВидовРасчета);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.РегистрыСведений);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.РегистрыНакопления);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.РегистрыБухгалтерии);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.РегистрыРасчета);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.БизнесПроцессы);
	КоллекцииОбъектомМетаданных.Добавить(Метаданные.Задачи);
	
	СписокОшибочныхОбъектов = Новый СписокЗначений;
	
	Для каждого КоллекцияОбъектовМетаданных Из КоллекцииОбъектомМетаданных Цикл
		Для каждого ОбъектМетаданных Из КоллекцияОбъектовМетаданных Цикл
			Команда = ОбъектМетаданных.Команды.Найти(ОбъектМетаданных.Имя);
			Если Команда = Неопределено Тогда
				Продолжить;
			Иначе
				Для каждого Роль Из Метаданные.Роли Цикл
					ПравоОбъекта = ПравоДоступа("Просмотр", ОбъектМетаданных, Роль);
					ПравоКоманды = ПравоДоступа("Просмотр", Команда, Роль);
					Если ПравоОбъекта И НЕ ПравоКоманды Тогда
						
						// Пропускаем команды, которые открывают формы для редактирования объектов (списков объектов)
						// Такие команды не должны входить в роли, предназначенные только для просмотра объектов.
						КомандаДляРедактированияОбъекта = НСтр("ru = 'Открывает форму для редактирования'");
						Если Лев(Команда.Комментарий, СтрДлина(КомандаДляРедактированияОбъекта)) = КомандаДляРедактированияОбъекта 
							И НЕ ПравоДоступа("Редактирование", ОбъектМетаданных, Роль)	Тогда
							Продолжить;
						КонецЕсли;
						
						СписокОшибочныхОбъектов.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не хватает права Просмотр для дополнительной команды ""%1"" объекта метаданных ""%2"" в роле ""%3"".'"),
							Команда.Имя,
							ОбъектМетаданных.ПолноеИмя(),
							Роль.Имя));
							
					ИначеЕсли НЕ ПравоОбъекта И ПравоКоманды Тогда
						СписокОшибочныхОбъектов.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Лишнее право Просмотр для дополнительной команды ""%1"" объекта метаданных ""%2"" в роле ""%3"".'"),
							Команда.Имя,
							ОбъектМетаданных.ПолноеИмя(),
							Роль.Имя));
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ПредставлениеОшибки = НСтр("ru = 'Следующие объекты имеют различный доступ со своими дополнительными командами открытия списков.'");
	ОписаниеИсправления = НСтр("ru = 'Все объекты метаданых должны иметь одинаковые права на просмотр со своими дополнительными командами открытия списков.'");
	
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаСервере
Функция Подключаемый_ПроверитьРолиНаНаличиеНедопустимыхПрав()
	
	РолиИсключения = Новый Массив;
	РолиИсключения.Добавить("АдминистраторСистемы");
	РолиИсключения.Добавить("ДобавлениеИзменениеОбщейБазовойНСИ");
	РолиИсключения.Добавить("НастройкаСинхронизацииДанных");
	РолиИсключения.Добавить("НастройкаИОценкаПроизводительности");
	
	СписокОшибочныхОбъектов = Новый СписокЗначений;
	ПользователиСлужебный.ОбновитьПараметрыРаботыПользователей();
	
	НеразделенныеДанныеДоступныеДляИзменения =
		Константы.ПараметрыРаботыПользователей.СоздатьМенеджерЗначения(
			).НеразделенныеДанныеДоступныеДляИзменения();
	
	Для каждого Роль Из Метаданные.Роли Цикл
		ИмяРоли = Роль.Имя;
		
		Если РолиИсключения.Найти(ИмяРоли) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ПраваИзмененияОбъектов = НеразделенныеДанныеДоступныеДляИзменения.НайтиСтроки(
			Новый Структура("Роль", ИмяРоли));
		
		Если ПраваИзмененияОбъектов.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого СтрокаПрава Из ПраваИзмененияОбъектов Цикл
			ШаблонТекста = НСтр("ru = 'Для объекта %1 ошибочно установлено право %2 в роли %3'");
			СписокОшибочныхОбъектов.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, СтрокаПрава.Объект, СтрокаПрава.Право, ИмяРоли));
		КонецЦикла;
	КонецЦикла;
	
	ПредставлениеОшибки = НСтр("ru = 'Роли разделенных пользователей не должны содержать прав на изменение общих данных.'");
	ОписаниеИсправления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Для всех общих данных не должно быть прав на изменение
		           |в ролях, предназначенных для разделенных пользователей.
		           |
		           |(Если роль, по которой выдана ошибка, не предназначена
		           |для разделенных пользователей - ее необходимо внести в
		           |список исключений, в процедуре Подключаемый_ПроверитьРолиНаНаличиеНедопустимыхПрав модуля обработки ИнструментыРазработчикаПроверкаБСППередСборкой).'"),
		ИмяРоли);
	
	СтруктураОшибок =  Новый Структура;
	СтруктураОшибок.Вставить("ПредставлениеОшибки", ПредставлениеОшибки);
	СтруктураОшибок.Вставить("ОписаниеИсправления", ОписаниеИсправления);
	СтруктураОшибок.Вставить("ОшибочныеДанные",     СписокОшибочныхОбъектов);
	
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьКомментарииДляБлоковКодаПодсистем()
	
	СписокОшибочныхОбъектов = Новый СписокЗначений;
	
	Если НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt").Количество() = 0 Тогда
		ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей);
	КонецЕсли;
	
	МассивПодсистем = СписокИспользуемыхПодсистем();
	
	МассивМодулей = НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt");
	
	ТекстФайла = Новый ТекстовыйДокумент;
	
	Состояние(НСтр("ru = 'Проверка комментариев для блоков кода'"));
	
	Для Каждого Файл Из МассивМодулей Цикл
		
		Если Найти(Файл.Имя, "ИнструментыРазработчикаДополнительныеОтчетыИОбработкиРасстановкаФрагментовКода") > 0
			Или Найти(Файл.Имя, "ИнструментыРазработчикаПечатьРасстановкаФрагментовКода") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстФайла.Прочитать(Файл.ПолноеИмя);
		СтрокаТекста = ТекстФайла.ПолучитьТекст();
		
		Для Каждого Подсистема из МассивПодсистем Цикл
			Ошибки = "";
			ОпределитьОшибкиКомментариев(Файл.Имя, Подсистема, СтрокаТекста, Ошибки);
			Если Не ПустаяСтрока(Ошибки) Тогда 
				СписокОшибочныхОбъектов.Добавить(Ошибки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ПредставлениеОшибки = НСтр("ru = 'В следующих модулях указаны неверные комментарии для блоков кода подсистем.'");
	ОписаниеИсправления = НСтр("ru = 'Для всех блоков кода, относящихся к той или иной подсистеме, используются начальные и конечные комментарии вида:
	|<ПутьКПодсистеме>
	|...
	|Конец <ПутьКПодсистеме>'");
	
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьПравильностьУсловныхВызовов()
	
	СписокОшибочныхОбъектов = Новый СписокЗначений;
	
	Если НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt").Количество() = 0 Тогда
		ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей);
	КонецЕсли;
	
	МассивМодулей = НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt");
	
	СписокИспользуемыхПодсистем = СписокИспользуемыхПодсистем();
	ТекстФайла = Новый ТекстовыйДокумент;
	
	Состояние(НСтр("ru = 'Проверка условных вызовов'"));
	
	Для Каждого Файл Из МассивМодулей Цикл
		
		Если Найти(Файл.Имя, "ИнструментыРазработчикаПроверкаБСППередСборкой") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Ошибки = "";
		ТекстФайла.Прочитать(Файл.ПолноеИмя);
		СтрокаТекста = ТекстФайла.ПолучитьТекст();
		
		ОпределитьОшибкиУсловныхВызовов(Файл.Имя, СтрокаТекста, Ошибки, СписокИспользуемыхПодсистем);
		Если Не ПустаяСтрока(Ошибки) Тогда
			СписокОшибочныхОбъектов.Добавить(Ошибки);
		КонецЕсли;
		
	КонецЦикла;
	
	ПредставлениеОшибки = НСтр("ru = 'Возможно некорректный вызов функции ""ПодсистемаСуществует"" 
		|или допущена орфографическая ошибка в названии подсистемы.'");
	ОписаниеИсправления = НСтр("ru = 'Название подсистемы должно задаваться с учетом иерархии в виде - ""СтандартныеПодсистемы.ИмяПодсистемы""
		|или ""СтандартныеПодсистемы.ИмяПодсистемы1.ИмяПодсистемы2"" :'");
	
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьНаличиеПраваИнтерактивноеУдаление()
	
	СписокОшибочныхОбъектов = ПроверкаПравРолей("ИнтерактивноеУдаление", "ВсеРоли");
	
	ПредставлениеОшибки = НСтр("ru = 'Недопустимо наличие права ""Интерактивное удаление"".'");
	ОписаниеИсправления = Неопределено;
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьРольПолныеПраваДляРазделяемыхОбъектовМетаданных()
	
	СписокОшибочныхОбъектов = ПроверкаПравРолей("ПраваРазделенныхОМД", "ПолныеПрава");
	
	ПредставлениеОшибки = НСтр("ru = 'Роль ПолныеПрава должна давать все права на разделенные объекты метаданных.'");
	ОписаниеИсправления = Неопределено;
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьРольАдминистраторСистемыДляРазделяемыхОбъектовМетаданных()
	
	СписокОшибочныхОбъектов = ПроверкаПравРолей("ПраваРазделенныхОМД", "АдминистраторСистемы");
	
	ПредставлениеОшибки = НСтр("ru = 'Роль АдминистраторСистемы не должна давать права на разделенные объекты метаданных.'");
	ОписаниеИсправления = Неопределено;
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьРольАдминистраторСистемыДляНеразделяемыхОбъектовМетаданных()
	
	СписокОшибочныхОбъектов = ПроверкаПравРолей("ПраваНеразделенныхОМД", "АдминистраторСистемы");
	
	ПредставлениеОшибки = НСтр("ru = 'Роль АдминистраторСистемы должна давать все права на неразделенные объекты метаданных.'");
	ОписаниеИсправления = Неопределено;
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_НекорректныеСвязиМеждуПодсистемами()
	
	Если НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt").Количество() = 0 Тогда
		ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей);
	КонецЕсли;
	
	СписокОшибочныхОбъектов = НекорректныеСвязиМеждуПодсистемами();
	
	ПредставлениеОшибки = НСтр("ru = 'Недопустимо наличие недокументированных жестких связей между подсистемами.'");
	ОписаниеИсправления = Неопределено;
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ВыполнитьПоискЗапрещенныхКомментариев()
	
	Если НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt").Количество() = 0 Тогда
		ВыгрузитьМодулиВФайлы(КаталогВыгрузкиМодулей);
	КонецЕсли;
	
	СтрокаПоиска = "TO"+"DO";
	СписокОшибочныхОбъектов = ПоискЗапрещенныхКомментариевВКодеМодулей(СтрокаПоиска);
	
	ПредставлениеОшибки = НСтр("ru = 'Недопустимо наличие комментария ""%1"" в текстах модулей.'");
	ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеОшибки, СтрокаПоиска);
	ОписаниеИсправления = Неопределено;
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_ПроверитьЗначениеФлагаВключатьВСодержаниеСправки()
	
	СписокОшибочныхОбъектов = ПроцедурыСФлагомВключатьВСодержаниеСправки();
	
	ПредставлениеОшибки = НСтр("ru = 'Поставляемые подсистемы не должны содержать флаг ""Включать в содержание справки"".'");
	ОписаниеИсправления = Неопределено;
	СтруктураОшибок =  Новый Структура(
		"ПредставлениеОшибки, ОписаниеИсправления, ОшибочныеДанные",
		ПредставлениеОшибки, ОписаниеИсправления, СписокОшибочныхОбъектов);
	Возврат СтруктураОшибок;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вспомогательные процедуры и функции проверок

&НаКлиенте
Процедура ОпределитьОшибкиУсловныхВызовов(ИмяФайлаМодуля, Знач СтрокаТекста, Ошибки, СписокИспользуемыхПодсистем)
	
	Если Найти(ИмяФайлаМодуля, "ОбщегоНазначенияКлиентСервер") > 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "";
	СтрокаУсловногоВызова = "ОбщегоНазначения.ПодсистемаСуществует(";
	СтрокаУсловногоВызова2 = "ОбщегоНазначенияКлиент.ПодсистемаСуществует(";
	
	Если Найти(ИмяФайлаМодуля, ".Модуль.txt") Тогда
		СокращенноеИмяФайлаМодуля = СтрЗаменить(ИмяФайлаМодуля, ".Модуль.txt", "");
	ИначеЕсли Найти(ИмяФайлаМодуля, ".Module.txt") Тогда
		СокращенноеИмяФайлаМодуля = СтрЗаменить(ИмяФайлаМодуля, ".Module.txt", "");
	КонецЕсли;
	
	Пока Истина Цикл
		
		ПозицияНачалаУсловногоВызова = Найти(СтрокаТекста, СтрокаУсловногоВызова);
		
		Если ПозицияНачалаУсловногоВызова = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ТекущаяСтрокаТекста = Прав(СтрокаТекста, СтрДлина(СтрокаТекста) - ПозицияНачалаУсловногоВызова + 1);
		ПозицияОкончанияУсловногоВызова = Найти(ТекущаяСтрокаТекста, """)");
		Если ПозицияОкончанияУсловногоВызова = 0 Тогда
			Возврат;
		КонецЕсли;
		
		УсловныйВызов = Сред(ТекущаяСтрокаТекста, 0, ПозицияОкончанияУсловногоВызова + 2);
		УсловныйВызов = СокрЛП(УсловныйВызов);
		
		Если ИмяПодсистемыЗаданоНекорректно(УсловныйВызов, СписокИспользуемыхПодсистем) Тогда
			
			Если ТекстСообщения = "" Тогда
				ТекстСообщения = НСтр("ru = '%1: %2'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СокращенноеИмяФайлаМодуля, УсловныйВызов);
			Иначе
				ТекстСообщения = УсловныйВызов;
			КонецЕсли;
			
			Ошибки = Ошибки + Символы.ПС + ТекстСообщения;
			
		КонецЕсли;
		
		СтрокаТекста = Прав(ТекущаяСтрокаТекста, СтрДлина(ТекущаяСтрокаТекста) - ПозицияОкончанияУсловногоВызова + 1);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ИмяПодсистемыЗаданоНекорректно(УсловныйВызов, СписокИспользуемыхПодсистем)
	
	ИмяЗаданоКорректно = Ложь;
	
	Если Найти(УсловныйВызов, "ТехнологияСервиса.") > 0
		Или Найти(УсловныйВызов, "ОбменДаннымиXDTO") > 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого ИспользуемаяПодсистема Из СписокИспользуемыхПодсистем Цикл
		
		Если Найти(УсловныйВызов, ИспользуемаяПодсистема) > 0 Тогда
			ИмяЗаданоКорректно = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Не ИмяЗаданоКорректно;
	
КонецФункции

&НаСервере
Функция ВыполнитьПроверкуПоПринадлежностиОбъектов()
	СписокОбъектовПодсистем = ПолучитьСписокОбъектовПодсистем();
	
	// возьмем только список объектов метаданных, представленных в подсистемах	
	СписокТиповМетаданных = ПолучитьСписокТиповМетаданныхДляПроверки();
	
	// не будем проверять:
	УдалитьИзСписка(СписокТиповМетаданных,"ФункциональныеОпции");
	УдалитьИзСписка(СписокТиповМетаданных,"ПараметрыФункциональныхОпций");
	УдалитьИзСписка(СписокТиповМетаданных,"Языки");
	
	НеПодчиненныеОбъекты = Новый СписокЗначений;
	ИзбыточноПодчиненныеОбъекты = Новый СписокЗначений;
	
	Для Каждого ТипМетаданныхВСписке Из СписокТиповМетаданных Цикл
		ТипМетаданных = ТипМетаданныхВСписке.Значение;
		Для Каждого ОбъектМетаданныхЗаданногоТипа Из Метаданные[ТипМетаданных] Цикл
			// пропускаем демо-объекты
			Если Не ЭтоПоставляемыйОбъект(ОбъектМетаданныхЗаданногоТипа, ТипМетаданных) Тогда
				Продолжить;
			КонецЕсли;
			// ищем объекты, которые не подчинены ни одной подсистеме.
			ПараметрыОтбора = Новый Структура("Объект",ОбъектМетаданныхЗаданногоТипа);
			ПодчиненияПодсистемам = СписокОбъектовПодсистем.НайтиСтроки(ПараметрыОтбора);
			Если ПодчиненияПодсистемам.Количество() = 0 Тогда
				СтрокаНаименования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 : %2", Строка(ТипМетаданных), 
					ОбъектМетаданныхЗаданногоТипа.Имя);
				НеПодчиненныеОбъекты.Добавить(СтрокаНаименования);
			КонецЕсли;
			
			// ищем объекты, которые подчинены более чем одной подсистеме.
			Если ПодчиненияПодсистемам.Количество() > 0 Тогда
				ЧислоПоставляемыхПодсистем = 0;
				Для Каждого РодительскаяПодсистема Из ПодчиненияПодсистемам Цикл
					Если ЭтоПоставляемаяПодсистема(РодительскаяПодсистема.Подсистема) Тогда
						ЧислоПоставляемыхПодсистем = ЧислоПоставляемыхПодсистем + 1;
					КонецЕсли;
				КонецЦикла;
				Если ЧислоПоставляемыхПодсистем > 1 Тогда
					СтрокаНаименования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 : %2", Строка(ТипМетаданных), 
						ОбъектМетаданныхЗаданногоТипа.Имя);
					ИзбыточноПодчиненныеОбъекты.Добавить(СтрокаНаименования);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Новый Структура("НеПодчиненныеОбъекты,ИзбыточноПодчиненныеОбъекты", 
		НеПодчиненныеОбъекты, ИзбыточноПодчиненныеОбъекты);
	
КонецФункции

&НаСервере
Функция ЭтоПоставляемыйОбъект(ОбъектМетаданных, ТипМетаданных)
	
	Если ТипМетаданных = "ПараметрыСеанса" Тогда
		ИсключениеПроверки = (Найти(ОбъектМетаданных.Имя, "_Демо") > 0);
	Иначе
		ИсключениеПроверки = (Лев(ОбъектМетаданных.Имя, 5) =  "_Демо");
	КонецЕсли;
	
	Возврат НЕ ИсключениеПроверки;
КонецФункции

&НаСервере
Процедура УдалитьИзСписка(Список, ИмяОбъекта)
	УдаляемоеЗначение = Список.НайтиПоЗначению(ИмяОбъекта);
	Если УдаляемоеЗначение = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	Список.Удалить(УдаляемоеЗначение);
КонецПроцедуры

&НаСервере 
Функция ЭтоПоставляемаяПодсистема(ПодсистемаПроверки)
	ПоставляемаяПодсистема = Метаданные.Подсистемы.СтандартныеПодсистемы;
	Возврат ПодсистемаПроверки = ПоставляемаяПодсистема ИЛИ
		ПодсистемаПроверки.Родитель() = ПоставляемаяПодсистема ИЛИ
		ПодсистемаПроверки.Родитель().Родитель() = ПоставляемаяПодсистема;
КонецФункции

&НаСервере
Функция СписокИспользуемыхПодсистем()
	
	Результат = Новый Массив;
	СтандартныеПодсистемы = Метаданные.Подсистемы.Найти("СтандартныеПодсистемы");
	Если СтандартныеПодсистемы <> Неопределено Тогда
		СписокПодсистем = СтандартныеПодсистемы.Подсистемы;
		ПолучитьСписокПодсистемИзДерева(Результат, СписокПодсистем, "")
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПолучитьСписокПодсистемИзДерева(СписокПодсистем, ВложенныеПодсистемы, ПутьКПодсистеме)
	
	Если ВложенныеПодсистемы.Количество() > 0 Тогда 
		Для каждого Подсистема из ВложенныеПодсистемы Цикл
			РезервныйПуть = ПутьКПодсистеме;
			ПутьКПодсистеме = ПутьКПодсистеме + "." + Строка(Подсистема.Имя);
			ПолучитьСписокПодсистемИзДерева(СписокПодсистем, Подсистема.Подсистемы, ПутьКПодсистеме);
			СписокПодсистем.Добавить("СтандартныеПодсистемы." + Сред(ПутьКПодсистеме, 2));
			ПутьКПодсистеме = РезервныйПуть;
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ПодсистемаИсключение Из ВнедряемыеПодсистемы() Цикл
		СписокПодсистем.Добавить(ПодсистемаИсключение);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВнедряемыеПодсистемы()
	
	Исключения = Новый Массив;
	Исключения.Добавить("СтандартныеПодсистемы.ВнешниеБизнесПроцессыИЗадачи");
	Исключения.Добавить("СтандартныеПодсистемы.РезервноеКопированиеОбластейДанныхМС");
	
	Возврат Исключения;
	
КонецФункции

&НаКлиенте
Процедура ОпределитьОшибкиКомментариев(ИмяФайлаМодуля, ИмяПодсистемы, Знач СтрокаТекста, Ошибки)
	
	НачалоФрагмента = НайтиНачалоФрагмента(СтрокаТекста, ИмяПодсистемы);
	Пока НачалоФрагмента > 0 Цикл
		
		ПозицияКонцаФрагмента = НайтиКонецФрагмента(СтрокаТекста, ИмяПодсистемы);
		Если ПозицияКонцаФрагмента = 0 Тогда
			ТекстСообщения = НСтр("ru = '[ИмяФайлаМодуля]: для открывающей скобки [НачалоФрагмента] не обнаружена закрывающая скобка.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "[НачалоФрагмента]", "// " + ИмяПодсистемы);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "[ИмяФайлаМодуля]", ИмяФайлаМодуля);
			Ошибки = Ошибки + Символы.ПС + ТекстСообщения;
			Возврат;
		КонецЕсли;
		
		Если ПозицияКонцаФрагмента < НачалоФрагмента Тогда
			ТекстСообщения = НСтр("ru = '[ИмяФайлаМодуля]: для открывающей скобки [НачалоФрагмента] закрывающая скобка расположена выше по тексту.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "[НачалоФрагмента]", "// " + ИмяПодсистемы);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "[ИмяФайлаМодуля]", ИмяФайлаМодуля);
			Ошибки = Ошибки + Символы.ПС + ТекстСообщения;
			Возврат;
		КонецЕсли;
		
		ДлинаНачалаФрагмента = СтрДлина("// " + ИмяПодсистемы);
		ПромежуточнаяСтрока = Сред(СтрокаТекста, НачалоФрагмента + ДлинаНачалаФрагмента + 1, ПозицияКонцаФрагмента - (НачалоФрагмента + ДлинаНачалаФрагмента) + 1);
		Если НайтиНачалоФрагмента(ПромежуточнаяСтрока, ИмяПодсистемы) > 0 Тогда 
			ТекстСообщения = НСтр("ru = '[ИмяФайлаМодуля]: внутри открывающейся скобки [НачалоФрагмента] есть еще одна открывающаяся скобка, до закрывающейся. Фрагмент кода: [ФрагментКода]'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "[НачалоФрагмента]", "// " + ИмяПодсистемы);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "[ИмяФайлаМодуля]", ИмяФайлаМодуля);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "[ФрагментКода]", Лев(ПромежуточнаяСтрока, 200));
			Ошибки = Ошибки + Символы.ПС + ТекстСообщения;
			Возврат;
		КонецЕсли;
		
		ПозицияПоследнегоСимвола = ПозицияКонцаФрагмента + СтрДлина("// Конец " + ИмяПодсистемы);
		СтрокаТекста = Сред(СтрокаТекста, ПозицияПоследнегоСимвола);
		
		НачалоФрагмента = НайтиНачалоФрагмента(СтрокаТекста, ИмяПодсистемы);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиНачалоФрагмента(Знач СтрокаТекста, Знач ИмяПодсистемы)
	
	СтрокаТекста  = НРег(СтрокаТекста);
	ИмяПодсистемы = НРег(ИмяПодсистемы);
	
	ПервыйВариант = "// " + ИмяПодсистемы;
	ВторойВариант = "//" + ИмяПодсистемы;
	
	Если Найти(СтрокаТекста, ПервыйВариант) = 0 и Найти(СтрокаТекста, ВторойВариант) = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Для Итерация = 1 по СтрДлина(СтрокаТекста) Цикл
		
		Если Сред(СтрокаТекста, Итерация, СтрДлина(ПервыйВариант)) = (ПервыйВариант) Тогда
			Если Не ПустаяСтрока(Сред(СтрокаТекста, Итерация + СтрДлина(ПервыйВариант), 1)) Тогда 
				Продолжить;
			КонецЕсли;
			Возврат Итерация;
		КонецЕсли;
		
		Если Сред(СтрокаТекста, Итерация, СтрДлина(ВторойВариант)) = (ВторойВариант) Тогда 
			Если Не ПустаяСтрока(Сред(СтрокаТекста, Итерация + СтрДлина(ВторойВариант), 1)) Тогда 
				Продолжить;
			КонецЕсли;
			Возврат Итерация;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

&НаКлиенте
Функция НайтиКонецФрагмента(Знач СтрокаТекста, Знач ИмяПодсистемы)
	
	СтрокаТекста  = НРег(СтрокаТекста);
	ИмяПодсистемы = НРег(ИмяПодсистемы);
	
	ПервыйВариант = "// конец " + ИмяПодсистемы;
	ВторойВариант = "//конец " + ИмяПодсистемы;
	
	Если Найти(СтрокаТекста, ПервыйВариант) = 0 и Найти(СтрокаТекста, ВторойВариант) = 0 Тогда
		Возврат 0;
	КонецЕсли;
	
	Для Итерация = 1 по СтрДлина(СтрокаТекста) Цикл
		
		Если Сред(СтрокаТекста, Итерация, СтрДлина(ПервыйВариант)) = (ПервыйВариант) Тогда 
			Если Не ПустаяСтрока(Сред(СтрокаТекста, Итерация + СтрДлина(ПервыйВариант), 1)) Тогда 
				Продолжить;
			КонецЕсли;
			Возврат Итерация;
		КонецЕсли;
		
		Если Сред(СтрокаТекста, Итерация, СтрДлина(ВторойВариант)) = (ВторойВариант) Тогда 
			Если Не ПустаяСтрока(Сред(СтрокаТекста, Итерация + СтрДлина(ВторойВариант), 1)) Тогда 
				Продолжить;
			КонецЕсли;
			Возврат Итерация;
		КонецЕсли;
	КонецЦикла;
	
	Возврат 0;
	
КонецФункции

&НаСервере
Функция ПроверкаПравРолей(ТипПроверки, НазваниеРоли)
	
	СтруктураПрав = ПраваОбъектовМетаданных();
	
	НеверноЗаполненныеРоли = Новый СписокЗначений;
	
	// Проверка прав на планы обмена
	Для Каждого Элемент Из Метаданные.ПланыОбмена Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'ПланыОбмена'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.ПланыОбмена, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	// Проверка прав на константы
	Для Каждого Элемент Из Метаданные.Константы Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'Константы'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.Константы, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	// Проверка прав на справочники
	Для Каждого Элемент Из Метаданные.Справочники Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'Справочники'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.Справочники, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	// Проверка прав на документы
	Для Каждого Элемент Из Метаданные.Документы Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'Документы'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.Документы, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	// Проверка прав на планы видов характеристик
	Для Каждого Элемент Из Метаданные.ПланыВидовХарактеристик Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'ПланыВидовХарактеристик'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.ПланыВидовХарактеристик, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	// Проверка прав на регистры сведений
	Для Каждого Элемент Из Метаданные.РегистрыСведений Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'РегистрыСведений'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.РегистрыСведений, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	// Проверка прав на регистры накопления
	Для Каждого Элемент Из Метаданные.РегистрыНакопления Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'РегистрыНакопления'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.РегистрыНакопления, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	// Проверка прав на бизнес-процессы
	Для Каждого Элемент Из Метаданные.БизнесПроцессы Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'БизнесПроцессы'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.БизнесПроцессы, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	// Проверка прав на задачи
	Для Каждого Элемент Из Метаданные.Задачи Цикл
		
		ИмяОбъектаМетаданных = НСтр("ru = 'Задачи'");
		НеверноЗаполненныеРоли = ПроверкаРоли(
			Элемент, СтруктураПрав.Задачи, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных);
		
	КонецЦикла;
	
	Возврат НеверноЗаполненныеРоли;
	
КонецФункции

&НаСервере
Функция ПроверкаРоли(ОбъектМД, СписокПрав, НеверноЗаполненныеРоли, ТипПроверки, НазваниеРоли, ИмяОбъектаМетаданных)
	
	ЭлементСоставаОбщегоРеквизита = Метаданные.ОбщиеРеквизиты.ОбластьДанныхОсновныеДанные.Состав.Найти(ОбъектМД);
	
	Если СписокИсключений.НайтиПоЗначению(ЭлементСоставаОбщегоРеквизита.Метаданные.Имя) <> Неопределено Тогда
		Возврат НеверноЗаполненныеРоли;
	КонецЕсли;
	
	Если ТипПроверки = "ИнтерактивноеУдаление" Тогда
		
		Если СписокПрав.НайтиПоЗначению("ИнтерактивноеУдаление") = Неопределено Тогда
			Возврат НеверноЗаполненныеРоли;
		КонецЕсли;
		
		Для Каждого ЭлементРоль Из Метаданные.Роли Цикл
			ДоступКОбъектуУстановлен = ПравоДоступа(
				"ИнтерактивноеУдаление", ЭлементСоставаОбщегоРеквизита.Метаданные, ЭлементРоль);
				
			Если ДоступКОбъектуУстановлен Тогда
				СтрокаОшибки = НСтр("ru = 'Роль %1, объект %2.%3'");
				СтрокаОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаОшибки,
					ЭлементРоль.Имя, ИмяОбъектаМетаданных,ЭлементСоставаОбщегоРеквизита.Метаданные.Имя);
				НеверноЗаполненныеРоли.Добавить(СтрокаОшибки);
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого Элемент Из СписокПрав Цикл
			
			Если Элемент.Значение = "ИнтерактивноеУдаление" Тогда
				Продолжить;
			КонецЕсли;
			
			Если НазваниеРоли = "АдминистраторСистемы" Тогда
				ДоступКОбъектуУстановлен = ПравоДоступа(
					Элемент.Значение, ЭлементСоставаОбщегоРеквизита.Метаданные, Метаданные.Роли.АдминистраторСистемы);
			ИначеЕсли НазваниеРоли = "ПолныеПрава" Тогда
				ДоступКОбъектуУстановлен = ПравоДоступа(
					Элемент.Значение, ЭлементСоставаОбщегоРеквизита.Метаданные, Метаданные.Роли.ПолныеПрава);
			КонецЕсли;
			
			Если ТипПроверки = "ПраваРазделенныхОМД" Тогда
				
				Если НазваниеРоли = "АдминистраторСистемы" Тогда
					
					Если ЭлементСоставаОбщегоРеквизита.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Авто Тогда
						
						Если ДоступКОбъектуУстановлен Тогда
							НеверноЗаполненныеРоли.Добавить(ИмяОбъектаМетаданных + "." + ЭлементСоставаОбщегоРеквизита.Метаданные.Имя);
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
					
				ИначеЕсли НазваниеРоли = "ПолныеПрава" Тогда
					
					Если ЭлементСоставаОбщегоРеквизита.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.Авто
						И Не ДоступКОбъектуУстановлен Тогда
						НеверноЗаполненныеРоли.Добавить(ИмяОбъектаМетаданных + "." + ЭлементСоставаОбщегоРеквизита.Метаданные.Имя);
						Прервать;
					КонецЕсли;
					
				КонецЕсли;
				
			ИначеЕсли ТипПроверки = "ПраваНеразделенныхОМД" Тогда
				
				Если НазваниеРоли = "АдминистраторСистемы" Тогда
					
					Если ЭлементСоставаОбщегоРеквизита.Использование = Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.НеИспользовать Тогда
						
						Если Не ДоступКОбъектуУстановлен Тогда
							НеверноЗаполненныеРоли.Добавить(ИмяОбъектаМетаданных + "." + ЭлементСоставаОбщегоРеквизита.Метаданные.Имя);
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
					
				ИначеЕсли НазваниеРоли = "ПолныеПрава" Тогда
					
					Если ЭлементСоставаОбщегоРеквизита.Использование = 
							Метаданные.СвойстваОбъектов.ИспользованиеОбщегоРеквизита.НеИспользовать Тогда
							
						Если Элемент.Значение = "Чтение"
							Или Элемент.Значение = "Просмотр"
							Или Элемент.Значение = "ВводПоСтроке" Тогда
							Продолжить;
						ИначеЕсли ДоступКОбъектуУстановлен Тогда
							НеверноЗаполненныеРоли.Добавить(ИмяОбъектаМетаданных + "." + ЭлементСоставаОбщегоРеквизита.Метаданные.Имя);
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат НеверноЗаполненныеРоли;
	
КонецФункции

&НаСервере
Функция ПраваОбъектовМетаданных()
	
	СтруктураПрав = Новый Структура;
	
	// Планы обмена
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Добавление");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Удаление");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("ИнтерактивноеДобавление");
	СписокПрав.Добавить("Редактирование");
	СписокПрав.Добавить("ИнтерактивноеУдаление");
	СписокПрав.Добавить("ИнтерактивнаяПометкаУдаления");
	СписокПрав.Добавить("ИнтерактивноеСнятиеПометкиУдаления");
	СписокПрав.Добавить("ИнтерактивноеУдалениеПомеченных");
	СписокПрав.Добавить("ВводПоСтроке");
	СтруктураПрав.Вставить("ПланыОбмена", СписокПрав);
	
	// Константы
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("Редактирование");
	СтруктураПрав.Вставить("Константы", СписокПрав);
	
	// Справочники
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Добавление");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Удаление");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("ИнтерактивноеДобавление");
	СписокПрав.Добавить("Редактирование");
	СписокПрав.Добавить("ИнтерактивноеУдаление");
	СписокПрав.Добавить("ИнтерактивнаяПометкаУдаления");
	СписокПрав.Добавить("ИнтерактивноеСнятиеПометкиУдаления");
	СписокПрав.Добавить("ИнтерактивноеУдалениеПомеченных");
	СписокПрав.Добавить("ВводПоСтроке");
	СтруктураПрав.Вставить("Справочники", СписокПрав);
	
	//Документы
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Добавление");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Удаление");
	СписокПрав.Добавить("Проведение");
	СписокПрав.Добавить("ОтменаПроведения");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("ИнтерактивноеДобавление");
	СписокПрав.Добавить("Редактирование");
	СписокПрав.Добавить("ИнтерактивноеУдаление");
	СписокПрав.Добавить("ИнтерактивнаяПометкаУдаления");
	СписокПрав.Добавить("ИнтерактивноеСнятиеПометкиУдаления");
	СписокПрав.Добавить("ИнтерактивноеУдалениеПомеченных");
	СписокПрав.Добавить("ИнтерактивноеПроведение");
	СписокПрав.Добавить("ИнтерактивноеПроведениеНеОперативное");
	СписокПрав.Добавить("ИнтерактивнаяОтменаПроведения");
	СписокПрав.Добавить("ИнтерактивноеИзменениеПроведенных");
	СписокПрав.Добавить("ВводПоСтроке");
	СтруктураПрав.Вставить("Документы", СписокПрав);
	
	// Планы видов характеристик
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Добавление");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Удаление");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("ИнтерактивноеДобавление");
	СписокПрав.Добавить("Редактирование");
	СписокПрав.Добавить("ИнтерактивноеУдаление");
	СписокПрав.Добавить("ИнтерактивнаяПометкаУдаления");
	СписокПрав.Добавить("ИнтерактивноеСнятиеПометкиУдаления");
	СписокПрав.Добавить("ИнтерактивноеУдалениеПомеченных");
	СписокПрав.Добавить("ВводПоСтроке");
	СтруктураПрав.Вставить("ПланыВидовХарактеристик", СписокПрав);
	
	// Регистры сведений
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("Редактирование");
	СтруктураПрав.Вставить("РегистрыСведений", СписокПрав);
	
	// Регистры накопления
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("Редактирование");
	СписокПрав.Добавить("УправлениеИтогами");
	СтруктураПрав.Вставить("РегистрыНакопления", СписокПрав);
	
	// Бизнес-процессы
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Добавление");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Удаление");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("ИнтерактивноеДобавление");
	СписокПрав.Добавить("Редактирование");
	СписокПрав.Добавить("ИнтерактивноеУдаление");
	СписокПрав.Добавить("ИнтерактивнаяПометкаУдаления");
	СписокПрав.Добавить("ИнтерактивноеСнятиеПометкиУдаления");
	СписокПрав.Добавить("ИнтерактивноеУдалениеПомеченных");
	СписокПрав.Добавить("ВводПоСтроке");
	СписокПрав.Добавить("ИнтерактивнаяАктивация");
	СписокПрав.Добавить("Старт");
	СписокПрав.Добавить("ИнтерактивныйСтарт");
	СтруктураПрав.Вставить("БизнесПроцессы", СписокПрав);
	
	// Задачи
	СписокПрав = Новый СписокЗначений;
	СписокПрав.Добавить("Чтение");
	СписокПрав.Добавить("Добавление");
	СписокПрав.Добавить("Изменение");
	СписокПрав.Добавить("Удаление");
	СписокПрав.Добавить("Просмотр");
	СписокПрав.Добавить("ИнтерактивноеДобавление");
	СписокПрав.Добавить("Редактирование");
	СписокПрав.Добавить("ИнтерактивноеУдаление");
	СписокПрав.Добавить("ИнтерактивнаяПометкаУдаления");
	СписокПрав.Добавить("ИнтерактивноеСнятиеПометкиУдаления");
	СписокПрав.Добавить("ИнтерактивноеУдалениеПомеченных");
	СписокПрав.Добавить("ВводПоСтроке");
	СписокПрав.Добавить("ИнтерактивнаяАктивация");
	СписокПрав.Добавить("Выполнение");
	СписокПрав.Добавить("ИнтерактивноеВыполнение");
	СтруктураПрав.Вставить("Задачи", СписокПрав);
	
	Возврат СтруктураПрав;
КонецФункции

&НаСервере
Функция НекорректныеСвязиМеждуПодсистемами()
	
	СписокОшибочныхОбъектов = Новый СписокЗначений;
	НекорректныеСвязи = 
		Отчеты.ИнструментыРазработчикаЗависимостиПодсистем.НекорректныеЗависимостиПодсистемБСП(КаталогВыгрузкиМодулей);
		
	КопияТаблицы = НекорректныеСвязи.Скопировать();
	КопияТаблицы.Свернуть("ВызывающаяПодсистема");
	Для Каждого СтрокаВызывающаяПодсистема Из КопияТаблицы Цикл
		
		ПараметрыОтбора = Новый Структура("ВызывающаяПодсистема", СтрокаВызывающаяПодсистема.ВызывающаяПодсистема);
		НайденныеСтроки = НекорректныеСвязи.НайтиСтроки(ПараметрыОтбора);
		СформированнаяСтрока = "";
		ТекущаяВызываемаяПодсистема = "";
		НомерСтроки = 0;
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			Если СформированнаяСтрока = ""
				Или ТекущаяВызываемаяПодсистема <> НайденнаяСтрока.ВызываемаяПодсистема Тогда
				
				Если ТекущаяВызываемаяПодсистема <> ""
					И СформированнаяСтрока <> "" Тогда
					СформированнаяСтрока = СформированнаяСтрока + Символы.ПС;
				КонецЕсли;
				
				ТекущаяВызываемаяПодсистема = НайденнаяСтрока.ВызываемаяПодсистема;
				НомерСтроки = 0;
				СформированнаяСтрока = СформированнаяСтрока +
				                       НайденнаяСтрока.ВызывающаяПодсистема + " -> " +
				                       НайденнаяСтрока.ВызываемаяПодсистема + Символы.ПС;
			КонецЕсли;
			НомерСтроки = НомерСтроки + 1;
			СформированнаяСтрока = СформированнаяСтрока + Символы.Таб + НомерСтроки + ". " +
			                       НайденнаяСтрока.ВызывающийОбъект + " (" +
			                       НайденнаяСтрока.МестоВызова + ")";
			СформированнаяСтрока = СформированнаяСтрока +
			                       ?(НайденнаяСтрока.ВызываемыйОбъект <> "", " -> " + НайденнаяСтрока.ВызываемыйОбъект, "") + Символы.ПС;
			
		КонецЦикла;
		
		СформированнаяСтрока = СформированнаяСтрока + Символы.ПС;
		СписокОшибочныхОбъектов.Добавить(СформированнаяСтрока);
		
	КонецЦикла;
	
	Возврат СписокОшибочныхОбъектов;
	
КонецФункции

&НаСервере
Функция ПоискЗапрещенныхКомментариевВКодеМодулей(СтрокаПоиска)
	
	СписокОшибочныхОбъектов = Новый СписокЗначений;
	МассивМодулей = НайтиФайлы(КаталогВыгрузкиМодулей, "*.txt");
	ТекстФайла = Новый ТекстовыйДокумент;
	НомерСтроки = 0;
	
	Для Каждого Файл Из МассивМодулей Цикл
		
		Если Найти(Файл.Имя, "ИнструментыРазработчикаПроверкаБСППередСборкой") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстФайла.Прочитать(Файл.ПолноеИмя);
		СтрокаТекста = ТекстФайла.ПолучитьТекст();
		СтрокаТекста = ВРег(СтрокаТекста);
		
		НомерСимвола = Найти(СтрокаТекста, СтрокаПоиска);
		КомментарийНайден = (НомерСимвола > 0);
		Пока КомментарийНайден Цикл
			
			НомерСтроки = НомерСтроки(СтрокаТекста, НомерСимвола, НомерСтроки);
			СписокОшибочныхОбъектов.Добавить(Файл.Имя + " - строка " + НомерСтроки);
			СтрокаТекста = Сред(СтрокаТекста, НомерСимвола + 4);
			
			НомерСимвола = Найти(СтрокаТекста, СтрокаПоиска);
			КомментарийНайден = (НомерСимвола > 0);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат СписокОшибочныхОбъектов;
	
КонецФункции

&НаСервере
Функция ПроцедурыСФлагомВключатьВСодержаниеСправки()
	
	СписокОшибочныхОбъектов = Новый СписокЗначений;
	Для Каждого Подсистема Из Метаданные.Подсистемы.СтандартныеПодсистемы.Подсистемы Цикл
		Если Подсистема.ВключатьСправкуВСодержание Тогда
			СписокОшибочныхОбъектов.Добавить(Подсистема.ПолноеИмя());
		КонецЕсли;
		Для Каждого ДочерняяПодсистема Из Подсистема.Подсистемы Цикл
			Если Подсистема.ВключатьСправкуВСодержание Тогда
				СписокОшибочныхОбъектов.Добавить(ДочерняяПодсистема.ПолноеИмя());
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат СписокОшибочныхОбъектов;
	
КонецФункции

&НаСервере
Функция НомерСтроки(СтрокаТекста, НомерСимвола, НомерСтроки)
	
	НомерСтроки = НомерСтроки + СтрЧислоВхождений(Лев(СтрокаТекста, НомерСимвола), Символы.ПС) + ?(НомерСтроки = 0, 1, 0);
	
	Возврат НомерСтроки;
	
КонецФункции

#КонецОбласти
