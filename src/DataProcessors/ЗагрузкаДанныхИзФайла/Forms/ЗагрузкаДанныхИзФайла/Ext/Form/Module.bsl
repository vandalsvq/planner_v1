&НаКлиенте
Перем ПараметрыОбработчика;

&НаКлиенте
Перем ФормаДлительнойОперации;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Сценарий = "ПоискСсылок" ИЛИ Параметры.Сценарий = "ВставкаИзБуфераОбмена" Тогда
		ТипЗагрузки = "ВставкаИзБуфераОбмена";
	ИначеЕсли ЗначениеЗаполнено(Параметры.ПолноеИмяТабличнойЧасти) Тогда
		ТипЗагрузки = "ТабличнаяЧасть";
	ИначеЕсли НЕ Пользователи.ЭтоПолноправныйПользователь() Тогда
		ВызватьИсключение(НСтр("ru = 'Недостаточно прав для открытия загрузки данных из файла'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьОформлениеДанных();
	СтандартныеПодсистемыСервер.УстановитьОтображениеЗаголовковГрупп(ЭтотОбъект);
	
	СоздаватьЕслиНеСопоставлено = 1;
	ОбновлятьСуществующие = 0;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда 
		ЭтотОбъект.АвтоЗаголовок = Ложь;
		ЭтотОбъект.Заголовок = Параметры.Заголовок;
	Иначе
		ЭтотОбъект.АвтоЗаголовок = Ложь;
		ЭтотОбъект.Заголовок = НСтр("ru = 'Загрузка данных из файла'")
	КонецЕсли;
	
	ВебКлиент = ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент();
	Если ВебКлиент Тогда
		Элементы.СтраницаВариантЗаполнениеТаблицы.Видимость = Ложь;
		Элементы.СтраницыЗаполненияДанных.ТекущаяСтраница = Элементы.СтраницаВариантЗагрузкаИзФайла;
		Элементы.ВариантЗагрузки.Видимость = Ложь;
		ВидЗагрузкиДанных = 1;
	КонецЕсли;
	
	Если ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
		ФильтрТаблицаСопоставления = "Несопоставленные";
		Если Параметры.Свойство("ПредставлениеПоля") Тогда
			Заголовок = НСтр("ru = 'Вставка из буфера обмена'") + " (" + Параметры.ПредставлениеПоля + ")";
			ЭтотОбъект.АвтоЗаголовок = Ложь;
		Иначе
			Заголовок = НСтр("ru = 'Вставка из буфера обмена'");
		КонецЕсли;
		
		Обработки.ЗагрузкаДанныхИзФайла.ИнициализироватьРежимПоискСсылок(ШаблонСДанными, ИнформацияПоКолонкам, Параметры.ОписаниеТипов);
		СоздатьТаблицуСопоставленияПоИнформацииОКолонкахАвто(Параметры.ОписаниеТипов);
		
		Если ИнформацияПоКолонкам.Количество() = 1 Тогда 
			Элементы.СтраницыЗаполненияДанных.ТекущаяСтраница = Элементы.СтраницаОднаКолонка;
			Элементы.ВариантЗагрузки.Видимость = Ложь;
			Элементы.ВставитьВСписок.Видимость = Ложь;
			Элементы.Далее.Заголовок = Нстр("ru = 'Вставить в список'");
		Иначе
			Элементы.СтраницыЗаполненияДанных.ТекущаяСтраница = Элементы.СтраницаМногоКолонок;
		КонецЕсли;
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ЗаполнениеТаблицыДанными;
		Элементы.ГруппаНастройкиСопоставления.Видимость = Ложь;
		Элементы.СписокКолонокСопоставления.Видимость = Ложь;
		Элементы.Закрыть.Заголовок = НСтр("ru = 'Отмена'");
		
	Иначе
		Если НЕ ЗначениеЗаполнено(Параметры.ПолноеИмяТабличнойЧасти) тогда 
			ЗаполнитьСписокВидЗагрузкиДанных();
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ВыборСправочникаДляЗагрузки;
		Иначе
			ИмяОбъектаСопоставления = Обработки.ЗагрузкаДанныхИзФайла.ПолноеИмяОбъектаТабличнаяЧасть(Параметры.ПолноеИмяТабличнойЧасти);
			
			Обработки.ЗагрузкаДанныхИзФайла.ИнициализироватьЗагрузкуВТабличнуюЧасть(ИмяОбъектаСопоставления, Параметры.ИмяМакетаСШаблоном, ИнформацияПоКолонкам, ШаблонСДанными, Отказ);
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			ПоказатьИнформационнуюСтрокуПроОбязательныеКолонки();
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ЗаполнениеТаблицыДанными;
			Элементы.ГруппаНастройкиСопоставления.Видимость = Ложь;
			Элементы.СписокКолонокСопоставления.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ОткрытьСправочникПослеЗакрытияПомощника Тогда 
		ОткрытьФорму(ФормаСписка(ИмяОбъектаСопоставления));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтменитьСопоставление(Команда)
	Оповещение = новый ОписаниеОповещения("ПослеВопросаПроОтменитьСопоставление", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Отменить сопоставление?'"), РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьБланк(Команда)
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИнформацияПоКолонкам", ИнформацияПоКолонкам);
	ПараметрыФормы.Вставить("ИмяОбъектаСопоставления", ИмяОбъектаСопоставления);
		
	Оповещение = новый ОписаниеОповещения("ПослеВызоваФормыИзменитьБланк", ЭтотОбъект);
	ОткрытьФорму("Обработка.ЗагрузкаДанныхИзФайла.Форма.РедактированиеБланка", ПараметрыФормы, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьНеоднозначность(Команда)
	ОткрытьФормуРазрешенияНеоднозначности(Элементы.ТаблицаСопоставленияДанных.ТекущаяСтрока,Элементы.ТаблицаСопоставленияДанных.ТекущийЭлемент.Имя, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ВставитьВСписок(Команда)
	ЗакрытьФормуИВернутьМассивСсылок();
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ВыборСправочникаДляЗагрузки Тогда 
		ОписаниеСтрокиВыбора = Элементы.ВидЗагрузкиДанных.ТекущиеДанные.Значение;
		ВыполнитьШагЗаполнениеТаблицыДаннымиНаСервере(ОписаниеСтрокиВыбора);
		ВыполнитьШагЗаполнениеТаблицыДаннымиНаКлиенте();
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ЗаполнениеТаблицыДанными Тогда
		ВыполнитьШагСопоставлениеЗагружаемыхДанных();
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.РезультатыСопоставления Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СопоставлениеЗагружаемыхДанных;
		Элементы.ВставитьВСписок.Видимость = Ложь;
		Элементы.Далее.Заголовок = НСтр("ru = 'Вставить в список'");
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		Элементы.Назад.Заголовок = НСтр("ru = '< В начало'");
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СопоставлениеЗагружаемыхДанных Тогда
		Элементы.ВставитьВСписок.Видимость = Ложь;
		Если ТипЗагрузки = "ТабличнаяЧасть" Тогда
			Отбор = Новый Структура("РезультатСопоставленияСтроки", "НеСопоставлен");
			Строки = ТаблицаСопоставленияДанных.НайтиСтроки(Отбор);
			
			Если Строки.Количество() > 0 Тогда
				Оповещение = Новый ОписаниеОповещения("ПослеВопросаОДобавлениеВТабличнуюЧасть", ЭтотОбъект);
				ПоказатьВопрос(Оповещение, НСтр("ru = 'Строки в которых не заполнены обязательные колонки будут пропущены.'") + 
					Символы.ПС + НСтр("ru = 'Продолжить?'"), РежимДиалогаВопрос.ДаНет);
				Возврат;
			КонецЕсли;
			
			АдресЗагруженныхДанных = АдресВХранилищеТаблицыСопоставления();
			Закрыть(АдресЗагруженныхДанных);
			
		ИначеЕсли ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
			Элементы.Назад.Заголовок = НСтр("ru = '< В начало_'");
			ЗакрытьФормуИВернутьМассивСсылок();
		Иначе
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ДлительныеОперации;
			ЗаписатьЗагружаемыеДанныеКлиент();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОДобавлениеВТабличнуюЧасть(Результат, ДополнительныеПараметры) Экспорт 
	Если Результат = КодВозвратаДиалога.Да Тогда 
		АдресЗагруженныхДанных = АдресВХранилищеТаблицыСопоставления();
		Закрыть(АдресЗагруженныхДанных);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСопоставленияИзВременногоХранилища()
	СопоставленныеДанные = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
	ЗначениеВРеквизитФормы(СопоставленныеДанные, "ТаблицаСопоставленияДанных");
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуИВернутьМассивСсылок()
	МассивСсылок = Новый Массив;
	Для каждого Строка Из ТаблицаСопоставленияДанных Цикл
		Если ЗначениеЗаполнено(Строка.ОбъектСопоставления) Тогда
			МассивСсылок.Добавить(Строка.ОбъектСопоставления);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(МассивСсылок);
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ЗаполнениеТаблицыДанными Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ВыборСправочникаДляЗагрузки;
		Элементы.Назад.Видимость = Ложь;
		ОчиститьТаблицу();
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СопоставлениеЗагружаемыхДанных ИЛИ Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.НеНайдено Тогда
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ЗаполнениеТаблицыДанными; 
		Элементы.ВставитьВСписок.Видимость = Ложь;
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		Элементы.Далее.Видимость = Истина;
		Если ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда 
			Элементы.Далее.Заголовок = НСтр("ru = 'Вставить в список'");
		Иначе
			Элементы.Далее.Заголовок = НСтр("ru = 'Далее >'");
		КонецЕсли;
		Если ТипЗагрузки = "ТабличнаяЧасть" ИЛИ ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
			Элементы.Назад.Видимость = Ложь;
		КонецЕсли;

		
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ОтчетОЗагрузкеДанных Тогда
		Элементы.ОткрытьСправочникПослеЗакрытияПомощника.Видимость = Ложь;
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СопоставлениеЗагружаемыхДанных;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьШаблонВФайл(Команда)
	
	Если ПодключитьРасширениеРаботыСФайлами() Тогда
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ФайловыеФункции") Тогда
			МодульФайловыеФункцииСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ФайловыеФункцииСлужебныйКлиент");
			ПутьКФайлу = МодульФайловыеФункцииСлужебныйКлиент.КаталогМоиДокументы();
		Иначе
			ПутьКФайлу = "";
		КонецЕсли;
	Иначе
		Оповещение = Новый ОписаниеОповещения("ПослеВыбораРасширенияФайла", ЭтотОбъект);
		ОткрытьФорму("Обработка.ЗагрузкаДанныхИзФайла.Форма.РасширениеФайла",, ЭтотОбъект, Истина,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		ПутьКФайлу = "";
	КонецЕсли;
	
	ИмяФайла = СформироватьИмяФайлаДляОбъектаМетаданных(ИмяОбъектаСопоставления);
	ПолучитьПутьКФайлуНачалоВыбора(РежимДиалогаВыбораФайла.Сохранение, ПутьКФайлу, ИмяФайла);
	ВыбранныйФайл = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПутьКФайлу);
	РасширениеФайла = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(ВыбранныйФайл.Расширение);
	Если ЗначениеЗаполнено(ВыбранныйФайл.Имя) тогда
		Если РасширениеФайла = "csv" тогда
			СохранитьТаблицуВCSVФайл(ПутьКФайлу);
		ИначеЕсли РасширениеФайла = "xlsx" тогда
			АдресВоВременномХранилище = ТабличныйДокументУдалениеПримечаний();
			ТабличныйДокументБезПримечаний = ПолучитьИзВременногоХранилища(АдресВоВременномХранилище);
			ТабличныйДокументБезПримечаний.Записать(ПутьКФайлу, ТипФайлаТабличногоДокумента.xlsx);
			ШаблонСДанными = ТабличныйДокументБезПримечаний;
		ИначеЕсли РасширениеФайла = "mxl" тогда 
			ШаблонСДанными.Записать(ПутьКФайлу, ТипФайлаТабличногоДокумента.mxl);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Шаблон файла не был сохранен.'"));
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

// Функция для обхода ошибки сохранения, если в документе есть примечания
//
&НаСервере
Функция ТабличныйДокументУдалениеПримечаний()
	ТабличныйДокументДляСохранения = новый ТабличныйДокумент;
	ИзменитьБланкПоИнформацииПоКолонкам(ТабличныйДокументДляСохранения);
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ТабличныйДокументДляСохранения);
	Возврат АдресВоВременномХранилище;
КонецФункции

&НаКлиенте
Процедура ЗагрузитьШаблонИзФайла(Команда)
	
	АдресВременногоХранилища = "";
	Оповещение = Новый ОписаниеОповещения("ПередатьФайлНаСервер", ЭтотОбъект);
	НачатьПомещениеФайла(Оповещение, АдресВременногоХранилища);
	
	ТаблицаОтчет.ФиксацияСверху = 1;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидЗагрузкиДанныхВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	СтандартнаяОбработка=Ложь;
	Далее(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ФильтрОтчетПриИзменении(Элемент)

	ФоновоеЗаданиеОтчетНаКлиенте();
	
	Если ФильтрОтчет = "Пропущенные" Тогда 
		Элементы.ИзменитьРеквизиты.Доступность=Ложь;
	Иначе
		Элементы.ИзменитьРеквизиты.Доступность=Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФильтрТаблицаСопоставленияПриИзменении(Элемент)
	УстановитьФильтрациюТаблицыСопоставления();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФильтрациюТаблицыСопоставления()

	Фильтр = ФильтрТаблицаСопоставления;
	
	Если ТипЗагрузки = "ТабличнаяЧасть" Тогда
		Если Фильтр = "Сопоставленные" Тогда
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("РезультатСопоставленияСтроки", "СтрокаСопоставлена"); 
		ИначеЕсли Фильтр = "Несопоставленные" Тогда 
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("РезультатСопоставленияСтроки", "НеСопоставлен"); 
		ИначеЕсли Фильтр = "Неоднозначные" Тогда 
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("ОписаниеОшибки", "");
		Иначе
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Неопределено;
		КонецЕсли;
	ИначеЕсли ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
		Если Фильтр = "Сопоставленные" Тогда 
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("РезультатСопоставленияСтроки", "СтрокаСопоставлена");
		ИначеЕсли Фильтр = "Несопоставленные" Тогда
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("РезультатСопоставленияСтроки", "Не");
		ИначеЕсли Фильтр = "Неоднозначные" Тогда
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("РезультатСопоставленияСтроки", "Неоднозначность");
		Иначе
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Неопределено;
		КонецЕсли;
	Иначе
		Если Фильтр = "Сопоставленные" Тогда 
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("РезультатСопоставленияСтроки", "СтрокаСопоставлена");
		ИначеЕсли Фильтр = "Несопоставленные" Тогда 
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("РезультатСопоставленияСтроки", "Не");
		ИначеЕсли Фильтр = "Неоднозначные" Тогда 
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Новый ФиксированнаяСтруктура("РезультатСопоставленияСтроки", "Неоднозначность"); 
		Иначе
			Элементы.ТаблицаСопоставленияДанных.ОтборСтрок = Неопределено;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура СписокКолонокСопоставленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если СопоставитьПоКолонке.Количество() = 0 Тогда
		Для каждого строка Из ИнформацияПоКолонкам Цикл
			Если ЗначениеЗаполнено(Строка.Синоним) Тогда
				ПредставлениеКолонки = Строка.Синоним;
			Иначе
				ПредставлениеКолонки = Строка.ПредставлениеКолонки;
			КонецЕсли;
			СопоставитьПоКолонке.Добавить(строка.ИмяКолонки, ПредставлениеКолонки);
		КонецЦикла;
	КонецЕсли;
	ПараметрыФормы  = Новый Структура("СписокКолонок", СопоставитьПоКолонке);
	ОписаниеОповещения  = НОвый ОписаниеОповещения("ПослеВыбораКолонокДляСопоставления", ЭтотОбъект);
	ОткрытьФорму("Обработка.ЗагрузкаДанныхИзФайла.Форма.ВыборКолонок", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКолонокДляСопоставления(Результат, Параметр) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
		 
	СопоставитьПоКолонке = Результат;
	КолонкиВСтроку = "";
	Разделитель = "";
	КоличествоВыбранныхКолонок = 0;
	Для каждого Элемент Из СопоставитьПоКолонке Цикл 
		Если Элемент.Пометка Тогда 
			КолонкиВСтроку = КолонкиВСтроку + Разделитель + Элемент.Представление;
			Разделитель = ", ";
			КоличествоВыбранныхКолонок = КоличествоВыбранныхКолонок + 1;
		КонецЕсли;
	КонецЦикла;
	
	СписокКолонокСопоставления = КолонкиВСтроку;
	ВыполнитьСопоставление();
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗагрузкиПриИзменении(Элемент)
	
	Если ВариантЗагрузки = 0 Тогда 
		Элементы.СтраницыЗаполненияДанных.ТекущаяСтраница = Элементы.СтраницаВариантЗаполнениеТаблицы;
	Иначе
		Элементы.СтраницыЗаполненияДанных.ТекущаяСтраница = Элементы.СтраницаВариантЗагрузкаИзФайла;
	КонецЕсли;
	
	ПоказатьИнформационнуюСтрокуПроОбязательныеКолонки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыШаблонСДанными

&НаКлиенте
Процедура ШаблонСДаннымиВыбор(Элемент, Область, СтандартнаяОбработка)
	Если Область.Верх = 1 Тогда 
		СтандартнаяОбработка = Ложь; // Нельзя редактировать шапку
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыСопоставленияДанных

&НаКлиенте
Процедура ТаблицаСопоставленияДанныхПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ТипЗагрузки <> "ТабличнаяЧасть" Тогда
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.ОбъектСопоставления) тогда 
			Элемент.ТекущиеДанные.РезультатСопоставленияСтроки = "СтрокаСопоставлена";
		иначе
			Элемент.ТекущиеДанные.РезультатСопоставленияСтроки = "НеСопоставлен";
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.тч_Номенклатура) тогда 
			Элемент.ТекущиеДанные.РезультатСопоставленияСтроки = "СтрокаСопоставлена";
		иначе
			Элемент.ТекущиеДанные.РезультатСопоставленияСтроки = "НеСопоставлен";
		КонецЕсли;
	КонецЕсли;
	
	ПоказатьСтатистикуПоСопоставлениюЗагрузкаИзФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСопоставленияДанныхПриАктивизацииЯчейки(Элемент)
	Элементы.РазрешитьНеоднозначность.Доступность = Ложь;
	Элементы.ТаблицаСопоставленияДанныхКонтекстноеМенюРазрешитьНеоднозначность.Доступность = Ложь;
	
	Если Элемент.ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(Элемент.ТекущиеДанные.РезультатСопоставленияСтроки) Тогда 
		Если ТипЗагрузки = "ТабличнаяЧасть" Тогда 
			Если СтрДлина(Элемент.ТекущийЭлемент.Имя) > 3 И Лев(Элемент.ТекущийЭлемент.Имя,3) = "тч_" Тогда 
				ИмяКолонки = Сред(Элемент.ТекущийЭлемент.Имя, 4);
				Если Найти(Элемент.ТекущиеДанные.ОписаниеОшибки, ИмяКолонки) > 0 Тогда 
					Элементы.РазрешитьНеоднозначность.Доступность = Истина;
					Элементы.ТаблицаСопоставленияДанныхКонтекстноеМенюРазрешитьНеоднозначность.Доступность = Истина;
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Элемент.ТекущиеДанные.РезультатСопоставленияСтроки = "Неоднозначность" Тогда 
			Элементы.РазрешитьНеоднозначность.Доступность = Истина;
			Элементы.ТаблицаСопоставленияДанныхКонтекстноеМенюРазрешитьНеоднозначность.Доступность = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСопоставленияДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьФормуРазрешенияНеоднозначности(ВыбраннаяСтрока, Поле.Имя, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыОтчет

&НаКлиенте
Процедура ГрупповоеИзменениеРеквизитов(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов") Тогда
		массивСсылок = ГрупповоеИзменениеРеквизитовНаСервере(ТаблицаОтчет.ТекущаяОбласть.Верх, ТаблицаОтчет.ТекущаяОбласть.Низ);
		Если массивСсылок.Количество() > 0 Тогда
			ПараметрыФормы = Новый Структура("МассивОбъектов", МассивСсылок);
			ИмяОбъекта = "Обработка.";
			ОткрытьФорму(ИмяОбъекта + "ГрупповоеИзменениеРеквизитов.Форма", ПараметрыФормы);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


#Область ШагВыборВариантаЗагрузки

&НаСервере
Процедура ЗаполнитьСписокВидЗагрузкиДанных()
	Обработки.ЗагрузкаДанныхИзФайла.СоздатьСписокСправочниковДляЗагрузки(СписокВариантовЗагрузки);
КонецПроцедуры 

#КонецОбласти


#Область ШагЗаполнениеТаблицыДанными

&НаКлиенте
Процедура ВыполнитьШагЗаполнениеТаблицыДаннымиНаКлиенте()
	
	Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ЗаполнениеТаблицыДанными;
	Элементы.Назад.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Функция ТаблицаСДаннымиПустая()
	
	Если ИнформацияПоКолонкам.Количество() = 1 Тогда
		Если НЕ ЗначениеЗаполнено(ШаблонСДаннымиОднаКолонка) Тогда
			Возврат Истина;
		КонецЕсли;
	Иначе 
		Если ШаблонСДанными.ВысотаТаблицы < 2 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПолноеИмяОбъектаМетаданных(Имя)
	ОбъектМетаданных = Метаданные.Справочники.Найти(Имя);
	Если ОбъектМетаданных <> Неопределено Тогда 
		Возврат ОбъектМетаданных.ПолноеИмя();
	КонецЕсли;
	ОбъектМетаданных = Метаданные.Документы.Найти(Имя);
	Если ОбъектМетаданных <> Неопределено Тогда 
		Возврат ОбъектМетаданных.ПолноеИмя();
	КонецЕсли;
	ОбъектМетаданных = Метаданные.ПланыВидовХарактеристик.Найти(Имя);
	Если ОбъектМетаданных <> Неопределено Тогда 
		Возврат ОбъектМетаданных.ПолноеИмя();
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

&НаСервере
Процедура ВыполнитьШагЗаполнениеТаблицыДаннымиНаСервере(ОписаниеСтрокиВыбора)
	
	Если Найти(ОписаниеСтрокиВыбора.ПолноеИмяОбъектаМетаданных, ".") > 0 Тогда
		ИмяОбъектаСопоставления = ОписаниеСтрокиВыбора.ПолноеИмяОбъектаМетаданных;
	Иначе
		ИмяОбъектаСопоставления = ПолучитьПолноеИмяОбъектаМетаданных(ОписаниеСтрокиВыбора.ПолноеИмяОбъектаМетаданных);
	КонецЕсли;
	
	ТипЗагрузки = ОписаниеСтрокиВыбора.Тип;
	Если ТипЗагрузки = "ВнешняяЗагрузка" Тогда
		ВнешняяОбработкаСсылка = ОписаниеСтрокиВыбора.Ссылка;
	КонецЕсли;
	
	СформироватьМакетПоТипуЗагрузки();
	СоздатьТаблицуСопоставленияПоИнформацииОКолонках();
	ПоказатьИнформационнуюСтрокуПроОбязательныеКолонки();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьМакетПоТипуЗагрузки()
	
	ПараметрыЗагрузки = Неопределено;
	Если ТипЗагрузки = "УниверсальнаяЗагрузка" Тогда
		ЭтотОбъект.Заголовок = НСтр("ru = 'Загрузка из файла в справочник ""'") + ПредставлениеСправочника(ИмяОбъектаСопоставления)+"""";
		ЭтотОбъект.АвтоЗаголовок = Ложь;
	ИначеЕсли ТипЗагрузки = "ПрикладнаяЗагрузка" Тогда
		ЗагрузитьМакетВТабличныйДокумент(ПараметрыЗагрузки);
		ЭтотОбъект.Заголовок = НСтр("ru = 'Загрузка из файла в справочник ""'") + ПредставлениеСправочника(ИмяОбъектаСопоставления)+"""";
	ИначеЕсли ТипЗагрузки = "ВнешняяЗагрузка" Тогда
		ИдентификаторКоманды = ИмяОбъектаСопоставления;
		ПараметрыЗагрузки = Обработки.ЗагрузкаДанныхИзФайла.ПараметрыЗагрузкиИзФайлаВнешняяОбработка(ИмяОбъектаСопоставления,
			ВнешняяОбработкаСсылка); 
	КонецЕсли;
	
	СформироватьМакет(ПараметрыЗагрузки);
КонецПроцедуры


&НаСервере
Процедура СформироватьМакет(ПараметрыЗагрузки)
	
	ИнформацияПоКолонкамИзХранилища = ХранилищеСистемныхНастроек.Загрузить("ЗагрузкаДанныхИзФайла", ИмяОбъектаСопоставления);
	Если ИнформацияПоКолонкамИзХранилища <> Неопределено Тогда
		ЗначениеВРеквизитФормы(ИнформацияПоКолонкамИзХранилища, "ИнформацияПоКолонкам");
	Иначе
		Если ТипЗагрузки = "УниверсальнаяЗагрузка" Тогда
			Обработки.ЗагрузкаДанныхИзФайла.ИнформациюПоКолонкамИзРеквизитовСправочника(ИмяОбъектаСопоставления, ИнформацияПоКолонкам);
		ИначеЕсли ТипЗагрузки = "ПрикладнаяЗагрузка" Тогда
			Макет = МенеджерОбъекта(ИмяОбъектаСопоставления).ПолучитьМакет("ЗагрузкаИзФайла");
			ОбластьЗаголовокТаблицы = ОбластьЗаголовкаШаблонаТаблицы(Макет);
			Обработки.ЗагрузкаДанныхИзФайла.СоздатьИнформациюПоКолонкамНаОснованиеШаблона(ОбластьЗаголовокТаблицы, ПараметрыЗагрузки, ИнформацияПоКолонкам);
		ИначеЕсли ТипЗагрузки = "ВнешняяЗагрузка" Тогда
			ЗагрузитьМакетВТабличныйДокументВнешняяОбработка(ПараметрыЗагрузки);
		КонецЕсли;
	КонецЕсли;
	
	ИзменитьБланкПоИнформацииПоКолонкам();
	
КонецПроцедуры

&НаСервере
Процедура СохранитьТаблицуВCSVФайл(ПолноеИмяФайла)
	Обработки.ЗагрузкаДанныхИзФайла.СохранитьТаблицуВCSVФайл(ПолноеИмяФайла, ИнформацияПоКолонкам);
КонецПроцедуры

#КонецОбласти

#Область ШагСопоставлениеЗагруженныхДанных

&НаСервере
Процедура СкопироватьОднуКолонкуВШаблонСДанными()
	
	ОчисткаШаблонСДанными();
	
	КоличествоСтрок = СтрЧислоСтрок(ШаблонСДаннымиОднаКолонка);
	НомерСтрокиВШаблоне = 2;
	Для НомерСтроки = 1 По КоличествоСтрок Цикл 
		Строка = СтрПолучитьСтроку(ШаблонСДаннымиОднаКолонка, НомерСтроки);
		Если ЗначениеЗаполнено(Строка) Тогда
			Ячейка = ШаблонСДанными.ПолучитьОбласть(НомерСтрокиВШаблоне, 1, НомерСтрокиВШаблоне, 1);
			Ячейка.ТекущаяОбласть.Текст = Строка;
			ШаблонСДанными.Вывести(Ячейка);
			НомерСтрокиВШаблоне = НомерСтрокиВШаблоне + 1;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция СоздатьТаблицуСоСпискомНеоднозначностей()
	СписокНеоднозначностей = Новый ТаблицаЗначений;
	СписокНеоднозначностей.Колонки.Добавить("Идентификатор");
	СписокНеоднозначностей.Колонки.Добавить("Колонка");
	
	Возврат СписокНеоднозначностей;
КонецФункции

&НаСервере
Процедура ВыполнитьШагСопоставлениеЗагружаемыхДанныхНаСервере(ФоновоеЗадание = Ложь)
	
	Если ИнформацияПоКолонкам.Количество() = 1 Тогда
		СкопироватьОднуКолонкуВШаблонСДанными();
	КонецЕсли;
	
	ТаблицаСопоставления = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных");
	Если ТипЗагрузки = "ТабличнаяЧасть" Тогда
		АдресЗагруженныхДанных = "";
		АдресКопииТабличнойЧасти = "";
		СписокНеоднозначностей = СоздатьТаблицуСоСпискомНеоднозначностей();
		
		Обработки.ЗагрузкаДанныхИзФайла.ВыгрузитьДанныеДляТЧ(ШаблонСДанными, ИнформацияПоКолонкам, АдресЗагруженныхДанных);
		СкопироватьСтруктуруТабличнойЧасти(АдресКопииТабличнойЧасти);
		
		МенеджерОбъекта = МенеджерОбъекта(ИмяОбъектаСопоставления);
		МенеджерОбъекта.СопоставитьЗагружаемыеДанные(АдресЗагруженныхДанных, АдресКопииТабличнойЧасти, СписокНеоднозначностей, ИмяОбъектаСопоставления);
		
		Если НЕ РеквизитыСозданы Тогда
			СоздатьТаблицуСопоставленияПоИнформацииОКолонкахДляТЧ();
		КонецЕсли;
		
		ПоместитьДанныеВТаблицуСопоставления(АдресЗагруженныхДанных, АдресКопииТабличнойЧасти, СписокНеоднозначностей);
	ИначеЕсли ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
		Обработки.ЗагрузкаДанныхИзФайла.ЗаполнитьТаблицуСопоставленияДаннымиИзШаблона(ШаблонСДанными, ТаблицаСопоставления, ИнформацияПоКолонкам);
		Обработки.ЗагрузкаДанныхИзФайла.СопоставитьЗначениеКолонкиАвто(ТаблицаСопоставления, "Ссылки");
		ЗначениеВРеквизитФормы(ТаблицаСопоставления, "ТаблицаСопоставленияДанных");
	Иначе
		ПараметрыВызоваСервера = Новый Структура();
		ПараметрыВызоваСервера.Вставить("ШаблонСДанными", ШаблонСДанными);
		ПараметрыВызоваСервера.Вставить("ТаблицаСопоставления", ТаблицаСопоставления);
		ТаблицаИнформацияПоКолонкам = РеквизитФормыВЗначение("ИнформацияПоКолонкам");
		
		ПараметрыВызоваСервера.Вставить("ИнформацияПоКолонкам", ТаблицаИнформацияПоКолонкам);
		
		РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
		"Обработки.ЗагрузкаДанныхИзФайла.ЗаполнитьТаблицуСопоставленияДаннымиИзШаблонаФон",
		ПараметрыВызоваСервера, 
		НСтр("ru = 'ЗагрузкаДанныхИзФайла: Выполнение серверного метода обработки ЗаполнитьТаблицуСопоставленияДаннымиИзШаблона'"));
		
		Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
			ВыполнитьШагСопоставлениеЗагружаемыхДанныхПослеСопоставленияНаСервере();
		Иначе 
			ФоновоеЗадание = Истина;
			ФоновоеЗаданиеИдентификатор  = РезультатФоновогоЗадания.ИдентификаторЗадания;
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьШагСопоставлениеЗагружаемыхДанныхПослеСопоставленияНаСервере()
	
	ТаблицаСопоставления = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
	
	Если ТипЗагрузки = "ПрикладнаяЗагрузка" Тогда
		СопоставитьДанныеПрикладнаяЗагрузка(ТаблицаСопоставления);
		Элементы.ПояснениеДляПрикладнойЗагрузки.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Элементы.ПояснениеДляПрикладнойЗагрузки.Заголовок, ПредставлениеСправочника(ИмяОбъектаСопоставления));
	ИначеЕсли ТипЗагрузки = "ВнешняяЗагрузка" Тогда
		СопоставитьДанныеВнешняяОбработка(ТаблицаСопоставления);
	КонецЕсли;
	
	Элементы.ПояснениеДляПрикладнойЗагрузки.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	Элементы.ПояснениеДляПрикладнойЗагрузки.Заголовок, ПредставлениеСправочника(ИмяОбъектаСопоставления));
	
	ЗначениеВРеквизитФормы(ТаблицаСопоставления, "ТаблицаСопоставленияДанных");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьШагСопоставлениеЗагружаемыхДанных()
	
	Если ТаблицаСДаннымиПустая() Тогда
		ПоказатьПредупреждение(, (НСтр("ru ='Для перехода к этапу сопоставления и загрузки данных, необходимо заполнить таблицу.'")));	
		Возврат;
	КонецЕсли;
	
	СписокНезаполненныхКолонок = НезаполненныеОбязательныеКолонки();
	Если СписокНезаполненныхКолонок.Количество() > 0 Тогда 
		Если СписокНезаполненныхКолонок.Количество() =1  Тогда 
			ТекстПроКолонки = НСтр("ru = 'Обязательная колонка""'") + " " + СписокНезаполненныхКолонок[0] +
				НСтр("ru = '"" содержит незаполненные строки, эти строки будут пропущены при загрузке'");
		Иначе
			ТекстПроКолонки = НСтр("ru = 'Обязательные колонки""'") + " " + СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(СписокНезаполненныхКолонок,", ") +
				НСтр("ru = '"" содержат незаполненные строки, эти строки будут пропущены при загрузке'");
		КонецЕсли;
		ТекстПроКолонки = ТекстПроКолонки + Символы.ПС + НСтр("ru = 'Продолжить?'");
		
		Оповещение = Новый ОписаниеОповещения("ПослеВопросаОНезаполненныхСтроках", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстПроКолонки, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	Иначе
		ВыполнитьШагСопоставлениеЗагружаемыхДанныхПослеПроверки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОНезаполненныхСтроках(Результат, Параметр) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда 
		ВыполнитьШагСопоставлениеЗагружаемыхДанныхПослеПроверки();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьШагСопоставлениеЗагружаемыхДанныхПослеПроверки()
	
	ФоновоеЗадание = Ложь;
	ВыполнитьШагСопоставлениеЗагружаемыхДанныхНаСервере(ФоновоеЗадание);
	
	Если ФоновоеЗадание = Истина Тогда 
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ДлительныеОперации;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеСопоставлениеНаКлиенте", 1, Истина);
		ПараметрыОбработчика.МаксимальныйИнтервал = 5;
	Иначе 
		Если ВсеДанныеСопоставлены() И ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
			ЗакрытьФормуИВернутьМассивСсылок();
		Иначе
			ВыполнитьШагСопоставлениеЗагружаемыхДанныхКлиент();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#Область ДлительныеОперации

&НаКлиенте
Процедура ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте()
	Результат = ФоновоеЗаданиеЗагрузкаФайлаПолучитьРезультат();
	Если Результат.ФоновоеЗаданиеВыполнено Тогда
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ФоновоеЗаданиеИдентификатор Тогда
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		КонецЕсли;
		ШаблонСДанными = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
		Элементы.ШаблонСДанными.Видимость = Истина;
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте", ПараметрыОбработчика.ТекущийИнтервал, Истина);
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФоновоеЗаданиеСопоставлениеНаКлиенте()
	Результат = ФоновоеЗаданиеСопоставлениеПолучитьРезультат();
	Если Результат.ФоновоеЗаданиеВыполнено Тогда
		Если ВсеДанныеСопоставлены() И ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
			ЗакрытьФормуИВернутьМассивСсылок();
		Иначе
			ВыполнитьШагСопоставлениеЗагружаемыхДанныхКлиент();
		КонецЕсли;
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеСопоставлениеНаКлиенте", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФоновоеЗаданиеЗаписьНаКлиенте()
	Результат = ФоновоеЗаданиеЗаписьПолучитьРезультат();
	Если Результат.ФоновоеЗаданиеВыполнено Тогда
		ЗаполнитьТаблицуСопоставленияИзВременногоХранилища();
		ФоновоеЗаданиеОтчетНаКлиенте(Ложь);
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗаписьНаКлиенте", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗагружаемыеДанныеКлиент()
	
	ФоновоеЗаданиеПроцент = 0;
	ФоновоеЗадание = Ложь;
	ЗаписатьЗагружаемыеДанныеОтчет(ФоновоеЗадание);
	
	Если ФоновоеЗадание = Истина Тогда 
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ДлительныеОперации;
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗаписьНаКлиенте", 1, Истина);
		ПараметрыОбработчика.МаксимальныйИнтервал = 5;
	Иначе 
		ФоновоеЗаданиеОтчетНаКлиенте(Ложь);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ФоновоеЗаданиеЗаписьПолучитьРезультат()
	Результат = Новый Структура;
	Результат.Вставить("ФоновоеЗаданиеВыполнено", Ложь);
	Результат.ФоновоеЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор);
	Если НЕ Результат.ФоновоеЗаданиеВыполнено Тогда
		ФоновоеЗаданиеПрочитатьПромежуточныйРезультат(Результат);
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ФоновоеЗаданиеОтчетНаКлиенте(ВыводитьОкноОжидания = Истина)
	
	ФоновоеЗадание = Ложь;
	СформироватьОтчетОЗагрузке(ФильтрОтчет, ФоновоеЗадание, НЕ ВыводитьОкноОжидания);
	
	Если ФоновоеЗадание Тогда
		Если ВыводитьОкноОжидания Тогда 
			ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтотОбъект, ФоновоеЗаданиеИдентификатор);
		КонецЕсли;
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеСозданиеОтчетаНаКлиенте", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	Иначе
		Результат = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
		ПоказатьОтчет(Результат);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОтчет(Отчет)
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница <> Элементы.ОтчетОЗагрузкеДанных Тогда
		ВыполнитьШагОтчетОЗагрузкеДанныхКлиент();
	КонецЕсли;
	
	ИтогоОтчетСоздано = Отчет.Создано;
	ИтогоОтчетОбновлено = Отчет.Обновлено;
	ИтогоОтчетПропущено = Отчет.Пропущено;
	ИтогоОтчетНекорректных = Отчет.Некорректных;
	
	Элементы.ФильтрОтчет.СписокВыбора.Очистить();
	Элементы.ФильтрОтчет.СписокВыбора.Добавить("ВсеЭлементы", НСтр("ru = 'Все ('") + Отчет.Всего + ")");
	Элементы.ФильтрОтчет.СписокВыбора.Добавить("Новые", НСтр("ru = 'Новые ('") + Отчет.Создано+ ")");
	Элементы.ФильтрОтчет.СписокВыбора.Добавить("Обновленные", НСтр("ru = 'Обновленные ('") + Отчет.Обновлено+ ")");
	Элементы.ФильтрОтчет.СписокВыбора.Добавить("Пропущенные", НСтр("ru = 'Пропущенные ('") + Отчет.Пропущено+ ")");
	ФильтрОтчет = Отчет.ТипОтчета;

	ТаблицаОтчет = Отчет.ТаблицаОтчет;
	
КонецПроцедуры

&НаКлиенте
Процедура ФоновоеЗаданиеСозданиеОтчетаНаКлиенте()

	РезультатВыполнения = ФоновоеЗаданиеОтчетПолучитьРезультат();
	Если РезультатВыполнения.ФоновоеЗаданиеВыполнено Тогда
		Попытка
			Если ФормаДлительнойОперации.Открыта() 
				И ФормаДлительнойОперации.ИдентификаторЗадания = ФоновоеЗаданиеИдентификатор Тогда
					ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
				КонецЕсли;
		Исключение
		КонецПопытки;
		
		Результат = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
		ПоказатьОтчет(Результат);
	Иначе
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеСозданиеОтчетаНаКлиенте", ПараметрыОбработчика.ТекущийИнтервал, Истина);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ФоновоеЗаданиеЗагрузкаФайлаПолучитьРезультат()
	Результат = Новый Структура;
	Результат.Вставить("ФоновоеЗаданиеВыполнено", Ложь);
	Результат.ФоновоеЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор);
	Возврат Результат;
КонецФункции

&НаСервере
Функция ФоновоеЗаданиеСопоставлениеПолучитьРезультат()
	Результат = Новый Структура;
	Результат.Вставить("ФоновоеЗаданиеВыполнено", Ложь);
	Результат.ФоновоеЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор);
	Если Результат.ФоновоеЗаданиеВыполнено Тогда
		ВыполнитьШагСопоставлениеЗагружаемыхДанныхПослеСопоставленияНаСервере();
	Иначе
		ФоновоеЗаданиеПрочитатьПромежуточныйРезультат(Результат);
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаСервере
Функция ФоновоеЗаданиеОтчетПолучитьРезультат()
	Результат = Новый Структура;
	Результат.Вставить("ФоновоеЗаданиеВыполнено", Ложь);
	Результат.ФоновоеЗаданиеВыполнено = ДлительныеОперации.ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор);
	Если НЕ Результат.ФоновоеЗаданиеВыполнено Тогда
		ФоновоеЗаданиеПрочитатьПромежуточныйРезультат(Результат);
	КонецЕсли;
	Возврат Результат;
КонецФункции

&НаСервере
Процедура ФоновоеЗаданиеПрочитатьПромежуточныйРезультат(Результат)
	Прогресс = ДлительныеОперации.ПрочитатьПрогресс(ФоновоеЗаданиеИдентификатор);
	Если Прогресс <> Неопределено Тогда
		ФоновоеЗаданиеПроцент = Прогресс.Процент;
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

&НаКлиенте
Процедура ВыполнитьШагСопоставлениеЗагружаемыхДанныхКлиент()
	
	Если ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
		Статистика = СтатисткаСопоставления();
		
		Если Статистика.Сопоставленных > 0 Тогда
			ТекстНайдено = НСтр("ru = 'Из %1 введенных строк в список будут вставлены: %2.'");
			Элементы.НадписьРезультатСопоставления.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстНайдено,
				Статистика.Всего, Статистика.Сопоставленных);
			
			Если Статистика.Неоднозначных > 0 и Статистика.НеНайдено > 0 Тогда 
				ТекстНеНайдено = НСтр("ru = '11 строк будут пропущены:'") + Символы.ПС + "  - " + НСтр("ru = 'Нет данных в программе: %1'") 
					+ Символы.ПС + "  - " +НСтр("ru = 'Несколько вариантов для вставки: %2'");
				ТекстНеНайдено = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстНеНайдено, Статистика.НеНайдено, Статистика.Неоднозначных);
			ИначеЕсли Статистика.Неоднозначных > 0 Тогда
				ТекстНеНайдено = НСтр("ru = 'Строки, для которых в программе имеется несколько вариантов, будут пропущены: %1'");
				ТекстНеНайдено = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстНеНайдено, Статистика.Неоднозначных);
			ИначеЕсли Статистика.НеНайдено > 0 Тогда
				ТекстНеНайдено = НСтр("ru = 'Строки, для которых в программе нет соответствующих данных, будут пропущены: %1'");
				ТекстНеНайдено = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстНеНайдено, Статистика.НеНайдено);
			КонецЕсли;
			ТекстНеНайдено = ТекстНеНайдено + Символы.ПС + НСтр("ru = 'Для просмотра пропущенных строк и подбора данных для вставки нажмите ""Далее"".'");
			Элементы.ДекорацияНеНайденоИНеоднозначность.Заголовок = ТекстНеНайдено;
			
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.РезультатыСопоставления;
			Элементы.Назад.Видимость = Истина;
			Элементы.ВставитьВСписок.Видимость = Истина;
			Элементы.Далее.Видимость = Истина;
			Элементы.Назад.Заголовок = НСтр("ru = '< Назад'");
			Элементы.Далее.Заголовок = Нстр("ru = 'Далее >'");
			Элементы.Далее.АктивизироватьПоУмолчанию = Ложь;
			Элементы.ВставитьВСписок.АктивизироватьПоУмолчанию = Истина;
			Элементы.ВставитьВСписок.КнопкаПоУмолчанию = Истина;
			
			ПоказатьСтатистикуПоСопоставлениюЗагрузкаИзФайла();
			УстановитьОформлениеДляСтраницыСопоставления(Ложь, Элементы.ПояснениеДляПоискаСсылок, Ложь, НСтр("ru = 'Далее >'"));
		Иначе
			Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.НеНайдено;
			Элементы.Закрыть.Заголовок = НСтр("ru = 'Закрыть'");
			Элементы.Назад.Видимость = Истина;
			Элементы.ВставитьВСписок.Видимость = Ложь;
			Элементы.Далее.Видимость = Ложь;
		КонецЕсли;
		
	Иначе 
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СопоставлениеЗагружаемыхДанных;
		ПоказатьСтатистикуПоСопоставлениюЗагрузкаИзФайла();
		
		Если ТипЗагрузки = "УниверсальнаяЗагрузка" Тогда
			УстановитьОформлениеДляСтраницыСопоставления(Истина, Элементы.ПояснениеДляУниверсальнойЗагрузки, Истина, НСтр("ru = 'Загрузить данные >'"));
		ИначеЕсли ТипЗагрузки = "ТабличнаяЧасть" Тогда
			УстановитьОформлениеДляСтраницыСопоставления(Ложь, Элементы.ПояснениеДляТабличнойЧасти, Истина, НСтр("ru = 'Загрузить данные'"));
		ИначеЕсли ТипЗагрузки = "ВнешняяЗагрузка" Тогда
			УстановитьОформлениеДляСтраницыСопоставления(Ложь, Элементы.ПояснениеДляПрикладнойЗагрузки, Ложь, НСтр("ru = 'Загрузить данные >'"));
		Иначе
			УстановитьОформлениеДляСтраницыСопоставления(Ложь, Элементы.ПояснениеДляПрикладнойЗагрузки, Ложь, НСтр("ru = 'Загрузить данные >'"));
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОформлениеДляСтраницыСопоставления(ВидимостьКнопкиСопоставление, ЭлементДляПоясняющегоТекста, ВидимостьКнопкиРазрешитьНеоднозначность, ТекстКнопкиДалее)
	
	Элементы.СписокКолонокСопоставления.Видимость = ВидимостьКнопкиСопоставление;
	Элементы.Назад.Видимость = Истина;
	Элементы.ПояснениеДляУниверсальнойЗагрузки.Видимость = Ложь;
	Элементы.ПояснениеДляПрикладнойЗагрузки.Видимость = Ложь;
	Элементы.ПояснениеДляТабличнойЧасти.Видимость = Ложь;
	Элементы.ПояснениеДляПоискаСсылок.Видимость = Ложь;
	Если ЭлементДляПоясняющегоТекста = Элементы.ПояснениеДляУниверсальнойЗагрузки Тогда
		Элементы.ПояснениеДляУниверсальнойЗагрузки.Видимость = Истина;
	ИначеЕсли ЭлементДляПоясняющегоТекста = Элементы.ПояснениеДляТабличнойЧасти Тогда
		Элементы.ПояснениеДляТабличнойЧасти.Видимость = Истина;
	ИначеЕсли ЭлементДляПоясняющегоТекста = Элементы.ПояснениеДляПоискаСсылок Тогда
		Элементы.ПояснениеДляПоискаСсылок.Видимость = Истина;
		Элементы.ПояснениеДляСопоставленияДанных.ОтображатьЗаголовок = Ложь;
	Иначе
		Элементы.ПояснениеДляПрикладнойЗагрузки.Видимость = Истина;
	КонецЕсли;
	
	Элементы.РазрешитьНеоднозначность.Видимость = ВидимостьКнопкиРазрешитьНеоднозначность;
	Элементы.Далее.Заголовок = ТекстКнопкиДалее;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРазрешенияНеоднозначности(ВыбраннаяСтрока, ПолеИмя, СтандартнаяОбработка)
	Строка = ТаблицаСопоставленияДанных.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если ТипЗагрузки = "ТабличнаяЧасть" Тогда
		Если Строка.РезультатСопоставленияСтроки = "НеСопоставлен" и СтрДлина(Строка.ОписаниеОшибки) > 0 Тогда
			
			Если СтрДлина(ПолеИмя) > 3 И Лев(ПолеИмя,3) = "тч_" Тогда
				Имя = Сред(ПолеИмя, 4);
				Если Найти(Строка.ОписаниеОшибки, Имя) Тогда 
					СтандартнаяОбработка = Ложь;
					СтрокаИзТаблицы = новый Массив;
					ЗначенияЗагружаемыхКолонок = Новый Структура();
					Для каждого Колонка из ИнформацияПоКолонкам Цикл 
						м = Новый Массив();
						м.Добавить(Колонка.ИмяКолонки);
						м.Добавить(Колонка.ПредставлениеКолонки);
						м.Добавить(Строка["фл_" + Колонка.ИмяКолонки]);
						м.Добавить(Колонка.ТипКолонки);
						СтрокаИзТаблицы.Добавить(м);
						Если Имя = Колонка.Ассоциация Тогда
							ЗначенияЗагружаемыхКолонок.Вставить(Колонка.ИмяКолонки, Строка["фл_" + Колонка.ИмяКолонки]); 
						КонецЕсли;
					КонецЦикла;
					
					ПараметрыФормы = Новый Структура();
					ПараметрыФормы.Вставить("ТипЗагрузки", ТипЗагрузки);
					ПараметрыФормы.Вставить("Имя", Имя);
					ПараметрыФормы.Вставить("СтрокаИзТаблицы", СтрокаИзТаблицы);
					ПараметрыФормы.Вставить("ЗначенияЗагружаемыхКолонок", ЗначенияЗагружаемыхКолонок);
					ПараметрыФормы.Вставить("СписокНеоднозначностей", Неопределено);
					ПараметрыФормы.Вставить("ПолноеИмяТабличнойЧасти", ИмяОбъектаСопоставления);
					
					Параметр = Новый Структура();
					Параметр.Вставить("Идентификатор", ВыбраннаяСтрока);
					Параметр.Вставить("Имя", Имя);
					
					Оповещение = Новый ОписаниеОповещения("ПослеСопоставленияНеоднозначностей", ЭтотОбъект, Параметр);
					ОткрытьФорму("Обработка.ЗагрузкаДанныхИзФайла.Форма.РазрешенияНеоднозначностей", ПараметрыФормы, ЭтотОбъект, Истина , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если Строка.РезультатСопоставленияСтроки = "Неоднозначность" Тогда
			СтандартнаяОбработка = Ложь;
			
			СтрокаИзТаблицы = новый Массив;
			Для каждого Колонка из ИнформацияПоКолонкам Цикл 
				м = Новый Массив();
				м.Добавить(Колонка.ИмяКолонки);
				м.Добавить(Колонка.ПредставлениеКолонки);
				м.Добавить(Строка[Колонка.ИмяКолонки]);
				м.Добавить(Колонка.ТипКолонки);
				СтрокаИзТаблицы.Добавить(м);
			КонецЦикла;
			
			КолонкиСопоставления = Новый СписокЗначений;
			Для каждого Элемент из СопоставитьПоКолонке Цикл 
				Если Элемент.Пометка Тогда
					КолонкиСопоставления.Добавить(Элемент.Значение);
				КонецЕсли;
			КонецЦикла;
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("СтрокаИзТаблицы", СтрокаИзТаблицы);
			ПараметрыФормы.Вставить("СписокНеоднозначностей", Строка.СписокНеоднозначностей);
			ПараметрыФормы.Вставить("КолонкиСопоставления", КолонкиСопоставления);
			ПараметрыФормы.Вставить("ТипЗагрузки", ТипЗагрузки);
			
			Параметр = Новый Структура("Идентификатор", ВыбраннаяСтрока);
			
			Оповещение = Новый ОписаниеОповещения("ПослеСопоставленияНеоднозначностей", ЭтотОбъект, Параметр);
			ОткрытьФорму("Обработка.ЗагрузкаДанныхИзФайла.Форма.РазрешенияНеоднозначностей", ПараметрыФормы, ЭтотОбъект, Истина , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СопоставитьДанныеПрикладнаяЗагрузка(ТаблицаСопоставленияДанныхСервер)
	
	МенеджерОбъект = МенеджерОбъекта(ИмяОбъектаСопоставления);
	
	МенеджерОбъект.СопоставитьЗагружаемыеДанныеИзФайла(ТаблицаСопоставленияДанныхСервер);
	Для каждого Строка из ТаблицаСопоставленияДанныхСервер Цикл 
		Если ЗначениеЗаполнено(Строка.ОбъектСопоставления) Тогда 
			Строка.РезультатСопоставленияСтроки = "СтрокаСопоставлена";
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ШагОтчетОЗагрузке

&НаСервере
Процедура ЗаписатьЗагружаемыеДанныеОтчет(ФоновоеЗадание = Ложь)
	
	СопоставленныеДанные = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных");
	
	Если ТипЗагрузки = "УниверсальнаяЗагрузка" Тогда
		
		ПараметрыЗагрузки = Новый Структура();
		ПараметрыЗагрузки.Вставить("СоздаватьЕслиНеСопоставлено", СоздаватьЕслиНеСопоставлено);
		ПараметрыЗагрузки.Вставить("ОбновлятьСуществующие", ОбновлятьСуществующие);

		ПараметрыВызоваСервера = Новый Структура();
		ПараметрыВызоваСервера.Вставить("СопоставленныеДанные", СопоставленныеДанные);
		ПараметрыВызоваСервера.Вставить("ПараметрыЗагрузки", ПараметрыЗагрузки);
		ПараметрыВызоваСервера.Вставить("ИмяОбъектаСопоставления", ИмяОбъектаСопоставления);
		ТаблицаИнформацияПоКолонкам = РеквизитФормыВЗначение("ИнформацияПоКолонкам");
		ПараметрыВызоваСервера.Вставить("ИнформацияПоКолонкам", ТаблицаИнформацияПоКолонкам);
		
		РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
				"Обработки.ЗагрузкаДанныхИзФайла.ЗаписатьСопоставленныеДанные",
				ПараметрыВызоваСервера, 
				НСтр("ru = 'Подсистема ЗагрузкаДанныхИзФайла: Запись загружаемых  данных'"));
		
		Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
			СопоставленныеДанные = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
		Иначе 
			ФоновоеЗадание = Истина;
			ФоновоеЗаданиеИдентификатор  = РезультатФоновогоЗадания.ИдентификаторЗадания;
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
		КонецЕсли;
	ИначеЕсли ТипЗагрузки = "ВнешняяЗагрузка" Тогда
		ЗаписатьСопоставленныеДанныеВнешняяОбработка(СопоставленныеДанные);
	Иначе
		ЗаписатьСопоставленныеДанныеПрикладнаяЗагрузка(СопоставленныеДанные);
	КонецЕсли;
	
	Если НЕ ФоновоеЗадание Тогда
		ЗначениеВРеквизитФормы(СопоставленныеДанные, "ТаблицаСопоставленияДанных");
	КонецЕсли;
	
	Элементы.ОткрытьСправочникПослеЗакрытияПомощника.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Элементы.ОткрытьСправочникПослеЗакрытияПомощника.Заголовок, ПредставлениеСправочника(ИмяОбъектаСопоставления));
	Элементы.ПояснениеОтчетаОЗагрузке.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Элементы.ПояснениеОтчетаОЗагрузке.Заголовок, ПредставлениеСправочника(ИмяОбъектаСопоставления));
	
	ТипОтчета = "ВсеЭлементы";
	
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьШагОтчетОЗагрузкеДанныхКлиент()
	
	Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ОтчетОЗагрузкеДанных;
	Элементы.ОткрытьСправочникПослеЗакрытияПомощника.Видимость = Истина;
	Элементы.Закрыть.Заголовок = "Готово";
	Элементы.Далее.Видимость = Ложь;
	Элементы.Назад.Видимость = Ложь;
	
КонецПроцедуры
#КонецОбласти

&НаСервере
Процедура ОчиститьТаблицу()
	
	ТаблицаСопоставленияДанныхСервер = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных");
	ТаблицаСопоставленияДанныхСервер.Колонки.Очистить();
	ИнформацияПоКолонкам.Очистить();
	
	Пока Элементы.ТаблицаСопоставленияДанных.ПодчиненныеЭлементы.Количество() > 0 Цикл
		ЭтотОбъект.Элементы.Удалить(Элементы.ТаблицаСопоставленияДанных.ПодчиненныеЭлементы.Получить(0));
	КонецЦикла;
	ШаблонСДанными = Новый ТабличныйДокумент;
	
	РеквизитыТаблицыСопоставления = ЭтотОбъект.ПолучитьРеквизиты("ТаблицаСопоставленияДанных");
	МассивПутейРеквизитов = Новый Массив;
	Для каждого РеквизитТаблицы Из РеквизитыТаблицыСопоставления Цикл
		МассивПутейРеквизитов.Добавить("ТаблицаСопоставленияДанных." + РеквизитТаблицы.Имя);
	КонецЦикла;
	Если МассивПутейРеквизитов.Количество() > 0 Тогда
		ИзменитьРеквизиты(,МассивПутейРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеДанных()
	
	Если ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда 
		ТекстОбъектНеНайден = НСтр("ru='<Не найден>'");
		ЦветОбъектНеНайден = ЦветаСтиля.ФайлЗанятыйДругимПользователем;
		ЦветНеоднозначность = ЦветаСтиля.ИзмененноеЗначениеРеквизитаЦвет;
	Иначе
		ТекстОбъектНеНайден = НСтр("ru='<Новый>'");
		ЦветОбъектНеНайден = ЦветаСтиля.РезультатУспехЦвет;
		ЦветНеоднозначность = ЦветаСтиля.ПоясняющийОшибкуТекст;
	КонецЕсли;
	
	УсловноеОформление.Элементы.Очистить();
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("ОбъектСопоставления");
	ПолеОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСопоставленияДанных.ОбъектСопоставления"); 
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено; 
	ЭлементОтбора.Использование = Истина;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветОбъектНеНайден);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", ТекстОбъектНеНайден);
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("ОбъектСопоставления");
	ПолеОформления.Использование = Истина;
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСопоставленияДанных.РезультатСопоставленияСтроки"); 
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно; 
	ЭлементОтбора.ПравоеЗначение = "Неоднозначность"; 
	ЭлементОтбора.Использование = Истина;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветНеоднозначность);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<неоднозначность>'"));
	
КонецПроцедуры

&НаСервере
Функция ИнформацияОКолонке(ИмяКолонки)
	Отбор = новый Структура("ИмяКолонки", ИмяКолонки);
	Результат = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
	Если Результат.Количество() > 0 Тогда
		Возврат Результат[0];
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

&НаСервере
Функция ИнформацияОбОбъектеМетаданныхПоТипу(ПолныйТипОбъекта)
	ОписаниеОбъекта = новый Структура("ТипОбъекта, ИмяОбъекта");
	ПолноеИмя = Метаданные.НайтиПоТипу(ПолныйТипОбъекта).ПолноеИмя();
	Результат = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолноеИмя,".");
	Если Результат.Количество()>1 Тогда
		ОписаниеОбъекта.ТипОбъекта = Результат[0];
		ОписаниеОбъекта.ИмяОбъекта = Результат[1];
		
		Возврат ОписаниеОбъекта;
	Иначе
		Возврат Неопределено;		
	КонецЕсли;
	
КонецФункции 

&НаСервере
Функция УсловияПоВыбраннымКолонкам()
	
	Разделитель = "";
	РазделительИ = "";
	ТипСравнения = " = ";
	сГде = "";
	СтрокаУсловие = "";
	
	Для каждого Элемент Из СопоставитьПоКолонке Цикл
		Если Элемент.Пометка Тогда
			Колонка = ИнформацияОКолонке(Элемент.Значение);
			// Создаем запрос в зависимости от типов
			Если Колонка <> Неопределено тогда
				ТипКолонки = Колонка.ТипКолонки.Типы()[0];
				Если ТипКолонки = Тип("Строка") Тогда
					Если Колонка.ТипКолонки.КвалификаторыСтроки.Длина = 0 ТОгда
						СтрокаУсловие = СтрокаУсловие + РазделительИ + "СправочникСопоставления." + Колонка.ИмяКолонки +  " ПОДОБНО ТаблицаСопоставления." + Колонка.ИмяКолонки;
						сГДЕ = сГДЕ + " И СправочникСопоставления." + Колонка.ИмяКолонки + " <> """"";
					Иначе
						СтрокаУсловие = СтрокаУсловие + РазделительИ + "СправочникСопоставления." + Колонка.ИмяКолонки +  " = ТаблицаСопоставления." + Колонка.ИмяКолонки;
						сГДЕ = сГДЕ + " И СправочникСопоставления." + Колонка.ИмяКолонки + " <> """"";
					КонецЕсли;
				ИначеЕсли ТипКолонки = Тип("Число") Тогда
					СтрокаУсловие = СтрокаУсловие + РазделительИ + "СправочникСопоставления." + Колонка.ИмяКолонки + " =  ТаблицаСопоставления." + Колонка.ИмяКолонки;
				ИначеЕсли ТипКолонки = Тип("Дата") Тогда 
					СтрокаУсловие = СтрокаУсловие + РазделительИ + "СправочникСопоставления." + Колонка.ИмяКолонки + " =  ТаблицаСопоставления." + Колонка.ИмяКолонки;
				ИначеЕсли ТипКолонки = Тип("Булево") Тогда 
					СтрокаУсловие = СтрокаУсловие + РазделительИ + "СправочникСопоставления." + Колонка.ИмяКолонки + " =  ТаблицаСопоставления." + Колонка.ИмяКолонки;
				Иначе
					ИнфоОбъект = ИнформацияОбОбъектеМетаданныхПоТипу(ТипКолонки);
					Если ИнфоОбъект.ТипОбъекта = "Справочник" Тогда
						Справочник = Метаданные.Справочники.Найти(ИнфоОбъект.ИмяОбъекта);
						ТекстУсловияСправочник = "";
						РазделительИЛИ = "";
						Для каждого СтрокаВвода из Справочник.ВводПоСтроке Цикл 
							Если СтрокаВвода.Имя = "Код" И НЕ Справочник.Автонумерация Тогда 
								ТекстУсловияВВодаПоСтроке = "СправочникСопоставления." + Колонка.ИмяКолонки+ ".Код " + ТипСравнения + " ТаблицаСопоставления." + Колонка.ИмяКолонки;	
							Иначе
								ТекстУсловияВВодаПоСтроке = "СправочникСопоставления." + Колонка.ИмяКолонки+ "." + СтрокаВвода.Имя  + ТипСравнения + " ТаблицаСопоставления." + Колонка.ИмяКолонки;
							КонецЕсли;	
							ТекстУсловияСправочник = ТекстУсловияСправочник + РазделительИЛИ + ТекстУсловияВВодаПоСтроке;
							РазделительИЛИ = " ИЛИ ";
						КонецЦикла;
						СтрокаУсловие = СтрокаУсловие + РазделительИ + " ( "+ ТекстУсловияСправочник + " )";
					ИначеЕсли ИнфоОбъект.ТипОбъекта = "Перечисление" Тогда 
						СтрокаУсловие = СтрокаУсловие + РазделительИ + "СправочникСопоставления." + Колонка.ИмяКолонки + " =  ТаблицаСопоставления." + Колонка.ИмяКолонки;	
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
			РазделительИ = " И ";
			Разделитель = ",";
			
		КонецЕсли;
	КонецЦикла;
	
	Условия = Новый Структура("УсловиеОбъединения , Где");
	Условия.УсловиеОбъединения  = СтрокаУсловие;
	Условия.Где = сГДЕ;
	Возврат Условия;
КонецФункции

&НаСервере
Процедура ВыполнитьСопоставлениеПоВыбранномуРеквизиту(КоличествоСопоставленных = 0, СписокКолонокСопоставления = "")
	
	Условия = УсловияПоВыбраннымКолонкам();
	
	Если Не ЗначениеЗаполнено(Условия.УсловиеОбъединения ) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОбъекта = Обработки.ЗагрузкаДанныхИзФайла.РазложитьПолноеИмяОбъекта(ИмяОбъектаСопоставления);
	ИмяСправочника = СтруктураОбъекта.НазваниеОбъекта;
	ТаблицаСопоставления = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных");
	
	СписокКолонок = "";
	Разделитель = "";
	Для каждого Колонка из ТаблицаСопоставления.Колонки Цикл
		Если Колонка.Имя <> "СписокНеоднозначностей" И Колонка.Имя <> "РезультатСопоставленияСтроки" И Колонка.Имя <> "ОписаниеОшибки" Тогда
			СписокКолонок = СписокКолонок + Разделитель + Колонка.Имя;
			Разделитель = ", ";
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ " + СписокКолонок + "
	|ПОМЕСТИТЬ ТаблицаСопоставления
	|ИЗ &ТаблицаСопоставления КАК ТаблицаСопоставления
	|;
	|ВЫБРАТЬ
	|	СправочникСопоставления.Ссылка, ТаблицаСопоставления.Идентификатор
	|ИЗ
	|	Справочник." + ИмяСправочника + " КАК СправочникСопоставления
	|		ПРАВОЕ СОЕДИНЕНИЕ ТаблицаСопоставления КАК ТаблицаСопоставления
	|	        ПО " + Условия.УсловиеОбъединения + "
	|ГДЕ
	|	       СправочникСопоставления.ПометкаУдаления = ЛОЖЬ " + Условия.Где + "
	|	УПОРЯДОЧИТЬ ПО ТаблицаСопоставления.Идентификатор ИТОГИ ПО ТаблицаСопоставления.Идентификатор";
	
	Запрос.УстановитьПараметр("ТаблицаСопоставления", ТаблицаСопоставления);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПустоеЗначение = МенеджерОбъекта(ИмяОбъектаСопоставления).ПустаяСсылка();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Строка = ТаблицаСопоставления.Найти(ВыборкаДетальныеЗаписи.Идентификатор, "Идентификатор");
		
		Если ЗначениеЗаполнено(Строка.ОбъектСопоставления) Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписиГруппа = ВыборкаДетальныеЗаписи.Выбрать();
		
		Если ВыборкаДетальныеЗаписиГруппа.Количество() > 1 Тогда
			СписокНеоднозначностей = Новый СписокЗначений;
			Пока ВыборкаДетальныеЗаписиГруппа.Следующий() Цикл
				СписокНеоднозначностей.Добавить(ВыборкаДетальныеЗаписиГруппа.Ссылка);
			КонецЦикла;
			Строка.РезультатСопоставленияСтроки = "Неоднозначность";
			Строка.ОписаниеОшибки = СписокКолонокСопоставления;
			Строка.СписокНеоднозначностей = СписокНеоднозначностей;
		Иначе
			ВыборкаДетальныеЗаписиГруппа.Следующий();
			КоличествоСопоставленных = КоличествоСопоставленных + 1;
			Строка.РезультатСопоставленияСтроки = "СтрокаСопоставлена";
			Строка.ОписаниеОшибки = "";
			Строка.ОбъектСопоставления = ВыборкаДетальныеЗаписиГруппа.Ссылка;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаСопоставления, "ТаблицаСопоставленияДанных");
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьДанныеВТаблицуСопоставления(АдресЗагруженныхДанных, АдресКопииТабличнойЧасти, СписокНеоднозначностей)
	
	ТабличнаяЧасть =  ПолучитьИзВременногоХранилища(АдресКопииТабличнойЧасти);
	
	Если ТабличнаяЧасть = Неопределено ИЛИ ТипЗнч(ТабличнаяЧасть) <> Тип("ТаблицаЗначений") ИЛИ ТабличнаяЧасть.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	ТаблицаСопоставленияДанных.Очистить();
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	
	Для каждого Строка Из ТабличнаяЧасть Цикл 
		НоваяСтрока = ТаблицаСопоставленияДанных.Добавить();
		НоваяСтрока.Идентификатор = Строка.Идентификатор;
		Для каждого Колонка Из ТабличнаяЧасть.Колонки Цикл
			Если Колонка.Имя <> "Идентификатор" Тогда
				НоваяСтрока["тч_" + Колонка.Имя] = Строка[Колонка.Имя];
			КонецЕсли;
			Если Колонка.Имя = "Номенклатура" Тогда 
				Если НЕ ЗначениеЗаполнено(Строка[Колонка.Имя]) Тогда 
					НоваяСтрока["РезультатСопоставленияСтроки"] = "НеСопоставлен";
				иначе
					НоваяСтрока["РезультатСопоставленияСтроки"] = "СтрокаСопоставлена";
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Отбор = Новый Структура("Идентификатор", Строка.Идентификатор); 
		
		Неоднозначности = СписокНеоднозначностей.НайтиСтроки(Отбор);
		Если Неоднозначности.Количество() > 0 Тогда 
			НоваяСтрока["РезультатСопоставленияСтроки"] = "НеСопоставлен";
			Для каждого Неоднозначность Из Неоднозначности Цикл
				НоваяСтрока["ОписаниеОшибки"] = НоваяСтрока["ОписаниеОшибки"] + Неоднозначность.Колонка+ ";";
				
				ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
				ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
				ПолеОформления.Поле = Новый ПолеКомпоновкиДанных("тч_" + Неоднозначность.Колонка);
				ПолеОформления.Использование = Истина;
				ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСопоставленияДанных.ОписаниеОшибки"); 
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит; 
				ЭлементОтбора.ПравоеЗначение = Неоднозначность.Колонка; 
				ЭлементОтбора.Использование = Истина;
				ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
				ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<неоднозначность>'"));
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Строка Из ЗагружаемыеДанные Цикл 
		Отбор = Новый Структура("Идентификатор", Строка.Идентификатор);
		
		Строки = ТаблицаСопоставленияДанных.НайтиСтроки(Отбор);
		Если Строки.Количество() > 0 Тогда 
			НоваяСтрока = Строки[0];
			Для каждого Колонка Из ЗагружаемыеДанные.Колонки Цикл
				Если Колонка.Имя <> "Идентификатор" И Колонка.Имя <> "РезультатСопоставленияСтроки" И Колонка.Имя <> "ОписаниеОшибки" 
					И Колонка.Имя <> "Количество"  И Колонка.Имя <> "Цена" Тогда
					НоваяСтрока["фл_" + Колонка.Имя] = Строка[Колонка.Имя];
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция АдресВХранилищеТаблицыСопоставления()
	Таблица = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных");
	
	ТаблицаДляТЧ = Новый ТаблицаЗначений;
	Для Каждого Колонка Из Таблица.Колонки цикл 
		Если Лев(Колонка.Имя, 3) = "тч_" тогда 
			ТаблицаДляТЧ.Колонки.Добавить(Сред(Колонка.имя, 4), Колонка.ТипЗначения, Колонка.Заголовок, Колонка.Ширина);
		ИначеЕсли  Колонка.Имя = "РезультатСопоставленияСтроки" ИЛИ Колонка.Имя = "ОписаниеОшибки" ИЛИ Колонка.Имя = "Идентификатор" тогда 
			ТаблицаДляТЧ.Колонки.Добавить(Колонка.имя, Колонка.ТипЗначения, Колонка.Заголовок, Колонка.Ширина);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из Таблица цикл 
		НоваяСтрока = ТаблицаДляТЧ.Добавить();	
		Для Каждого Колонка из ТаблицаДляТЧ.Колонки Цикл
			Если Колонка.Имя = "Идентификатор" Тогда 
				НоваяСтрока[Колонка.Имя] = Строка[Колонка.Имя];
			ИначеЕсли Колонка.Имя <> "РезультатСопоставленияСтроки" И Колонка.Имя <> "ОписаниеОшибки" Тогда
				НоваяСтрока[Колонка.Имя] = Строка["тч_"+ Колонка.Имя];				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаДляТЧ);
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеСправочника(ПолноеИмяОбъектаМетаданных)
	Возврат Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъектаМетаданных).Представление();
КонецФункции

&НаСервереБезКонтекста
Функция МенеджерОбъекта(ИмяОбъектаСопоставления)
		МассивОбъекта = Обработки.ЗагрузкаДанныхИзФайла.РазложитьПолноеИмяОбъекта(ИмяОбъектаСопоставления);
		Если МассивОбъекта.ТипОбъекта = "Документ" Тогда
			МенеджерОбъекта = Документы[МассивОбъекта.НазваниеОбъекта];
		ИначеЕсли МассивОбъекта.ТипОбъекта = "Справочник" Тогда
			МенеджерОбъекта = Справочники[МассивОбъекта.НазваниеОбъекта];
		Иначе
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Объект ""%1"" не найден'"), ИмяОбъектаСопоставления);
		КонецЕсли;
		
		Возврат МенеджерОбъекта;
КонецФункции

&НаСервереБезКонтекста
Функция ФормаСписка(ИмяОбъектаСопоставления)
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяОбъектаСопоставления);
	Возврат ОбъектМетаданных.ОсновнаяФормаСписка.ПолноеИмя();
КонецФункции


&НаСервере
Функция ОписаниеТипаПоМетаданным(ПолноеИмяОбъектаМетаданных)
	Результат = Обработки.ЗагрузкаДанныхИзФайла.РазложитьПолноеИмяОбъекта(ПолноеИмяОбъектаМетаданных);
	Если Результат.ТипОбъекта = "Справочник" Тогда 
		Возврат Новый ОписаниеТипов("СправочникСсылка." +  Результат.НазваниеОбъекта);
	ИначеЕсли Результат.ТипОбъекта = "Документ" Тогда 
		Возврат Новый ОписаниеТипов("ДокументСсылка." +  Результат.НазваниеОбъекта);
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

&НаСервере
Функция НезаполненныеОбязательныеКолонки()
	НазванияКолонокБезДанных = Новый Массив;
	
	Отбор = Новый Структура("ОбязательнаДляЗаполнения", Истина);
	ОбязательныеКолонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
	
	Шапка = ОбластьЗаголовкаШаблонаТаблицы(ШаблонСДанными);
	Для НомерКолонки = 1 По Шапка.ШиринаТаблицы Цикл 
		Ячейка = Шапка.ПолучитьОбласть(1, НомерКолонки, 1, НомерКолонки);
		ИмяКолонки = СокрЛП(Ячейка.ТекущаяОбласть.Текст);
		
		ИнформацияОКолонке = Неопределено;
		Отбор = Новый Структура("ПредставлениеКолонки", ИмяКолонки);
		КолонкиОтбор = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
		
		Если КолонкиОтбор.Количество() > 0 Тогда
			ИнформацияОКолонке = КолонкиОтбор[0];
		Иначе
			Отбор = Новый Структура("ИмяКолонки", ИмяКолонки);
			КолонкиОтбор = ИнформацияПоКолонкам.НайтиСтроки(Отбор);	
			
			Если КолонкиОтбор.Количество() > 0 Тогда
				ИнформацияОКолонке = КолонкиОтбор[0];
			КонецЕсли;
		КонецЕсли;
		Если ИнформацияОКолонке <> Неопределено Тогда
			Если ИнформацияОКолонке.ОбязательнаДляЗаполнения Тогда 
				Для НомерСтроки = 2 По ШаблонСДанными.ВысотаТаблицы Цикл 
					Ячейка = ШаблонСДанными.ПолучитьОбласть(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
					Если НЕ ЗначениеЗаполнено(Ячейка.ТекущаяОбласть.Текст) Тогда
						НазванияКолонокБезДанных.Добавить(ИнформацияОКолонке.ПредставлениеКолонки);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НазванияКолонокБезДанных;
КонецФункции

#Область ВнешняяЗагрузка

&НаСервере
Процедура СопоставитьДанныеВнешняяОбработка(ТаблицаСопоставленияДанныхСервер )
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
		ВнешнийОбъект = МодульДополнительныеОтчетыИОбработки.ПолучитьОбъектВнешнейОбработки(ВнешняяОбработкаСсылка);
		ВнешнийОбъект.СопоставитьЗагружаемыеДанныеИзФайла(ИдентификаторКоманды, ТаблицаСопоставленияДанныхСервер);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСопоставленныеДанныеВнешняяОбработка(СопоставленныеДанные) 
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
		ВнешнийОбъект = МодульДополнительныеОтчетыИОбработки.ПолучитьОбъектВнешнейОбработки(ВнешняяОбработкаСсылка);
	КонецЕсли;
	
	Отказ = Ложь;
	ПараметрыЗагрузки = Новый Структура();
	ПараметрыЗагрузки.Вставить("СоздаватьНовые", СоздаватьЕслиНеСопоставлено);
	ПараметрыЗагрузки.Вставить("ОбновлятьСуществующие", ОбновлятьСуществующие);
	ВнешнийОбъект.ЗагрузитьИзФайла(ИдентификаторКоманды, СопоставленныеДанные, ПараметрыЗагрузки, Отказ); 
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьМакетВТабличныйДокументВнешняяОбработка(ПараметрыЗагрузки)
	
	ОбластьЗаголовокТаблицы = ОбластьЗаголовкаШаблонаТаблицы(ПараметрыЗагрузки.Макет);
	ОбластьЗаголовокТаблицы.Защита = Истина;
	ШаблонСДанными.ФиксацияСверху = 1;
	ШаблонСДанными.Вывести(ОбластьЗаголовокТаблицы);
	
	Обработки.ЗагрузкаДанныхИзФайла.СоздатьИнформациюПоКолонкамНаОснованиеШаблона(ОбластьЗаголовокТаблицы, ПараметрыЗагрузки.СоответствиеТипаДанныхКолонок, ИнформацияПоКолонкам);
КонецПроцедуры

#КонецОбласти

#Область ЗагрузкаИзФайла

&НаСервере
Процедура ЗаписатьСопоставленныеДанныеПрикладнаяЗагрузка(СопоставленныеДанные)
	
	МенеджерОбъекта =  МенеджерОбъекта(ИмяОбъектаСопоставления);
	
	Отказ = Ложь;
	ПараметрыЗагрузки = Новый Структура();
	ПараметрыЗагрузки.Вставить("СоздаватьНовые", СоздаватьЕслиНеСопоставлено);
	ПараметрыЗагрузки.Вставить("ОбновлятьСуществующие", ОбновлятьСуществующие);
	МенеджерОбъекта.ЗагрузитьИзФайла(СопоставленныеДанные, ПараметрыЗагрузки, Отказ)
	
КонецПроцедуры


#КонецОбласти

#Область ЗагрузкаВТабличнуюЧасть

&НаСервере
Процедура СкопироватьСтруктуруТабличнойЧасти(АдресТабличнойЧасти)
	
	ТабличнаяЧасть = Метаданные.НайтиПоПолномуИмени(ИмяОбъектаСопоставления);
	
	ДанныеДляТабличнойЧасти = Новый ТаблицаЗначений;
	ДанныеДляТабличнойЧасти.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Число"), "Идентификатор");
	Для каждого РеквизитТабличнойЧасти из ТабличнаяЧасть.Реквизиты Цикл
		ДанныеДляТабличнойЧасти.Колонки.Добавить(РеквизитТабличнойЧасти.Имя, РеквизитТабличнойЧасти.Тип, РеквизитТабличнойЧасти.Представление());	
	КонецЦикла;
	
	АдресТабличнойЧасти = ПоместитьВоВременноеХранилище(ДанныеДляТабличнойЧасти);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура СформироватьОтчетОЗагрузке(ТипОтчета = "ВсеЭлементы", ФоновоеЗадание = Ложь, РассчитыватьПроцентПрогресса = Ложь)
	
	СопоставленныеДанные = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных");
	
	ПараметрыВызоваСервера = Новый Структура();
	
	ПараметрыВызоваСервера.Вставить("ТаблицаОтчет", ТаблицаОтчет);
	ПараметрыВызоваСервера.Вставить("ТипОтчета", ТипОтчета);
	ПараметрыВызоваСервера.Вставить("СопоставленныеДанные", СопоставленныеДанные);
	ПараметрыВызоваСервера.Вставить("ШаблонСДанными", ШаблонСДанными);
	ПараметрыВызоваСервера.Вставить("ИмяОбъектаСопоставления", ИмяОбъектаСопоставления);
	ТаблицаИнформацияПоКолонкам = РеквизитФормыВЗначение("ИнформацияПоКолонкам");
	ПараметрыВызоваСервера.Вставить("ИнформацияПоКолонкам", ТаблицаИнформацияПоКолонкам);
	
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ПараметрыВызоваСервера.Вставить("РассчитыватьПроцентПрогресса", Ложь);
		АдресХранилища = ПоместитьВоВременноеХранилище("");
		Обработки.ЗагрузкаДанныхИзФайла.СформироватьОтчетОЗагрузкеФон(ПараметрыВызоваСервера, АдресХранилища);
		ФоновоеЗаданиеАдресХранилища = АдресХранилища;
	Иначе
		ПараметрыВызоваСервера.Вставить("РассчитыватьПроцентПрогресса", РассчитыватьПроцентПрогресса);
		РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
				"Обработки.ЗагрузкаДанныхИзФайла.СформироватьОтчетОЗагрузкеФон",
				ПараметрыВызоваСервера, 
				НСтр("ru = 'Подсистема ЗагрузкаДанныхИзФайла: Выполнение серверного метода обработки сформировать отчет о загрузке'"));
		
		Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
		Иначе 
			ФоновоеЗадание = Истина;
			ФоновоеЗаданиеИдентификатор  = РезультатФоновогоЗадания.ИдентификаторЗадания;
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбластьЗаголовкаШаблонаТаблицы(Шаблон)
	МетаданныеОбластьЗаголовокТаблицы = Шаблон.Области.Найти("Шапка");
	
	Если МетаданныеОбластьЗаголовокТаблицы = Неопределено тогда 
		ОбластьЗаголовокТаблицы = Шаблон.ПолучитьОбласть("R1");
	Иначе 
		ОбластьЗаголовокТаблицы = Шаблон.ПолучитьОбласть("Шапка"); 
	КонецЕсли;
	
	Возврат ОбластьЗаголовокТаблицы;
	
КонецФункции

&НаСервере
Процедура ПоказатьИнформационнуюСтрокуПроОбязательныеКолонки()
	
	Если Элементы.СтраницыЗаполненияДанных.ТекущаяСтраница = Элементы.СтраницаВариантЗагрузкаИзФайла Тогда
		ТекстПодсказки = НСтр("ru = 'Для загрузки данных необходимо заполнить таблицу сохранив бланк в файл для заполнения в другой программе.'") + Символы.ПС;
		ТекстПодсказки = ТекстПодсказки  + НСтр("ru = 'После редактирования загрузить готовые данные в таблицу.'") + Символы.ПС;
	Иначе
		ТекстПодсказки = НСтр("ru = 'Для заполнения таблицы необходимо скопировать данные в таблицу из внешнего файла через буфер обмена.'") + Символы.ПС;
	КонецЕсли;
	
	Отбор = Новый Структура("ОбязательнаДляЗаполнения", Истина);
	ОбязательныеКолонки= ИнформацияПоКолонкам.НайтиСтроки(Отбор);
	
	Если ОбязательныеКолонки.Количество() > 0 Тогда 
		СписокКолонок = "";
		
		Для каждого Колонка Из ОбязательныеКолонки Цикл 
			Если ЗначениеЗаполнено(Колонка.Синоним) Тогда
				СписокКолонок = СписокКолонок + ", """ + Колонка.Синоним + """";
			Иначе
				СписокКолонок = СписокКолонок + ", """ + Колонка.ПредставлениеКолонки + """";
			КонецЕсли;
		КонецЦикла;
		СписокКолонок = Сред(СписокКолонок, 3);
		
		Если ОбязательныеКолонки.Количество() = 1 Тогда
			ТекстПодсказки = ТекстПодсказки + НСтр("ru = 'Колонка обязательная для заполнения:'") + " " + СписокКолонок;
		Иначе
			ТекстПодсказки = ТекстПодсказки + НСтр("ru = 'Колонки обязательные для заполнения:'") + " " + СписокКолонок;
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.НадписьПодсказкаДляЗаполнения.Заголовок = ТекстПодсказки;
	Элементы.ПояснениеВариантЗагрузкаИзФайла.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьМакетВТабличныйДокумент(ПараметрыЗагрузкиИзФайла)
	
	МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ИмяОбъектаСопоставления);
	ПараметрыЗагрузкиИзФайла = Обработки.ЗагрузкаДанныхИзФайла.ПараметрыЗагрузкиИзФайла(МетаданныеОбъекта);
	
	МенеджерОбъекта(ИмяОбъектаСопоставления).ОпределитьПараметрыЗагрузкиДанныхИзФайла(ПараметрыЗагрузкиИзФайла);
	
	Если ЗначениеЗаполнено(ПараметрыЗагрузкиИзФайла.Заголовок) Тогда
		ЭтотОбъект.Заголовок = ПараметрыЗагрузкиИзФайла.Заголовок;
		ЭтотОбъект.АвтоЗаголовок = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтандартныеКолонкиВТаблицуСопоставления(ВременнаяТЗ, СтруктураОбъектаСопоставления = Неопределено, ДобавитьИдентификатор = Истина,
		ДобавитьОписаниеОшибки = Истина, ДобавитьРезультатСопоставленияСтроки = Истина,
		ДобавитьСписокНеоднозначностей = Истина)
		
	Если ДобавитьИдентификатор Тогда 
		ВременнаяТЗ.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Число"), НСтр("ru = 'п/п'"));
	КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураОбъектаСопоставления) Тогда 
		Если Не ЗначениеЗаполнено(СтруктураОбъектаСопоставления.Синоним) Тогда
			ЗаголовокКолонки = "";
			Если СтруктураОбъектаСопоставления.ОписаниеТипаОбъектаСопоставления.Типы().Количество() > 1 Тогда 
				ЗаголовокКолонки = "Объекты";
			Иначе
				ЗаголовокКолонки = Строка(СтруктураОбъектаСопоставления.ОписаниеТипаОбъектаСопоставления.Типы()[0]);
			КонецЕсли;
			
		Иначе
			ЗаголовокКолонки = СтруктураОбъектаСопоставления.Синоним;
		КонецЕсли;
		ВременнаяТЗ.Колонки.Добавить("ОбъектСопоставления", СтруктураОбъектаСопоставления.ОписаниеТипаОбъектаСопоставления, ЗаголовокКолонки);
	КонецЕсли;
	Если ДобавитьРезультатСопоставленияСтроки Тогда 
		ВременнаяТЗ.Колонки.Добавить("РезультатСопоставленияСтроки", Новый ОписаниеТипов("Строка"), НСтр("ru = 'Результат'"));
	КонецЕсли;
	Если ДобавитьОписаниеОшибки Тогда
		ВременнаяТЗ.Колонки.Добавить("ОписаниеОшибки", Новый ОписаниеТипов("Строка"), НСтр("ru = 'Причина'"));
	КонецЕсли;

	Если ДобавитьСписокНеоднозначностей Тогда 
		ТипСЗ = Новый ОписаниеТипов("СписокЗначений");
		ВременнаяТЗ.Колонки.Добавить("СписокНеоднозначностей", ТипСЗ, "СписокНеоднозначностей");
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтандартныеКолонкиВМассивРеквизитов(МассивРеквизитов, СтруктураОбъектаСопоставления = Неопределено, ДобавитьИдентификатор = Истина,
		ДобавитьОписаниеОшибки = Истина, ДобавитьРезультатСопоставленияСтроки = Истина,
		ДобавитьСписокНеоднозначностей = Истина)
		
		ТипСтрока = Новый ОписаниеТипов("Строка");
		Если ДобавитьИдентификатор Тогда 
			ТипЧисло = Новый ОписаниеТипов("Число");
			МассивРеквизитов.Добавить(Новый РеквизитФормы("Идентификатор", ТипЧисло, "ТаблицаСопоставленияДанных", "Идентификатор"));
		КонецЕсли;
		Если ЗначениеЗаполнено(СтруктураОбъектаСопоставления) Тогда 
			МассивРеквизитов.Добавить(Новый РеквизитФормы("ОбъектСопоставления", СтруктураОбъектаСопоставления.ОписаниеТипаОбъектаСопоставления, "ТаблицаСопоставленияДанных", ИмяОбъектаСопоставления));
		КонецЕсли;
		
		Если ДобавитьРезультатСопоставленияСтроки Тогда
			МассивРеквизитов.Добавить(Новый РеквизитФормы("РезультатСопоставленияСтроки", ТипСтрока, "ТаблицаСопоставленияДанных", "Результат"));
		КонецЕсли;
		Если ДобавитьОписаниеОшибки Тогда 
			МассивРеквизитов.Добавить(Новый РеквизитФормы("ОписаниеОшибки", ТипСтрока, "ТаблицаСопоставленияДанных", "Причина"));
		КонецЕсли;

	Если ДобавитьСписокНеоднозначностей Тогда 
		ТипСЗ = Новый ОписаниеТипов("СписокЗначений");
		МассивРеквизитов.Добавить(Новый РеквизитФормы("СписокНеоднозначностей", ТипСЗ, "ТаблицаСопоставленияДанных", "СписокНеоднозначностей"));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СоздатьТаблицуСопоставленияПоИнформацииОКолонкахАвто(ОписаниеТипаОбъектаСопоставления)
	
	МассивРеквизитов = Новый Массив;
	
	ВременнаяТЗ = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных");
	ВременнаяТЗ.Колонки.Очистить();
	
	СтруктураОбъектаСопоставления = Новый Структура("ОписаниеТипаОбъектаСопоставления, Синоним", ОписаниеТипаОбъектаСопоставления, "");
	ДобавитьСтандартныеКолонкиВТаблицуСопоставления(ВременнаяТЗ, СтруктураОбъектаСопоставления,, Ложь);
	ДобавитьСтандартныеКолонкиВМассивРеквизитов(МассивРеквизитов, СтруктураОбъектаСопоставления, ,Ложь);
	
	Для каждого Колонка из ИнформацияПоКолонкам Цикл
		ВременнаяТЗ.Колонки.Добавить(Колонка.ИмяКолонки, Колонка.ТипКолонки, Колонка.ПредставлениеКолонки);
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.ИмяКолонки, Колонка.ТипКолонки, "ТаблицаСопоставленияДанных", Колонка.ПредставлениеКолонки));
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов);
	
	ЗначениеВРеквизитФормы(ВременнаяТЗ, "ТаблицаСопоставленияДанных");
	
	Для Каждого Колонка Из ВременнаяТЗ.Колонки Цикл
		НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), Элементы.ТаблицаСопоставленияДанных);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = "ТаблицаСопоставленияДанных." + Колонка.Имя;
		НовыйЭлемент.Заголовок = Колонка.Заголовок;
		НовыйЭлемент.ТолькоПросмотр = Истина;
		Если НовыйЭлемент.Вид <> ВидПоляФормы.ПолеНадписи Тогда
			ОбязательнаДляЗаполнения = ЭтаКолонкаОбязательнаДляЗаполнения(Колонка.Имя);
			НовыйЭлемент.АвтоОтметкаНезаполненного  = ОбязательнаДляЗаполнения;
			НовыйЭлемент.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
			
		КонецЕсли;
		Если Колонка.Имя = "ОбъектСопоставления" Тогда
			НовыйЭлемент.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
			НовыйЭлемент.ЦветФона = ЦветаСтиля.ФонУправляющегоПоля;
			НовыйЭлемент.КартинкаШапки = БиблиотекаКартинок.Изменить;
			НовыйЭлемент.ТолькоПросмотр = Ложь;
			
			НовыйЭлемент.РежимРедактирования = РежимРедактированияКолонки.Вход;
			НовыйЭлемент.КнопкаВыпадающегоСписка = Ложь;
			НовыйЭлемент.КнопкаСоздания = Ложь;
			НовыйЭлемент.РедактированиеТекста = Ложь;
			НовыйЭлемент.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
			НовыйЭлемент.РежимВыбораНезаполненного = РежимВыбораНезаполненного.ПриАктивизации;
		ИначеЕсли Колонка.Имя = "Идентификатор" Тогда
			НовыйЭлемент.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
			НовыйЭлемент.ТолькоПросмотр = Истина;
			НовыйЭлемент.Ширина = 4;
		ИначеЕсли Колонка.Имя = "РезультатСопоставленияСтроки" ИЛИ Колонка.Имя = "СписокНеоднозначностей" Тогда
			НовыйЭлемент.Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьТаблицуСопоставленияПоИнформацииОКолонках()
	
	МассивРеквизитов = Новый Массив;
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ИмяОбъектаСопоставления);
	ОписаниеТипаОбъектаСопоставления = ОписаниеТипаПоМетаданным(ИмяОбъектаСопоставления);
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ТипЧисло = Новый ОписаниеТипов("Число");
	ТипСЗ = Новый ОписаниеТипов("СписокЗначений");
	
	ВременнаяТЗ = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных"); 
	ВременнаяТЗ.Колонки.Очистить();
	
	Синоним = ОбъектМетаданных.Синоним;
	СтруктураОбъектаСопоставления = Новый Структура("ОписаниеТипаОбъектаСопоставления, Синоним", ОписаниеТипаОбъектаСопоставления, Синоним);
	ДобавитьСтандартныеКолонкиВТаблицуСопоставления(ВременнаяТЗ, СтруктураОбъектаСопоставления);
	ДобавитьСтандартныеКолонкиВМассивРеквизитов(МассивРеквизитов, СтруктураОбъектаСопоставления);
	
	Для каждого Колонка из ИнформацияПоКолонкам Цикл 
		ПредставлениеКолонки = ?(ЗначениеЗаполнено(Колонка.Синоним), Колонка.Синоним, Колонка.ПредставлениеКолонки);
		ВременнаяТЗ.Колонки.Добавить(Колонка.ИмяКолонки, Колонка.ТипКолонки,ПредставлениеКолонки);
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Колонка.ИмяКолонки, Колонка.ТипКолонки, "ТаблицаСопоставленияДанных", ПредставлениеКолонки));
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов);
	
	Для Каждого Колонка Из ВременнаяТЗ.Колонки Цикл
		НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), Элементы.ТаблицаСопоставленияДанных);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = "ТаблицаСопоставленияДанных." + Колонка.Имя;
		НовыйЭлемент.Заголовок = Колонка.Заголовок;
		НовыйЭлемент.ТолькоПросмотр = Истина;
		Если НовыйЭлемент.Вид <> ВидПоляФормы.ПолеНадписи Тогда 
			ОбязательнаДляЗаполнения = ЭтаКолонкаОбязательнаДляЗаполнения(Колонка.Имя);
			НовыйЭлемент.АвтоОтметкаНезаполненного  = ОбязательнаДляЗаполнения;
			НовыйЭлемент.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
		КонецЕсли;
		Если Колонка.Имя = "ОбъектСопоставления" Тогда 
			НовыйЭлемент.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
			НовыйЭлемент.ЦветФона = ЦветаСтиля.ФонУправляющегоПоля;
			НовыйЭлемент.КартинкаШапки = БиблиотекаКартинок.Изменить;
			НовыйЭлемент.ТолькоПросмотр = Ложь;
			НовыйЭлемент.РежимРедактирования =  РежимРедактированияКолонки.Непосредственно;
			НовыйЭлемент.РежимВыбораНезаполненного = РежимВыбораНезаполненного.ПриАктивизации;
		ИначеЕсли Колонка.Имя = "Идентификатор" Тогда
			НовыйЭлемент.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
			НовыйЭлемент.ТолькоПросмотр = Истина;
			НовыйЭлемент.Ширина = 4;
		ИначеЕсли Колонка.Имя = "РезультатСопоставленияСтроки" ИЛИ Колонка.Имя = "ОписаниеОшибки" ИЛИ Колонка.Имя = "СписокНеоднозначностей" Тогда
			НовыйЭлемент.Видимость = Ложь;
		КонецЕсли;
		
		Отбор = Новый Структура("ИмяКолонки", Колонка.Имя);
		Колонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
		Если Колонки.Количество() > 0 Тогда 
			НовыйЭлемент.Видимость = Колонки[0].Видимость;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ВременнаяТЗ, "ТаблицаСопоставленияДанных");
КонецПроцедуры

&НаСервере
Процедура СоздатьТаблицуСопоставленияПоИнформацииОКолонкахДляТЧ() 
	
	МассивРеквизитов = Новый Массив;
	ТипСтрока = Новый ОписаниеТипов("Строка");
	ТипЧисло = Новый ОписаниеТипов("Число");
	
	ВременнаяТЗ = РеквизитФормыВЗначение("ТаблицаСопоставленияДанных"); 
	ВременнаяТЗ.Колонки.Очистить();
	
	ДобавитьСтандартныеКолонкиВТаблицуСопоставления(ВременнаяТЗ,,,,, Ложь);
	ДобавитьСтандартныеКолонкиВМассивРеквизитов(МассивРеквизитов,,,,, Ложь);

	ОбязательныеКолонки = Новый Массив;
	РеквизитыТЧ = Метаданные.НайтиПоПолномуИмени(ИмяОбъектаСопоставления).Реквизиты; 
	Для каждого Колонка из РеквизитыТЧ Цикл 
		Если Колонка.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку  Тогда 
			ОбязательныеКолонки.Добавить("тч_" + Колонка.Имя);
		КонецЕсли;
		ВременнаяТЗ.Колонки.Добавить("тч_" + Колонка.Имя, Колонка.Тип, Колонка.Представление());	
		МассивРеквизитов.Добавить(Новый РеквизитФормы("тч_" + Колонка.Имя, Колонка.Тип, "ТаблицаСопоставленияДанных", Колонка.Представление()));
	КонецЦикла;
	
	Для каждого Колонка из ИнформацияПоКолонкам Цикл 
		ВременнаяТЗ.Колонки.Добавить("фл_" + Колонка.ИмяКолонки, ТипСтрока, Колонка.ПредставлениеКолонки);	
		МассивРеквизитов.Добавить(Новый РеквизитФормы("фл_" + Колонка.ИмяКолонки, ТипСтрока, "ТаблицаСопоставленияДанных", Колонка.ПредставлениеКолонки));
	КонецЦикла;
	
	ИзменитьРеквизиты(МассивРеквизитов);
	РеквизитыСозданы = Истина;
	
	ГруппаКолонокЗагружаемыеДанные = Элементы.Добавить("ЗагружаемыеДанные", Тип("ГруппаФормы"), Элементы.ТаблицаСопоставленияДанных);
	ГруппаКолонокЗагружаемыеДанные.Группировка = ГруппировкаКолонок.Горизонтальная; 
	
	Для Каждого Колонка Из ВременнаяТЗ.Колонки Цикл
		
		
		Если Лев(Колонка.Имя, 3) = "тч_" Тогда 
			ГруппаКолонокЗагружаемыеДанныеТЧ = Элементы.Добавить("ЗагружаемыеДанные_" + Колонка.Имя , Тип("ГруппаФормы"), ГруппаКолонокЗагружаемыеДанные);
			ГруппаКолонокЗагружаемыеДанныеТЧ.Группировка = ГруппировкаКолонок.Вертикальная;
			род = ГруппаКолонокЗагружаемыеДанныеТЧ; 
		иначеЕсли Лев(Колонка.Имя, 3) = "фл_" Тогда
			Продолжить;
		иначе 
			род = ГруппаКолонокЗагружаемыеДанные;
		КонецЕсли;
		
		НовыйЭлемент = Элементы.Добавить(Колонка.Имя, Тип("ПолеФормы"), род); 
		
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = "ТаблицаСопоставленияДанных." + Колонка.Имя;
		НовыйЭлемент.Заголовок = Колонка.Заголовок;
		НовыйЭлемент.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
		НовыйЭлемент.Ширина = 5;
		
		Если СтрДлина(Колонка.Имя) > 3 и Лев(Колонка.Имя, 3) = "тч_" Тогда
			Отбор = НОвый Структура("ИмяКолонки", Сред(Колонка.Имя, 4));
			Колонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
			Если Колонки.Количество() > 0 Тогда 
				НовыйЭлемент.Видимость = Колонки[0].Видимость;
			КонецЕсли;
		КонецЕсли;
		
		Если Колонка.Имя = "Идентификатор" Тогда
			НовыйЭлемент.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
			НовыйЭлемент.ТолькоПросмотр = Истина;
			НовыйЭлемент.Ширина = 1;
		ИначеЕсли Колонка.Имя = "РезультатСопоставленияСтроки" ИЛИ Колонка.Имя = "ОписаниеОшибки" Тогда
			НовыйЭлемент.Видимость = Ложь;
		КонецЕсли;
		
		Если ОбязательныеКолонки.Найти(Колонка.Имя) <> Неопределено Тогда 
			НовыйЭлемент.АвтоОтметкаНезаполненного = Истина;
		КонецЕсли;
		
		Если Лев(Колонка.Имя, 3) = "тч_" Тогда 
			ТипКолонки = Метаданные.НайтиПоТипу(Колонка.ТипЗначения.Типы()[0]);
			Если ТипКолонки <> Неопределено И Найти(ТипКолонки.ПолноеИмя(), "Справочник") > 0 тогда 
				НовыйЭлемент.КартинкаШапки = БиблиотекаКартинок.Изменить;
			КонецЕсли;
			
			Отбор = Новый Структура("Ассоциация", Сред(Колонка.Имя, 4));
			КолонкиАссоциация = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
			
			Если КолонкиАссоциация.Количество() = 1 Тогда
				
				КолонкаУровень2 = ВременнаяТЗ.Колонки.Найти("фл_" + КолонкиАссоциация[0].ИмяКолонки);
				Если КолонкаУровень2 <> Неопределено Тогда 
					НовыйЭлемент = Элементы.Добавить(КолонкаУровень2.Имя, Тип("ПолеФормы"), род); 
					НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
					НовыйЭлемент.ПутьКДанным = "ТаблицаСопоставленияДанных." + КолонкаУровень2.Имя;
					ТипКолонки = Метаданные.НайтиПоТипу(КолонкаУровень2.ТипЗначения.Типы()[0]);
					Если ТипКолонки <> Неопределено И Найти(ТипКолонки.ПолноеИмя(), "Справочник") > 0 тогда 
						НовыйЭлемент.Заголовок = Нстр("ru = 'Данные из файла'");
					Иначе
						НовыйЭлемент.Заголовок = " ";
					КонецЕсли;
					НовыйЭлемент.ТолькоПросмотр = Истина;
					НовыйЭлемент.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
				КонецЕсли;
				
			ИначеЕсли КолонкиАссоциация.Количество() > 1 Тогда
				ГруппаКолонокЗагружаемыеДанныеТЧ = Элементы.Добавить("ЗагружаемыеДанные_фл_" + Колонка.Имя , Тип("ГруппаФормы"), род);
				ГруппаКолонокЗагружаемыеДанныеТЧ.Группировка = ГруппировкаКолонок.ВЯчейке;
				род = ГруппаКолонокЗагружаемыеДанныеТЧ;
				
				Префикс = Нстр("ru = 'Данные из файла:'");
				Для каждого КолонкаАссоциация Из КолонкиАссоциация Цикл 
					Колонка2 = ВременнаяТЗ.Колонки.Найти("фл_" + КолонкаАссоциация.ИмяКолонки);
					Если Колонка2 <> Неопределено Тогда 
						НовыйЭлемент = Элементы.Добавить(Колонка2.Имя, Тип("ПолеФормы"), род); 
						НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
						НовыйЭлемент.ПутьКДанным = "ТаблицаСопоставленияДанных." + Колонка2.Имя;
						НовыйЭлемент.Заголовок = Префикс + Колонка2.Заголовок;
						НовыйЭлемент.ТолькоПросмотр = Истина;
						НовыйЭлемент.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
						
						Если СтрДлина(Колонка.Имя) > 3 и Лев(Колонка.Имя, 3) = "фл_" Тогда
						Отбор = НОвый Структура("ИмяКолонки", Сред(Колонка.Имя, 4));
						Колонки = ИнформацияПоКолонкам.НайтиСтроки(Отбор);
							Если Колонки.Количество() > 0 Тогда 
								НовыйЭлемент.Видимость = Колонки[0].Видимость;
							КонецЕсли;
						КонецЕсли;

						
					КонецЕсли;
					Префикс = "";
				КонецЦикла;
			Иначе
				НовыйЭлемент.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		

	КонецЦикла; 
	
	ЗначениеВРеквизитФормы(ВременнаяТЗ, "ТаблицаСопоставленияДанных");
КонецПроцедуры

&НаСервере
Функция ЭтаКолонкаОбязательнаДляЗаполнения(ИмяКолонки)
	Отбор = Новый Структура("ИмяКолонки", ИмяКолонки);
	Колонка =  ИнформацияПоКолонкам.НайтиСтроки(Отбор);
	Если Колонка.Количество()>0 Тогда 
		Возврат Колонка[0].ОбязательнаДляЗаполнения;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

&НаСервере
Процедура ОчисткаШаблонСДанными()
	ОбластьЗаголовка = ШаблонСДанными.ПолучитьОбласть(1, 1, 1, ШаблонСДанными.ШиринаТаблицы);
	ШаблонСДанными.Очистить();
	ШаблонСДанными.Вывести(ОбластьЗаголовка);
КонецПроцедуры

&НаКлиенте
Процедура ПослеСопоставленияНеоднозначностей(Результат, Параметр) Экспорт
	
	Если ТипЗагрузки  = "ТабличнаяЧасть" Тогда
		Если Результат <> Неопределено Тогда
			Строка = ТаблицаСопоставленияДанных.НайтиПоИдентификатору(Параметр.Идентификатор);
			
			Строка["тч_" +  Параметр.Имя] = Результат;
			Строка.ОписаниеОшибки = СтрЗаменить(Строка.ОписаниеОшибки, Параметр.Имя+";", "");
			Строка.РезультатСопоставленияСтроки = ?(СтрДлина(Строка.ОписаниеОшибки) = 0, "СтрокаСопоставлена", "НеСопоставлен");
		КонецЕсли;
	Иначе
		Строка = ТаблицаСопоставленияДанных.НайтиПоИдентификатору(Параметр.Идентификатор);
		Строка.ОбъектСопоставления = Результат;
		Если Результат <> Неопределено Тогда
			Строка.РезультатСопоставленияСтроки = "СтрокаСопоставлена";
			Строка.СписокНеоднозначностей = Неопределено;
		Иначе 
			Если Строка.РезультатСопоставленияСтроки <> "Неоднозначность" Тогда 
				Строка.РезультатСопоставленияСтроки = "НеСопоставлен";
				Строка.СписокНеоднозначностей = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ПоказатьСтатистикуПоСопоставлениюЗагрузкаИзФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСопоставление()
	КоличествоСопоставленныхПоКолонкам = 0;
	СписокКолонок = "";
	ВыполнитьСопоставлениеПоВыбранномуРеквизиту(КоличествоСопоставленныхПоКолонкам, СписокКолонок);
	ПоказатьОповещениеПользователя(Нстр("ru = 'Выполнено сопоставление'"),, Нстр("ru = 'Сопоставлено элементов:'") + " " + Строка(КоличествоСопоставленныхПоКолонкам));
	ПоказатьСтатистикуПоСопоставлениюЗагрузкаИзФайла();
КонецПроцедуры

&НаКлиенте
Функция ВсеДанныеСопоставлены()
	Отбор = Новый Структура("РезультатСопоставленияСтроки", "СтрокаСопоставлена");
	Результат = ТаблицаСопоставленияДанных.НайтиСтроки(Отбор);
	КоличествоСопоставленных = Результат.Количество();
	
	Если ТаблицаСопоставленияДанных.Количество() = КоличествоСопоставленных Тогда 
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция СтатисткаСопоставления()
	Отбор = Новый Структура("РезультатСопоставленияСтроки", "СтрокаСопоставлена");
	Результат = ТаблицаСопоставленияДанных.НайтиСтроки(Отбор);
	КоличествоСопоставленных = Результат.Количество();
	
	Отбор = Новый Структура("РезультатСопоставленияСтроки", "Неоднозначность");
	Результат = ТаблицаСопоставленияДанных.НайтиСтроки(Отбор);
	КолоВоНеоднозначных  = Результат.Количество();
	
	КоличествоНеСопоставленных = ТаблицаСопоставленияДанных.Количество() - КоличествоСопоставленных;
	
	Результат = Новый Структура;
	Результат.Вставить("Всего", ТаблицаСопоставленияДанных.Количество());
	Результат.Вставить("Сопоставленных", КоличествоСопоставленных);
	Результат.Вставить("Неоднозначных", КолоВоНеоднозначных);
	Результат.Вставить("Несопоставленных", КоличествоНеСопоставленных);
	Результат.Вставить("НеНайдено", КоличествоНеСопоставленных - КолоВоНеоднозначных);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьСтатистикуПоСопоставлениюЗагрузкаИзФайла()
	
	Статистика = СтатисткаСопоставления();
	
	ДанныеОСопоставление = СтатисткаСопоставления();
	
	ТекстВсе = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Все (%1)'"), Статистика.Всего);
	
	Элементы.СоздаватьЕслиНеСопоставлено.Заголовок = НСтр("ru = 'Несопоставленные ('") + Статистика.НеСопоставленных + ")";
	Элементы.ОбновлятьСуществующие.Заголовок = НСтр("ru = 'Сопоставленные элементы ('") + Строка(Статистика.Сопоставленных) + ")";
	
	СписокВыбора = Элементы.ФильтрТаблицаСопоставления.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить("Все", ТекстВсе, Истина);
	СписокВыбора.Добавить("Несопоставленные", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Несопоставленные (%1 из %2)'"), Статистика.НеСопоставленных, Статистика.Всего));
	СписокВыбора.Добавить("Сопоставленные", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Сопоставленные (%1 из %2)'"), Статистика.Сопоставленных, Статистика.Всего));
	СписокВыбора.Добавить("Неоднозначные", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Неоднозначные (%1 из %2)'"), Статистика.Неоднозначных, Статистика.Всего));
	
	Если Статистика.Неоднозначных > 0 Тогда 
		Элементы.ОписаниеНеоднозначность.Видимость=Истина;
		Элементы.ОписаниеНеоднозначность.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '(неоднозначностей: %1)'"), Статистика.Неоднозначных);
	Иначе 
		Элементы.ОписаниеНеоднозначность.Видимость=Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ФильтрТаблицаСопоставления) Тогда 
		ФильтрТаблицаСопоставления = "Несопоставленные";
	КонецЕсли;
	
	Если ТипЗагрузки = "ВставкаИзБуфераОбмена" Тогда
		УстановитьФильтрациюТаблицыСопоставления();
	Иначе
		УстановитьФильтрациюТаблицыСопоставления();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ГрупповоеИзменениеРеквизитовНаСервере(ВерхняяПозиция, НижняяПозиция)
	МассивСсылок = новый Массив;
	Для Позиция = ВерхняяПозиция По НижняяПозиция Цикл 
		Ячейка = ТаблицаОтчет.ПолучитьОбласть(Позиция, 2, Позиция, 2);	
		Если ЗначениеЗаполнено(Ячейка.ТекущаяОбласть.Расшифровка) Тогда 
			МассивСсылок.Добавить(Ячейка.ТекущаяОбласть.Расшифровка);
		КонецЕсли;
	КонецЦикла;
	Возврат МассивСсылок;
КонецФункции

#Область РаботаСФайлами

&НаКлиенте
Процедура ПередатьФайлНаСервер(Результат, АдресВременногоХранилища, ИмяФайла, Параметр) Экспорт
	
	Если Результат = Истина Тогда
		Расширение = ОбщегоНазначенияКлиентСервер.РасширениеБезТочки(
		ОбщегоНазначенияКлиентСервер.ПолучитьРасширениеИмениФайла(ИмяФайла));
		Если Расширение = "csv" ИЛИ Расширение = "xlsx" ИЛИ Расширение = "mxl" Тогда
			
			ФоновоеЗадание = Ложь;
			ЗагрузитьФайлСДаннымиВТабличныйДокументНаСервере(АдресВременногоХранилища, Расширение, ФоновоеЗадание);
			Если ФоновоеЗадание Тогда
				ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчика);
				ПодключитьОбработчикОжидания("ФоновоеЗаданиеЗагрузкаФайлаНаКлиенте", 1, Истина);
				ПараметрыОбработчика.МаксимальныйИнтервал = 5;
				ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтотОбъект, ФоновоеЗаданиеИдентификатор);
			Иначе
				Далее(Неопределено);
			КонецЕсли;
		Иначе
			ПоказатьПредупреждение(,НСтр("ru ='Не получилось загрузить данные из этого файла. Убедитесь в корректности данных в файле.'"));        
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораРасширенияФайла(Результат, Параметр) Экспорт
	АдресВоВременномХранилище = ЭтотОбъект.УникальныйИдентификатор;
	СохранитьШаблонВоВременноеХранилище(Результат, АдресВоВременномХранилище);
	ПолучитьФайл(АдресВоВременномХранилище, ИмяОбъектаСопоставления + "." + Результат, Истина);
КонецПроцедуры

&НаСервере
Процедура СохранитьШаблонВоВременноеХранилище(РасширениеФайла, АдресВоВременномХранилище)
	
	ИмяФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
	Если РасширениеФайла = "csv" тогда 
		СохранитьТаблицуВCSVФайл(ИмяФайла);
	ИначеЕсли РасширениеФайла = "xlsx" тогда 
		ШаблонСДанными.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.xlsx);
	Иначе 
		ШаблонСДанными.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.mxl);
	КонецЕсли;
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайла);
	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, АдресВоВременномХранилище);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьИмяФайлаДляОбъектаМетаданных(ИмяОбъектаМетаданных)
	МетаданныеСправочника = Метаданные.НайтиПоПолномуИмени(ИмяОбъектаМетаданных);
	
	Если МетаданныеСправочника <> Неопределено Тогда 
		ИмяФайла = СокрЛП(МетаданныеСправочника.Синоним);
		Если СтрДлина(ИмяФайла) = 0 Тогда 
			ИмяФайла = ИмяОбъектаМетаданных;	
		КонецЕсли;
	Иначе
		ИмяФайла = ИмяОбъектаМетаданных;
	КонецЕсли;
	
	ИмяФайла = СтрЗаменить(ИмяФайла,":","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"*","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"\","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"/","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"&","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"<","");
	ИмяФайла = СтрЗаменить(ИмяФайла,">","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"|","");
	ИмяФайла = СтрЗаменить(ИмяФайла,"""","");
	
	Возврат ИмяФайла;
КонецФункции 

&НаКлиенте
Процедура ПолучитьПутьКФайлуНачалоВыбора(РежимДиалога, ПутьКФайлу, ИмяФайла = "")
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалога);
	
	ДиалогВыбораФайла.Фильтр                      = НСтр("ru='Книга Excel 2007 (*.xlsx)|*.xlsx|Текстовый документ c разделителями (*.csv)|*.csv|Табличный документ (*.mxl)|*.mxl'");
	ДиалогВыбораФайла.Заголовок                   = Заголовок;
	ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;
	ДиалогВыбораФайла.Расширение                  = "csv";
	ДиалогВыбораФайла.ИндексФильтра               = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла              = ИмяФайла;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ПутьКФайлу = ДиалогВыбораФайла.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаПроОтменитьСопоставление(Результат, Параметр) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Для каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
			СтрокаТаблицы.ОбъектСопоставления = Неопределено;
			СтрокаТаблицы.РезультатСопоставленияСтроки = "НеСопоставлен";
			СтрокаТаблицы.СписокНеоднозначностей = Неопределено;
			СтрокаТаблицы.ОписаниеОшибки = "";
		КонецЦикла;
		ПоказатьСтатистикуПоСопоставлениюЗагрузкаИзФайла();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьФайлСДаннымиВТабличныйДокументНаСервере(АдресВременногоХранилища, Расширение, ФоновоеЗадание = Ложь)
	ИмяВременногоФайла=ПолучитьИмяВременногоФайла(Расширение);
	ДвоичныеДанные = ПолучитьИЗВременногоХранилища(АдресВременногоХранилища);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ОчисткаШаблонСДанными();

	ПараметрыВызоваСервера = Новый Структура();
	ПараметрыВызоваСервера.Вставить("Расширение", Расширение);
	ПараметрыВызоваСервера.Вставить("ШаблонСДанными", ШаблонСДанными);
	ПараметрыВызоваСервера.Вставить("ИмяВременногоФайла", ИмяВременногоФайла);
	ТаблицаИнформацияПоКолонкам = РеквизитФормыВЗначение("ИнформацияПоКолонкам");
	ПараметрыВызоваСервера.Вставить("ИнформацияПоКолонкам", ТаблицаИнформацияПоКолонкам);
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Обработки.ЗагрузкаДанныхИзФайла.ЗагрузитьФайлВТаблицу(ПараметрыВызоваСервера, АдресВременногоХранилища);
		ШаблонСДанными = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	Иначе
		РезультатФоновогоЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(УникальныйИдентификатор, 
		"Обработки.ЗагрузкаДанныхИзФайла.ЗагрузитьФайлВТаблицу",
		ПараметрыВызоваСервера, 
		НСтр("ru = 'Подсистема ЗагрузкаДанныхИзФайла: Выполнение серверного метода загрузка данных из файла'"));
		
		Если РезультатФоновогоЗадания.ЗаданиеВыполнено Тогда
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
			ШаблонСДанными = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресХранилища);
		Иначе 
			ФоновоеЗадание = Истина;
			ФоновоеЗаданиеИдентификатор  = РезультатФоновогоЗадания.ИдентификаторЗадания;
			ФоновоеЗаданиеАдресХранилища = РезультатФоновогоЗадания.АдресХранилища;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВызоваФормыИзменитьБланк(Результат, Параметр) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат.Количество() > 0 Тогда
			Для Позиция = 0 По Результат.Количество() - 1 Цикл
				НоваяКолонка = Результат.Получить(Позиция);
				Фильтр = Новый Структура("ИмяКолонки", НоваяКолонка.ИмяКолонки);
				Колонки = ИнформацияПоКолонкам.НайтиСтроки(Фильтр);
				Если Колонки.Количество() >= 1 Тогда
					Колонка = Колонки[0];
					Колонка.Видимость = НоваяКолонка.Видимость;
					Колонка.Синоним = НоваяКолонка.Синоним;
					Колонка.Позиция = НоваяКолонка.Позиция;
				КонецЕсли;
			КонецЦикла;
		Иначе
			СформироватьМакетПоТипуЗагрузки();
		КонецЕсли;
		ИзменитьБланкПоИнформацииПоКолонкам();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьБланкПоИнформацииПоКолонкам(Бланк = Неопределено)

	СПримечаниями = Ложь;
	Если Бланк = Неопределено Тогда 
		Бланк = ШаблонСДанными;
		СПримечаниями = Истина;
	КонецЕсли;
	
	ТаблицаКолонок = РеквизитФормыВЗначение("ИнформацияПоКолонкам");
	ХранилищеСистемныхНастроек.Сохранить("ЗагрузкаДанныхИзФайла", ИмяОбъектаСопоставления, ТаблицаКолонок, НСтр("ru = 'Настройки бланка'"));
	Бланк.Очистить();
	Шапка = Обработки.ЗагрузкаДанныхИзФайла.ШапкаБланкаДляЗаполненияПоИнформацииПоКолонкам(ТаблицаКолонок, СПримечаниями);
	Бланк.Вывести(Шапка);
	ПоказатьИнформационнуюСтрокуПроОбязательныеКолонки();
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти





