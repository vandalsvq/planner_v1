&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Объект.РеквизитОбъекта = НСтр("ru = 'Тестовое значение реквизита'");
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////
// Вывод сообщения связанного с реквизитом объекта

&НаКлиенте
Процедура ПоказатьСообщениеПоляФормыСвязанногоСРеквизитомОбъектаНаКлиенте(Команда)
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Пример сообщения, связанного с реквизитом объекта (Объект.РеквизитОбъекта).'"), ,
		"РеквизитОбъекта",
		"Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщениеПоляФормыСвязанногоСРеквизитомОбъектаНаСервере(Команда)
	
	ПоказатьСообщениеПоляФормыСвязанногоСРеквизитомОбъектаСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьСообщениеПоляФормыСвязанногоСРеквизитомОбъектаСервер()
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Пример сообщения, связанного с реквизитом объекта (Объект.РеквизитОбъекта).'"), ,
		"РеквизитОбъекта",
		"Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ПосмотретьКодВариантПервый(Команда)
	
	ОткрытьФорму("Обработка._ДемоВыводСообщенийПользователю.Форма.ПримерКода",
		Новый Структура("ПримерКода", "ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		|	НСтр(""ru = 'Пример сообщения связанного с реквизитом объекта.'""),
		|	,
		|	""РеквизитОбъекта"",
		|	""Объект"");"));
	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////
// Вывод сообщения связанного с реквизитом формы

&НаКлиенте
Процедура ПоказатьСообщениеПоляФормыСвязанногоСРеквизитомФормыНаКлиенте(Команда)
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Пример сообщения, связанного с реквизитом формы (РеквизитФормы).'"), ,
		"РеквизитФормы");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщениеПоляФормыСвязанногоСРеквизитомФормыНаСервере(Команда)
	
	ПоказатьСообщениеПоляФормыСвязанногоСРеквизитомФормыСервер();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьСообщениеПоляФормыСвязанногоСРеквизитомФормыСервер()
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Пример сообщения, связанного с реквизитом формы (РеквизитФормы).'"), ,
		"РеквизитФормы");
	
КонецПроцедуры

&НаКлиенте
Процедура ПосмотретьКодВариантВторой(Команда)
	
	ОткрытьФорму("Обработка._ДемоВыводСообщенийПользователю.Форма.ПримерКода",
		Новый Структура("ПримерКода", "ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		|	НСтр(""ru = 'Пример сообщения, связанного с реквизитом формы (РеквизитФормы).'""),
		|	,
		|	""РеквизитФормы"");"));
		
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////
// Вывод сообщения связанного с реквизитом объекта информационной базы

&НаКлиенте
Процедура ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазы(Команда)
	
	Если НЕ ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазСервер() Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазыЗавершение", ЭтотОбъект);
		ПоказатьПредупреждение(ОписаниеОповещения, НСтр("ru = 'Для выполнения теста необходимо предварительно создать хотя бы один документ _ДемоСчетНаОплатуПокупателю.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазыЗавершение(ДополнительныеПараметры) Экспорт
	
	ОткрытьФорму("Документ._ДемоСчетНаОплатуПокупателю.Форма.ФормаСписка");
	
КонецПроцедуры

&НаСервере
Функция ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазСервер()
	
	Перем РезультатПустой;
	Ссылка = ПолучитьСсылку(РезультатПустой);
	Если РезультатПустой Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Пример сообщения, связанного с реквизитом ""Ответственный"" документа _ДемоСчетНаОплатуПокупателю 
			| (объект информационной базы).'"),
		Ссылка.ПолучитьОбъект(), "Ответственный");
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПосмотретьКодВариантТретий(Команда)
	
	ОткрытьФорму("Обработка._ДемоВыводСообщенийПользователю.Форма.ПримерКода",
		Новый Структура("ПримерКода", "ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		|	НСтр(""ru = 'Пример сообщения, связанного с реквизитом ""Ответственный"" документа _ДемоСчетНаОплатуПокупателю 
		|		|(объект информационной базы).'""),
		|	Ссылка.ПолучитьОбъект(),
		|	""Ответственный"");"));
		
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// Вывод сообщения связанного с реквизитом объекта информационной базы по ссылке

&НаКлиенте
Процедура ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазыПоСсылкеНаКлиенте(Команда)
	
	Перем РезультатПустой;
	
	Ссылка = ПолучитьСсылку(РезультатПустой);
	
	Если РезультатПустой Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не найдено доступных объектов.'"));
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Пример сообщения, связанного с реквизитом ""Ответственный"" документа _ДемоСчетНаОплатуПокупателю
				| (объект информационной базы).'"),
			Ссылка,
			"Ответственный");
			
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазыПоСсылкеНаСервере(Команда)
	
	Если НЕ ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазыПоСсылкеСервер() Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазыПоСсылкеНаСервереЗавершение", ЭтотОбъект);
		ПоказатьПредупреждение(ОписаниеОповещения, НСтр("ru = 'Для выполнения теста необходимо предварительно создать хотя бы один документ _ДемоСчетНаОплатуПокупателю.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазыПоСсылкеНаСервереЗавершение(ДополнительныеПараметры) Экспорт
	
	ОткрытьФорму("Документ._ДемоСчетНаОплатуПокупателю.Форма.ФормаСписка");
	
КонецПроцедуры

&НаСервере
Функция ПоказатьСообщениеСвязанноеСРеквизитомОбъектаИнформационнойБазыПоСсылкеСервер()
	
	Перем РезультатПустой;
	
	Ссылка = ПолучитьСсылку(РезультатПустой);
	
	Если РезультатПустой Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Пример сообщения, связанного с реквизитом ""Ответственный"" документа _ДемоСчетНаОплатуПокупателю
				| (ссылка на объект).'"),
			Ссылка,
			"Ответственный");
			
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПосмотретьКодВариантЧетвертый(Команда)
	
	ОткрытьФорму("Обработка._ДемоВыводСообщенийПользователю.Форма.ПримерКода",
		Новый Структура("ПримерКода", "ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		|	НСтр(""ru = 'Тестовое сообщение об ошибке, связанное с реквизитом ""Ответственный"" документа _ДемоСчетНаОплатуПокупателю
		|		|(ссылка на объект).'""),
		|	Ссылка,
		|	""Ответственный"")"));
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСсылку(РезультатПустой)
	
	ТекстЗапроса = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	Док.Ссылка КАК Ссылка
			|ИЗ
			|	Документ._ДемоСчетНаОплатуПокупателю КАК Док";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		РезультатПустой = Истина;
		Возврат Документы._ДемоСчетНаОплатуПокупателю.ПустаяСсылка();
	КонецЕсли;
	
	РезультатПустой = Ложь;
	Возврат Результат.Выгрузить()[0].Ссылка
	
КонецФункции
