////////////////////////////////////////////////////////////////////////////////
// Подсистема "Варианты отчетов" (сервер)
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Формирует список разделов, в которых размещены команды вызова панели отчетов.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Информация о разделах.
//       * Ссылка        - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//       * Метаданные    - ОбъектМетаданных
//       * Имя           - Строка
//       * Представление - Строка
//
Функция ИспользуемыеРазделы() Экспорт
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Ссылка",          Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	Результат.Колонки.Добавить("Метаданные",      Новый ОписаниеТипов("ОбъектМетаданных"));
	Результат.Колонки.Добавить("Имя",             ОписаниеТиповСтрока());
	Результат.Колонки.Добавить("Представление",   ОписаниеТиповСтрока());
	Результат.Колонки.Добавить("ЗаголовокПанели", ОписаниеТиповСтрока());
	
	СписокРазделов = Новый СписокЗначений;
	
	ВариантыОтчетовПереопределяемый.ОпределитьРазделыСВариантамиОтчетов(СписокРазделов);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.НастройкиПрограммы") Тогда
		МодульОбработкиПанельАдминистрированияБСП = ОбщегоНазначения.ОбщийМодуль("Обработки.ПанельАдминистрированияБСП");
		МодульОбработкиПанельАдминистрированияБСП.ПриОпределенииРазделовСВариантамиОтчетов(СписокРазделов);
	КонецЕсли;
	
	Для Каждого ЭлементСписка Из СписокРазделов Цикл
		РазделМетаданные = ЭлементСписка.Значение;
		Если ЗначениеЗаполнено(ЭлементСписка.Представление) Тогда
			ШаблонЗаголовка = ЭлементСписка.Представление;
		Иначе
			ШаблонЗаголовка = НСтр("ru = 'Отчеты раздела ""%1""'");
		КонецЕсли;
		
		Строка = Результат.Добавить();
		Строка.Ссылка          = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(РазделМетаданные);
		Строка.Метаданные      = РазделМетаданные;
		Строка.Имя             = РазделМетаданные.Имя;
		Строка.Представление   = РазделМетаданные.Представление();
		Строка.ЗаголовокПанели = СтрЗаменить(ШаблонЗаголовка, "%1", Строка.Представление);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Получает ссылку варианта отчета по набору ключевых реквизитов.
//
// Параметры:
//   Отчет        - Соответствует реквизиту справочника - Полное имя или ссылка отчета.
//   КлючВарианта - Соответствует реквизиту справочника - Имя варианта отчета.
//
// Возвращаемое значение: 
//   * СправочникСсылка.ВариантыОтчетов - Когда вариант найден.
//   * Неопределено                     - Когда вариант не найден.
//
Функция ПолучитьСсылку(Отчет, КлючВарианта) Экспорт
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВариантыОтчетов.Ссылка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Отчет = &Отчет
	|	И ВариантыОтчетов.КлючВарианта = &КлючВарианта";
	Запрос.УстановитьПараметр("Отчет", Отчет);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Формирует массив отчетов, доступных текущему пользователю.
//
// Возвращаемое значение:
//   Массив - Ссылки отчетов, доступных текущему пользователю.
//       Типы значений соответствуют типу реквизита Отчет справочника ВариантыОтчетов.
//
// Описание:
//   Данный массив следует использовать во всех запросах к таблице
//   справочника "ВариантыОтчетов" как отбор по реквизиту "Отчет",
//   кроме случаев выборки вариантов из внешних отчетов.
//
Функция ОтчетыТекущегоПользователя() Экспорт
	
	ДоступныеОтчеты = ВариантыОтчетовПовтИсп.ДоступныеОтчеты();
	
	// Дополнить массив ссылкам дополнительных отчетов, доступных текущему пользователю.
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
		МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
		МодульДополнительныеОтчетыИОбработки.ПриДобавленииДополнительныхОтчетовДоступныхТекущемуПользователю(ДоступныеОтчеты);
	КонецЕсли;
	
	Возврат ДоступныеОтчеты;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Поддержка переопределяемых модулей

// Вызывает модуль менеджера отчета для заполнения его настроек.
//   Для использования в ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//   см. описание функции ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Параметры:
//   Настройки - Коллекция - Передается "как есть" из процедуры НастроитьВариантыОтчетов.
//   ОтчетМетаданные - ОбъектМетаданных - Метаданные отчета.
//
// Важно:
//   Для использования в модуле менеджера отчета должна быть размещена экспортная процедура по шаблону:
//	// Настройки размещения в панели отчетов.
//	//
//	// Параметры:
//	//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
//	//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации()
//	//   НастройкиОтчета - СтрокаДереваЗначений - Настройки размещения всех вариантов отчета.
//	//       см. "Реквизиты для изменения" функции ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации().
//	//
//	// Описание:
//	//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//	//
//	// Вспомогательные методы:
//	//   НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//	//   ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь); // Отчет поддерживает только этот режим.
//	//
//	Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
//		НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//		НастройкиВарианта.Описание = НСтр("ru = '<Описание>'");
//	КонецПроцедуры
//
Процедура НастроитьОтчетВМодулеМенеджера(Настройки, ОтчетМетаданные) Экспорт
	НастройкиОтчета = ОписаниеОтчета(Настройки, ОтчетМетаданные);
	Отчеты[ОтчетМетаданные.Имя].НастроитьВариантыОтчета(Настройки, НастройкиОтчета);
КонецПроцедуры

// Формирует описание настроек отчета.
//   Для использования в ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//   см. описание функции ДеревоНастроекВариантовОтчетовКонфигурации().
//
// Параметры:
//   ДеревоВариантов - ДеревоЗначений - Используется для описания настроек отчетов и вариантов
//       См. описание функции ДеревоНастроекВариантовОтчетовКонфигурации().
//   ОтчетЗначениеИлиМетаданные - ОбъектМетаданных, СправочникСсылка.ИдентификаторыОбъектовМетаданных -
//       Метаданные или ссылка отчета.
//
// Возвращаемое значение:
//   СтрокаДереваЗначений - Отчет.
//       см. "Реквизиты для изменения" функции ДеревоНастроекВариантовОтчетовКонфигурации().
//
Функция ОписаниеОтчета(ДеревоВариантов, ОтчетЗначениеИлиМетаданные) Экспорт
	ЭтоМетаданные = (ТипЗнч(ОтчетЗначениеИлиМетаданные) = Тип("ОбъектМетаданных"));
	Если ЭтоМетаданные Тогда
		СтрокаОтчет = ДеревоВариантов.Строки.Найти(ОтчетЗначениеИлиМетаданные, "Метаданные", Ложь);
	Иначе
		СтрокаОтчет = ДеревоВариантов.Строки.Найти(ОтчетЗначениеИлиМетаданные, "Отчет", Ложь);
	КонецЕсли;
	
	Если СтрокаОтчет = Неопределено Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка получения описания отчета ""%1"",
			|он не подключен к подсистеме ""%2""'"),
			СтрокаОтчет.Метаданные.Имя,
			ВариантыОтчетовКлиентСервер.НаименованиеПодсистемы(""));
	КонецЕсли;
	
	Возврат СтрокаОтчет;
КонецФункции

// Формирует описание настроек варианта отчета.
//   Для использования в ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Параметры:
//   ДеревоВариантов - ДеревоЗначений - Используется для описания настроек отчетов и вариантов
//       см. описание функции ДеревоНастроекВариантовОтчетовКонфигурации().
//   ОтчетСтрокаДереваИлиЗначениеИлиМетаданные - СтрокаДерева, ОбъектМетаданных,
//       СправочникСсылка.ИдентификаторыОбъектовМетаданных - Описание настроек, метаданные или ссылка отчета.
//   КлючВарианта - Строка - Имя варианта отчета как оно задано в схеме компоновки данных.
//
// Возвращаемое значение: 
//   СтрокаДереваЗначений - Вариант.
//       см. "Реквизиты для изменения" функции ДеревоНастроекВариантовОтчетовКонфигурации().
//
Функция ОписаниеВарианта(ДеревоВариантов, ОтчетСтрокаДереваИлиЗначениеИлиМетаданные, КлючВарианта) Экспорт
	Если ТипЗнч(ОтчетСтрокаДереваИлиЗначениеИлиМетаданные) = Тип("СтрокаДереваЗначений") Тогда
		СтрокаОтчет = ОтчетСтрокаДереваИлиЗначениеИлиМетаданные;
	Иначе
		СтрокаОтчет = ОписаниеОтчета(ДеревоВариантов, ОтчетСтрокаДереваИлиЗначениеИлиМетаданные);
	КонецЕсли;
	
	Если КлючВарианта = "" Тогда
		СтрокаВариант = СтрокаОтчет.ОсновнойВариант;
	Иначе
		СтрокаВариант = СтрокаОтчет.Строки.Найти(КлючВарианта, "КлючВарианта", Ложь);
	КонецЕсли;
	
	Если СтрокаВариант = Неопределено Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Вариант ""%1"" не найден для отчета ""%2"".'"),
			КлючВарианта,
			СтрокаОтчет.Метаданные.Имя);
	КонецЕсли;
	
	ЗаполнитьОписаниеСтрокиВарианта(СтрокаВариант, СтрокаОтчет);
	
	Возврат СтрокаВариант;
КонецФункции

// Устанавливает режим вывода Отчетов и Вариантов в панелях отчетов.
//
// Параметры:
//   Настройки - Передается "как есть" из процедуры НастроитьВариантыОтчетов.
//   ОтчетИлиПодсистема - СтрокаДереваЗначений, ОбъектМетаданных: Отчет, ОбъектМетаданных: Подсистема -
//       Описание отчета или подсистемы, для которой устанавливается режим вывода.
//       Когда передана подсистема режим устанавливается рекурсивно для всех отчетов из ее состава.
//   ВыводитьОтчетыВместоВариантов - Булево, Строка - Режим вывода гиперссылок этого отчета в панели отчетов:
//       - Истина, "ПоОтчетам" - Варианты отчета по умолчанию скрыты, а отчет (вариант с пустым ключем) включен и видим.
//       - Ложь, "ПоВариантам" - Варианты отчета по умолчанию видимы, а отчет отключен. Как в версии 2.2.1 и ранее.
//
Процедура УстановитьРежимВыводаВПанеляхОтчетов(Настройки, ОтчетИлиПодсистема, ВыводитьОтчетыВместоВариантов) Экспорт
	Если ТипЗнч(ВыводитьОтчетыВместоВариантов) <> Тип("Булево") Тогда
		ВыводитьОтчетыВместоВариантов = (ВыводитьОтчетыВместоВариантов = ВРег("ПоОтчетам"));
	КонецЕсли;
	Если ТипЗнч(ОтчетИлиПодсистема) = Тип("СтрокаДереваЗначений")
		Или Метаданные.Отчеты.Содержит(ОтчетИлиПодсистема) Тогда
		УстановитьРежимВыводаОтчетаВПанеляхОтчетов(Настройки, ОтчетИлиПодсистема, ВыводитьОтчетыВместоВариантов);
	Иначе
		Подсистемы = Новый Массив;
		Подсистемы.Добавить(ОтчетИлиПодсистема);
		Количество = 1;
		ОбработанныеОбъекты = Новый Массив;
		Пока Количество > 0 Цикл
			Количество = Количество - 1;
			Подсистема = Подсистемы[0];
			Подсистемы.Удалить(0);
			Для Каждого ВложеннаяПодсистема Из Подсистема.Подсистемы Цикл
				Количество = Количество + 1;
				Подсистемы.Добавить(ВложеннаяПодсистема);
			КонецЦикла;
			Для Каждого ОбъектМетаданных Из ОтчетИлиПодсистема.Состав Цикл
				Если ОбработанныеОбъекты.Найти(ОбъектМетаданных) = Неопределено Тогда
					ОбработанныеОбъекты.Добавить(ОбъектМетаданных);
					Если Метаданные.Отчеты.Содержит(ОбъектМетаданных) Тогда
						УстановитьРежимВыводаОтчетаВПанеляхОтчетов(Настройки, ОбъектМетаданных, ВыводитьОтчетыВместоВариантов);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Для вызова из форм отчетов.

// Обновляет состав справочника ПользовательскиеНастройкиОтчетов после сохранения новой настройки.
//   Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Настройки - ОбъектМетаданных - Передается "как есть" из процедуры ПриСохраненииПользовательскихНастроекНаСервере.
//
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Форма, Настройки) Экспорт
	
	РеквизитыФормы = Новый Структура("КлючОбъекта, ВариантСсылка");
	ЗаполнитьЗначенияСвойств(РеквизитыФормы, Форма);
	Если Не ЗначениеЗаполнено(РеквизитыФормы.КлючОбъекта)
		Или Не ЗначениеЗаполнено(РеквизитыФормы.ВариантСсылка) Тогда
		ОтчетОбъект = Форма.РеквизитФормыВЗначение("Отчет");
		ОтчетМетаданные = ОтчетОбъект.Метаданные();
		Если Не ЗначениеЗаполнено(РеквизитыФормы.КлючОбъекта) Тогда
			РеквизитыФормы.КлючОбъекта = ОтчетМетаданные.ПолноеИмя();
		КонецЕсли;
		Если Не ЗначениеЗаполнено(РеквизитыФормы.ВариантСсылка) Тогда
			ОтчетИнформация = СформироватьИнформациюОбОтчетеПоПолномуИмени(РеквизитыФормы.КлючОбъекта);
			Если НЕ ЗначениеЗаполнено(ОтчетИнформация.ТекстОшибки) Тогда
				ОтчетСсылка = ОтчетИнформация.Отчет;
			Иначе
				ОтчетСсылка = РеквизитыФормы.КлючОбъекта;
			КонецЕсли;
			РеквизитыФормы.ВариантСсылка = ПолучитьСсылку(ОтчетСсылка, Форма.КлючТекущегоВарианта);
		КонецЕсли;
	КонецЕсли;
	
	КлючНастроек = РеквизитыФормы.КлючОбъекта + "/" + Форма.КлючТекущегоВарианта;
	СписокНастроек = ХранилищеПользовательскихНастроекОтчетов.ПолучитьСписок(КлючНастроек);
	КоличествоНастроек = СписокНастроек.Количество();
	ПользовательСсылка = Пользователи.АвторизованныйПользователь();
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	*
	|ИЗ
	|	Справочник.ПользовательскиеНастройкиОтчетов КАК ПользовательскиеНастройкиОтчетов
	|ГДЕ
	|	ПользовательскиеНастройкиОтчетов.Вариант = &ВариантСсылка
	|	И ПользовательскиеНастройкиОтчетов.Пользователь = &ПользовательСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПользовательскиеНастройкиОтчетов.ПометкаУдаления";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВариантСсылка", РеквизитыФормы.ВариантСсылка);
	Запрос.УстановитьПараметр("ПользовательСсылка", ПользовательСсылка);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ЭлементСписка = СписокНастроек.НайтиПоЗначению(Выборка.КлючПользовательскойНастройки);
		
		ПометкаУдаления = (ЭлементСписка = Неопределено);
		Если ПометкаУдаления <> Выборка.ПометкаУдаления Тогда
			НастройкаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			НастройкаОбъект.УстановитьПометкуУдаления(ПометкаУдаления);
		КонецЕсли;
		Если ПометкаУдаления Тогда
			Если КоличествоНастроек = 0 Тогда
				Прервать;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		Если Выборка.Наименование <> ЭлементСписка.Представление Тогда
			НастройкаОбъект = Выборка.Ссылка.ПолучитьОбъект();
			НастройкаОбъект.Наименование = ЭлементСписка.Представление;
			НастройкаОбъект.Записать();
		КонецЕсли;
		
		СписокНастроек.Удалить(ЭлементСписка);
		КоличествоНастроек = КоличествоНастроек - 1;
	КонецЦикла;
	
	Для Каждого ЭлементСписка Из СписокНастроек Цикл
		НастройкаОбъект = Справочники.ПользовательскиеНастройкиОтчетов.СоздатьЭлемент();
		НастройкаОбъект.Наименование                  = ЭлементСписка.Представление;
		НастройкаОбъект.КлючПользовательскойНастройки = ЭлементСписка.Значение;
		НастройкаОбъект.Вариант                       = РеквизитыФормы.ВариантСсылка;
		НастройкаОбъект.Пользователь                  = ПользовательСсылка;
		НастройкаОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Для вызова из события "ПриДобавленииОбработчиковОбновления" конфигурации.

// Полное обновление индекса поиска вариантов отчетов.
//   Следует вызывать из процедуры ПриДобавленииОбработчиковОбновления конфигурации.
//   Внимание! Эта процедура должна вызываться только 1 раз - из модуля конечного прикладного решения.
//     Не предназначена для вызова из библиотек.
//
// Параметры:
//   Обработчики - Коллекция - Передается "как есть" из вызываемой процедуры.
//   Версия - Строка - Версия конфигурации, при переходе на которую
//       необходимо полностью обновить индекс поиска.
//     Рекомендуется указывать последнюю функциональную версию,
//       при обновлении на которую были внесены изменения
//       в представления объектов метаданных или их реквизитов,
//       которые могут выводиться в отчетах.
//     Поднимается при необходимости.
//
// Например:
//	ВариантыОтчетов.ДобавитьОбработчикиПолногоОбновления(Обработчики, "11.1.7.8");
//
// Места использования:
//   <МодульКонфигурации>.ПриДобавленииОбработчиковОбновления().
//
// См. также:
//   ПодсистемыКонфигурацииПереопределяемый.ПриДобавленииПодсистем().
//
Процедура ДобавитьОбработчикиПолногоОбновления(Обработчики, Версия) Экспорт
	
	Если РазрешеноИндексированиеНеразделенныхДанных() Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.РежимВыполнения = ?(ОбщегоНазначенияПовтИсп.РазделениеВключено(), "Монопольно", "Отложенно");
		Обработчик.ОбщиеДанные     = Истина;
		Обработчик.Версия    = Версия;
		Обработчик.Процедура = "ВариантыОтчетов.ОтложенноеПолноеОбновлениеОбщихДанных";
		Обработчик.Комментарий = НСтр("ru = 'Полное обновление индекса поиска отчетов, предусмотренных в программе.
			|Поиск отчетов временно недоступен.'");
	КонецЕсли;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.ОбщиеДанные     = Ложь;
	Обработчик.Версия    = Версия;
	Обработчик.Процедура = "ВариантыОтчетов.ОтложенноеПолноеОбновлениеРазделенныхДанных";
	Обработчик.Комментарий = НСтр("ru = 'Полное обновление индекса поиска отчетов, сохраненных пользователями.
		|Поиск отчетов временно недоступен.'");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Для вызова из обработчиков обновления прикладной конфигурации

// Переносит пользовательские варианты из стандартного хранилища вариантов в хранилище подсистемы.
//   Используется при частичном внедрении - когда ХранилищеВариантовОтчетов установлено не для всей конфигурации,
//   а в свойствах конкретных отчетов, подключенных к подсистеме.
//   Рекомендуется использовать в обработчиках обновления на конкретную версию.
//
// Параметры:
//   ИменаОтчетов - Строка - Необязательный. Имена отчетов, разделенные запятыми.
//
// Пример:
//	// Перенос всех пользовательских вариантов отчетов при обновлении.
//	ВариантыОтчетов.ПеренестиВариантыОтчетовИзСтандартногоХранилища();
//	// Либо перенос пользовательских вариантов отчетов, переведенных на хранилище подсистемы "Варианты отчетов".
//	ВариантыОтчетов.ПеренестиВариантыОтчетовИзСтандартногоХранилища("АнализЖурналаРегистрации, ЗадачиИстекающиеНаДату");
//
Процедура ПеренестиПользовательскиеИзСтандартногоХранилища(ИменаОтчетов = "") Экспорт
	ПредставлениеПроцедуры = НСтр("ru = 'Прямая конвертация вариантов отчетов'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	// Результат, который будет сохранен в хранилище
	ТаблицаВариантов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПереносВариантовОтчетов", "ТаблицаВариантов", , , "");
	Если ТипЗнч(ТаблицаВариантов) <> Тип("ТаблицаЗначений") Или ТаблицаВариантов.Количество() = 0 Тогда
		ТаблицаВариантов = Новый ТаблицаЗначений;
		ТаблицаВариантов.Колонки.Добавить("Отчет",     ОписаниеТиповСтрока());
		ТаблицаВариантов.Колонки.Добавить("Вариант",   ОписаниеТиповСтрока());
		ТаблицаВариантов.Колонки.Добавить("Автор",     ОписаниеТиповСтрока());
		ТаблицаВариантов.Колонки.Добавить("Настройка", Новый ОписаниеТипов("ХранилищеЗначения"));
		ТаблицаВариантов.Колонки.Добавить("ПредставлениеОтчета",   ОписаниеТиповСтрока());
		ТаблицаВариантов.Колонки.Добавить("ПредставлениеВарианта", ОписаниеТиповСтрока());
		ТаблицаВариантов.Колонки.Добавить("ИдентификаторАвтора",   Новый ОписаниеТипов("УникальныйИдентификатор"));
	КонецЕсли;
	
	УдалятьВсе = (ИменаОтчетов = "" Или ИменаОтчетов = "*");
	МассивУдаляемыхКлючейОбъектов = Новый Массив;
	
	ВыборкаХранилища = ХранилищеВариантовОтчетов.Выбрать(НовыйФильтрПоКлючуОбъекта(ИменаОтчетов));
	ОшибокЧтенияПодряд = 0;
	Пока Истина Цикл
		Попытка
			ЭлементВыборкиПолучен = ВыборкаХранилища.Следующий();
			ОшибокЧтенияПодряд = 0;
		Исключение
			ЭлементВыборкиПолучен = Неопределено;
			ОшибокЧтенияПодряд = ОшибокЧтенияПодряд + 1;
			ОшибкаПоВарианту(
				Неопределено,
				НСтр("ru = 'В процессе выборки вариантов отчетов из стандартного хранилища возникла ошибка:'")
				+ Символы.ПС
				+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		Если ЭлементВыборкиПолучен = Ложь Тогда
			Если ИменаОтчетов = "" Или ИменаОтчетов = "*" Тогда
				Прервать;
			Иначе
				ВыборкаХранилища = ХранилищеВариантовОтчетов.Выбрать(НовыйФильтрПоКлючуОбъекта(ИменаОтчетов));
				Продолжить;
			КонецЕсли;
		ИначеЕсли ЭлементВыборкиПолучен = Неопределено Тогда
			Если ОшибокЧтенияПодряд > 100 Тогда
				Прервать;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Пропуск не подключенных внутренних отчетов.
		ОтчетМетаданные = Метаданные.НайтиПоПолномуИмени(ВыборкаХранилища.КлючОбъекта);
		Если ОтчетМетаданные <> Неопределено Тогда
			ХранилищеМетаданные = ОтчетМетаданные.ХранилищеВариантов;
			Если ХранилищеМетаданные = Неопределено Или ХранилищеМетаданные.Имя <> "ХранилищеВариантовОтчетов" Тогда
				УдалятьВсе = Ложь;
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Внешние варианты отчетов переносятся все, поскольку для них невозможно определить
		// подключены они к хранилищу подсистемы или нет.
		МассивУдаляемыхКлючейОбъектов.Добавить(ВыборкаХранилища.КлючОбъекта);
		
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ВыборкаХранилища.Пользователь);
		Если ПользовательИБ = Неопределено Тогда
			Пользователь = Справочники.Пользователи.НайтиПоНаименованию(ВыборкаХранилища.Пользователь, Истина);
			Если Не ЗначениеЗаполнено(Пользователь) Тогда
				Продолжить;
			КонецЕсли;
			ИдентификаторПользователя = Пользователь.ИдентификаторПользователяИБ;
		Иначе
			ИдентификаторПользователя = ПользовательИБ.УникальныйИдентификатор;
		КонецЕсли;
		
		СтрокаТаблицы = ТаблицаВариантов.Добавить();
		СтрокаТаблицы.Отчет     = ВыборкаХранилища.КлючОбъекта;
		СтрокаТаблицы.Вариант   = ВыборкаХранилища.КлючНастроек;
		СтрокаТаблицы.Автор     = ВыборкаХранилища.Пользователь;
		СтрокаТаблицы.Настройка = Новый ХранилищеЗначения(ВыборкаХранилища.Настройки, Новый СжатиеДанных(9));
		СтрокаТаблицы.ПредставлениеВарианта = ВыборкаХранилища.Представление;
		СтрокаТаблицы.ИдентификаторАвтора   = ИдентификаторПользователя;
		Если ОтчетМетаданные = Неопределено Тогда
			СтрокаТаблицы.ПредставлениеОтчета = ВыборкаХранилища.КлючОбъекта;
		Иначе
			СтрокаТаблицы.ПредставлениеОтчета = ОтчетМетаданные.Представление();
		КонецЕсли;
	КонецЦикла;
	
	// Очистка стандартного хранилища
	Если УдалятьВсе Тогда
		ХранилищеВариантовОтчетов.Удалить(Неопределено, Неопределено, Неопределено);
	Иначе
		Для Каждого КлючОбъекта Из МассивУдаляемыхКлючейОбъектов Цикл
			ХранилищеВариантовОтчетов.Удалить(КлючОбъекта, Неопределено, Неопределено);
		КонецЦикла;
	КонецЕсли;
	
	// Результат выполнения
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
	
	// Загрузка вариантов в хранилище подсистемы
	ЗагрузитьПользовательские(ТаблицаВариантов);
КонецПроцедуры

// Загружает в хранилище подсистемы варианты отчетов, предварительно сохраненные
//   из системного хранилища вариантов в хранилище общих настроек.
//   Используется для загрузки вариантов отчетов при полном или частичном внедрении.
//   При полном внедрении может вызваться из обработки "ПереносВариантовОтчетов".
//   Рекомендуется использовать в обработчиках обновления на конкретную версию.
//
// Параметры:
//   ТаблицаВариантов - ТаблицаЗначений - Необязательный. Используется в служебных сценариях.
//       * Отчет   - Строка - Полное имя отчета в формате "Отчет.<ИмяОтчета>".
//       * Вариант - Строка - Имя варианта отчета.
//       * Автор   - Строка - Имя пользователя.
//       * Настройка - ХранилищеЗначения - ПользовательскиеНастройкиКомпоновкиДанных.
//       * ПредставлениеОтчета   - Строка - Представление отчета.
//       * ПредставлениеВарианта - Строка - Представление варианта.
//       * ИдентификаторАвтора - УникальныйИдентификатор - Идентификатор пользователя.
//
Процедура ЗагрузитьПользовательские(ТаблицаВариантов = Неопределено) Экспорт
	
	Если ТаблицаВариантов = Неопределено Тогда
		ТаблицаВариантов = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПереносВариантовОтчетов", "ТаблицаВариантов", , , "");
	КонецЕсли;
	
	Если ТипЗнч(ТаблицаВариантов) <> Тип("ТаблицаЗначений") Или ТаблицаВариантов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПредставлениеПроцедуры = НСтр("ru = 'Завершить конвертацию вариантов отчетов'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	// Замена имен колонок под структуру справочника.
	ТаблицаВариантов.Колонки.Отчет.Имя = "ОтчетПолноеИмя";
	ТаблицаВариантов.Колонки.Вариант.Имя = "КлючВарианта";
	ТаблицаВариантов.Колонки.ПредставлениеВарианта.Имя = "Наименование";
	
	// Преобразование имен отчетов в ссылки справочника ИОМ.
	ТаблицаВариантов.Колонки.Добавить("Отчет", Метаданные.Справочники.ВариантыОтчетов.Реквизиты.Отчет.Тип);
	ТаблицаВариантов.Колонки.Добавить("Определен", Новый ОписаниеТипов("Булево"));
	ТаблицаВариантов.Колонки.Добавить("ТипОтчета", Метаданные.Справочники.ВариантыОтчетов.Реквизиты.ТипОтчета.Тип);
	Для Каждого СтрокаТаблицы Из ТаблицаВариантов Цикл
		ОтчетИнформация = СформироватьИнформациюОбОтчетеПоПолномуИмени(СтрокаТаблицы.ОтчетПолноеИмя);
		
		// Проверить результат
		Если ТипЗнч(ОтчетИнформация.ТекстОшибки) = Тип("Строка") Тогда
			ОшибкаПоВарианту(Неопределено, ОтчетИнформация.ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы.Определен = Истина;
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ОтчетИнформация, "Отчет, ТипОтчета");
	КонецЦикла;
	
	ТаблицаВариантов.Сортировать("ОтчетПолноеИмя Возр, КлючВарианта Возр");
	
	// Существующие варианты отчетов
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаВариантов.Отчет,
	|	ТаблицаВариантов.ОтчетПолноеИмя,
	|	ТаблицаВариантов.ТипОтчета,
	|	ТаблицаВариантов.КлючВарианта,
	|	ТаблицаВариантов.Автор
	|ПОМЕСТИТЬ втВарианты
	|ИЗ
	|	&ТаблицаВариантов КАК ТаблицаВариантов
	|ГДЕ
	|	ТаблицаВариантов.Определен = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВарианты.Отчет,
	|	втВарианты.ОтчетПолноеИмя,
	|	втВарианты.ТипОтчета,
	|	втВарианты.КлючВарианта,
	|	ВариантыОтчетов.Ссылка,
	|	ПользователиПоИмени.Ссылка КАК ПользовательПоИмени
	|ИЗ
	|	втВарианты КАК втВарианты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Пользователи КАК ПользователиПоИмени
	|		ПО втВарианты.Автор = ПользователиПоИмени.Наименование
	|			И (ПользователиПоИмени.ПометкаУдаления = ЛОЖЬ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ПО втВарианты.Отчет = ВариантыОтчетов.Отчет
	|			И втВарианты.КлючВарианта = ВариантыОтчетов.КлючВарианта
	|			И втВарианты.ТипОтчета = ВариантыОтчетов.ТипОтчета";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаВариантов", ТаблицаВариантов);
	Запрос.Текст = ТекстЗапроса;
	
	ВариантыБД = Запрос.Выполнить().Выгрузить();
	
	// Авторы вариантов
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	Пользователи.ИдентификаторПользователяИБ КАК Идентификатор
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|ГДЕ
	|	Пользователи.ИдентификаторПользователяИБ В(&Идентификаторы)
	|	И Пользователи.ПометкаУдаления = ЛОЖЬ";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Идентификаторы", ТаблицаВариантов.ВыгрузитьКолонку("ИдентификаторАвтора"));
	Запрос.Текст = ТекстЗапроса;
	
	ПользователиПоИдентификатору = Запрос.Выполнить().Выгрузить();
	
	ПодсистемыОтчетов = РазмещениеОтчетовВПодсистемах();
	
	// Загрузка вариантов в хранилище подсистемы
	ОпределенныеВарианты = ТаблицаВариантов.НайтиСтроки(Новый Структура("Определен", Истина));
	Для Каждого СтрокаТаблицы Из ОпределенныеВарианты Цикл
		Найденные = ВариантыБД.НайтиСтроки(Новый Структура("Отчет, КлючВарианта", СтрокаТаблицы.Отчет, СтрокаТаблицы.КлючВарианта));
		ВариантБД = Найденные[0];
		
		// Если вариант уже загружен в справочник "Варианты отчетов" - не загружаем его.
		Если ЗначениеЗаполнено(ВариантБД.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		// СправочникОбъект
		ВариантОбъект = Справочники.ВариантыОтчетов.СоздатьЭлемент();
		
		// Уже подготовленные параметры
		ЗаполнитьЗначенияСвойств(ВариантОбъект, СтрокаТаблицы, "Наименование, Отчет, ТипОтчета, КлючВарианта");
		
		// Настройки
		Настройки = СтрокаТаблицы.Настройка;
		Если ТипЗнч(Настройки) = Тип("ХранилищеЗначения") Тогда
			Настройки = Настройки.Получить();
		КонецЕсли;
		ВариантОбъект.Настройки = Новый ХранилищеЗначения(Настройки);
		
		// В стандартном хранилище хранятся только пользовательские ВО
		ВариантОбъект.Пользовательский = Истина;
		ВариантОбъект.ТолькоДляАвтора = Истина;
		
		// Автор варианта
		ПользовательПоИД = ПользователиПоИдентификатору.Найти(СтрокаТаблицы.ИдентификаторАвтора, "Идентификатор");
		Если ПользовательПоИД <> Неопределено И ЗначениеЗаполнено(ПользовательПоИД.Пользователь) Тогда
			ВариантОбъект.Автор = ПользовательПоИД.Пользователь;
		ИначеЕсли ВариантБД <> Неопределено И ЗначениеЗаполнено(ВариантБД.ПользовательПоИмени) Тогда
			ВариантОбъект.Автор = ВариантБД.ПользовательПоИмени;
		Иначе
			ОшибкаПоВарианту(
				ВариантОбъект.Ссылка,
				НСтр("ru = 'Вариант ""%1"" отчета ""%2"": не найден автор ""%3""'"),
				ВариантОбъект.Наименование,
				СтрокаТаблицы.ПредставлениеОтчета,
				СтрокаТаблицы.Автор);
		КонецЕсли;
		
		// Поскольку переносятся пользовательские варианты отчетов,
		// то настройки размещения можно взять только из метаданных отчета.
		Найденные = ПодсистемыОтчетов.НайтиСтроки(Новый Структура("ОтчетПолноеИмя", СтрокаТаблицы.ОтчетПолноеИмя));
		Для Каждого СтрокаПодсистема Из Найденные Цикл
			СтрокаРаздел = ВариантОбъект.Размещение.Добавить();
			СтрокаРаздел.Использование = Истина;
			СтрокаРаздел.Подсистема = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(СтрокаПодсистема.ПодсистемаМетаданные);
		КонецЦикла;
		
		ВариантОбъект.Записать();
	КонецЦикла;
	
	// Очистка
	ОбщегоНазначения.ХранилищеОбщихНастроекУдалить("ПереносВариантовОтчетов", "ТаблицаВариантов", "");
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Добавление обработчиков служебных событий (подписок)

// См. описание этой же процедуры в модуле СтандартныеПодсистемыСервер.
Процедура ПриДобавленииСлужебныхСобытий(КлиентскиеСобытия, СерверныеСобытия) Экспорт
	
	// СЕРВЕРНЫЕ СОБЫТИЯ.
	
	// Содержит настройки размещения вариантов отчетов в панели отчетов.
	//
	// Параметры:
	//   Настройки - Коллекция - Используется для описания настроек отчетов и вариантов
	//       см. описание к ВариантыОтчетов.ДеревоНастроекВариантовОтчетовКонфигурации()
	//
	// Описание:
	//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
	//
	СерверныеСобытия.Добавить(
		"СтандартныеПодсистемы.ВариантыОтчетов\ПриНастройкеВариантовОтчетов");
	
	// Регистрация изменений в именах вариантов отчетов.
	//   Используется в обработчиках обновления, в целях контроля ссылочной целостности
	//   и для сохранения настроек, сделанных администратором для предопределенных вариантов.
	//
	// Параметры:
	//   Изменения - ТаблицаЗначений - Изменения в именах вариантов отчетов.
	//       * Отчет - ОбъектМетаданных - Метаданные отчета, в схеме которого изменилось имя варианта.
	//       * СтароеИмяВарианта - Строка - Старое имя варианта, до изменения.
	//       * АктуальноеИмяВарианта - Строка - Текущее (последнее актуальное) имя варианта.
	//
	// Описание:
	//   См. ВариантыОтчетовПереопределяемый.ЗарегистрироватьИзмененияКлючейВариантовОтчетов().
	//
	СерверныеСобытия.Добавить(
		"СтандартныеПодсистемы.ВариантыОтчетов\ПриРегистрацииИзмененийИменВариантовОтчетов");
	
КонецПроцедуры

// См. описание этой же процедуры в модуле СтандартныеПодсистемыСервер.
Процедура ПриДобавленииОбработчиковСлужебныхСобытий(КлиентскиеОбработчики, СерверныеОбработчики) Экспорт
	// СЕРВЕРНЫЕ ОБРАБОТЧИКИ.
	СерверныйМодуль = "ВариантыОтчетов";
	
	Событие = "СтандартныеПодсистемы.ОбновлениеВерсииИБ\ПриДобавленииОбработчиковОбновления";
	СерверныеОбработчики[Событие].Добавить(СерверныйМодуль);
	
	Событие = "СтандартныеПодсистемы.БазоваяФункциональность\ПриДобавленииИсключенийПоискаСсылок";
	СерверныеОбработчики[Событие].Добавить(СерверныйМодуль);
	
	Событие = "СтандартныеПодсистемы.БазоваяФункциональность\ПриДобавленииПереименованийОбъектовМетаданных";
	СерверныеОбработчики[Событие].Добавить(СерверныйМодуль);
	
	Событие = "СтандартныеПодсистемы.БазоваяФункциональность\ПриПолученииОбязательныхОбъектовПланаОбмена";
	СерверныеОбработчики[Событие].Добавить(СерверныйМодуль);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.ВыгрузкаЗагрузкаДанных") Тогда
		Событие = "ТехнологияСервиса.ВыгрузкаЗагрузкаДанных\ПриЗаполненииТиповОбщихДанныхПоддерживающихСопоставлениеСсылокПриЗагрузке";
		СерверныеОбработчики[Событие].Добавить(СерверныйМодуль);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики служебных событий

// Добавляет процедуры-обработчики обновления, необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - Обработчики обновления.
//       См. описание метода ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления().
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	////////////////////////////////////////////////////////////////////////////////
	// План обновления:
	
	////////////////////////////////////////////////////////////////////////////////
	// 1. Обновить общие данные.
	Обработчик = Обработчики.Добавить();
	Обработчик.ВыполнятьВГруппеОбязательных = Ложь;
	Обработчик.ОбщиеДанные                  = Истина;
	Обработчик.УправлениеОбработчиками      = Истина;
	Обработчик.РежимВыполнения              = "Оперативно";
	Обработчик.Версия    = "*";
	Обработчик.Процедура = "ВариантыОтчетов.ОбновитьОбщиеДанные";
	Обработчик.Приоритет = 90;
	
	////////////////////////////////////////////////////////////////////////////////
	// 2. Обновить разделенные данные.
	// 2.1. Перевести разделенные данные на версию 2.1.1.0.
	Обработчик = Обработчики.Добавить();
	Обработчик.ВыполнятьВГруппеОбязательных = Истина;
	Обработчик.ОбщиеДанные                  = Ложь;
	Обработчик.УправлениеОбработчиками      = Ложь;
	Обработчик.РежимВыполнения              = "Монопольно";
	Обработчик.Версия    = "2.1.1.0";
	Обработчик.Процедура = "ВариантыОтчетов.ПерейтиКРедакции21";
	Обработчик.Приоритет = 80;
	
	// 2.2. Перевести разделенные данные на версию 2.1.3.6.
	Обработчик = Обработчики.Добавить();
	Обработчик.ВыполнятьВГруппеОбязательных = Истина;
	Обработчик.ОбщиеДанные                  = Ложь;
	Обработчик.УправлениеОбработчиками      = Ложь;
	Обработчик.РежимВыполнения              = "Монопольно";
	Обработчик.Версия    = "2.1.3.6";
	Обработчик.Процедура = "ВариантыОтчетов.ЗаполнитьСсылкиПредопределенных";
	Обработчик.Приоритет = 80;
	
	// 2.3. Обновить разделенные данные в локальном режиме.
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Версия    = "*";
	Обработчик.Процедура = "ВариантыОтчетов.ОбновитьРазделенныеДанные";
	Обработчик.Приоритет = 70;
	
	////////////////////////////////////////////////////////////////////////////////
	// 3. Отложенное обновление.
	// 3.1. Заполнить информацию для поиска вариантов отчетов.
	
	Если РазрешеноИндексированиеНеразделенныхДанных() Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.РежимВыполнения = ?(ОбщегоНазначенияПовтИсп.РазделениеВключено(), "Оперативно", "Отложенно");
		Обработчик.ОбщиеДанные     = Истина;
		Обработчик.Версия      = "*";
		Обработчик.Процедура   = "ВариантыОтчетов.ОтложенноеИнкрементальноеОбновлениеОбщихДанных";
		Обработчик.Комментарий = НСтр("ru = 'Инкрементальное обновление индекса поиска отчетов, предусмотренных в программе.
			|Поиск отчетов временно недоступен.'");
	КонецЕсли;
	
	// 3.2. Заполнить информацию для поиска вариантов отчетов.
	Обработчик = Обработчики.Добавить();
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.ОбщиеДанные     = Ложь;
	Обработчик.Версия    = "2.2.3.31";
	Обработчик.Процедура = "ВариантыОтчетов.СократитьКоличествоБыстрыхНастроек";
	Обработчик.Комментарий = НСтр("ru = 'Уменьшает количество быстрых настроек
	|в пользовательских отчетах до 2 шт.'");
	
КонецПроцедуры

// Возникает при получении исключений поиска ссылок.
Процедура ПриДобавленииИсключенийПоискаСсылок(ИсключенияПоискаСсылок) Экспорт
	
	ИсключенияПоискаСсылок.Добавить(Метаданные.Справочники.ВариантыОтчетов.ТабличныеЧасти.Размещение.Реквизиты.Подсистема);
	
КонецПроцедуры

// Заполняет переименования тех объектов метаданных, которые невозможно
// автоматически найти по типу, но ссылки на которые требуется сохранять
// в базе данных (например: подсистемы, роли).
//
// См. также:
//   ОбщегоНазначения.ДобавитьПереименование().
//
Процедура ПриДобавленииПереименованийОбъектовМетаданных(Итог) Экспорт
	
	Библиотека = "СтандартныеПодсистемы";
	
	ОбщегоНазначения.ДобавитьПереименование(
		Итог, "2.1.0.2", "Роль.ЧтениеВариантовОтчетов", "Роль.ИспользованиеВариантовОтчетов", Библиотека);
	
КонецПроцедуры

// Используется для получения объектов метаданных обязательных для плана обмена.
// Если подсистема имеет объекты метаданных обязательные для включения в состав плана обмена,
// то в параметр <Объект> необходимо добавить эти объекты метаданных.
//
// См. также:
//   СтандартныеПодсистемыСервер.ПроверитьСоставПланаОбмена().
//
Процедура ПриПолученииОбязательныхОбъектовПланаОбмена(Объекты, Знач РаспределеннаяИнформационнаяБаза) Экспорт
	
	Если РаспределеннаяИнформационнаяБаза Тогда
		
		Объекты.Добавить(Метаданные.Константы.ПараметрыВариантовОтчетов);
		Объекты.Добавить(Метаданные.Справочники.ВариантыОтчетов);
		Объекты.Добавить(Метаданные.Справочники.ПредопределенныеВариантыОтчетов);
		Объекты.Добавить(Метаданные.Справочники.ПользовательскиеНастройкиОтчетов);
		Объекты.Добавить(Метаданные.РегистрыСведений.НастройкиВариантовОтчетов);
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет массив типов неразделенных данных, для которых поддерживается сопоставление ссылок
// при загрузке данных в другую информационную базу.
//
// Параметры:
//   Типы - Массив из ОбъектМетаданных
//
Процедура ПриЗаполненииТиповОбщихДанныхПоддерживающихСопоставлениеСсылокПриЗагрузке(Типы) Экспорт
	
	Типы.Добавить(Метаданные.Справочники.ПредопределенныеВариантыОтчетов);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов

// Получает объект отчета по ссылке варианта отчета.
//
// Параметры:
//   СсылкаВариантаИлиОтчета -
//     - СправочникСсылка.ВариантыОтчетов - Ссылка варианта отчета, подключенного к хранилищу подсистемы.
//     - СправочникСсылка.ИдентификаторыОбъектовМетаданных - Ссылка отчета конфигурации.
//     - Произвольный - Ссылка дополнительного или внешнего отчета.
//
// Возвращаемое значение:
//   Структура - Параметры отчета, в том числе Объект отчета.
//       * Объект     - ОтчетОбъект.<Имя отчета>, ВнешнийОтчет - Объект отчета.
//       * Имя        - Строка           - Имя объекта отчета.
//       * Метаданные - ОбъектМетаданных - Объект метаданных отчета.
//       * Ссылка     - Произвольный     - Ссылка отчета.
//       * Ошибки     - Строка           - Текст ошибки.
//
// Места использования:
//   РассылкаОтчетов.ИнициализироватьОтчет().
//
Функция ПодключитьОтчетОбъект(СсылкаВариантаИлиОтчета) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Объект",     Неопределено);
	Результат.Вставить("Имя",        "");
	Результат.Вставить("Метаданные", Неопределено);
	Результат.Вставить("Ссылка",     Неопределено);
	Результат.Вставить("Подключен",  Ложь);
	Результат.Вставить("Ошибки",     "");
	
	Если ТипЗнч(СсылкаВариантаИлиОтчета) = Тип("СправочникСсылка.ВариантыОтчетов") Тогда
		Результат.Ссылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаВариантаИлиОтчета, "Отчет");
	Иначе
		Результат.Ссылка = СсылкаВариантаИлиОтчета;
	КонецЕсли;
	
	Если Результат.Ссылка = Неопределено Тогда
		Результат.Ошибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Вариант отчета ""%1"" не найден в программе'"),
			Строка(СсылкаВариантаИлиОтчета));
		Возврат Результат;
	ИначеЕсли ТипЗнч(Результат.Ссылка) = Тип("Строка") Тогда
		Результат.Ошибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для варианта ""%1"" отчет ""%2"" записан как внешний и не может быть подключен из программы'"),
			Строка(СсылкаВариантаИлиОтчета),
			Результат.Ссылка);
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЗнч(Результат.Ссылка) <> Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
			МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
			МодульДополнительныеОтчетыИОбработки.ПриПодключенииДопОтчета(Результат.Ссылка, Результат, Результат.Подключен);
		КонецЕсли;
		Возврат Результат;
	КонецЕсли;
	
	Результат.Имя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Результат.Ссылка, "Имя");
	Результат.Метаданные = Метаданные.Отчеты.Найти(Результат.Имя);
	Если Результат.Метаданные = Неопределено Тогда
		Результат.Ошибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для варианта ""%1"" отчет ""%2"" не найден в программе'"),
			Строка(СсылкаВариантаИлиОтчета),
			Результат.Имя);
		Возврат Результат;
	КонецЕсли;
	
	Результат.Объект = Отчеты[Результат.Имя].Создать();
	Результат.Подключен = Истина;
	Возврат Результат;
	
КонецФункции

// Обновляет варианты дополнительного отчета при его записи.
//
// Места использования:
//   Справочник.ДополнительныеОтчетыИОбработки.ПриЗаписиГлобальногоОтчета().
//
Процедура ПриЗаписиДополнительногоОтчета(ТекущийОбъект, Отказ, ВнешнийОбъект) Экспорт
	
	Если Не ВариантыОтчетовПовтИсп.ПравоДобавления() Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Недостаточно прав доступа для записи вариантов дополнительного отчета ""%1"".'"),
			ТекущийОбъект.Наименование);
		ОшибкаПоВарианту(ТекущийОбъект.Ссылка, ТекстОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	ПометкаУдаления = ТекущийОбъект.ПометкаУдаления;
	Если Не ТекущийОбъект.ИспользуетХранилищеВариантов Или Не ТекущийОбъект.ДополнительныеСвойства.ПубликацияИспользуется Тогда
		ПометкаУдаления = Истина;
	КонецЕсли;
	
	ПредопределенныеВарианты = Новый СписокЗначений;
	Если ТекущийОбъект.ИспользуетХранилищеВариантов Тогда
		ОтчетМетаданные = ВнешнийОбъект.Метаданные();
		СхемаКДМетаданные = ОтчетМетаданные.ОсновнаяСхемаКомпоновкиДанных;
		Если СхемаКДМетаданные <> Неопределено Тогда
			СхемаКД = ВнешнийОбъект.ПолучитьМакет(СхемаКДМетаданные.Имя);
			Для Каждого ВариантНастроекКД Из СхемаКД.ВариантыНастроек Цикл
				ПредопределенныеВарианты.Добавить(ВариантНастроекКД.Имя, ВариантНастроекКД.Представление);
			КонецЦикла;
		Иначе
			ПредопределенныеВарианты.Добавить("", ОтчетМетаданные.Представление());
		КонецЕсли;
	КонецЕсли;
	
	// При снятии пометки удаления доп. отчета пометка удаления снимается только для предопределенных вариантов отчетов (для пользовательских не снимается).
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.Ссылка,
	|	Таблица.КлючВарианта,
	|	Таблица.Пользовательский,
	|	Таблица.ПометкаУдаления,
	|	Таблица.Наименование
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК Таблица
	|ГДЕ
	|	Таблица.Отчет = &Отчет
	|	И Таблица.Пользовательский = ЛОЖЬ";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Отчет", ТекущийОбъект.Ссылка);
	Если ПометкаУдаления = Истина Тогда
		// При установке пометки удаления доп. отчета пометка удаления устанавливается для всех вариантов отчетов - и пользовательских и предопределенных.
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Таблица.Пользовательский = ЛОЖЬ", "");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	// Установка пометки удаления.
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПометкаУдаленияВарианта = ПометкаУдаления;
		ЭлементСписка = ПредопределенныеВарианты.НайтиПоЗначению(Выборка.КлючВарианта);
		Если Не ПометкаУдаленияВарианта И Не Выборка.Пользовательский И ЭлементСписка = Неопределено Тогда
			// Предопределенный вариант, который не найден в списке предопределенных для данного отчета
			ПометкаУдаленияВарианта = Истина;
		КонецЕсли;
		
		Если Выборка.ПометкаУдаления <> ПометкаУдаленияВарианта Тогда
			ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ВариантОбъект.ДополнительныеСвойства.Вставить("ЗаполнениеПредопределенных", Истина);
			Если ПометкаУдаленияВарианта Тогда
				ВариантОбъект.ДополнительныеСвойства.Вставить("ИндексироватьСхему", Ложь);
			Иначе
				ВариантОбъект.ДополнительныеСвойства.Вставить("ОтчетОбъект", ВнешнийОбъект);
			КонецЕсли;
			ВариантОбъект.УстановитьПометкуУдаления(ПометкаУдаленияВарианта);
		КонецЕсли;
		
		Если ЭлементСписка <> Неопределено Тогда
			ПредопределенныеВарианты.Удалить(ЭлементСписка);
			Если Выборка.Наименование <> ЭлементСписка.Представление Тогда
				ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
				ВариантОбъект.Наименование = ЭлементСписка.Представление;
				ВариантОбъект.ДополнительныеСвойства.Вставить("ОтчетОбъект", ВнешнийОбъект);
				ВариантОбъект.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПометкаУдаления Тогда
		// Зарегистрировать новые
		Для Каждого ЭлементСписка Из ПредопределенныеВарианты Цикл
			ВариантОбъект = Справочники.ВариантыОтчетов.СоздатьЭлемент();
			ВариантОбъект.Отчет                = ТекущийОбъект.Ссылка;
			ВариантОбъект.ТипОтчета            = Перечисления.ТипыОтчетов.Дополнительный;
			ВариантОбъект.КлючВарианта         = ЭлементСписка.Значение;
			ВариантОбъект.Наименование         = ЭлементСписка.Представление;
			ВариантОбъект.Пользовательский     = Ложь;
			ВариантОбъект.ДополнительныеСвойства.Вставить("ОтчетОбъект", ВнешнийОбъект);
			ВариантОбъект.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Получает варианты переданного отчета и их представления
//
// Места использования:
//   ПользователиСлужебный.ПриПолученииПользовательскихВариантовОтчетов().
//
Процедура ПользовательскиеВариантыОтчетов(ОтчетМетаданные, ПользовательИнформационнойБазы, ТаблицаВариантовОтчетов, СтандартнаяОбработка) Экспорт
	
	КлючОтчета = "Отчет" + "." + ОтчетМетаданные.Имя;
	ВсеВариантыОтчета = ХранилищаНастроек.ХранилищеВариантовОтчетов.ПолучитьСписок(КлючОтчета, ПользовательИнформационнойБазы);
	ВариантыОтчета = Новый СписокЗначений;
	
	Для Каждого ВариантОтчета Из ВсеВариантыОтчета Цикл
		
		ЭлементСправочника = Справочники.ВариантыОтчетов.НайтиПоНаименованию(ВариантОтчета.Представление);
		
		Если ЭлементСправочника <> Неопределено
			И ЭлементСправочника.ТолькоДляАвтора Тогда
			
			СтрокаВариантовОтчетов = ТаблицаВариантовОтчетов.Добавить();
			СтрокаВариантовОтчетов.КлючОбъекта = "Отчет." + ОтчетМетаданные.Имя;
			СтрокаВариантовОтчетов.КлючВарианта = ВариантОтчета.Значение;
			СтрокаВариантовОтчетов.Представление = ВариантОтчета.Представление;
			СтрокаВариантовОтчетов.СтандартнаяОбработка = Ложь;
			
			СтандартнаяОбработка = Ложь;
			
		ИначеЕсли ЭлементСправочника <> Неопределено Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Удаляет переданный вариант отчета из хранилища вариантов отчетов
//
// Места использования:
//   ПользователиСлужебный.ПриУдаленииПользовательскихВариантовОтчета().
//
Процедура УдалитьПользовательскийВариантОтчета(СведенияОВариантеОтчета, ПользовательИнформационнойБазы, СтандартнаяОбработка) Экспорт
	
	Если СведенияОВариантеОтчета.СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ХранилищаНастроек.ХранилищеВариантовОтчетов.Удалить(СведенияОВариантеОтчета.КлючОбъекта,
		СведенияОВариантеОтчета.КлючВарианта, ПользовательИнформационнойБазы);
	
КонецПроцедуры

// Добавляет оповещение для открытых форм подсистемы если изменилось значение функциональной опции.
//
// Параметры:
//   Результат - Структура - Результат работы сервера, который возвращается на клиент.
//       См. СтандартныеПодсистемыКлиент.ПоказатьРезультатВыполнения().
//   КонстантаМенеджер - КонстантаМенеджер.<Имя константы> - Менеджер константы, значение которой изменилось.
//
// Описание:
//   Данную процедуру следует использовать при изменении значений констант,
//   связанных с функциональными опциями для последующего обновления открытых форм.
//
// Места использования:
//   Обработка.ПанельАдминистрированияБСП.Форма.*.
//
Процедура ДобавитьОповещениеПриИзмененииЗначенияКонстанты(Результат, КонстантаМенеджер) Экспорт
	// Варианты отчетов связаны с константами через функциональные опции.
	// ПовтИсп не используется поскольку при изменении значения функциональной опции их значения как правило сбрасываются.
	ПеременныеИнициализированы = Ложь;
	КонстантаМетаданные = КонстантаМенеджер.СоздатьМенеджерЗначения().Метаданные();
	
	// Поиск функциональных опций, связанных с этой константой
	Для Каждого ФункциональнаяОпцияМетаданные Из Метаданные.ФункциональныеОпции Цикл
		Если ФункциональнаяОпцияМетаданные.Хранение = КонстантаМетаданные Тогда
			
			// Поиск связи вариантов отчетов с этой функциональной опцией.
			Если Не ПеременныеИнициализированы Тогда
				ПеременныеИнициализированы = Истина;
				ТаблицаОпций = Константы.ПараметрыВариантовОтчетов.Получить().Получить().ТаблицаФункциональныхОпций;
				ИмяСобытия = ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта();
			КонецЕсли;
			
			Если ТаблицаОпций.Найти(ФункциональнаяОпцияМетаданные.Имя, "ИмяФункциональнойОпции") <> Неопределено Тогда
				СтандартныеПодсистемыКлиентСервер.РезультатВыполненияДобавитьОповещениеОткрытыхФорм(Результат, ИмяСобытия);
				Возврат;
			КонецЕсли;
			
			// Поиск связи отчетов с этой функциональной опцией.
			Для Каждого ЭлементСоставаФункциональнойОпции Из ФункциональнаяОпцияМетаданные.Состав Цикл
				Если ТипЗнч(ЭлементСоставаФункциональнойОпции.Объект) = Тип("ОбъектМетаданных")
					И Метаданные.Отчеты.Содержит(ЭлементСоставаФункциональнойОпции.Объект) Тогда
					СтандартныеПодсистемыКлиентСервер.РезультатВыполненияДобавитьОповещениеОткрытыхФорм(Результат, ИмяСобытия);
					Возврат;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Прочие процедуры служебного программного интерфейса

// Актуализирует данные подсистемы с учетом режима работы программы.
//   Пример использования: после очистки хранилищ настроек.
Процедура Обновить(Настройки = Неопределено) Экспорт
	
	Если Настройки = Неопределено Тогда
		Настройки = Новый Структура("ОбщиеДанные, РазделенныеДанные");
		
		РежимРаботы = ОбщегоНазначенияПовтИсп.РежимРаботыПрограммы();
		Если РежимРаботы.МодельСервиса Тогда
			Если ОбщегоНазначения.ИспользованиеРазделителяСеанса() Тогда
				Настройки.ОбщиеДанные       = Ложь;
				Настройки.РазделенныеДанные = Истина;
			Иначе
				Настройки.ОбщиеДанные       = Истина;
				Настройки.РазделенныеДанные = Ложь;
			КонецЕсли;
		Иначе
			Если РежимРаботы.Автономный Тогда
				Настройки.ОбщиеДанные       = Ложь;
				Настройки.РазделенныеДанные = Истина;
			Иначе
				Настройки.ОбщиеДанные       = Истина;
				Настройки.РазделенныеДанные = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Настройки.ОбщиеДанные = Истина Тогда
		ПараметрыОбновления = Новый Структура("РазделенныеОбработчики");
		ПараметрыОбновления.РазделенныеОбработчики = ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления();
		ОбновитьОбщиеДанные(ПараметрыОбновления);
		ОтложенноеПолноеОбновлениеОбщихДанных(ПараметрыОбновления);
	КонецЕсли;
	
	Если Настройки.РазделенныеДанные = Истина Тогда
		ОбновитьРазделенныеДанные();
		ОтложенноеПолноеОбновлениеРазделенныхДанных();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует отчет с указанными настройками, используется в фоновых заданиях.
Процедура СформироватьОтчет(ПараметрыФормированияОтчета, АдресХранилища) Экспорт
	
	ОтчетСсылка = ПараметрыФормированияОтчета.ОтчетСсылка;
	Если ТипЗнч(ОтчетСсылка) = Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
		ОтчетОбъект = Отчеты[ОтчетСсылка.Имя].Создать();
	Иначе
		ОтчетОбъект = Неопределено;
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
			МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
			ОтчетОбъект = МодульДополнительныеОтчетыИОбработки.ПолучитьОбъектВнешнейОбработки(ОтчетСсылка);
		КонецЕсли;
	КонецЕсли;
	
	Если ОтчетОбъект = Неопределено Тогда
		ОшибкаПоВарианту(Неопределено, НСтр("ru = 'Не удалось получить объект отчета ""%1""'"), ОтчетСсылка);
		Возврат;
	КонецЕсли;
	
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьФиксированныеНастройки(ПараметрыФормированияОтчета.ФиксированныеНастройки);
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(ПараметрыФормированияОтчета.Настройки);
	ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПараметрыФормированияОтчета.ПользовательскиеНастройки);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Расшифровка = Неопределено;
	
	ОтчетОбъект.СкомпоноватьРезультат(ТабличныйДокумент, Расшифровка);
	
	ДопСвойства = ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	
	РезультатФормирования = Новый Структура;
	РезультатФормирования.Вставить("ОтчетТабличныйДокумент", ТабличныйДокумент);
	РезультатФормирования.Вставить("ОтчетРасшифровка", Расшифровка);
	РезультатФормирования.Вставить("ВариантМодифицирован", Ложь);
	РезультатФормирования.Вставить("ПользовательскиеНастройкиМодифицированы", Ложь);
	
	Если ВариантыОтчетовКлиентСервер.СвойствоСтруктуры(ДопСвойства, "ВариантМодифицирован") = Истина Тогда
		РезультатФормирования.ВариантМодифицирован = Истина;
		РезультатФормирования.Вставить("НастройкиКД", ОтчетОбъект.КомпоновщикНастроек.Настройки);
	КонецЕсли;
	
	Если РезультатФормирования.ВариантМодифицирован
		Или ВариантыОтчетовКлиентСервер.СвойствоСтруктуры(ДопСвойства, "ПользовательскиеНастройкиМодифицированы") = Истина Тогда
		РезультатФормирования.ПользовательскиеНастройкиМодифицированы = Истина;
		РезультатФормирования.Вставить("ПользовательскиеНастройкиКД", ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки);
	КонецЕсли;
	
	ДопСвойства.Удалить("ВариантМодифицирован");
	ДопСвойства.Удалить("ПользовательскиеНастройкиМодифицированы");
	ДопСвойства.Удалить("КлючВарианта");
	
	ПоместитьВоВременноеХранилище(РезультатФормирования, АдресХранилища);
КонецПроцедуры

// Формирует дерево настроек и размещения предопределенных вариантов отчетов.
//   Только для отчетов, подключенных к подсистеме.
//
// Возвращаемое значение: 
//   ДеревоВариантов - ДеревоЗначений - Настройки предопределенных вариантов отчетов, подключенных к подсистеме.
//     Реквизиты, открытые для изменения:
//       * Включен              - Булево - Если Ложь, то вариант отчета не регистрируется в подсистеме.
//       * ВидимостьПоУмолчанию - Булево - Если Ложь, то вариант отчета по умолчанию скрыт в панели отчетов.
//       * Наименование         - Строка - Наименование варианта отчета.
//       * Описание             - Строка - Информация о варианте отчета.
//       * Размещение           - Соответствие - Настройки размещения варианта отчета в разделах.
//           ** Ключ     - ОбъектМетаданных - Подсистема, в которой размещается отчет или вариант отчета.
//           ** Значение - Строка           - Настройки размещения в подсистеме (визуально - группами командного интерфейса).
//               *** ""        - Вывод отчета в подсистеме без специального выделение.
//               *** "Важный"  - Вывод отчета в подсистеме с выделением жирным шрифтом.
//               *** "СмТакже" - Вывод отчета в группе "См. также".
//       * ФункциональныеОпции - Массив из Строка - Имена функциональных опций варианта отчета.
//       * НастройкиДляПоиска  - Структура - Дополнительные настройки для поиска этого варианта отчета.
//           ** НаименованияПолей - Строка - Имена полей варианта отчета.
//           ** НаименованияПараметровИОтборов - Строка - Имена настроек варианта отчета.
//           ** ИменаМакетов - Строка - Используется вместо НаименованияПолей.
//               Имена макетов табличных или текстовых документов,
//               из которых требуется извлечь информацию о наименованиях полей.
//               Имена перечисляются через запятую.
//               К сожалению, в макетах отсутствует информация о связях полей и их представлений (которая есть в СКД),
//               поэтому для более точной работы механизма поиска рекомендуется заполнять НаименованияПолей, а не ИменаМакетов.
//       * ОпределитьНастройкиФормы - Булево - Отчет имеет программный интерфейс для тесной интеграции с формой отчета,
//           в том числе может переопределять некоторые настройки формы и подписываться на ее события.
//           Если Истина и отчет подключен к общей форме ФормаОтчета,
//           тогда в модуле объекта отчета следует определить процедуру по шаблону:
//               
//               // Настройки общей формы отчета подсистемы "Варианты отчетов".
//               //
//               // Параметры:
//               //   Форма - УправляемаяФорма - Форма отчета.
//               //   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//               //   Настройки - Структура - см. возвращаемое значение ФункцииОтчетовКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//               //
//               Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
//               	// Код процедуры.
//               КонецПроцедуры
//               
//     Служебные реквизиты (только для чтения):
//       * Отчет               - <см. Справочники.ВариантыОтчетов.Реквизиты.Отчет> - Полное имя или ссылка на отчет. 
//       * Метаданные          - ОбъектМетаданных: Отчет - Метаданные отчета.
//       * КлючВарианта        - Строка - Имя варианта отчета.
//       * ОписаниеПолучено    - Булево - Флажок что описание строки уже получено. Описание получается методом ОписаниеВарианта().
//       * СистемнаяИнформация - Структура - Другая служебная информация.
//
Функция ДеревоНастроекВариантовОтчетовКонфигурации() Экспорт
	РеквизитыСправочника = Метаданные.Справочники.ВариантыОтчетов.Реквизиты;
	
	ДеревоВариантов = Новый ДеревоЗначений;
	ДеревоВариантов.Колонки.Добавить("Отчет",                РеквизитыСправочника.Отчет.Тип);
	ДеревоВариантов.Колонки.Добавить("Метаданные",           Новый ОписаниеТипов("ОбъектМетаданных"));
	ДеревоВариантов.Колонки.Добавить("КлючВарианта",         РеквизитыСправочника.КлючВарианта.Тип);
	ДеревоВариантов.Колонки.Добавить("ОписаниеПолучено",     Новый ОписаниеТипов("Булево"));
	ДеревоВариантов.Колонки.Добавить("Включен",              Новый ОписаниеТипов("Булево"));
	ДеревоВариантов.Колонки.Добавить("ВидимостьПоУмолчанию", Новый ОписаниеТипов("Булево"));
	ДеревоВариантов.Колонки.Добавить("Наименование",         ОписаниеТиповСтрока());
	ДеревоВариантов.Колонки.Добавить("Описание",             ОписаниеТиповСтрока());
	ДеревоВариантов.Колонки.Добавить("Размещение",           Новый ОписаниеТипов("Соответствие"));
	ДеревоВариантов.Колонки.Добавить("НастройкиДляПоиска",   Новый ОписаниеТипов("Структура"));
	ДеревоВариантов.Колонки.Добавить("СистемнаяИнформация",  Новый ОписаниеТипов("Структура"));
	ДеревоВариантов.Колонки.Добавить("ЭтоВариант",           Новый ОписаниеТипов("Булево"));
	ДеревоВариантов.Колонки.Добавить("ФункциональныеОпции",  Новый ОписаниеТипов("Массив"));
	ДеревоВариантов.Колонки.Добавить("ГруппироватьПоОтчету", Новый ОписаниеТипов("Булево"));
	ДеревоВариантов.Колонки.Добавить("ОсновнойВариант");
	ДеревоВариантов.Колонки.Добавить("ОпределитьНастройкиФормы", Новый ОписаниеТипов("Булево"));
	
	ВыводитьОтчетыВместоВариантов = ГлобальныеНастройки().ВыводитьОтчетыВместоВариантов;
	РазрешеноИндексирование = РазрешеноИндексированиеНеразделенныхДанных();
	
	ПодсистемыОтчетов = РазмещениеОтчетовВПодсистемах();
	ПодключеныВсеОтчеты = ПодключеныВсеОтчеты();
	Для Каждого ОтчетМетаданные Из Метаданные.Отчеты Цикл
		Если Не ПодключеныВсеОтчеты Тогда
			// Проверка подключен ли конкретный отчет.
			Если ОтчетМетаданные.ХранилищеВариантов = Неопределено
				Или ОтчетМетаданные.ХранилищеВариантов.Имя <> "ХранилищеВариантовОтчетов" Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Настройки.
		СтрокаОтчет = ДеревоВариантов.Строки.Добавить();
		СтрокаОтчет.Отчет                = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОтчетМетаданные);
		СтрокаОтчет.Метаданные           = ОтчетМетаданные;
		СтрокаОтчет.Включен              = Истина;
		СтрокаОтчет.ВидимостьПоУмолчанию = Истина;
		СтрокаОтчет.Описание             = ОтчетМетаданные.Пояснение;
		СтрокаОтчет.Наименование         = ОтчетМетаданные.Представление();
		СтрокаОтчет.ОписаниеПолучено     = Истина;
		СтрокаОтчет.ЭтоВариант           = Ложь;
		СтрокаОтчет.ГруппироватьПоОтчету = ВыводитьОтчетыВместоВариантов;
		СтрокаОтчет.СистемнаяИнформация  = Новый Структура;
		СтрокаОтчет.СистемнаяИнформация.Вставить("ПостроенНаСКД", ОтчетМетаданные.ОсновнаяСхемаКомпоновкиДанных <> Неопределено);
		
		// Подсистемы.
		Найденные = ПодсистемыОтчетов.НайтиСтроки(Новый Структура("ОтчетМетаданные", ОтчетМетаданные));
		Для Каждого СтрокаПодсистема Из Найденные Цикл
			СтрокаОтчет.Размещение.Вставить(СтрокаПодсистема.ПодсистемаМетаданные, "");
		КонецЦикла;
		
		// Поиск.
		СтрокаОтчет.НастройкиДляПоиска = Новый Структура("НаименованияПолей, НаименованияПараметровИОтборов, ИменаМакетов");
		
		// Предопределенные варианты.
		Если СтрокаОтчет.СистемнаяИнформация.ПостроенНаСКД Тогда
			ОтчетМенеджер = Отчеты[ОтчетМетаданные.Имя];
			СхемаКД = Неопределено;
			ВариантыНастроек = Неопределено;
			Попытка
				ОтчетОбъект = ОтчетМенеджер.Создать();
				СтрокаОтчет.СистемнаяИнформация.Вставить("ОтчетОбъект", ОтчетОбъект);
				СхемаКД = ОтчетОбъект.СхемаКомпоновкиДанных;
			Исключение
				ТекстОшибки = НСтр("ru = 'Не удалось прочитать схему отчета ""%1"":'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%1", ОтчетМетаданные.Имя);
				ТекстОшибки = ТекстОшибки + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ПредупреждениеПоВарианту(Неопределено, ТекстОшибки);
			КонецПопытки;
			// Чтение настроек вариантов отчета из схемы.
			Если СхемаКД <> Неопределено Тогда
				Попытка
					ВариантыНастроек = СхемаКД.ВариантыНастроек;
				Исключение
					ТекстОшибки = НСтр("ru = 'Не удалось прочитать список вариантов отчета ""%1"" из схемы:'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%1", ОтчетМетаданные.Имя);
					ТекстОшибки = ТекстОшибки + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ПредупреждениеПоВарианту(Неопределено, ТекстОшибки);
				КонецПопытки;
			КонецЕсли;
			//// Чтение настроек по умолчанию из схемы.
			//Если РазрешеноИндексирование И СхемаКД <> Неопределено Тогда
			//	Попытка
			//		ВариантПоУмолчанию.СистемнаяИнформация.Вставить("НастройкиКД", СхемаКД.НастройкиПоУмолчанию);
			//	Исключение
			//		ТекстОшибки = НСтр("ru = 'Не удалось прочитать настройки по умолчанию отчета ""%1"":'");
			//		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%1", ОтчетМетаданные.Имя);
			//		ТекстОшибки = ТекстОшибки + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			//		ПредупреждениеПоВарианту(Неопределено, ТекстОшибки);
			//	КонецПопытки;
			//КонецЕсли;
			// Чтение настроек вариантов отчета из модуля менеджера (если не получилось из схемы).
			Если ВариантыНастроек = Неопределено Тогда
				Попытка
					ВариантыНастроек = ОтчетМенеджер.ВариантыНастроек();
				Исключение
					ТекстОшибки = НСтр("ru = 'Не удалось прочитать список вариантов отчета ""%1"" из модуля менеджера:'");
					ТекстОшибки = СтрЗаменить(ТекстОшибки, "%1", ОтчетМетаданные.Имя);
					ТекстОшибки = ТекстОшибки + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ОшибкаПоВарианту(Неопределено, ТекстОшибки);
				КонецПопытки;
			КонецЕсли;
			// Регистрация найденных вариантов.
			Если ВариантыНастроек <> Неопределено Тогда
				Для Каждого ВариантНастроекКД Из ВариантыНастроек Цикл
					Вариант = СтрокаОтчет.Строки.Добавить();
					Вариант.Отчет        = СтрокаОтчет.Отчет;
					Вариант.КлючВарианта = ВариантНастроекКД.Имя;
					Вариант.Наименование = ВариантНастроекКД.Представление;
					Вариант.ЭтоВариант   = Истина;
					Если СтрокаОтчет.ОсновнойВариант = Неопределено Тогда
						СтрокаОтчет.ОсновнойВариант = Вариант;
					КонецЕсли;
					Если РазрешеноИндексирование И ТипЗнч(ВариантНастроекКД) = Тип("ВариантНастроекКомпоновкиДанных") Тогда
						Попытка
							Вариант.СистемнаяИнформация.Вставить("НастройкиКД", ВариантНастроекКД.Настройки);
						Исключение
							ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не удалось прочитать настройки варианта ""%1"" отчета ""%2"":'"),
								Вариант.КлючВарианта,
								ОтчетМетаданные.Имя)
								+ Символы.ПС
								+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
							ПредупреждениеПоВарианту(Неопределено, ТекстОшибки);
						КонецПопытки;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		// Сам отчет.
		Если СтрокаОтчет.ОсновнойВариант = Неопределено Тогда
			Вариант = СтрокаОтчет.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(Вариант, СтрокаОтчет, "Отчет, Наименование");
			Вариант.КлючВарианта = "";
			Вариант.ЭтоВариант   = Истина;
			СтрокаОтчет.ОсновнойВариант = Вариант;
		КонецЕсли;
		
	КонецЦикла;
	
	// Механизмы расширения:
	// Подключаемые обработчики подсистем БСП.
	Обработчики = ОбщегоНазначения.ОбработчикиСлужебногоСобытия("СтандартныеПодсистемы.ВариантыОтчетов\ПриНастройкеВариантовОтчетов");
	Для Каждого Обработчик Из Обработчики Цикл
		Обработчик.Модуль.ПриНастройкеВариантовОтчетов(ДеревоВариантов);
	КонецЦикла;
	
	// Переопределяемая часть.
	ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов(ДеревоВариантов);
	
	// Определение основных вариантов.
	Для Каждого СтрокаОтчет Из ДеревоВариантов.Строки Цикл
		Если СтрокаОтчет.ГруппироватьПоОтчету = Истина Тогда
			Если СтрокаОтчет.ОсновнойВариант = Неопределено
				Или Не СтрокаОтчет.ОсновнойВариант.Включен Тогда
				Для Каждого Вариант Из СтрокаОтчет.Строки Цикл
					ЗаполнитьОписаниеСтрокиВарианта(Вариант, СтрокаОтчет);
					Если Вариант.Включен Тогда
						СтрокаОтчет.ОсновнойВариант = Вариант;
						Вариант.ВидимостьПоУмолчанию = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Иначе
			СтрокаОтчет.ОсновнойВариант = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДеревоВариантов;
КонецФункции

// Заполняет описание настроек для строки варианта отчета если оно еще не заполнено.
//
// Параметры:
//   СтрокаВариант - СтрокаДерева - Описание настроек варианта отчета.
//   СтрокаОтчет   - СтрокаДерева - Необязательный. Описание настроек отчета.
//
Процедура ЗаполнитьОписаниеСтрокиВарианта(СтрокаВариант, СтрокаОтчет = Неопределено) Экспорт
	Если СтрокаВариант.ОписаниеПолучено Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрокаОтчет = Неопределено Тогда
		СтрокаОтчет = СтрокаВариант.Родитель;
	КонецЕсли;
	
	// Флажок изменения настроек
	СтрокаВариант.ОписаниеПолучено = Истина;
	
	// Копирование настроек отчета.
	ЗаполнитьЗначенияСвойств(СтрокаВариант, СтрокаОтчет, "Включен, ВидимостьПоУмолчанию, ГруппироватьПоОтчету");
	
	Если СтрокаВариант = СтрокаОтчет.ОсновнойВариант Тогда
		// Вариант "по умолчанию".
		СтрокаВариант.Описание = СтрокаОтчет.Описание;
		СтрокаВариант.ВидимостьПоУмолчанию = Истина;
	Иначе
		// Предопределенный вариант.
		Если СтрокаВариант.ГруппироватьПоОтчету Тогда
			СтрокаВариант.ВидимостьПоУмолчанию = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаВариант.Размещение = ОбщегоНазначенияКлиентСервер.СкопироватьСоответствие(СтрокаОтчет.Размещение);
	СтрокаВариант.ФункциональныеОпции = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(СтрокаОтчет.ФункциональныеОпции);
	СтрокаВариант.НастройкиДляПоиска = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтрокаОтчет.НастройкиДляПоиска);
	
КонецПроцедуры

// Устанавливает режим вывода вариантов отчета в панелях отчетов.
//
// Параметры:
//   ДеревоВариантов - ДеревоЗначений - Передается "как есть" из процедуры НастроитьВариантыОтчетов.
//   Отчет - СтрокаДереваЗначений, ОбъектМетаданных: Отчет - Описание настроек или метаданные отчета.
//   ВыводитьОтчетыВместоВариантов - Булево - Режим вывода в панели отчетов:
//       - Истина - По отчетам (варианты скрыты, а отчет включен и видим).
//       - Ложь - По вариантам (варианты видимы, а отчет отключен).
//
Процедура УстановитьРежимВыводаОтчетаВПанеляхОтчетов(ДеревоВариантов, Отчет, ВыводитьОтчетыВместоВариантов)
	Если ТипЗнч(Отчет) = Тип("СтрокаДереваЗначений") Тогда
		СтрокаОтчет = Отчет;
	Иначе
		СтрокаОтчет = ДеревоВариантов.Строки.Найти(Отчет, "Метаданные", Ложь);
		Если СтрокаОтчет = Неопределено Тогда
			ПредупреждениеПоВарианту(Неопределено, НСтр("ru = 'Отчет ""%1"" не подключен к подсистеме.'"), Отчет.Имя);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	СтрокаОтчет.ГруппироватьПоОтчету = ВыводитьОтчетыВместоВариантов;
КонецПроцедуры

// Формирует таблицу замен старых ключей вариантов на актуальные.
//
// Возвращаемое значение:
//   ТаблицаЗначений - Таблица изменений имен вариантов. Колонки:
//       * ОтчетМетаданные - ОбъектМетаданных: Отчет - Метаданные отчета, в схеме которого изменилось имя варианта.
//       * СтароеИмяВарианта - Строка - Старое имя варианта, до изменения.
//       * АктуальноеИмяВарианта - Строка - Текущее (последнее актуальное) имя варианта.
//       * Отчет - СправочникСсылка.ИдентификаторыОбъектовМетаданных, Строка - Ссылка или имя отчета, использующееся для хранения.
//
// См. также:
//   ВариантыОтчетовПереопределяемый.ЗарегистрироватьИзмененияКлючейВариантовОтчетов().
//   Служебное событие "СтандартныеПодсистемы.ВариантыОтчетов\ПриРегистрацииИзмененийИменВариантовОтчетов".
//
Функция ИзмененияКлючей()
	
	ОписаниеТиповРеквизитаКлючВарианта = Метаданные.Справочники.ВариантыОтчетов.Реквизиты.КлючВарианта.Тип;
	
	Изменения = Новый ТаблицаЗначений;
	Изменения.Колонки.Добавить("Отчет",                 Новый ОписаниеТипов("ОбъектМетаданных"));
	Изменения.Колонки.Добавить("СтароеИмяВарианта",     ОписаниеТиповРеквизитаКлючВарианта);
	Изменения.Колонки.Добавить("АктуальноеИмяВарианта", ОписаниеТиповРеквизитаКлючВарианта);
	
	// Подключаемые обработчики подсистем БСП.
	Обработчики = ОбщегоНазначения.ОбработчикиСлужебногоСобытия("СтандартныеПодсистемы.ВариантыОтчетов\ПриРегистрацииИзмененийИменВариантовОтчетов");
	Для Каждого Обработчик Из Обработчики Цикл
		Обработчик.Модуль.ПриРегистрацииИзмененийИменВариантовОтчетов(Изменения);
	КонецЦикла;
	
	// Переопределяемая часть.
	ВариантыОтчетовПереопределяемый.ЗарегистрироватьИзмененияКлючейВариантовОтчетов(Изменения);
	
	Изменения.Колонки.Отчет.Имя = "ОтчетМетаданные";
	Изменения.Колонки.Добавить("Отчет", Метаданные.Справочники.ПредопределенныеВариантыОтчетов.Реквизиты.Отчет.Тип);
	
	// Проверить корректность замен.
	Для Каждого Изменение Из Изменения Цикл
		Изменение.Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Изменение.ОтчетМетаданные);
		Найденные = Изменения.НайтиСтроки(Новый Структура("ОтчетМетаданные, СтароеИмяВарианта", Изменение.ОтчетМетаданные, Изменение.АктуальноеИмяВарианта));
		Если Найденные.Количество() > 0 Тогда
			Конфликт = Найденные[0];
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка регистрации изменений имени варианта отчета ""%1"":
				|Актуальное имя варианта ""%2"" (старое имя ""%3"")
				|так же числится как старое имя ""%4"" (актуальное имя ""%5"").'"),
				Строка(Изменение.Отчет),
				Изменение.АктуальноеИмяВарианта,
				Изменение.СтароеИмяВарианта,
				Конфликт.СтароеИмяВарианта,
				Конфликт.АктуальноеИмяВарианта);
		КонецЕсли;
		Найденные = Изменения.НайтиСтроки(Новый Структура("ОтчетМетаданные, СтароеИмяВарианта", Изменение.ОтчетМетаданные, Изменение.СтароеИмяВарианта));
		Если Найденные.Количество() > 2 Тогда
			Конфликт = Найденные[1];
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка регистрации изменений имени варианта отчета ""%1"":
				|Старое имя варианта ""%2"" (актуальное имя ""%3"")
				|так же числится как старое имя 
				|варианта отчета ""%4"" (актуальное имя ""%5"").'"),
				Строка(Изменение.Отчет),
				Изменение.СтароеИмяВарианта,
				Изменение.АктуальноеИмяВарианта,
				Строка(Конфликт.ОтчетМетаданные.Представление()),
				Конфликт.АктуальноеИмяВарианта);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Изменения;
КонецФункции

// Формирует ссылку и тип отчета по полному имени.
//
// Параметры:
//   ОтчетПолноеИмя - Строка - Полное имя отчета в формате
//       "Отчет.<ИмяОтчета>" или "ВнешнийОтчет.<ИмяОтчета>"
//
// Возвращаемое значение: 
//   Результат - Структура -
//       * Отчет
//       * ТипОтчета
//       * ОтчетИмя
//       * ОтчетМетаданные
//       * ТекстОшибки - Строка, Неопределено - Текст ошибки.
//
Функция СформироватьИнформациюОбОтчетеПоПолномуИмени(ОтчетПолноеИмя) Экспорт
	Результат = Новый Структура("Отчет, ТипОтчета, ОтчетПолноеИмя, ОтчетИмя, ОтчетМетаданные, ТекстОшибки");
	Результат.ОтчетПолноеИмя = ОтчетПолноеИмя;
	
	ПозицияТочки = Найти(ОтчетПолноеИмя, ".");
	Если ПозицияТочки = 0 Тогда
		Префикс = "";
		Результат.ОтчетИмя = ОтчетПолноеИмя;
	Иначе
		Префикс = Лев(ОтчетПолноеИмя, ПозицияТочки - 1);
		Результат.ОтчетИмя = Сред(ОтчетПолноеИмя, ПозицияТочки + 1);
	КонецЕсли;
	
	Если ВРег(Префикс) = "ОТЧЕТ" Тогда
		Результат.ОтчетМетаданные = Метаданные.Отчеты.Найти(Результат.ОтчетИмя);
		Если Результат.ОтчетМетаданные = Неопределено Тогда
			Результат.ОтчетПолноеИмя = "ВнешнийОтчет." + Результат.ОтчетИмя;
			ПредупреждениеПоВарианту(
				Неопределено,
				НСтр("ru = 'Отчет ""%1"" не найден в программе, он будет значиться как внешний.'"),
				ОтчетПолноеИмя);
		ИначеЕсли Не ПравоДоступа("Просмотр", Результат.ОтчетМетаданные) Тогда
			Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Недостаточно прав доступа к отчету ""%1"".'"),
				ОтчетПолноеИмя);
		КонецЕсли;
	ИначеЕсли ВРег(Префикс) = "ВНЕШНИЙОТЧЕТ" Тогда
		// Получение метаданных и проверок не требуется.
	Иначе
		Результат.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для отчета ""%1"" невозможно определить тип (не установлен префикс).'"),
			ОтчетПолноеИмя);
		Возврат Результат;
	КонецЕсли;
	
	Если Результат.ОтчетМетаданные = Неопределено Тогда
		
		Результат.Отчет = Результат.ОтчетПолноеИмя;
		Результат.ТипОтчета = Перечисления.ТипыОтчетов.Внешний;
		
		// Заменить тип и ссылку внешнего отчета для дополнительных отчетов, подключенных к хранилищу подсистемы.
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки") Тогда
			
			Результат.Вставить("ПодключеныВсеОтчеты", ПодключеныВсеОтчеты());
			
			МодульДополнительныеОтчетыИОбработки = ОбщегоНазначения.ОбщийМодуль("ДополнительныеОтчетыИОбработки");
			МодульДополнительныеОтчетыИОбработки.ПриОпределенииТипаИСсылкиЕслиОтчетДополнительный(Результат);
			
			Если Результат.Свойство("ОтчетДополнительный") Тогда
				Результат.ТипОтчета = Перечисления.ТипыОтчетов.Дополнительный;
				Результат.Удалить("ОтчетДополнительный");
			КонецЕсли;
			Результат.Удалить("ПодключеныВсеОтчеты");
			
		КонецЕсли;
		
	Иначе
		
		Результат.Отчет = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Результат.ОтчетМетаданные);
		Результат.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует таблицу размещения отчетов по подсистемам конфигурации.
//
// Параметры:
//   Результат          - Неопределено - Используется для рекурсии.
//   ПодсистемаРодитель - Неопределено - Используется для рекурсии.
//
// Возвращаемое значение: 
//   Результат - ТаблицаЗначений - Настройки размещения отчетов по подсистемам.
//       * ОтчетМетаданные      - ОбъектМетаданных: Отчет
//       * ОтчетПолноеИмя       - Строка
//       * ПодсистемаМетаданные - ОбъектМетаданных: Подсистема
//       * ПодсистемаПолноеИмя  - Строка
//
Функция РазмещениеОтчетовВПодсистемах(Результат = Неопределено, ПодсистемаРодитель = Неопределено) Экспорт
	Если Результат = Неопределено Тогда
		ПолноеИмяОписаниеТипов = Метаданные.Справочники.ИдентификаторыОбъектовМетаданных.Реквизиты.ПолноеИмя.Тип;
		
		Результат = Новый ТаблицаЗначений;
		Результат.Колонки.Добавить("ОтчетМетаданные",      Новый ОписаниеТипов("ОбъектМетаданных"));
		Результат.Колонки.Добавить("ОтчетПолноеИмя",       ПолноеИмяОписаниеТипов);
		Результат.Колонки.Добавить("ПодсистемаМетаданные", Новый ОписаниеТипов("ОбъектМетаданных"));
		Результат.Колонки.Добавить("ПодсистемаПолноеИмя",  ПолноеИмяОписаниеТипов);
		
		ПодсистемаРодитель = Метаданные;
	КонецЕсли;
	
	// Перебор вложенных подсистем родителя
	Для Каждого ПодсистемаМетаданные Из ПодсистемаРодитель.Подсистемы Цикл
		Если Не ПодсистемаМетаданные.ВключатьВКомандныйИнтерфейс Тогда
			Продолжить;
		КонецЕсли;
		
		// Состав подсистемы
		Для Каждого ОтчетМетаданные Из ПодсистемаМетаданные.Состав Цикл
			Если Не Метаданные.Отчеты.Содержит(ОтчетМетаданные) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицы = Результат.Добавить();
			СтрокаТаблицы.ОтчетМетаданные      = ОтчетМетаданные;
			СтрокаТаблицы.ОтчетПолноеИмя       = ОтчетМетаданные.ПолноеИмя();
			СтрокаТаблицы.ПодсистемаМетаданные = ПодсистемаМетаданные;
			СтрокаТаблицы.ПодсистемаПолноеИмя  = ПодсистемаМетаданные.ПолноеИмя();
			
		КонецЦикла;
		
		РазмещениеОтчетовВПодсистемах(Результат, ПодсистемаМетаданные);;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Сброс настроек предопределенного элемента справочника "Варианты отчетов",
//   связанного с элементом справочника "Варианты отчетов".
//
// Параметры:
//   ВариантОбъект - СправочникОбъект.ВариантыОтчетов, ДанныеФормыСтруктура
//
Функция СброситьНастройкиВариантаОтчета(ВариантОбъект) Экспорт
	Если ВариантОбъект.Пользовательский
		Или ВариантОбъект.ТипОтчета <> Перечисления.ТипыОтчетов.Внутренний
		Или Не ЗначениеЗаполнено(ВариантОбъект.ПредопределенныйВариант) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВариантОбъект.Автор = Неопределено;
	ВариантОбъект.ТолькоДляАвтора = Ложь;
	ВариантОбъект.Описание = "";
	ВариантОбъект.Размещение.Очистить();
	ВариантОбъект.ВидимостьПоУмолчаниюПереопределена = Ложь;
	Предопределенный = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ВариантОбъект.ПредопределенныйВариант,
		"Наименование, ВидимостьПоУмолчанию");
	ЗаполнитьЗначенияСвойств(ВариантОбъект, Предопределенный);
	
	Возврат Истина;
КонецФункции

// Добавляет подсистемы родителя с фильтром по правам доступа и функциональным опциям.
//
Процедура ДобавитьПодсистемыТекущегоПользователя(РодительСтрока, РодительМетаданные = Неопределено, РазделСсылка = Неопределено) Экспорт
	ДляКорневойСтроки = (РодительМетаданные = Неопределено);
	ПриоритетРодителя = ?(ДляКорневойСтроки, "", РодительСтрока.Приоритет + ".");
	Подсистемы = ?(ДляКорневойСтроки, ИспользуемыеРазделы(), РодительМетаданные.Подсистемы);
	Приоритет = 0;
	Для Каждого Подсистема Из Подсистемы Цикл
		ПодсистемаМетаданные = ?(ДляКорневойСтроки, Подсистема.Метаданные, Подсистема);
		Если ПодсистемаМетаданные.ВключатьВКомандныйИнтерфейс
			И ПравоДоступа("Просмотр", ПодсистемаМетаданные)
			И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ПодсистемаМетаданные) Тогда
			
			Приоритет = Приоритет + 1;
			
			СтрокаДерева = РодительСтрока.Строки.Добавить();
			СтрокаДерева.Ссылка        = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПодсистемаМетаданные);
			СтрокаДерева.Имя           = ПодсистемаМетаданные.Имя;
			СтрокаДерева.ПолноеИмя     = ПодсистемаМетаданные.ПолноеИмя();
			СтрокаДерева.Представление = ПодсистемаМетаданные.Представление();
			СтрокаДерева.РазделСсылка  = ?(ДляКорневойСтроки, СтрокаДерева.Ссылка, РазделСсылка);
			СтрокаДерева.Приоритет     = ПриоритетРодителя + Формат(Приоритет, "ЧЦ=4; ЧДЦ=0; ЧВН=; ЧГ=0");
			Если ДляКорневойСтроки Тогда
				СтрокаДерева.ПолноеПредставление = Подсистема.ЗаголовокПанели;
			ИначеЕсли СтрЧислоВхождений(ПриоритетРодителя, ".") > 2 Тогда
				СтрокаДерева.ПолноеПредставление = РодительСтрока.Представление + ": " + СтрокаДерева.Представление;
			Иначе
				СтрокаДерева.ПолноеПредставление = СтрокаДерева.Представление;
			КонецЕсли;
			
			ДобавитьПодсистемыТекущегоПользователя(СтрокаДерева, ПодсистемаМетаданные, СтрокаДерева.РазделСсылка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Формирует описание типов Строка заданной длины.
Функция ОписаниеТиповСтрока(ДлинаСтроки = 1000) Экспорт
	Возврат Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(ДлинаСтроки));
КонецФункции

// По составу ролей определяет полные права к данным подсистемы.
Функция ПолныеПраваНаВарианты() Экспорт
	Возврат Пользователи.РолиДоступны("ДобавлениеИзменениеВариантовОтчетов");
КонецФункции

// Проверяет свободно ли наименование варианта отчета.
Функция НаименованиеЗанято(Отчет, Ссылка, Наименование) Экспорт
	Если Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Наименование") Тогда
		Возврат Ложь; // Проверка отключается т.к. наименование не изменилось.
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Отчет = &Отчет
	|	И ВариантыОтчетов.Ссылка <> &Ссылка
	|	И ВариантыОтчетов.Наименование = &Наименование
	|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)";
	Запрос.УстановитьПараметр("Отчет",        Отчет);
	Запрос.УстановитьПараметр("Ссылка",       Ссылка);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("ОтключенныеВариантыПрограммы", ВариантыОтчетовПовтИсп.ОтключенныеВариантыПрограммы());
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Не Запрос.Выполнить().Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
КонецФункции

// Проверяет свободен ли ключ варианта отчета.
Функция КлючВариантаЗанят(Отчет, Ссылка, КлючВарианта) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Поле1
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Отчет = &Отчет
	|	И ВариантыОтчетов.Ссылка <> &Ссылка
	|	И ВариантыОтчетов.КлючВарианта = &КлючВарианта
	|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("Отчет",        Отчет);
	Запрос.УстановитьПараметр("Ссылка",       Ссылка);
	Запрос.УстановитьПараметр("КлючВарианта", КлючВарианта);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Не Запрос.Выполнить().Пустой();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
КонецФункции

// Определяет способ внедрения подсистемы.
Функция ПодключеныВсеОтчеты() Экспорт
	Возврат (Метаданные.ХранилищеВариантовОтчетов <> Неопределено И Метаданные.ХранилищеВариантовОтчетов.Имя = "ХранилищеВариантовОтчетов");
КонецФункции

// Определяет способ подключения общей формы отчета.
Функция ФормаОтчетаИспользуетсяПоУмолчанию() Экспорт
	ФормаМетаданные = Метаданные.ОсновнаяФормаОтчета;
	Возврат (ФормаМетаданные <> Неопределено И ФормаМетаданные = Метаданные.ОбщиеФормы.ФормаОтчета);
КонецФункции

// Определяет способ подключения общей формы отчета.
Функция ФормаОтчетаИспользуетсяДляОтчета(ОтчетМетаданные, ФормаОтчетаИспользуетсяПоУмолчанию = Неопределено) Экспорт
	ФормаМетаданные = ОтчетМетаданные.ОсновнаяФорма;
	Если ФормаМетаданные = Неопределено Тогда
		Если ФормаОтчетаИспользуетсяПоУмолчанию = Неопределено Тогда
			ФормаОтчетаИспользуетсяПоУмолчанию = ФормаОтчетаИспользуетсяПоУмолчанию();
		КонецЕсли;
		ОтчетПодключен = ФормаОтчетаИспользуетсяПоУмолчанию;
	Иначе
		ОтчетПодключен = (ФормаМетаданные = Метаданные.ОбщиеФормы.ФормаОтчета);
	КонецЕсли;
	Возврат ОтчетПодключен;
КонецФункции

// Создает фильтр по реквизиту КлючОбъекта для СтандартноеХранилищеНастроекМенеджер.Выбрать().
Функция НовыйФильтрПоКлючуОбъекта(ИменаОтчетов) Экспорт
	Если ИменаОтчетов = "" Или ИменаОтчетов = "*" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПозицияРазделителя = Найти(ИменаОтчетов, ",");
	Если ПозицияРазделителя = 0 Тогда
		КлючОбъекта = ИменаОтчетов;
		ИменаОтчетов = "";
	Иначе
		КлючОбъекта = СокрЛП(Лев(ИменаОтчетов, ПозицияРазделителя - 1));
		ИменаОтчетов = Сред(ИменаОтчетов, ПозицияРазделителя + 1);
	КонецЕсли;
	
	Если Найти(КлючОбъекта, ".") = 0 Тогда
		КлючОбъекта = "Отчет." + КлючОбъекта;
	КонецЕсли;
	
	Возврат Новый Структура("КлючОбъекта", КлючОбъекта);
КонецФункции

// Глобальные настройки подсистемы.
Функция ГлобальныеНастройки() Экспорт
	Результат = Новый Структура;
	Результат.Вставить("ВыводитьОтчетыВместоВариантов", Ложь);
	Результат.Вставить("ВыводитьОписания", Истина);
	
	ВариантыОтчетовПереопределяемый.ОпределитьГлобальныеНастройки(Результат);
	
	Возврат Результат;
КонецФункции

// Глобальные настройки панели отчетов.
Функция ОбщиеНастройкиПанели() Экспорт
	ОбщиеНастройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ВариантыОтчетовКлиентСервер.ПолноеИмяПодсистемы(),
		"ПанельОтчетов");
	Если ОбщиеНастройки = Неопределено Тогда
		ОбщиеНастройки = Новый Структура("ПоказатьОповещениеОПодсказках, ПоказыватьПодсказки, ИскатьВоВсехРазделах");
		ОбщиеНастройки.ПоказатьОповещениеОПодсказках = Истина;
		ОбщиеНастройки.ПоказыватьПодсказки = ?(ГлобальныеНастройки().ВыводитьОписания, 1, 0);
		ОбщиеНастройки.ИскатьВоВсехРазделах = Ложь;
	Иначе
		ОбщиеНастройки.Вставить("ПоказатьОповещениеОПодсказках", Ложь);
	КонецЕсли;
	Возврат ОбщиеНастройки;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Журнал регистрации

// Запись ошибки в журнале регистрации по варианту отчета.
Процедура ОшибкаПоВарианту(Вариант, Сообщение, Реквизит1 = Неопределено, Реквизит2 = Неопределено, Реквизит3 = Неопределено) Экспорт
	ЗаписьЖурналаРегистрации(
		ВариантыОтчетовКлиентСервер.НаименованиеПодсистемы(Неопределено),
		УровеньЖурналаРегистрации.Ошибка,
		Метаданные.Справочники.ВариантыОтчетов,
		Вариант,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Сообщение,
			Строка(Реквизит1),
			Строка(Реквизит2),
			Строка(Реквизит3)));
КонецПроцедуры

// Запись информации в журнале регистрации по варианту отчета.
Процедура ИнформацияПоВарианту(Вариант, Сообщение, Реквизит1 = Неопределено, Реквизит2 = Неопределено, Реквизит3 = Неопределено) Экспорт
	ЗаписьЖурналаРегистрации(
		ВариантыОтчетовКлиентСервер.НаименованиеПодсистемы(Неопределено),
		УровеньЖурналаРегистрации.Информация,
		Метаданные.Справочники.ВариантыОтчетов,
		Вариант,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Сообщение,
			Строка(Реквизит1),
			Строка(Реквизит2),
			Строка(Реквизит3)));
КонецПроцедуры

// Запись предупреждения в журнале регистрации по варианту отчета.
Процедура ПредупреждениеПоВарианту(Вариант, Сообщение, Реквизит1 = Неопределено, Реквизит2 = Неопределено, Реквизит3 = Неопределено) Экспорт
	ЗаписьЖурналаРегистрации(
		ВариантыОтчетовКлиентСервер.НаименованиеПодсистемы(Неопределено),
		УровеньЖурналаРегистрации.Предупреждение,
		Метаданные.Справочники.ВариантыОтчетов,
		Вариант,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Сообщение,
			Строка(Реквизит1),
			Строка(Реквизит2),
			Строка(Реквизит3)));
КонецПроцедуры

// Записывает в журнал регистрации событие запуска процедуры и открывает транзакцию.
Процедура ЗапускПроцедуры(ПредставлениеПроцедуры)
	ИнформацияПоВарианту(Неопределено, НСтр("ru = 'Запуск процедуры ""%1"".'"), ПредставлениеПроцедуры);
	НачатьТранзакцию();
КонецПроцедуры

// Записывает в журнал регистрации событие завершения процедуры и фиксирует транзакцию.
Процедура ЗавершениеПроцедуры(ПредставлениеПроцедуры, Изменено = Неопределено)
	ЗафиксироватьТранзакцию();
	Текст = НСтр("ru = 'Завершение процедуры ""%1"".'");
	Если Изменено <> Неопределено Тогда
		Текст = Текст + " " + НСтр("ru = 'Изменено %2 объектов.'");
	КонецЕсли;
	ИнформацияПоВарианту(Неопределено, Текст, ПредставлениеПроцедуры, Изменено);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обработчики стандартных событий

// Удаление личных вариантов отчетов при удалении пользователя
Процедура ПриУдаленииПользователя(ПользовательОбъект, Отказ) Экспорт
	Если ПользовательОбъект.ЭтоНовый()
		Или ПользовательОбъект.ОбменДанными.Загрузка
		Или Отказ
		Или Не ПользовательОбъект.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	// Установить пометку удаления личных вариантов пользователя.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВариантыОтчетов.Ссылка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Автор = &ПользовательСсылка
	|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|	И ВариантыОтчетов.ТолькоДляАвтора = ИСТИНА";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПользовательСсылка", ПользовательОбъект.Ссылка);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.ДополнительныеСвойства.Вставить("ИндексироватьСхему", Ложь);
		ВариантОбъект.УстановитьПометкуУдаления(Истина);
	КонецЦикла;
КонецПроцедуры

// Удаление ссылок подсистем перед их удалением.
Процедура ПередУдалениемИдентификатораОбъектаМетаданных(ИдентификаторОбъектаМетаданныхОбъект, Отказ) Экспорт
	Если ИдентификаторОбъектаМетаданныхОбъект.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Подсистема = ИдентификаторОбъектаМетаданныхОбъект.Ссылка;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВариантыОтчетов.Ссылка
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.Размещение.Подсистема = &Подсистема";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Подсистема", Подсистема);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Найденные = ВариантОбъект.Размещение.НайтиСтроки(Новый Структура("Подсистема", Подсистема));
		Для Каждого СтрокаТаблицы Из Найденные Цикл
			ВариантОбъект.Размещение.Удалить(СтрокаТаблицы);
		КонецЦикла;
		
		ВариантОбъект.Записать();
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы

// [*] Актуализирует неразделенные данные: справочник ПредопределенныеВариантыОтчетов и константу ПараметрыВариантовОтчетов.
Процедура ОбновитьОбщиеДанные(ПараметрыОбновления) Экспорт
	
	////////////////////////////////////////////////////////////////////////////////
	// Выполняется только для предопределенных вариантов отчетов.
	// План обновления:
	
	Кэш = Новый Структура;
	Кэш.Вставить("ИспользуемыеРазделы", ИспользуемыеРазделы());
	Кэш.Вставить("ПодключеныВсеОтчеты", ПодключеныВсеОтчеты());
	Кэш.Вставить("ДеревоВариантов", ДеревоНастроекВариантовОтчетовКонфигурации());
	Кэш.Вставить("ОбновитьОбласти", Ложь);
	
	////////////////////////////////////////////////////////////////////////////////
	// 1. Заменить устаревшие ключи вариантов отчетов на актуальные.
	АктуализироватьКлючиПредопределенных(Кэш);
	
	////////////////////////////////////////////////////////////////////////////////
	// 2. Актуализировать предопределенные варианты отчетов и перезаписать
	//    константу, в которой хранятся привязки к функциональным опциям.
	ОбновитьНастройкиПредопределенных(Кэш);
	
	////////////////////////////////////////////////////////////////////////////////
	// 3. Установить пометку удаления вариантов удаленных отчетов.
	ПометитьНаУдалениеВариантыУдаленныхОтчетов(Кэш, Истина);
	
	////////////////////////////////////////////////////////////////////////////////
	// 4. Обновить разделенные данные в модели сервиса.
	Если Кэш.ОбновитьОбласти И ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Обработчик = ПараметрыОбновления.РазделенныеОбработчики.Добавить();
		Обработчик.РежимВыполнения = "Оперативно";
		Обработчик.Версия    = "*";
		Обработчик.Процедура = "ВариантыОтчетов.ОбновитьРазделенныеДанные";
		Обработчик.Приоритет = 70;
	КонецЕсли;
	
КонецПроцедуры

// [*] Актуализирует разделенные данные: справочник ВариантыОтчетов.
Процедура ОбновитьРазделенныеДанные() Экспорт
	
	////////////////////////////////////////////////////////////////////////////////
	// План обновления:
	
	////////////////////////////////////////////////////////////////////////////////
	// 1. Актуализировать разделенные варианты отчетов.
	АктуализироватьСоставОбласти();
	
	////////////////////////////////////////////////////////////////////////////////
	// 2. Установить пометку удаления вариантов удаленных отчетов.
	ПометитьНаУдалениеВариантыУдаленныхОтчетов(Неопределено, Ложь);
	
КонецПроцедуры

// [2.1.1.1] Выполняет перенос данных справочника "Варианты отчетов" для редакции 2.1.
Процедура ПерейтиКРедакции21() Экспорт
	ПредставлениеПроцедуры = НСтр("ru = 'Перейти к редакции 2.1'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВариантыОтчетов.Ссылка,
	|	ВариантыОтчетов.УдалитьКлючОбъекта КАК ОтчетПолноеИмя
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	ВариантыОтчетов.УдалитьКлючОбъекта <> """"";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// Сформировать информацию об отчете
		ОтчетИнформация = СформироватьИнформациюОбОтчетеПоПолномуИмени(Выборка.ОтчетПолноеИмя);
		
		// Проверить результат
		Если ТипЗнч(ОтчетИнформация.ТекстОшибки) = Тип("Строка") Тогда
			ОшибкаПоВарианту(Выборка.Ссылка, ОтчетИнформация.ТекстОшибки);
			Продолжить;
		КонецЕсли;
		
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Если ВариантОбъект.ТипОтчета = Перечисления.ТипыОтчетов.УдалитьПользовательский
			Или ВариантОбъект.ТипОтчета = Перечисления.ТипыОтчетов.Внешний Тогда
			ВариантОбъект.Пользовательский = Истина;
		Иначе
			ВариантОбъект.Пользовательский = Ложь;
		КонецЕсли;
		
		ВариантОбъект.Отчет = ОтчетИнформация.Отчет;
		ВариантОбъект.ТипОтчета = ОтчетИнформация.ТипОтчета;
		
		Если ОтчетИнформация.ТипОтчета = Перечисления.ТипыОтчетов.Внешний Тогда
			// Установка настроек варианта внешнего отчета, характерных для всех вариантов внешних отчетов.
			// Все варианты внешних отчетов являются пользовательскими поскольку
			// предопределенные варианты внешних отчетов не регистрируются в системе,
			// а зачитываются каждый раз динамически.
			ВариантОбъект.Пользовательский = Истина;
			
			// Варианты внешних отчетов не могут открываться из панели отчетов.
			ВариантОбъект.Размещение.Очистить();
			
		Иначе
			
			// Замена полных имен подсистем на ссылки справочника "Идентификаторы объектов метаданных".
			Редакция21ПривестиНастройкиПоРазделам(ВариантОбъект);
			
			// Перенос пользовательских настроек из табличной части в регистр сведений.
			Редакция21ПеренестиПользовательскиеНастройкиВРегистр(ВариантОбъект);
			
		КонецЕсли;
		
		// Варианты поставляются без автора.
		Если Не ВариантОбъект.Пользовательский Тогда
			ВариантОбъект.Автор = Неопределено;
		КонецЕсли;
		
		ВариантОбъект.УдалитьКлючОбъекта = "";
		ВариантОбъект.УдалитьПредставлениеОбъекта = "";
		ВариантОбъект.УдалитьИсключенияБыстрогоДоступа.Очистить();
		ЗаписатьПредопределенный(ВариантОбъект);
	КонецЦикла;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
КонецПроцедуры

// [2.1.3.6] Заполняет ссылки предопределенных элементов справочника "Варианты отчетов".
Процедура ЗаполнитьСсылкиПредопределенных() Экспорт
	ПредставлениеПроцедуры = НСтр("ru = 'Заполнить ссылки предопределенных вариантов отчетов'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	// Составить таблицу замен старых ключей вариантов на актуальные.
	Изменения = ИзмененияКлючей();
	
	// Получить ссылки вариантов отчетов для замены ключей,
	//   исключив из списка замен те варианты отчетов,
	//   актуальные ключи которых уже зарегистрированы,
	//   или старые ключи которых уже не заняты.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Изменения.Отчет КАК Отчет,
	|	Изменения.СтароеИмяВарианта КАК СтароеИмяВарианта,
	|	Изменения.АктуальноеИмяВарианта КАК АктуальноеИмяВарианта
	|ПОМЕСТИТЬ втИзменения
	|ИЗ
	|	&Изменения КАК Изменения
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Отчет,
	|	СтароеИмяВарианта,
	|	АктуальноеИмяВарианта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВариантыОтчетов.Ссылка КАК Ссылка,
	|	ВЫРАЗИТЬ(ВариантыОтчетов.Отчет КАК Справочник.ИдентификаторыОбъектовМетаданных) КАК Отчет,
	|	ЕСТЬNULL(втИзменения.АктуальноеИмяВарианта, ВариантыОтчетов.КлючВарианта) КАК АктуальныйКлючВарианта
	|ПОМЕСТИТЬ втАктуальные
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИзменения КАК втИзменения
	|		ПО ВариантыОтчетов.Отчет = втИзменения.Отчет
	|			И ВариантыОтчетов.КлючВарианта = втИзменения.СтароеИмяВарианта
	|ГДЕ
	|	ВариантыОтчетов.Пользовательский = ЛОЖЬ
	|	И ВариантыОтчетов.ТипОтчета = &ТипОтчета
	|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|	И ВариантыОтчетов.ПредопределенныйВариант = &ПустойПредопределенный
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Отчет,
	|	АктуальныйКлючВарианта,
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втАктуальные.Ссылка,
	|	ПредопределенныеВариантыОтчетов.Наименование,
	|	ПредопределенныеВариантыОтчетов.КлючВарианта,
	|	ЕСТЬNULL(ПредопределенныеВариантыОтчетов.Ссылка, НЕОПРЕДЕЛЕНО) КАК ПредопределенныйВариант
	|ИЗ
	|	втАктуальные КАК втАктуальные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ПредопределенныеВариантыОтчетов
	|		ПО втАктуальные.Отчет = ПредопределенныеВариантыОтчетов.Отчет
	|			И втАктуальные.АктуальныйКлючВарианта = ПредопределенныеВариантыОтчетов.КлючВарианта";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Изменения", Изменения);
	Запрос.УстановитьПараметр("ТипОтчета", Перечисления.ТипыОтчетов.Внутренний);
	Запрос.УстановитьПараметр("ПустойПредопределенный", Справочники.ПредопределенныеВариантыОтчетов.ПустаяСсылка());
	Запрос.Текст = ТекстЗапроса;
	
	// Заменить имена вариантов на ссылки.
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.ДополнительныеСвойства.Вставить("ЗаполнениеПредопределенных", Истина);
		ВариантОбъект.ДополнительныеСвойства.Вставить("ИндексироватьСхему", Ложь);
		Если ЗначениеЗаполнено(Выборка.ПредопределенныйВариант) Тогда
			ЗаполнитьЗначенияСвойств(ВариантОбъект, Выборка, "Наименование, КлючВарианта, ПредопределенныйВариант");
			Найденные = ВариантОбъект.Размещение.НайтиСтроки(Новый Структура("УдалитьПредопределенная", Истина));
			Для Каждого СтрокаТаблицы Из Найденные Цикл
				ВариантОбъект.Размещение.Удалить(СтрокаТаблицы);
			КонецЦикла;
			ВариантОбъект.Описание = "";
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВариантОбъект);
		Иначе
			ВариантОбъект.УстановитьПометкуУдаления(Истина);
		КонецЕсли;
	КонецЦикла;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
	
КонецПроцедуры

// Частичное индекса поиска предопределенных вариантов отчетов - только по изменениям в настройках вариантов.
Процедура ОтложенноеИнкрементальноеОбновлениеОбщихДанных(Параметры = Неопределено) Экспорт
	ОтложенноеОбновлениеОбщихДанных(Ложь);
КонецПроцедуры

// Полное обновление индекса поиска предопределенных вариантов отчетов.
Процедура ОтложенноеПолноеОбновлениеОбщихДанных(Параметры = Неопределено) Экспорт
	ОтложенноеОбновлениеОбщихДанных(Истина);
КонецПроцедуры

// Обновление индекса поиска предопределенных вариантов отчетов.
Процедура ОтложенноеОбновлениеОбщихДанных(Полное)
	Если Не РазрешеноИндексированиеНеразделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	Если Полное Тогда
		ПредставлениеПроцедуры = НСтр("ru = 'Отложенное обновление общих данных (полное)'");
	Иначе
		ПредставлениеПроцедуры = НСтр("ru = 'Отложенное обновление общих данных (по изменениям)'");
	КонецЕсли;
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	ДеревоВариантов = ДеревоНастроекВариантовОтчетовКонфигурации();
	
	СтарыеСведения = Новый Структура("ХешНастроек, НаименованияПолей, НаименованияПараметровИОтборов");
	Запрос = Новый Запрос("Выбрать Ссылка Из Справочник.ПредопределенныеВариантыОтчетов Где ПометкаУдаления = Ложь");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.ДополнительныеСвойства.Вставить("ДеревоВариантов", ДеревоВариантов);
		ЗаполнитьЗначенияСвойств(СтарыеСведения, ВариантОбъект);
		ВариантОбъект.НаименованияПолей = "";
		ВариантОбъект.НаименованияПараметровИОтборов = "";
		Если Полное Тогда // Переиндексировать принудительно, без проверки хеш-суммы.
			ВариантОбъект.ДополнительныеСвойства.Вставить("ИндексироватьСхему", Истина);
		КонецЕсли;
		СхемаПроиндексирована = ПроиндексироватьСодержимоеСхемы(ВариантОбъект);
		Если Полное Или (СхемаПроиндексирована
			И ИзменилисьНастройкиПоискаПредопределенного(ВариантОбъект, СтарыеСведения)) Тогда
			ЗаписатьПредопределенный(ВариантОбъект);
		КонецЕсли;
	КонецЦикла;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
КонецПроцедуры

// Полное обновление индекса поиска вариантов отчетов.
Процедура ОтложенноеПолноеОбновлениеРазделенныхДанных(Параметры = Неопределено) Экспорт
	ПредставлениеПроцедуры = НСтр("ru = 'Отложенное обновление разделенных данных (полное)'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	КэшОтчетов = Новый Соответствие;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Ссылка,
	|	Таблица.Отчет
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК Таблица
	|ГДЕ
	|	(Таблица.ПредопределенныйВариант = &ПустаяСсылка
	|			ИЛИ Таблица.ПредопределенныйВариант = НЕОПРЕДЕЛЕНО
	|			ИЛИ Таблица.ПредопределенныйВариант.ПометкаУдаления = ЛОЖЬ)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПустаяСсылка", Справочники.ПредопределенныеВариантыОтчетов.ПустаяСсылка());
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОтчетОбъект = КэшОтчетов.Получить(Выборка.Отчет); // Чтение кэша.
		Если ОтчетОбъект = Неопределено Тогда // Запись в кэш.
			РезультатПодключения = ПодключитьОтчетОбъект(Выборка.Отчет);
			Если РезультатПодключения.Подключен Тогда
				ОтчетОбъект = РезультатПодключения.Объект;
			Иначе // Отчет не найден.
				ОшибкаПоВарианту(Выборка.Ссылка, РезультатПодключения.Ошибки);
				ОтчетОбъект = "";
			КонецЕсли;
			КэшОтчетов.Вставить(Выборка.Отчет, ОтчетОбъект);
		КонецЕсли;
		Если ОтчетОбъект = "" Тогда
			Продолжить;
		КонецЕсли;
		
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.ДополнительныеСвойства.Вставить("ИндексироватьСхему", Истина);
		ВариантОбъект.ДополнительныеСвойства.Вставить("ОтчетОбъект", ОтчетОбъект);
		ЗаписатьПредопределенный(ВариантОбъект);
	КонецЦикла;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
КонецПроцедуры

// [2.2.3.30] Уменьшает количество быстрых настроек в пользовательских вариантах отчетов до 2 шт.
Процедура СократитьКоличествоБыстрыхНастроек(Параметры = Неопределено) Экспорт
	ПредставлениеПроцедуры = НСтр("ru = 'Сокращение количества быстрых настроек в отчетах'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	Записано = 0;
	
	КэшОтчетов = Новый Соответствие;
	
	ИспользуетсяПоУмолчанию = ФормаОтчетаИспользуетсяПоУмолчанию();
	
	Запрос = Новый Запрос("ВЫБРАТЬ Ссылка, Отчет ИЗ Справочник.ВариантыОтчетов ГДЕ Пользовательский И ТипОтчета <> &Внешний");
	Запрос.УстановитьПараметр("Внешний", Перечисления.ТипыОтчетов.Внешний);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОтчетОбъект = КэшОтчетов.Получить(Выборка.Отчет); // Чтение кэша.
		Если ОтчетОбъект = Неопределено Тогда // Запись в кэш.
			РезультатПодключения = ПодключитьОтчетОбъект(Выборка.Отчет);
			Если РезультатПодключения.Подключен Тогда
				ОтчетОбъект = РезультатПодключения.Объект;
				ОтчетМетаданные = РезультатПодключения.Метаданные;
				Если Не ФормаОтчетаИспользуетсяДляОтчета(ОтчетМетаданные, ИспользуетсяПоУмолчанию) Тогда
					// Отчет не подключен к общей форме отчета. Уменьшение количества быстрых настроек должно выполняться прикладным кодом.
					ОтчетОбъект = "";
				КонецЕсли;
			Иначе // Отчет не найден.
				ОшибкаПоВарианту(Выборка.Ссылка, РезультатПодключения.Ошибки);
				ОтчетОбъект = "";
			КонецЕсли;
			КэшОтчетов.Вставить(Выборка.Отчет, ОтчетОбъект);
		КонецЕсли;
		Если ОтчетОбъект = "" Тогда
			Продолжить;
		КонецЕсли;
		
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ТребуетсяЗапись = УменьшитьКоличествоБыстрыхНастроек(ВариантОбъект, ОтчетОбъект);
		Если Не ТребуетсяЗапись Тогда
			Продолжить;
		КонецЕсли;
		ВариантОбъект.ДополнительныеСвойства.Вставить("ИндексироватьСхему", Ложь);
		ВариантОбъект.ДополнительныеСвойства.Вставить("ОтчетОбъект", ОтчетОбъект);
		ЗаписатьПредопределенный(ВариантОбъект);
		Записано = Записано + 1;
	КонецЦикла;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры, Записано);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы / Начальное заполнение и актуализация справочников

// Замена старых ключей вариантов отчетов на актуальные
Процедура АктуализироватьКлючиПредопределенных(Кэш)
	ПредставлениеПроцедуры = НСтр("ru = 'Обновить ключи вариантов отчетов'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	// Составить таблицу замен старых ключей вариантов на актуальные.
	Изменения = ИзмененияКлючей();
	
	// Получить ссылки вариантов отчетов для замены ключей,
	//   исключив из списка замен те варианты отчетов,
	//   актуальные ключи которых уже зарегистрированы,
	//   или старые ключи которых уже не заняты.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Изменения.Отчет,
	|	Изменения.СтароеИмяВарианта,
	|	Изменения.АктуальноеИмяВарианта
	|ПОМЕСТИТЬ втИзменения
	|ИЗ
	|	&Изменения КАК Изменения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втИзменения.Отчет,
	|	втИзменения.АктуальноеИмяВарианта,
	|	ВариантыОтчетовСтарые.Ссылка
	|ИЗ
	|	втИзменения КАК втИзменения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ВариантыОтчетовАктуальные
	|		ПО втИзменения.Отчет = ВариантыОтчетовАктуальные.Отчет
	|			И втИзменения.АктуальноеИмяВарианта = ВариантыОтчетовАктуальные.КлючВарианта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК ВариантыОтчетовСтарые
	|		ПО втИзменения.Отчет = ВариантыОтчетовСтарые.Отчет
	|			И втИзменения.СтароеИмяВарианта = ВариантыОтчетовСтарые.КлючВарианта
	|ГДЕ
	|	ВариантыОтчетовАктуальные.Ссылка ЕСТЬ NULL 
	|	И Не ВариантыОтчетовСтарые.Ссылка ЕСТЬ NULL ";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Изменения", Изменения);
	Запрос.Текст = ТекстЗапроса;
	
	// Заменить старые имена вариантов на актуальные.
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Кэш.ОбновитьОбласти = Истина;
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.КлючВарианта = Выборка.АктуальноеИмяВарианта;
		ЗаписатьПредопределенный(ВариантОбъект);
	КонецЦикла;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
КонецПроцедуры

// Актуализация предопределенных вариантов отчетов
Процедура ОбновитьНастройкиПредопределенных(Кэш)
	ПредставлениеПроцедуры = НСтр("ru = 'Обновить предопределенные'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	ТаблицаФункциональныхОпций = Новый ТаблицаЗначений;
	ТаблицаФункциональныхОпций.Колонки.Добавить("Отчет", Новый ОписаниеТипов("СправочникСсылка.ИдентификаторыОбъектовМетаданных"));
	ТаблицаФункциональныхОпций.Колонки.Добавить("ПредопределенныйВариант", Новый ОписаниеТипов("СправочникСсылка.ПредопределенныеВариантыОтчетов"));
	ТаблицаФункциональныхОпций.Колонки.Добавить("ИмяФункциональнойОпции", Новый ОписаниеТипов("Строка"));
	Кэш.Вставить("ТаблицаФункциональныхОпций", ТаблицаФункциональныхОпций);
	
	ОтчетыСНастройкамиСписок = Новый СписокЗначений;
	
	Кэш.ДеревоВариантов.Колонки.Добавить("НайденВБазеДанных", Новый ОписаниеТипов("Булево"));
	Кэш.ДеревоВариантов.Колонки.Добавить("ВариантИзБазы", Новый ОписаниеТипов("СтрокаТаблицыЗначений"));
	Кэш.ДеревоВариантов.Колонки.Добавить("ВариантРодитель", Новый ОписаниеТипов("СправочникСсылка.ПредопределенныеВариантыОтчетов"));
	ПоискВарианта = Новый Структура("Отчет, КлючВарианта, НайденВБазеДанных, ЭтоВариант", , , Ложь, Истина);
	ПустаяСсылка = Справочники.ПредопределенныеВариантыОтчетов.ПустаяСсылка();
	
	// Сопоставление информации из базы и из метаданных и пометка на удаление устаревших объектов из базы.
	Запрос = Новый Запрос("ВЫБРАТЬ * ИЗ Справочник.ПредопределенныеВариантыОтчетов УПОРЯДОЧИТЬ ПО ПометкаУдаления");
	ТаблицаПредопределенные = Запрос.Выполнить().Выгрузить();
	Для Каждого ВариантИзБазы Из ТаблицаПредопределенные Цикл
		ЗаполнитьЗначенияСвойств(ПоискВарианта, ВариантИзБазы, "Отчет, КлючВарианта");
		Найденные = Кэш.ДеревоВариантов.Строки.НайтиСтроки(ПоискВарианта, Истина);
		Если Найденные.Количество() = 0 Тогда
			Если ВариантИзБазы.ПометкаУдаления И ВариантИзБазы.Родитель <> ПустаяСсылка Тогда
				Продолжить; // Действие не требуется.
			КонецЕсли;
			ВариантОбъект = ВариантИзБазы.Ссылка.ПолучитьОбъект();
			ВариантОбъект.ПометкаУдаления = Истина;
			ВариантОбъект.Родитель = ПустаяСсылка;
			ЗаписатьПредопределенный(ВариантОбъект);
			Кэш.ОбновитьОбласти = Истина;
		Иначе
			ОписаниеВарианта = Найденные[0];
			ЗаполнитьОписаниеСтрокиВарианта(ОписаниеВарианта);
			ОписаниеВарианта.НайденВБазеДанных = Истина;
			ОписаниеВарианта.ВариантИзБазы = ВариантИзБазы;
		КонецЕсли;
	КонецЦикла;
	
	// Добавление/обновление информации в базе данных.
	Для Каждого ОписаниеОтчета Из Кэш.ДеревоВариантов.Строки Цикл
		ОсновнойВариантСсылка = ПустаяСсылка;
		ОсновнойВариант = ОписаниеОтчета.ОсновнойВариант;
		Если ТипЗнч(ОсновнойВариант) = Тип("СтрокаДереваЗначений") Тогда
			ЗаполнитьОписаниеСтрокиВарианта(ОсновнойВариант);
			ОсновнойВариант.ВариантРодитель = ПустаяСсылка;
			ОсновнойВариантСсылка = ОбновитьПредопределенный(Кэш, ОсновнойВариант); // Вариант без родителя.
		КонецЕсли;
		Если ОписаниеОтчета.ОпределитьНастройкиФормы Тогда
			ОтчетыСНастройкамиСписок.Добавить(ОписаниеОтчета.Отчет);
		КонецЕсли;
		Для Каждого ОписаниеВарианта Из ОписаниеОтчета.Строки Цикл
			ЗаполнитьОписаниеСтрокиВарианта(ОписаниеВарианта);
			Если ОписаниеВарианта = ОсновнойВариант Тогда
				ВариантСсылка = ОсновнойВариантСсылка;
			Иначе
				ОписаниеВарианта.ВариантРодитель = ОсновнойВариантСсылка;
				ВариантСсылка = ОбновитьПредопределенный(Кэш, ОписаниеВарианта);
			КонецЕсли;
			Для Каждого ИмяФункциональнойОпции Из ОписаниеВарианта.ФункциональныеОпции Цикл
				СвязьСФункциональнойОпцией = ТаблицаФункциональныхОпций.Добавить();
				СвязьСФункциональнойОпцией.Отчет                   = ОписаниеВарианта.Отчет;
				СвязьСФункциональнойОпцией.ПредопределенныйВариант = ВариантСсылка;
				СвязьСФункциональнойОпцией.ИмяФункциональнойОпции  = ИмяФункциональнойОпции;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	// Перезапись константы только в случае если обработчик обновления был запущен "для записи" (в монопольном режиме).
	ТаблицаФункциональныхОпций.Сортировать("Отчет, ПредопределенныйВариант, ИмяФункциональнойОпции");
	ОтчетыСНастройкамиСписок.СортироватьПоЗначению();
	ОтчетыСНастройками = ОтчетыСНастройкамиСписок.ВыгрузитьЗначения();
	
	НовоеЗначение = Новый Структура;
	НовоеЗначение.Вставить("ТаблицаФункциональныхОпций", ТаблицаФункциональныхОпций);
	НовоеЗначение.Вставить("ОтчетыСНастройками", ОтчетыСНастройками);
	
	СтароеЗначение = Константы.ПараметрыВариантовОтчетов.Получить().Получить();
	Если ОбщегоНазначения.ЗначениеВСтрокуXML(НовоеЗначение) <> ОбщегоНазначения.ЗначениеВСтрокуXML(СтароеЗначение) Тогда
		КонстантаОбъект = Константы.ПараметрыВариантовОтчетов.СоздатьМенеджерЗначения();
		КонстантаОбъект.Значение = Новый ХранилищеЗначения(НовоеЗначение, Новый СжатиеДанных(9));
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(КонстантаОбъект, Ложь, Ложь);
	КонецЕсли;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
КонецПроцедуры

// Записывает настройки варианта в данные справочника.
Функция ОбновитьПредопределенный(Кэш, ОписаниеВарианта)
	Если ОписаниеВарианта.НайденВБазеДанных Тогда
		ВариантИзБазы = ОписаниеВарианта.ВариантИзБазы;
		Если ИзменилисьКлючевыеНастройкиПредопределенного(ОписаниеВарианта, ВариантИзБазы) Тогда
			Кэш.ОбновитьОбласти = Истина; // Перезапись с обновлением разделенных данных.
		ИначеЕсли ИзменилисьВторостепенныеНастройкиПредопределенного(ОписаниеВарианта, ВариантИзБазы) Тогда
			// Перезапись без обновления разделенных данных.
		Иначе
			Возврат ВариантИзБазы.Ссылка;
		КонецЕсли;
		
		ВариантОбъект = ОписаниеВарианта.ВариантИзБазы.Ссылка.ПолучитьОбъект();
		ВариантОбъект.Размещение.Очистить();
		Если ВариантОбъект.ПометкаУдаления Тогда
			ВариантОбъект.ПометкаУдаления = Ложь;
		КонецЕсли;
	Иначе
		Кэш.ОбновитьОбласти = Истина; // Регистрация нового.
		ВариантОбъект = Справочники.ПредопределенныеВариантыОтчетов.СоздатьЭлемент();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ВариантОбъект, ОписаниеВарианта, "Отчет, КлючВарианта, Наименование, Включен, ВидимостьПоУмолчанию, Описание, ГруппироватьПоОтчету");
	
	ВариантОбъект.Родитель = ОписаниеВарианта.ВариантРодитель;
	
	Для Каждого КлючИЗначение Из ОписаниеВарианта.Размещение Цикл
		СтрокаРазмещения = ВариантОбъект.Размещение.Добавить();
		СтрокаРазмещения.Подсистема = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(КлючИЗначение.Ключ);
		СтрокаРазмещения.Важный  = (НРег(КлючИЗначение.Значение) = НРег("Важный"));
		СтрокаРазмещения.СмТакже = (НРег(КлючИЗначение.Значение) = НРег("СмТакже"));
	КонецЦикла;
	
	ЗаписатьПредопределенный(ВариантОбъект);
	
	Возврат ВариантОбъект.Ссылка;
КонецФункции

// Определяет изменились ли ключевые настройки предопределенного варианта отчета.
Функция ИзменилисьКлючевыеНастройкиПредопределенного(ОписаниеВарианта, ВариантИзБазы)
	Если ВариантИзБазы.ПометкаУдаления = Истина // Описание получено => требуется снять пометку удаления.
		Или ВариантИзБазы.Наименование <> ОписаниеВарианта.Наименование
		Или ВариантИзБазы.Родитель <> ОписаниеВарианта.ВариантРодитель
		Или ВариантИзБазы.ВидимостьПоУмолчанию <> ОписаниеВарианта.ВидимостьПоУмолчанию Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

// Определяет изменились ли второстепенные настройки предопределенного варианта отчета.
Функция ИзменилисьВторостепенныеНастройкиПредопределенного(ОписаниеВарианта, ВариантИзБазы)
	// Шапка
	Если ВариантИзБазы.Включен <> ОписаниеВарианта.Включен
		Или ВариантИзБазы.Описание <> ОписаниеВарианта.Описание
		Или ВариантИзБазы.ГруппироватьПоОтчету <> ОписаниеВарианта.ГруппироватьПоОтчету Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Таблица "Размещение"
	ТаблицаРазмещения = ВариантИзБазы.Размещение;
	Если ТаблицаРазмещения.Количество() <> ОписаниеВарианта.Размещение.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из ОписаниеВарианта.Размещение Цикл
		Подсистема = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(КлючИЗначение.Ключ);
		СтрокаРазмещения = ТаблицаРазмещения.Найти(Подсистема, "Подсистема");
		Если СтрокаРазмещения = Неопределено
			Или СтрокаРазмещения.Важный <> (НРег(КлючИЗначение.Значение) = НРег("Важный"))
			Или СтрокаРазмещения.СмТакже <> (НРег(КлючИЗначение.Значение) = НРег("СмТакже")) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

// Определяет изменились ли настройки поиска предопределенного варианта отчета.
Функция ИзменилисьНастройкиПоискаПредопределенного(ВариантИзБазы, СтарыеСведения)
	Если ВариантИзБазы.ХешНастроек <> СтарыеСведения.ХешНастроек
		Или ВариантИзБазы.НаименованияПолей <> СтарыеСведения.НаименованияПолей
		Или ВариантИзБазы.НаименованияПараметровИОтборов <> СтарыеСведения.НаименованияПараметровИОтборов Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

// Приводит разделенные данные в соответствие с неразделенными данными.
Процедура АктуализироватьСоставОбласти()
	
	ПредставлениеПроцедуры = НСтр("ru = 'Обновить разделенные вариантов отчетов'");
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	// Обновление сведений предопределенных вариантов.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПредопределенныеВариантыОтчетов.Ссылка КАК ПредопределенныйВариант,
	|	ПредопределенныеВариантыОтчетов.Наименование КАК Наименование,
	|	ПредопределенныеВариантыОтчетов.Отчет КАК Отчет,
	|	ПредопределенныеВариантыОтчетов.ГруппироватьПоОтчету КАК ГруппироватьПоОтчету,
	|	ПредопределенныеВариантыОтчетов.КлючВарианта КАК КлючВарианта,
	|	ПредопределенныеВариантыОтчетов.ВидимостьПоУмолчанию КАК ВидимостьПоУмолчанию,
	|	ПредопределенныеВариантыОтчетов.Родитель КАК Родитель
	|ПОМЕСТИТЬ втПредопределенные
	|ИЗ
	|	Справочник.ПредопределенныеВариантыОтчетов КАК ПредопределенныеВариантыОтчетов
	|ГДЕ
	|	ПредопределенныеВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВариантыОтчетов.Ссылка,
	|	ВариантыОтчетов.ПометкаУдаления,
	|	ВариантыОтчетов.Отчет,
	|	ВариантыОтчетов.ТипОтчета,
	|	ВариантыОтчетов.КлючВарианта,
	|	ВариантыОтчетов.Наименование,
	|	ВариантыОтчетов.ПредопределенныйВариант,
	|	ВариантыОтчетов.ВидимостьПоУмолчанию,
	|	ВариантыОтчетов.Родитель
	|ПОМЕСТИТЬ втВариантыОтчетов
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|ГДЕ
	|	(ВариантыОтчетов.ТипОтчета = ЗНАЧЕНИЕ(Перечисление.ТипыОтчетов.Внутренний)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ВариантыОтчетов.Отчет) = ТИП(Справочник.ИдентификаторыОбъектовМетаданных))
	|	И ВариантыОтчетов.Пользовательский = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА втПредопределенные.ПредопределенныйВариант ЕСТЬ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УстановитьПометкуУдаления,
	|	ВЫБОР
	|		КОГДА втВариантыОтчетов.Ссылка ЕСТЬ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СоздатьНовый,
	|	втПредопределенные.ПредопределенныйВариант КАК ПредопределенныйВариант,
	|	втПредопределенные.Наименование КАК Наименование,
	|	втПредопределенные.Отчет КАК Отчет,
	|	втПредопределенные.КлючВарианта КАК КлючВарианта,
	|	втПредопределенные.ГруппироватьПоОтчету КАК ГруппироватьПоОтчету,
	|	втПредопределенные.Родитель КАК ПредопределенныйВариантРодитель,
	|	втПредопределенные.ВидимостьПоУмолчанию КАК ВидимостьПоУмолчанию,
	|	втВариантыОтчетов.Ссылка КАК РеквизитСсылка,
	|	втВариантыОтчетов.Родитель КАК РеквизитРодитель,
	|	втВариантыОтчетов.ТипОтчета КАК РеквизитТипОтчета,
	|	втВариантыОтчетов.Отчет КАК РеквизитОтчет,
	|	втВариантыОтчетов.КлючВарианта КАК РеквизитКлючВарианта,
	|	втВариантыОтчетов.Наименование КАК РеквизитНаименование,
	|	втВариантыОтчетов.ПредопределенныйВариант КАК РеквизитПредопределенныйВариант,
	|	втВариантыОтчетов.ПометкаУдаления КАК РеквизитПометкаУдаления,
	|	втВариантыОтчетов.ВидимостьПоУмолчанию КАК РеквизитВидимостьПоУмолчанию
	|ИЗ
	|	втВариантыОтчетов КАК втВариантыОтчетов
	|		ПОЛНОЕ СОЕДИНЕНИЕ втПредопределенные КАК втПредопределенные
	|		ПО втВариантыОтчетов.ПредопределенныйВариант = втПредопределенные.ПредопределенныйВариант";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПустаяСсылка", Справочники.ВариантыОтчетов.ПустаяСсылка());
	Кэш.Вставить("ПоискРодителей", Новый Соответствие);
	Кэш.Вставить("ОбработанныеПредопределенные", Новый Массив);
	Кэш.Вставить("ОсновныеВарианты", Новый ТаблицаЗначений);
	Кэш.ОсновныеВарианты.Колонки.Добавить("Отчет", Метаданные.Справочники.ВариантыОтчетов.Реквизиты.Отчет.Тип);
	Кэш.ОсновныеВарианты.Колонки.Добавить("Вариант", Новый ОписаниеТипов("СправочникСсылка.ВариантыОтчетов"));
	
	Шаблоны = Новый Структура;
	
	Шаблоны.Вставить("УстановкаПометкиУдаления", Новый Структура);
	Шаблоны.УстановкаПометкиУдаления.Вставить("Родитель", Кэш.ПустаяСсылка);
	Шаблоны.УстановкаПометкиУдаления.Вставить("ПометкаУдаления", Истина);
	
	Шаблоны.Вставить("ОбновлениеСведений", Новый Структура);
	Шаблоны.ОбновлениеСведений.Вставить("ТипОтчета", Перечисления.ТипыОтчетов.Внутренний);
	Шаблоны.ОбновлениеСведений.Вставить("ПометкаУдаления", Ложь);
	Шаблоны.ОбновлениеСведений.Вставить("Родитель", Неопределено);
	Шаблоны.ОбновлениеСведений.Вставить("Наименование", Неопределено);
	Шаблоны.ОбновлениеСведений.Вставить("Отчет", Неопределено);
	Шаблоны.ОбновлениеСведений.Вставить("КлючВарианта", Неопределено);
	Шаблоны.ОбновлениеСведений.Вставить("ПредопределенныйВариант", Неопределено);
	Шаблоны.ОбновлениеСведений.Вставить("ВидимостьПоУмолчанию", Неопределено);
	
	СводнаяТаблицаПредопределенных = Запрос.Выполнить().Выгрузить();
	СводнаяТаблицаПредопределенных.Колонки.Добавить("Обработана", Новый ОписаниеТипов("Булево"));
	СводнаяТаблицаПредопределенных.Колонки.Добавить("Родитель", Новый ОписаниеТипов("СправочникСсылка.ВариантыОтчетов"));
	
	// Обновление основных предопределенных вариантов (без родителя).
	Поиск = Новый Структура("ПредопределенныйВариантРодитель, УстановитьПометкуУдаления", Справочники.ПредопределенныеВариантыОтчетов.ПустаяСсылка(), Ложь);
	Найденные = СводнаяТаблицаПредопределенных.НайтиСтроки(Поиск);
	Для Каждого СтрокаТаблицы Из Найденные Цикл
		Если СтрокаТаблицы.Обработана Тогда
			Продолжить;
		КонецЕсли;
		Если Кэш.ОбработанныеПредопределенные.Найти(СтрокаТаблицы.ПредопределенныйВариант) <> Неопределено Тогда
			СтрокаТаблицы.УстановитьПометкуУдаления = Истина;
		КонецЕсли;
		
		СтрокаТаблицы.Родитель = Кэш.ПустаяСсылка;
		ОбновитьРазделенныйПредопределенный(Кэш, Шаблоны, СтрокаТаблицы);
		
		Если Не СтрокаТаблицы.УстановитьПометкуУдаления
			И СтрокаТаблицы.ГруппироватьПоОтчету
			И Кэш.ПоискРодителей.Получить(СтрокаТаблицы.Отчет) = Неопределено Тогда
			Кэш.ПоискРодителей.Вставить(СтрокаТаблицы.Отчет, СтрокаТаблицы.РеквизитСсылка);
			ОсновнойВариант = Кэш.ОсновныеВарианты.Добавить();
			ОсновнойВариант.Отчет   = СтрокаТаблицы.Отчет;
			ОсновнойВариант.Вариант = СтрокаТаблицы.РеквизитСсылка;
		КонецЕсли;
	КонецЦикла;
	
	// Обновление всех оставшихся предопределенных вариантов (подчиненных).
	СводнаяТаблицаПредопределенных.Сортировать("УстановитьПометкуУдаления Возр");
	Для Каждого СтрокаТаблицы Из СводнаяТаблицаПредопределенных Цикл
		Если СтрокаТаблицы.Обработана Тогда
			Продолжить;
		КонецЕсли;
		Если Кэш.ОбработанныеПредопределенные.Найти(СтрокаТаблицы.ПредопределенныйВариант) <> Неопределено Тогда
			СтрокаТаблицы.УстановитьПометкуУдаления = Истина;
		КонецЕсли;
		Если СтрокаТаблицы.УстановитьПометкуУдаления Тогда
			РодительСсылка = Кэш.ПустаяСсылка;
		Иначе
			РодительСсылка = Кэш.ПоискРодителей.Получить(СтрокаТаблицы.Отчет);
		КонецЕсли;
		
		СтрокаТаблицы.Родитель = РодительСсылка;
		ОбновитьРазделенныйПредопределенный(Кэш, Шаблоны, СтрокаТаблицы);
	КонецЦикла;
	
	// Обновление родителей пользовательских вариантов.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОсновныеВариантыОтчетов.Отчет,
	|	ОсновныеВариантыОтчетов.Вариант
	|ПОМЕСТИТЬ втОсновные
	|ИЗ
	|	&ОсновныеВариантыОтчетов КАК ОсновныеВариантыОтчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВариантыОтчетов.Ссылка,
	|	втОсновные.Вариант КАК Родитель
	|ИЗ
	|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втОсновные КАК втОсновные
	|		ПО ВариантыОтчетов.Отчет = втОсновные.Отчет
	|			И ВариантыОтчетов.Родитель <> втОсновные.Вариант
	|			И ВариантыОтчетов.Ссылка <> втОсновные.Вариант
	|ГДЕ
	|	(ВариантыОтчетов.Пользовательский
	|			ИЛИ НЕ ВариантыОтчетов.ПометкаУдаления)";
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОсновныеВариантыОтчетов", Кэш.ОсновныеВарианты);
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ВариантОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВариантОбъект.Родитель = Выборка.Родитель;
		ВариантОбъект.Заблокировать();
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВариантОбъект, ВариантОбъект.ЭтоНовый(), Ложь);
	КонецЦикла;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
КонецПроцедуры

// Обновляет предопределенные данные в разделенном режиме.
Процедура ОбновитьРазделенныйПредопределенный(Кэш, Шаблоны, СтрокаТаблицы)
	Если СтрокаТаблицы.Обработана Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы.Обработана = Истина;
	
	Если СтрокаТаблицы.УстановитьПометкуУдаления Тогда // Пометить к удалению.
		Если СовпадаютЗначенияСвойств(Шаблоны.УстановкаПометкиУдаления, СтрокаТаблицы, "Реквизит") Тогда
			Возврат; // Уже помечен.
		КонецЕсли;
		ВариантОбъект = СтрокаТаблицы.РеквизитСсылка.ПолучитьОбъект();
		ЗаполнитьЗначенияСвойств(ВариантОбъект, Шаблоны.УстановкаПометкиУдаления);
	Иначе
		Если СтрокаТаблицы.ГруппироватьПоОтчету И Не ЗначениеЗаполнено(СтрокаТаблицы.ПредопределенныйВариантРодитель) Тогда
			СтрокаТаблицы.Родитель = Кэш.ПустаяСсылка;
		КонецЕсли;
		Кэш.ОбработанныеПредопределенные.Добавить(СтрокаТаблицы.ПредопределенныйВариант);
		ЗаполнитьЗначенияСвойств(Шаблоны.ОбновлениеСведений, СтрокаТаблицы);
		Шаблоны.ОбновлениеСведений.ПометкаУдаления = Ложь;
		Если СтрокаТаблицы.СоздатьНовый Тогда // Добавить.
			ВариантОбъект = Справочники.ВариантыОтчетов.СоздатьЭлемент();
			ВариантОбъект.ПредопределенныйВариант = СтрокаТаблицы.ПредопределенныйВариант;
			ВариантОбъект.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний;
			ВариантОбъект.Пользовательский = Ложь;
		Иначе // Обновить (если есть изменения).
			Если СовпадаютЗначенияСвойств(Шаблоны.ОбновлениеСведений, СтрокаТаблицы, "Реквизит") Тогда
				Возврат; // Изменений нет.
			КонецЕсли;
			ЗаменитьКлючиПользовательскихНастроек(Шаблоны.ОбновлениеСведений, СтрокаТаблицы); // Перенос пользовательских настроек.
			ВариантОбъект = СтрокаТаблицы.РеквизитСсылка.ПолучитьОбъект();
		КонецЕсли;
		Если ВариантОбъект.ВидимостьПоУмолчаниюПереопределена Тогда
			ИсключаяСвойства = "ВидимостьПоУмолчанию";
		Иначе
			ИсключаяСвойства = Неопределено;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ВариантОбъект, Шаблоны.ОбновлениеСведений, , ИсключаяСвойства);
	КонецЕсли;
	
	ВариантОбъект.Заблокировать();
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВариантОбъект, ВариантОбъект.ЭтоНовый(), Ложь);
	
	СтрокаТаблицы.РеквизитСсылка = ВариантОбъект.Ссылка;
КонецПроцедуры

// Возвращает Истина если совпадают значения свойств Структуры и Коллекции с Префиксом.
Функция СовпадаютЗначенияСвойств(Структура, Коллекция, ПрефиксВКоллекции = "")
	Для Каждого КлючИЗначение Из Структура Цикл
		Если Коллекция[ПрефиксВКоллекции + КлючИЗначение.Ключ] <> КлючИЗначение.Значение Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	Возврат Истина;
КонецФункции

// Установка пометки удаления вариантов удаленных отчетов.
Процедура ПометитьНаУдалениеВариантыУдаленныхОтчетов(Кэш, ОбщиеДанные)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка
	|ИЗ
	|	Справочник.ПредопределенныеВариантыОтчетов КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.ПометкаУдаления = ЛОЖЬ
	|	И (ДанныеСправочника.Отчет = НЕОПРЕДЕЛЕНО
	|			Или ДанныеСправочника.Отчет.ПометкаУдаления = ИСТИНА)";
	
	Если Не ОбщиеДанные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ПредопределенныеВариантыОтчетов", "Справочник.ВариантыОтчетов");
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщиеДанные Тогда
		Кэш.ОбновитьОбласти = Истина;
		Суффикс = НСтр("ru = 'общие данные'");
	Иначе
		Суффикс = НСтр("ru = 'область данных'");
	КонецЕсли;
	
	ПредставлениеПроцедуры = НСтр("ru = 'Удаление вариантов удаленных отчетов'") + " (" + Суффикс + ")";
	ЗапускПроцедуры(ПредставлениеПроцедуры);
	
	МассивСсылокВариантов = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	Для Каждого ВариантСсылка Из МассивСсылокВариантов Цикл
		ВариантОбъект = ВариантСсылка.ПолучитьОбъект();
		ВариантОбъект.Заблокировать();
		ВариантОбъект.ПометкаУдаления = Истина;
		ЗаписатьПредопределенный(ВариантОбъект);
	КонецЦикла;
	
	ЗавершениеПроцедуры(ПредставлениеПроцедуры);
КонецПроцедуры

// Перенос пользовательских настроек варианта из соответствующего хранилища.
Процедура ЗаменитьКлючиПользовательскихНастроек(СтарыйВариант, АктуальныйВариант)
	Если СтарыйВариант.КлючВарианта = АктуальныйВариант.КлючВарианта
		Или Не ЗначениеЗаполнено(СтарыйВариант.КлючВарианта)
		Или Не ЗначениеЗаполнено(АктуальныйВариант.КлючВарианта)
		Или ТипЗнч(АктуальныйВариант.Отчет) <> Тип("СправочникСсылка.ИдентификаторыОбъектовМетаданных") Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетПолноеИмя = АктуальныйВариант.Отчет.ПолноеИмя;
	СтарыйКлючОбъекта = ОтчетПолноеИмя +"/"+ СтарыйВариант.КлючВарианта;
	НовыйКлючОбъекта = ОтчетПолноеИмя +"/"+ АктуальныйВариант.КлючВарианта;
	
	Отбор = Новый Структура("КлючОбъекта", СтарыйКлючОбъекта);
	ВыборкаХранилища = ХранилищеПользовательскихНастроекОтчетов.Выбрать(Отбор);
	ОшибокЧтенияПодряд = 0;
	Пока Истина Цикл
		// Чтение настроек из хранилища по старому ключу.
		Попытка
			ЭлементВыборкиПолучен = ВыборкаХранилища.Следующий();
			ОшибокЧтенияПодряд = 0;
		Исключение
			ЭлементВыборкиПолучен = Неопределено;
			ОшибокЧтенияПодряд = ОшибокЧтенияПодряд + 1;
			ОшибкаПоВарианту(
				СтарыйВариант.Ссылка,
				НСтр("ru = 'В процессе выборки вариантов отчетов из стандартного хранилища возникла ошибка:'")
				+ Символы.ПС
				+ ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
		Если ЭлементВыборкиПолучен = Ложь Тогда
			Прервать;
		ИначеЕсли ЭлементВыборкиПолучен = Неопределено Тогда
			Если ОшибокЧтенияПодряд > 100 Тогда
				Прервать;
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		// Чтение описания настроек.
		ОписаниеНастроек = ХранилищеПользовательскихНастроекОтчетов.ПолучитьОписание(
			ВыборкаХранилища.КлючОбъекта,
			ВыборкаХранилища.КлючНастроек,
			ВыборкаХранилища.Пользователь);
		
		// Запись настроек в хранилище по новому ключу.
		ХранилищеПользовательскихНастроекОтчетов.Сохранить(
			НовыйКлючОбъекта,
			ВыборкаХранилища.КлючНастроек,
			ВыборкаХранилища.Настройки,
			ОписаниеНастроек,
			ВыборкаХранилища.Пользователь);
	КонецЦикла;
	
	// Очистка старых настроек хранилища.
	ХранилищеПользовательскихНастроекОтчетов.Удалить(СтарыйКлючОбъекта, Неопределено, Неопределено);
КонецПроцедуры

// Записывает предопределенный объект.
Процедура ЗаписатьПредопределенный(ВариантОбъект)
	ВариантОбъект.ДополнительныеСвойства.Вставить("ЗаполнениеПредопределенных");
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(ВариантОбъект, ВариантОбъект.ЭтоНовый(), Ложь);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы / Переход к редакции 2.1

// Замена структуры хранения настроек по разделам, переход на ссылки справочника ИОМ.
//   Вызывается только для вариантов внутренних отчетов.
//
Процедура Редакция21ПривестиНастройкиПоРазделам(ВариантОбъект)
	
	Найденные = ВариантОбъект.Размещение.НайтиСтроки(Новый Структура("Подсистема", Справочники.ИдентификаторыОбъектовМетаданных.ПустаяСсылка()));
	Для Каждого СтрокаТаблицы Из Найденные Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.УдалитьПодсистема) Тогда
			ВариантОбъект.Размещение.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли;
		
		ПодсистемаПолноеИмя = "Подсистема." + СтрЗаменить(СтрокаТаблицы.УдалитьПодсистема, "\", ".Подсистема.");
		ПодсистемаМетаданные = Метаданные.НайтиПоПолномуИмени(ПодсистемаПолноеИмя);
		Если ПодсистемаМетаданные = Неопределено Тогда
			ВариантОбъект.Размещение.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли;
		
		ПодсистемаСсылка = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПодсистемаМетаданные);;
		Если Не ЗначениеЗаполнено(ПодсистемаСсылка) Тогда
			ВариантОбъект.Размещение.Удалить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы.Использование = Истина;
		СтрокаТаблицы.Подсистема = ПодсистемаСсылка;
		СтрокаТаблицы.УдалитьПодсистема = "";
		СтрокаТаблицы.УдалитьНазвание = "";
		
	КонецЦикла;
	
КонецПроцедуры

// Заполнение регистра "НастройкиВариантовОтчетов".
//   Вызывается только для вариантов внутренних отчетов.
//
Процедура Редакция21ПеренестиПользовательскиеНастройкиВРегистр(ВариантОбъект)
	ТаблицаПодсистем = ВариантОбъект.Размещение.Выгрузить(Новый Структура("Использование", Истина));
	ТаблицаПодсистем.Свернуть("Подсистема");
	
	ТаблицаПользователей = ВариантОбъект.УдалитьИсключенияБыстрогоДоступа.Выгрузить();
	ТаблицаПользователей.Колонки.УдалитьПользователь.Имя = "Пользователь";
	ТаблицаПользователей.Свернуть("Пользователь");
	
	ПакетНастроек = Новый ТаблицаЗначений;
	ПакетНастроек.Колонки.Добавить("Подсистема",   ТаблицаПодсистем.Колонки.Подсистема.Тип);
	ПакетНастроек.Колонки.Добавить("Пользователь", ТаблицаПользователей.Колонки.Пользователь.Тип);
	ПакетНастроек.Колонки.Добавить("Видимость",    Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтрокаПодсистема Из ТаблицаПодсистем Цикл
		Для Каждого СтрокаПользователь Из ТаблицаПользователей Цикл
			Настройка = ПакетНастроек.Добавить();
			Настройка.Подсистема   = СтрокаПодсистема.РазделИлиГруппа;
			Настройка.Пользователь = СтрокаПользователь.Пользователь;
			Настройка.Видимость    = Не ВариантОбъект.ВидимостьПоУмолчанию;
		КонецЦикла;
	КонецЦикла;
	
	Измерения = Новый Структура("Вариант", ВариантОбъект.Ссылка);
	Ресурсы   = Новый Структура("БыстрыйДоступ", Ложь);
	РегистрыСведений.НастройкиВариантовОтчетов.ЗаписатьПакетНастроек(ПакетНастроек, Измерения, Ресурсы, Истина);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Работа с деревом подсистем из форм

// Добавляет элементы условного оформления дерева подсистем.
Процедура ДеревоПодсистемДобавитьУсловноеОформление(Форма) Экспорт
	Форма.Элементы.ДеревоПодсистемВажность.СписокВыбора.Добавить(ВариантыОтчетовКлиентСервер.ПредставлениеВажный());
	Форма.Элементы.ДеревоПодсистемВажность.СписокВыбора.Добавить(ВариантыОтчетовКлиентСервер.ПредставлениеСмТакже());
	
	Инструкция = ИнструкцияУсловногоОформления();
	Инструкция.Поля = "ДеревоПодсистем";
	Инструкция.Отборы.Вставить("ДеревоПодсистем.Приоритет", "");
	Инструкция.Оформление.Вставить("ТолькоПросмотр", Истина);
	ДобавитьЭлементУсловногоОформления(Форма, Инструкция);
	
	Инструкция = ИнструкцияУсловногоОформления();
	Инструкция.Поля = "ДеревоПодсистемИспользование, ДеревоПодсистемВажность";
	Инструкция.Отборы.Вставить("ДеревоПодсистем.Приоритет", "");
	Инструкция.Оформление.Вставить("Отображать", Ложь);
	ДобавитьЭлементУсловногоОформления(Форма, Инструкция);
КонецПроцедуры

// Описание инструкции для добавления элемента условного оформления.
Функция ИнструкцияУсловногоОформления() Экспорт
	Возврат Новый Структура("Отборы, Оформление, Поля", Новый Соответствие, Новый Соответствие, "");
КонецФункции

// Добавляет элемент условного оформления согласно описанию из инструкции.
Функция ДобавитьЭлементУсловногоОформления(Форма, ИнструкцияУсловногоОформления) Экспорт
	ЭлементУсловногоОформленияКД = Форма.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформленияКД.Использование = Истина;
	
	Для Каждого КлючИЗначение Из ИнструкцияУсловногоОформления.Отборы Цикл
		ЭлементОтбораКД = ЭлементУсловногоОформленияКД.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораКД.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(КлючИЗначение.Ключ);
		Настройка = КлючИЗначение.Значение;
		Тип = ТипЗнч(Настройка);
		Если Тип = Тип("Структура") Тогда
			ЭлементОтбораКД.ВидСравнения = ВидСравненияКомпоновкиДанных[Настройка.Вид];
			ЭлементОтбораКД.ПравоеЗначение = Настройка.Значение;
		ИначеЕсли Тип = Тип("Массив") Тогда
			ЭлементОтбораКД.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			ЭлементОтбораКД.ПравоеЗначение = Настройка;
		ИначеЕсли Тип = Тип("ВидСравненияКомпоновкиДанных") Тогда
			ЭлементОтбораКД.ВидСравнения = Настройка;
		Иначе
			ЭлементОтбораКД.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ЭлементОтбораКД.ПравоеЗначение = Настройка;
		КонецЕсли;
		ЭлементОтбораКД.Применение = ТипПримененияОтбораКомпоновкиДанных.Элементы;
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из ИнструкцияУсловногоОформления.Оформление Цикл
		ЭлементУсловногоОформленияКД.Оформление.УстановитьЗначениеПараметра(
			Новый ПараметрКомпоновкиДанных(КлючИЗначение.Ключ),
			КлючИЗначение.Значение);
	КонецЦикла;
	
	Поля = ИнструкцияУсловногоОформления.Поля;
	Если ТипЗнч(Поля) = Тип("Строка") Тогда
		Поля = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Поля, ",");
	КонецЕсли;
	Для Каждого Поле Из Поля Цикл
		ПолеКД = ЭлементУсловногоОформленияКД.Поля.Элементы.Добавить();
		ПолеКД.Использование = Истина;
		ПолеКД.Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЦикла;
	
	Возврат ЭлементУсловногоОформленияКД;
КонецФункции

// Формирует дерево подсистем по данным варианта-основания.
Функция ДеревоПодсистемСформировать(Форма, ВариантОснование) Экспорт
	// Пустое дерево без настроек.
	Прототип = Форма.РеквизитФормыВЗначение("ДеревоПодсистем", Тип("ДеревоЗначений"));
	ДеревоПодсистем = ВариантыОтчетовПовтИсп.ПодсистемыТекущегоПользователя().Скопировать();
	Для Каждого КолонкаПрототипа Из Прототип.Колонки Цикл
		Если ДеревоПодсистем.Колонки.Найти(КолонкаПрототипа.Имя) = Неопределено Тогда
			ДеревоПодсистем.Колонки.Добавить(КолонкаПрототипа.Имя, КолонкаПрототипа.ТипЗначения);
		КонецЕсли;
	КонецЦикла;
	
	// Параметры.
	Контекст = Новый Структура("ДеревоПодсистем");
	Контекст.ДеревоПодсистем = ДеревоПодсистем;
	
	// Размещение, настроенное администратором.
	Подсистемы = Новый Массив;
	Для Каждого СтрокаРазмещения Из ВариантОснование.Размещение Цикл
		Подсистемы.Добавить(СтрокаРазмещения.Подсистема);
		ДеревоПодсистемЗарегистрироватьНастройкиПодсистемы(Контекст, СтрокаРазмещения, СтрокаРазмещения.Использование);
	КонецЦикла;
	
	// Размещение, предопределенное разработчиком.
	Запрос = Новый Запрос("Выбрать * Из Справочник.ПредопределенныеВариантыОтчетов.Размещение Где Ссылка = &Ссылка И Не Подсистема В (&Подсистемы)");
	Запрос.УстановитьПараметр("Ссылка", ВариантОснование.ПредопределенныйВариант);
	Запрос.УстановитьПараметр("Подсистемы", Подсистемы); // Не зачитывать настройки подсистем, переопределенные администратором.
	РазмещениеПредопределенного = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаРазмещения Из РазмещениеПредопределенного Цикл
		ДеревоПодсистемЗарегистрироватьНастройкиПодсистемы(Контекст, СтрокаРазмещения, Истина);
	КонецЦикла;
	
	Возврат Контекст.ДеревоПодсистем;
КонецФункции

// Добавляет подсистему в дерево.
Процедура ДеревоПодсистемЗарегистрироватьНастройкиПодсистемы(Контекст, СтрокаРазмещения, Использование)
	Найденные = Контекст.ДеревоПодсистем.Строки.НайтиСтроки(Новый Структура("Ссылка", СтрокаРазмещения.Подсистема), Истина);
	Если Найденные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДерева = Найденные[0];
	
	Если СтрокаРазмещения.Важный Тогда
		СтрокаДерева.Важность = ВариантыОтчетовКлиентСервер.ПредставлениеВажный();
	ИначеЕсли СтрокаРазмещения.СмТакже Тогда
		СтрокаДерева.Важность = ВариантыОтчетовКлиентСервер.ПредставлениеСмТакже();
	Иначе
		СтрокаДерева.Важность = "";
	КонецЕсли;
	СтрокаДерева.Использование = Использование;
КонецПроцедуры

// Сохраняет настройки размещения, измененные пользователем в табличную часть варианта отчета.
//
// Параметры:
//   Форма         - УправляемаяФорма - Форма, в которой хранятся настройки размещения.
//   ВариантОбъект - СправочникОбъект.ВариантыОтчетов, ДанныеФормыСтруктура - Объект варианта отчета.
//   Кэш           - Структура - Необязательный.
//
Процедура ДеревоПодсистемЗаписать(Форма, ВариантОбъект, Кэш = Неопределено) Экспорт
	Если Кэш = Неопределено Тогда
		Кэш = Новый Структура;
	КонецЕсли;
	Если Кэш.Свойство("ИзмененияПодсистем") Тогда
		ИзмененияПодсистем = Кэш.ИзмененияПодсистем;
	Иначе
		ДеревоПриемник = Форма.РеквизитФормыВЗначение("ДеревоПодсистем", Тип("ДеревоЗначений"));
		Если ВариантОбъект.ЭтоНовый() Тогда
			ИзмененияПодсистем = ДеревоПриемник.Строки.НайтиСтроки(Новый Структура("Использование", 1), Истина);
		Иначе
			ИзмененияПодсистем = ДеревоПриемник.Строки.НайтиСтроки(Новый Структура("Модифицированность", Истина), Истина);
		КонецЕсли;
		Кэш.Вставить("ИзмененияПодсистем", ИзмененияПодсистем);
	КонецЕсли;
	
	Для Каждого Подсистема Из ИзмененияПодсистем Цикл
		СтрокаТабличнойЧасти = ВариантОбъект.Размещение.Найти(Подсистема.Ссылка, "Подсистема");
		Если СтрокаТабличнойЧасти = Неопределено Тогда
			// Необходимо зарегистрировать настройку размещения варианта безусловно (даже флажок Использование отключен)
			// - только тогда эта настройка заменит предопределенную (из неразделенного справочника).
			СтрокаТабличнойЧасти = ВариантОбъект.Размещение.Добавить();
			СтрокаТабличнойЧасти.Подсистема = Подсистема.Ссылка;
		КонецЕсли;
		
		Если Подсистема.Использование = 0 Тогда
			СтрокаТабличнойЧасти.Использование = Ложь;
		ИначеЕсли Подсистема.Использование = 1 Тогда
			СтрокаТабличнойЧасти.Использование = Истина;
		Иначе
			// Оставить как есть
		КонецЕсли;
		
		Если Подсистема.Важность = ВариантыОтчетовКлиентСервер.ПредставлениеВажный() Тогда
			СтрокаТабличнойЧасти.Важный  = Истина;
			СтрокаТабличнойЧасти.СмТакже = Ложь;
		ИначеЕсли Подсистема.Важность = ВариантыОтчетовКлиентСервер.ПредставлениеСмТакже() Тогда
			СтрокаТабличнойЧасти.Важный  = Ложь;
			СтрокаТабличнойЧасти.СмТакже = Истина;
		Иначе
			СтрокаТабличнойЧасти.Важный  = Ложь;
			СтрокаТабличнойЧасти.СмТакже = Ложь;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Формирование представлений полей, параметров и отборов для поиска.

// Вызывается из события ПриЗаписи вариантов. Часть проверок выполняется до вызова.
Функция ПроиндексироватьСодержимоеСхемы(ВариантОбъект) Экспорт
	Перем ИндексироватьСхему, ОтчетОбъект, НастройкиКД, ЭтоПредопределенный;
	
	ДополнительныеСвойства = ВариантОбъект.ДополнительныеСвойства;
	
	// В некоторых случаях заранее известно что настройки уже проиндексированы.
	ДополнительныеСвойства.Свойство("ИндексироватьСхему", ИндексироватьСхему);
	Если ИндексироватьСхему = Ложь Тогда
		Возврат Ложь; // Заполнение не требуется.
	КонецЕсли;
	ПроверятьХеш = Истина;
	Если ИндексироватьСхему = Истина Тогда
		ПроверятьХеш = Ложь;
	КонецЕсли;
	
	// Механика ручного индексирования из данных варианта.
	ИндексироватьНаименованияПолей = Лев(ВариантОбъект.НаименованияПолей, 1) <> "#";
	ИндексироватьНаименованияПараметровИОтборов = Лев(ВариантОбъект.НаименованияПараметровИОтборов, 1) <> "#";
	Если Не ИндексироватьНаименованияПолей И Не ИндексироватьНаименованияПараметровИОтборов Тогда
		Возврат Ложь; // Заполнение не требуется.
	КонецЕсли;
	
	// Получение объекта отчета, настроек СКД и варианта.
	ЭтоПредопределенный = Ложь;
	
	// Настройки предопределенных получаются из дерева значений, которое передается в дополнительных параметрах.
	Если ТипЗнч(ВариантОбъект) = Тип("СправочникОбъект.ПредопределенныеВариантыОтчетов") Тогда
		ЭтоПредопределенный = Истина;
		
		Поиск = Новый Структура("Отчет, КлючВарианта, ЭтоВариант", ВариантОбъект.Отчет, ВариантОбъект.КлючВарианта, Истина);
		Найденные = ДополнительныеСвойства.ДеревоВариантов.Строки.НайтиСтроки(Поиск, Истина);
		Если Найденные.Количество() = 0 Тогда
			ТекстОшибки = НСтр("ru = 'Вариант ""%1"" не найден для отчета ""%2""'");
			ОшибкаПоВарианту(ВариантОбъект.Ссылка, ТекстОшибки, ВариантОбъект.КлючВарианта, ВариантОбъект.Отчет);
			Возврат Ложь; // Возникла проблема.
		КонецЕсли;
		
		ОписаниеВарианта = Найденные[0];
		СтрокаОтчет = ОписаниеВарианта.Родитель;
		ЗаполнитьОписаниеСтрокиВарианта(ОписаниеВарианта, СтрокаОтчет);
		
		// Если вариант отключен, то и в поиске он не участвует.
		Если Не ОписаниеВарианта.Включен Тогда
			Возврат Ложь; // Заполнение не требуется.
		КонецЕсли;
		
		// Предустановленные настройки поиска.
		Если ЗначениеЗаполнено(ОписаниеВарианта.НастройкиДляПоиска.НаименованияПолей) Тогда
			ВариантОбъект.НаименованияПолей = "#" + СокрЛП(ОписаниеВарианта.НастройкиДляПоиска.НаименованияПолей);
			ИндексироватьНаименованияПолей = Ложь;
		КонецЕсли;
		Если ЗначениеЗаполнено(ОписаниеВарианта.НастройкиДляПоиска.НаименованияПараметровИОтборов) Тогда
			ВариантОбъект.НаименованияПараметровИОтборов = "#" + СокрЛП(ОписаниеВарианта.НастройкиДляПоиска.НаименованияПараметровИОтборов);
			ИндексироватьНаименованияПараметровИОтборов = Ложь;
		КонецЕсли;
		Если Не ИндексироватьНаименованияПолей И Не ИндексироватьНаименованияПараметровИОтборов Тогда
			Возврат Истина; // Заполнение выполнено - объект надо записать.
		КонецЕсли;
		
		// Объект отчета.
		Если Не СтрокаОтчет.СистемнаяИнформация.ПостроенНаСКД Тогда
			Если СтрокаОтчет.Размещение.Количество() > 0 Тогда
				ТекстОшибки = НСтр("ru = 'Для отчета ""%2"" не заполнены настройки поиска: наименования полей или параметров и отборов.
				|Подробнее - см. процедуру ""НастроитьВариантыОтчетов"" модуля ""ВариантыОтчетовПереопределяемый"".'");
				ОшибкаПоВарианту(ВариантОбъект.Ссылка, ТекстОшибки, "", ВариантОбъект.Отчет);
			КонецЕсли;
			Возврат Ложь; // Возникла проблема.
		КонецЕсли;
		Если Не СтрокаОтчет.СистемнаяИнформация.Свойство("ОтчетОбъект") Тогда
			Возврат Ложь; // Возникла проблема (уже записана в функции ДеревоНастроекВариантовОтчетов).
		КонецЕсли;
		ОтчетОбъект = СтрокаОтчет.СистемнаяИнформация.ОтчетОбъект;
		
		// Извлечение текстов макетов возможно только после получения объекта отчета.
		Если ИндексироватьНаименованияПолей И ЗначениеЗаполнено(ОписаниеВарианта.НастройкиДляПоиска.ИменаМакетов) Тогда
			ВариантОбъект.НаименованияПолей = "#" + ИзвлечьТекстМакета(ОтчетОбъект, ОписаниеВарианта.НастройкиДляПоиска.ИменаМакетов);
			ИндексироватьНаименованияПолей = Ложь;
		КонецЕсли;
		Если Не ИндексироватьНаименованияПолей И Не ИндексироватьНаименованияПараметровИОтборов Тогда
			Возврат Истина; // Заполнение выполнено - объект надо записать.
		КонецЕсли;
		
		// Настройки компоновки данных.
		Если Не ОписаниеВарианта.СистемнаяИнформация.Свойство("НастройкиКД") Тогда
			Возврат Ложь; // Возникла проблема (уже записана в функции ДеревоНастроекВариантовОтчетов).
		КонецЕсли;
		НастройкиКД = ОписаниеВарианта.СистемнаяИнформация.НастройкиКД;
	Иначе
		Если Не ВариантОбъект.Пользовательский Тогда
			ЭтоПредопределенный = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// В некоторых сценариях объект может быть закэширован в дополнительных свойствах.
	Если ОтчетОбъект = Неопределено Тогда
		ДополнительныеСвойства.Свойство("ОтчетОбъект", ОтчетОбъект);
	КонецЕсли;
	
	// Когда объект отчета не закэширован - подключение отчета штатным способом.
	Если ОтчетОбъект = Неопределено Тогда
		РезультатПодключения = ПодключитьОтчетОбъект(ВариантОбъект.Отчет);
		Если Не РезультатПодключения.Подключен Тогда // Отчет не найден.
			ОшибкаПоВарианту(ВариантОбъект.Ссылка, РезультатПодключения.Ошибки);
			Возврат Ложь; // Возникла проблема.
		КонецЕсли;
		ОтчетОбъект = РезультатПодключения.Объект;
	КонецЕсли;
	
	// Схема компоновки, на основании которой будет выполняться отчет.
	СхемаКД = ОтчетОбъект.СхемаКомпоновкиДанных;
	
	// Если отчет не на СКД, следовательно представления не заполняются или заполняются прикладными механизмами.
	Если СхемаКД = Неопределено Тогда
		Если Не ЗначениеЗаполнено(ВариантОбъект.НаименованияПолей)
			Или Не ЗначениеЗаполнено(ВариантОбъект.НаименованияПараметровИОтборов) Тогда
			ТекстОшибки = НСтр("ru = 'Для варианта ""%1"" отчета ""%2"" не заполнены настройки поиска: наименования полей или параметров и отборов.'");
			ИнформацияПоВарианту(ВариантОбъект.Ссылка, ТекстОшибки, ВариантОбъект.КлючВарианта, ВариантОбъект.Отчет);
		КонецЕсли;
		Возврат Ложь; // Возникла проблема.
	КонецЕсли;
	
	// Чтение настроек из переданных параметров.
	Если ТипЗнч(НастройкиКД) <> Тип("НастройкиКомпоновкиДанных") Тогда
		ДополнительныеСвойства.Свойство("НастройкиКД", НастройкиКД);
	КонецЕсли;
	
	// Чтение настроек из схемы.
	Если ТипЗнч(НастройкиКД) <> Тип("НастройкиКомпоновкиДанных") Тогда
		ВариантНастроекКД = СхемаКД.ВариантыНастроек.Найти(ВариантОбъект.КлючВарианта);
		Если ВариантНастроекКД <> Неопределено Тогда
			НастройкиКД = ВариантНастроекКД.Настройки;
		КонецЕсли;
	КонецЕсли;
	
	// Чтение настроек из данных варианта.
	Если ТипЗнч(НастройкиКД) <> Тип("НастройкиКомпоновкиДанных") Тогда
		НастройкиКД = ВариантОбъект.Настройки.Получить();
	КонецЕсли;
	
	// Последняя проверка.
	Если ТипЗнч(НастройкиКД) <> Тип("НастройкиКомпоновкиДанных") Тогда
		Возврат Ложь; // Возникла проблема.
	КонецЕсли;
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(ОбщегоНазначения.ЗначениеВСтрокуXML(НастройкиКД));
	ХешЭтихНастроек = СтрЗаменить(ХешированиеДанных.ХешСумма, " ", "");
	Если ПроверятьХеш И ВариантОбъект.ХешНастроек = ХешЭтихНастроек Тогда
		Возврат Ложь; // Настройки не изменились.
	КонецЕсли;
	ВариантОбъект.ХешНастроек = ХешЭтихНастроек;
	
	// Описывает связь настроек компоновки данных и схемы компоновки данных.
	КомпоновщикНастроекКД = ОтчетОбъект.КомпоновщикНастроек;
	
	// Выполняет инициализацию компоновщика и его настроек (Настройки) источником доступных настроек.
	КомпоновщикНастроекКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКД));
	
	// Загружает настройки в компоновщик.
	КомпоновщикНастроекКД.ЗагрузитьНастройки(НастройкиКД);
	
	Если ИндексироватьНаименованияПолей Тогда
		// Преобразование всех настроек автоматической группировки в наборы полей.
		//   См. "АвтоВыбранноеПолеКомпоновкиДанных", "АвтоПолеГруппировкиКомпоновкиДанных",
		//   "АвтоЭлементПорядкаКомпоновкиДанных" в синтакс-помощнике.
		КомпоновщикНастроекКД.РазвернутьАвтоПоля();
		
		ВариантОбъект.НаименованияПолей = СформироватьПредставленияПолей(КомпоновщикНастроекКД);
	КонецЕсли;
	
	Если ИндексироватьНаименованияПараметровИОтборов Тогда
		ВариантОбъект.НаименованияПараметровИОтборов = СформироватьПредставленияПараметровИОтборов(КомпоновщикНастроекКД);
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

// Представления группировок и полей из СКД.
Функция СформироватьПредставленияПолей(КомпоновщикНастроекКД) Экспорт
	Результат = Новый Массив;
	
	ДополнитьМассивИзСтрокиСРазделителями(Результат, Строка(КомпоновщикНастроекКД.Настройки.Выбор));
	
	МассивКоллекций = Новый Массив;
	МассивКоллекций.Добавить(КомпоновщикНастроекКД.Настройки.Структура);
	Пока МассивКоллекций.Количество() > 0 Цикл
		Коллекция = МассивКоллекций[0];
		МассивКоллекций.Удалить(0);
		
		Для Каждого Настройка Из Коллекция Цикл
			
			Если ТипЗнч(Настройка) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
				Если Не Настройка.Использование Тогда
					Продолжить;
				КонецЕсли;
				Настройка = Настройка.Настройки;
			КонецЕсли;
			
			ДополнитьМассивИзСтрокиСРазделителями(Результат, Строка(Настройка.Выбор));
			
			Если ТипЗнч(Настройка) = Тип("НастройкиКомпоновкиДанных") Тогда
				МассивКоллекций.Добавить(Настройка.Структура);
			ИначеЕсли ТипЗнч(Настройка) = Тип("ГруппировкаКомпоновкиДанных") Тогда
				Если Не Настройка.Использование Тогда
					Продолжить;
				КонецЕсли;
				МассивКоллекций.Добавить(Настройка.Структура);
			ИначеЕсли ТипЗнч(Настройка) = Тип("ТаблицаКомпоновкиДанных") Тогда
				Если Не Настройка.Использование Тогда
					Продолжить;
				КонецЕсли;
				МассивКоллекций.Добавить(Настройка.Строки);
			ИначеЕсли ТипЗнч(Настройка) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
				Если Не Настройка.Использование Тогда
					Продолжить;
				КонецЕсли;
				МассивКоллекций.Добавить(Настройка.Структура);
			ИначеЕсли ТипЗнч(Настройка) = Тип("ДиаграммаКомпоновкиДанных") Тогда
				Если Не Настройка.Использование Тогда
					Продолжить;
				КонецЕсли;
				МассивКоллекций.Добавить(Настройка.Серии);
				МассивКоллекций.Добавить(Настройка.Точки);
			ИначеЕсли ТипЗнч(Настройка) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
				Если Не Настройка.Использование Тогда
					Продолжить;
				КонецЕсли;
				МассивКоллекций.Добавить(Настройка.Структура);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	РазделительХранения = ВариантыОтчетовКлиентСервер.РазделительХранения();
	Возврат СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(Результат, РазделительХранения);
КонецФункции

// Представления параметров и отборов из СКД.
Функция СформироватьПредставленияПараметровИОтборов(КомпоновщикНастроекКД) Экспорт
	Результат = Новый Массив;
	
	НастройкиКД = КомпоновщикНастроекКД.Настройки;
	
	Режимы = РежимОтображенияЭлементаНастройкиКомпоновкиДанных;
	
	Для Каждого ПользовательскаяНастройка Из КомпоновщикНастроекКД.ПользовательскиеНастройки.Элементы Цикл
		ТипНастройки = ТипЗнч(ПользовательскаяНастройка);
		Если ТипНастройки = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			ЭтоОтбор = Ложь;
		ИначеЕсли ТипНастройки = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			ЭтоОтбор = Истина;
		Иначе
			Продолжить;
		КонецЕсли;
		
		Если ПользовательскаяНастройка.РежимОтображения = Режимы.Недоступный Тогда
			Продолжить;
		КонецЕсли;
		
		ИдентификаторСКД = ПользовательскаяНастройка.ИдентификаторПользовательскойНастройки;
		
		ОбщаяНастройка = ФункцииОтчетовКлиентСервер.ПолучитьОбъектПоПользовательскомуИдентификатору(НастройкиКД, ИдентификаторСКД);
		Если ОбщаяНастройка = Неопределено Тогда
			Продолжить;
		ИначеЕсли ПользовательскаяНастройка.РежимОтображения = Режимы.Авто
			И ОбщаяНастройка.РежимОтображения <> Режимы.БыстрыйДоступ Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураПредставлений = Новый Структура("Представление, ПредставлениеПользовательскойНастройки", "", "");
		ЗаполнитьЗначенияСвойств(СтруктураПредставлений, ОбщаяНастройка);
		Если ЗначениеЗаполнено(СтруктураПредставлений.ПредставлениеПользовательскойНастройки) Тогда
			ЭлементЗаголовок = СтруктураПредставлений.ПредставлениеПользовательскойНастройки;
		ИначеЕсли ЗначениеЗаполнено(СтруктураПредставлений.Представление) Тогда
			ЭлементЗаголовок = СтруктураПредставлений.Представление;
		Иначе
			ДоступнаяНастройка = ФункцииОтчетовКлиентСервер.НайтиДоступнуюНастройку(НастройкиКД, ОбщаяНастройка);
			Если ДоступнаяНастройка <> Неопределено И ЗначениеЗаполнено(ДоступнаяНастройка.Заголовок) Тогда
				ЭлементЗаголовок = ДоступнаяНастройка.Заголовок;
			Иначе
				ЭлементЗаголовок = Строка(?(ЭтоОтбор, ОбщаяНастройка.ЛевоеЗначение, ОбщаяНастройка.Параметр));
			КонецЕсли;
		КонецЕсли;
		
		ЭлементЗаголовок = СокрЛП(ЭлементЗаголовок);
		Если ЭлементЗаголовок <> "" И Результат.Найти(ЭлементЗаголовок) = Неопределено Тогда
			Результат.Добавить(ЭлементЗаголовок);
		КонецЕсли;
		
	КонецЦикла;
	
	РазделительХранения = ВариантыОтчетовКлиентСервер.РазделительХранения();
	Возврат СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(Результат, РазделительХранения);
КонецФункции

// Извлекает текстовую информацию из макета.
Функция ИзвлечьТекстМакета(ОтчетОбъект, ИменаМакетов) Экспорт
	ИзвлеченныйТекст = "";
	Если ТипЗнч(ИменаМакетов) = Тип("Строка") Тогда
		ИменаМакетов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			ИменаМакетов,
			",",
			Истина,
			Истина);
	КонецЕсли;
	Для Каждого ИмяМакета Из ИменаМакетов Цикл
		Макет = ОтчетОбъект.ПолучитьМакет(ИмяМакета);
		Если ТипЗнч(Макет) = Тип("ТабличныйДокумент") Тогда
			Низ = Макет.ВысотаТаблицы;
			Право = Макет.ШиринаТаблицы;
			ПроверенныеЯчейки = Новый Соответствие;
			Для НомерКолонки = 1 По Право Цикл
				Для НомерСтроки = 1 По Низ Цикл
					Ячейка = Макет.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
					Если ПроверенныеЯчейки.Получить(Ячейка.Имя) = Неопределено Тогда
						ПроверенныеЯчейки.Вставить(Ячейка.Имя, Истина);
						Если ТипЗнч(Ячейка) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
							ТекстОбласти = СокрЛП(Ячейка.Текст);
							Если ТекстОбласти <> "" Тогда
								ИзвлеченныйТекст = ИзвлеченныйТекст + Символы.ПС + ТекстОбласти;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		ИначеЕсли ТипЗнч(Макет) = Тип("ТекстовыйДокумент") Тогда
			ИзвлеченныйТекст = ИзвлеченныйТекст + Символы.ПС + СокрЛП(Макет.ПолучитьТекст());
		КонецЕсли;
	КонецЦикла;
	ИзвлеченныйТекст = СокрЛ(ИзвлеченныйТекст);
	Возврат ИзвлеченныйТекст;
КонецФункции

// Добавляет в Массив элементы из СтрокаСРазделителями, если их там нет.
Процедура ДополнитьМассивИзСтрокиСРазделителями(Массив, СтрокаСРазделителями)
	СтрокаСРазделителями = СокрЛП(СтрокаСРазделителями);
	Если СтрокаСРазделителями = "" Тогда
		Возврат;
	КонецЕсли;
	Позиция = Найти(СтрокаСРазделителями, ",");
	Пока Позиция > 0 Цикл
		Подстрока = СокрП(Лев(СтрокаСРазделителями, Позиция - 1));
		Если Подстрока <> "" И Массив.Найти(Подстрока) = Неопределено Тогда
			Массив.Добавить(Подстрока);
		КонецЕсли;
		СтрокаСРазделителями = СокрЛ(Сред(СтрокаСРазделителями, Позиция + 1));
		Позиция = Найти(СтрокаСРазделителями, ",");
	КонецЦикла;
	Если СтрокаСРазделителями <> "" И Массив.Найти(СтрокаСРазделителями) = Неопределено Тогда
		Массив.Добавить(СтрокаСРазделителями);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Уменьшение количества пользовательских настроек.

// Вызывается из события ПриЗаписи вариантов. Часть проверок выполняется до вызова.
Функция УменьшитьКоличествоБыстрыхНастроек(ВариантОбъект, ОтчетОбъект) Экспорт
	Перем НастройкиКД, ЭтоПредопределенный;
	
	// Схема компоновки, на основании которой будет выполняться отчет.
	СхемаКД = ОтчетОбъект.СхемаКомпоновкиДанных;
	Если СхемаКД = Неопределено Тогда
		Возврат Ложь; // Отчет не на СКД. Заполнение не требуется.
	КонецЕсли;
	
	// Чтение настроек из данных варианта.
	НастройкиКД = ВариантОбъект.Настройки.Получить();
	Если ТипЗнч(НастройкиКД) <> Тип("НастройкиКомпоновкиДанных") Тогда
		ТекстОшибки = НСтр("ru = 'Обнаружены пустые настройки пользовательского варианта ""%1"" отчета ""%2"".'");
		ОшибкаПоВарианту(ВариантОбъект.Ссылка, ТекстОшибки, ВариантОбъект.КлючВарианта, ВариантОбъект.Отчет);
		Возврат Ложь; // Возникла проблема.
	КонецЕсли;
	
	ИнформацияОбОшибке = Неопределено;
	Попытка
		// Описывает связь настроек компоновки данных и схемы компоновки данных.
		КомпоновщикНастроекКД = ОтчетОбъект.КомпоновщикНастроек;
		
		// Выполняет инициализацию компоновщика и его настроек (Настройки) источником доступных настроек.
		КомпоновщикНастроекКД.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКД));
		
		// Загружает настройки в компоновщик.
		КомпоновщикНастроекКД.ЗагрузитьНастройки(НастройкиКД);
		
		Информация = РасширеннаяИнформацияОНастройках(КомпоновщикНастроекКД);
		БыстрыеНастройки = Информация.ТаблицаНастроек.Скопировать(Новый Структура("ВыводРазрешен, Быстрая", Истина, Истина));
		Если БыстрыеНастройки.Количество() <= 2 Тогда
			Возврат Ложь; // Уменьшение количества не требуется.
		КонецЕсли;
		
		Исключаемые = БыстрыеНастройки.НайтиСтроки(Новый Структура("ТипЭлементов", "СтандартныйПериод"));
		Для Каждого СтрокаТаблицы Из Исключаемые Цикл
			БыстрыеНастройки.Удалить(СтрокаТаблицы);
		КонецЦикла;
		
		Потрачено = Исключаемые.Количество();
		Для Каждого СтрокаТаблицы Из БыстрыеНастройки Цикл
			Если Потрачено < 2 Тогда
				Потрачено = Потрачено + 1;
				Продолжить;
			КонецЕсли;
			СтрокаТаблицы.ВариантаСКД.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
		КонецЦикла;
		
		НовыеНастройки = Новый ХранилищеЗначения(КомпоновщикНастроекКД.Настройки);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
	КонецПопытки;
	
	Если ИнформацияОбОшибке <> Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Вариант ""%1"" отчета ""%2"":'")
		+ Символы.ПС + НСтр("ru = 'При уменьшении количества быстрых настроек пользовательского возникла ошибка:'")
		+ Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ОшибкаПоВарианту(ВариантОбъект.Ссылка, ТекстОшибки, ВариантОбъект.КлючВарианта, ВариантОбъект.Отчет);
		Возврат Ложь; // Возникла проблема.
	КонецЕсли;
	
	ВариантОбъект.Настройки = НовыеНастройки;
	Возврат Истина;
КонецФункции

Функция РасширеннаяИнформацияОНастройках(КомпоновщикНастроекКД)
	НастройкиКД                 = КомпоновщикНастроекКД.Настройки;
	ПользовательскиеНастройкиКД = КомпоновщикНастроекКД.ПользовательскиеНастройки;
	
	ДеревоВарианта = Новый ДеревоЗначений;
	ДеревоВарианта.Колонки.Добавить("Настройка");
	ДеревоВарианта.Колонки.Добавить("ТипНастройки", Новый ОписаниеТипов("Строка"));
	ДеревоВарианта.Колонки.Добавить("Заголовок", Новый ОписаниеТипов("Строка"));
	ДеревоВарианта.Колонки.Добавить("ЕстьСтруктура", Новый ОписаниеТипов("Булево"));
	ДеревоВарианта.Колонки.Добавить("ЕстьПоляИОформление", Новый ОписаниеТипов("Булево"));
	ДеревоВариантаЗарегистрироватьУзел(НастройкиКД, ДеревоВарианта.Строки);
	
	ТаблицаНастроек = Новый ТаблицаЗначений;
	ТаблицаНастроек.Колонки.Добавить("ПользовательскаяСКД");
	ТаблицаНастроек.Колонки.Добавить("ВариантаСКД");
	ТаблицаНастроек.Колонки.Добавить("ДоступнаяСКД");
	ТаблицаНастроек.Колонки.Добавить("ИдентификаторСКД", Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("ИдентификаторЭлемента", Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("ИндексВКоллекции", Новый ОписаниеТипов("Число"));
	ТаблицаНастроек.Колонки.Добавить("ИдентификаторВКоллекции");
	
	ТаблицаНастроек.Колонки.Добавить("СтрокаДерева");
	ТаблицаНастроек.Колонки.Добавить("ИнформацияОТипах");
	
	ТаблицаНастроек.Колонки.Добавить("Тип", Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("Подтип", Новый ОписаниеТипов("Строка"));
	
	ТаблицаНастроек.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("Быстрая", Новый ОписаниеТипов("Булево"));
	ТаблицаНастроек.Колонки.Добавить("Обычная", Новый ОписаниеТипов("Булево"));
	ТаблицаНастроек.Колонки.Добавить("ВыводРазрешен", Новый ОписаниеТипов("Булево"));
	ТаблицаНастроек.Колонки.Добавить("ВыводитьФлажок", Новый ОписаниеТипов("Булево"));
	ТаблицаНастроек.Колонки.Добавить("ВыводитьЗначение", Новый ОписаниеТипов("Булево"));
	ТаблицаНастроек.Колонки.Добавить("ВводСписком", Новый ОписаниеТипов("Булево"));
	ТаблицаНастроек.Колонки.Добавить("ЗначенияДляВыбора", Новый ОписаниеТипов("Массив"));
	ТаблицаНастроек.Колонки.Добавить("ОграничиватьВыборУказаннымиЗначениями", Новый ОписаниеТипов("Булево"));
	ТаблицаНастроек.Колонки.Добавить("ДобавляемыеРеквизиты", Новый ОписаниеТипов("Массив"));
	
	ТаблицаНастроек.Колонки.Добавить("Значение");
	ТаблицаНастроек.Колонки.Добавить("ПолеСКД");
	ТаблицаНастроек.Колонки.Добавить("ВидСравнения");
	ТаблицаНастроек.Колонки.Добавить("ТипЭлементов", Новый ОписаниеТипов("Строка"));
	ТаблицаНастроек.Колонки.Добавить("ВыборТолькоГрупп", Новый ОписаниеТипов("Булево"));
	
	Режимы = РежимОтображенияЭлементаНастройкиКомпоновкиДанных;
	
	// Перебор пользовательских настроек.
	НаборПользовательскихНастроек = ПользовательскиеНастройкиКД.Элементы;
	Для Каждого ПользовательскаяНастройка Из НаборПользовательскихНастроек Цикл
		СвойстваНастройки = ТаблицаНастроек.Добавить();
		СвойстваНастройки.ПользовательскаяСКД = ПользовательскаяНастройка;
		
		РежимОтображения = ПользовательскаяНастройка.РежимОтображения;
		Если РежимОтображения = Режимы.Недоступный Тогда
			Продолжить;
		КонецЕсли;
		
		СвойстваНастройки.ИндексВКоллекции = НаборПользовательскихНастроек.Индекс(ПользовательскаяНастройка);
		СвойстваНастройки.ИдентификаторВКоллекции = ПользовательскиеНастройкиКД.ПолучитьИдентификаторПоОбъекту(ПользовательскаяНастройка);
		СвойстваНастройки.ИдентификаторСКД = ПользовательскаяНастройка.ИдентификаторПользовательскойНастройки;
		Если Не ЗначениеЗаполнено(СвойстваНастройки.ИдентификаторСКД) Тогда
			Продолжить;
		КонецЕсли;
		СвойстваНастройки.ИдентификаторЭлемента = ПривестиИдентификаторКИмени(СвойстваНастройки.ИдентификаторСКД);
		СвойстваНастройки.Тип = ТипНастройкиСтрокой(ТипЗнч(ПользовательскаяНастройка));
		СвойстваНастройки.ПользовательскаяСКД = ПользовательскаяНастройка;
		
		ДеревоВариантаНайти(НастройкиКД, ДеревоВарианта, СвойстваНастройки);
		Если СвойстваНастройки.ВариантаСКД = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
			И СвойстваНастройки.ДоступнаяСКД = Неопределено Тогда
			Продолжить; // Имя параметра изменилось или параметр был удален.
		ИначеЕсли СвойстваНастройки.Тип = "ЭлементОтбора"
			И СвойстваНастройки.ДоступнаяСКД = Неопределено Тогда
			Продолжить; // Имя поля изменилось или поле было удалено.
		КонецЕсли;
		
		Если РежимОтображения = Режимы.Авто Тогда
			РежимОтображения = СвойстваНастройки.ВариантаСКД.РежимОтображения;
		КонецЕсли;
		Если РежимОтображения = Режимы.БыстрыйДоступ Тогда
			СвойстваНастройки.Быстрая = Истина;
		ИначеЕсли РежимОтображения = Режимы.Обычный Тогда
			СвойстваНастройки.Обычная = Истина;
		Иначе
			Продолжить;
		КонецЕсли;
		
		СвойстваНастройки.ВыводРазрешен = Истина;
		СвойстваНастройки.ВыводитьФлажок = Истина;
		СвойстваНастройки.ВыводитьЗначение = Истина;
		
		СтруктураПредставлений = Новый Структура("Представление, ПредставлениеПользовательскойНастройки", "", "");
		ЗаполнитьЗначенияСвойств(СтруктураПредставлений, СвойстваНастройки.ВариантаСКД);
		Если ЗначениеЗаполнено(СтруктураПредставлений.Представление) Тогда
			СвойстваНастройки.Представление = СтруктураПредставлений.Представление;
			СвойстваНастройки.ВыводитьЗначение = Ложь;
		ИначеЕсли ЗначениеЗаполнено(СтруктураПредставлений.ПредставлениеПользовательскойНастройки) Тогда
			СвойстваНастройки.Представление = СтруктураПредставлений.ПредставлениеПользовательскойНастройки;
		ИначеЕсли СвойстваНастройки.ДоступнаяСКД <> Неопределено И ЗначениеЗаполнено(СвойстваНастройки.ДоступнаяСКД.Заголовок) Тогда
			СвойстваНастройки.Представление = СвойстваНастройки.ДоступнаяСКД.Заголовок;
		Иначе
			СвойстваНастройки.Представление = ПредставлениеПользовательскойНастройки(СвойстваНастройки);
		КонецЕсли;
		
		Если СвойстваНастройки.Тип = "ГруппаЭлементовОтбора"
			Или СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта"
			Или СвойстваНастройки.Тип = "Группировка"
			Или СвойстваНастройки.Тип = "Таблица"
			Или СвойстваНастройки.Тип = "ГруппировкаТаблицы"
			Или СвойстваНастройки.Тип = "Диаграмма"
			Или СвойстваНастройки.Тип = "ГруппировкаДиаграммы"
			Или СвойстваНастройки.Тип = "ЭлементУсловногоОформления" Тогда
			
			СвойстваНастройки.ВыводитьЗначение = Ложь;
			
		ИначеЕсли СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
			Или СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
			
			Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
				СвойстваНастройки.Значение = ПользовательскаяНастройка.Значение;
			Иначе
				СвойстваНастройки.Значение = ПользовательскаяНастройка.ПравоеЗначение;
			КонецЕсли;
			
			СвойстваНастройки.ИнформацияОТипах = АнализТипов(СвойстваНастройки.ДоступнаяСКД.ТипЗначения); // Определение типа значения настройки.
			
			Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
				Если СвойстваНастройки.ДоступнаяСКД.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда Тогда
					СвойстваНастройки.ВыводитьФлажок = Ложь;
					ПользовательскаяНастройка.Использование = Истина;
				КонецЕсли;
				Если СвойстваНастройки.ДоступнаяСКД.ДоступенСписокЗначений Тогда
					СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
				Иначе
					СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				КонецЕсли;
				СвойстваНастройки.ПолеСКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных." + Строка(СвойстваНастройки.ВариантаСКД.Параметр));
			ИначеЕсли СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
				СвойстваНастройки.ВидСравнения = ПользовательскаяНастройка.ВидСравнения;
				СвойстваНастройки.ПолеСКД = СвойстваНастройки.ВариантаСКД.ЛевоеЗначение;
			КонецЕсли;
			
			Если СвойстваНастройки.ИнформацияОТипах.СодержитТипПериод
				И СвойстваНастройки.ИнформацияОТипах.КоличествоТипов = 1 Тогда
				
				СвойстваНастройки.ТипЭлементов = "СтандартныйПериод";
				
			ИначеЕсли Не СвойстваНастройки.ВыводитьФлажок
				И СвойстваНастройки.ИнформацияОТипах.СодержитТипБулево
				И СвойстваНастройки.ИнформацияОТипах.КоличествоТипов = 1 Тогда
				
				СвойстваНастройки.ТипЭлементов = "ТолькоФлажок";
				
			ИначеЕсли СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено
				Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено Тогда
				
				СвойстваНастройки.ВыводитьЗначение = Ложь;
				СвойстваНастройки.ТипЭлементов = "ТолькоФлажок";
				СвойстваНастройки.Представление = СвойстваНастройки.Представление + ": " + НРег(Строка(СвойстваНастройки.ВидСравнения));
				
			ИначеЕсли СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
				Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
				Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
				Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
				
				СвойстваНастройки.ВводСписком = Истина;
				СвойстваНастройки.ТипЭлементов = "СписокСПодбором";
				
			Иначе
				
				СвойстваНастройки.ТипЭлементов = "ПолеВвода";
				Если СвойстваНастройки.ВидСравнения <> ВидСравненияКомпоновкиДанных.Равно
					И СвойстваНастройки.ВидСравнения <> ВидСравненияКомпоновкиДанных.Содержит Тогда
					СвойстваНастройки.Представление = СвойстваНастройки.Представление + " (" + НРег(Строка(СвойстваНастройки.ВидСравнения)) + ")";
				КонецЕсли;
				Если СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии
					Или СвойстваНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВИерархии Тогда
					СвойстваНастройки.ВыборТолькоГрупп = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли СвойстваНастройки.Тип = "ВыбранныеПоля" Тогда
			
			СвойстваНастройки.ТипЭлементов = "ТаблицаПорядкаСФлажками";
			
		ИначеЕсли СвойстваНастройки.Тип = "Порядок" Тогда
			
			СвойстваНастройки.ТипЭлементов = "ТаблицаПорядкаСФлажками";
			
		ИначеЕсли СвойстваНастройки.Тип = "СтруктураНастроек"
			И СвойстваНастройки.Подтип = "Структура" Тогда
			
			СвойстваНастройки.ТипЭлементов = "ТаблицаПорядкаСФлажками";
			
		Иначе
			
			СвойстваНастройки.ТипЭлементов = "СвязьСКомпоновщиком";
			
		КонецЕсли;
		
		Если Не СвойстваНастройки.ВыводитьЗначение Тогда
			СвойстваНастройки.ТипЭлементов = "Нет";
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ДеревоВарианта",  ДеревоВарианта);
	Результат.Вставить("ТаблицаНастроек", ТаблицаНастроек);
	
	Возврат Результат;
КонецФункции

Функция ПривестиИдентификаторКИмени(Идентификатор)
	Возврат СтрЗаменить(Строка(Идентификатор), "-", "");
КонецФункции

Функция ТипНастройкиСтрокой(ТипНастройки)
	Если ТипНастройки = Тип("НастройкиКомпоновкиДанных") Тогда
		Возврат "Настройки";
	ИначеЕсли ТипНастройки = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Возврат "НастройкиВложенногоОбъекта";
	
	ИначеЕсли ТипНастройки = Тип("ОтборКомпоновкиДанных") Тогда
		Возврат "Отбор";
	ИначеЕсли ТипНастройки = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
		Возврат "ЭлементОтбора";
	ИначеЕсли ТипНастройки = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		Возврат "ГруппаЭлементовОтбора";
	
	ИначеЕсли ТипНастройки = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
		Возврат "ЗначениеПараметраНастроек";
	
	ИначеЕсли ТипНастройки = Тип("ГруппировкаКомпоновкиДанных") Тогда
		Возврат "Группировка";
	ИначеЕсли ТипНастройки = Тип("ПорядокКомпоновкиДанных") Тогда
		Возврат "Порядок";
	ИначеЕсли ТипНастройки = Тип("ВыбранныеПоляКомпоновкиДанных") Тогда
		Возврат "ВыбранныеПоля";
	
	ИначеЕсли ТипНастройки = Тип("УсловноеОформлениеКомпоновкиДанных") Тогда
		Возврат "УсловноеОформление";
	ИначеЕсли ТипНастройки = Тип("ЭлементУсловногоОформленияКомпоновкиДанных") Тогда
		Возврат "ЭлементУсловногоОформления";
	
	ИначеЕсли ТипНастройки = Тип("СтруктураНастроекКомпоновкиДанных") Тогда
		Возврат "СтруктураНастроек";
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных") Тогда
		Возврат "КоллекцияЭлементовСтруктурыНастроек";
	
	ИначеЕсли ТипНастройки = Тип("ТаблицаКомпоновкиДанных") Тогда
		Возврат "Таблица";
	ИначеЕсли ТипНастройки = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		Возврат "ГруппировкаТаблицы";
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		Возврат "КоллекцияЭлементовСтруктурыТаблицы";
	
	ИначеЕсли ТипНастройки = Тип("ДиаграммаКомпоновкиДанных") Тогда
		Возврат "Диаграмма";
	ИначеЕсли ТипНастройки = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		Возврат "ГруппировкаДиаграммы";
	ИначеЕсли ТипНастройки = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") Тогда
		Возврат "КоллекцияЭлементовСтруктурыДиаграммы";
	
	ИначеЕсли ТипНастройки = Тип("ПолеГруппировкиКомпоновкиДанных") Тогда
		Возврат "ПолеГруппировки";
	ИначеЕсли ТипНастройки = Тип("ПоляГруппировкиКомпоновкиДанных") Тогда
		Возврат "ПоляГруппировки";
	ИначеЕсли ТипНастройки = Тип("КоллекцияПолейГруппировкиКомпоновкиДанных") Тогда
		Возврат "КоллекцияПолейГруппировки";
	
	Иначе
		Возврат "";
	КонецЕсли;
КонецФункции

Процедура ДеревоВариантаЗарегистрироватьУзел(ЭлементСтруктурыСКД, НаборСтрокДерева)
	СтрокаДерева = НаборСтрокДерева.Добавить();
	СтрокаДерева.Настройка = ЭлементСтруктурыСКД;
	СтрокаДерева.ТипНастройки = ТипНастройкиСтрокой(ТипЗнч(ЭлементСтруктурыСКД));
	Если СтрокаДерева.ТипНастройки = "Настройки" Тогда
		СтрокаДерева.ЕстьСтруктура = Истина;
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.ТипНастройки = "Группировка" Тогда
		СтрокаДерева.ЕстьСтруктура = Истина;
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.ТипНастройки = "Таблица" Тогда
		СтрокаДерева.ЕстьПоляИОформление = Истина;
		// Колонки:
		ПодчиненнаяСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ПодчиненнаяСтрокаДерева.Настройка = ЭлементСтруктурыСКД;
		ПодчиненнаяСтрокаДерева.ТипНастройки = СтрокаДерева.ТипНастройки + "_Колонки";
		Для Каждого ВложенныйЭлемент Из ЭлементСтруктурыСКД.Колонки Цикл
			ДеревоВариантаЗарегистрироватьУзел(ВложенныйЭлемент, ПодчиненнаяСтрокаДерева.Строки);
		КонецЦикла;
		// Строки:
		ПодчиненнаяСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ПодчиненнаяСтрокаДерева.Настройка = ЭлементСтруктурыСКД;
		ПодчиненнаяСтрокаДерева.ТипНастройки = СтрокаДерева.ТипНастройки + "_Строки";
		Для Каждого ВложенныйЭлемент Из ЭлементСтруктурыСКД.Строки Цикл
			ДеревоВариантаЗарегистрироватьУзел(ВложенныйЭлемент, ПодчиненнаяСтрокаДерева.Строки);
		КонецЦикла;
	ИначеЕсли СтрокаДерева.ТипНастройки = "Диаграмма" Тогда
		СтрокаДерева.ЕстьПоляИОформление = Истина;
		// Серии:
		ПодчиненнаяСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ПодчиненнаяСтрокаДерева.Настройка = ЭлементСтруктурыСКД;
		ПодчиненнаяСтрокаДерева.ТипНастройки = СтрокаДерева.ТипНастройки + "_Серии";
		Для Каждого ВложенныйЭлемент Из ЭлементСтруктурыСКД.Серии Цикл
			ДеревоВариантаЗарегистрироватьУзел(ВложенныйЭлемент, ПодчиненнаяСтрокаДерева.Строки);
		КонецЦикла;
		// Точки:
		ПодчиненнаяСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ПодчиненнаяСтрокаДерева.Настройка = ЭлементСтруктурыСКД;
		ПодчиненнаяСтрокаДерева.ТипНастройки = СтрокаДерева.ТипНастройки + "_Точки";
		Для Каждого ВложенныйЭлемент Из ЭлементСтруктурыСКД.Точки Цикл
			ДеревоВариантаЗарегистрироватьУзел(ВложенныйЭлемент, ПодчиненнаяСтрокаДерева.Строки);
		КонецЦикла;
	ИначеЕсли СтрокаДерева.ТипНастройки = "НастройкиВложенногоОбъекта" Тогда
		ДеревоВариантаЗарегистрироватьУзел(ЭлементСтруктурыСКД.Настройки, СтрокаДерева.Строки);
	ИначеЕсли СтрокаДерева.ТипНастройки = "ГруппировкаТаблицы" Тогда
		СтрокаДерева.ЕстьСтруктура = Истина;
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	ИначеЕсли СтрокаДерева.ТипНастройки = "ГруппировкаДиаграммы" Тогда
		СтрокаДерева.ЕстьСтруктура = Истина;
		СтрокаДерева.ЕстьПоляИОформление = Истина;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если СтрокаДерева.ТипНастройки <> "НастройкиВложенногоОбъекта" Тогда
		ПараметрЗаголовок = ЭлементСтруктурыСКД.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Заголовок"));
		Если ПараметрЗаголовок <> Неопределено И ПараметрЗаголовок.Использование Тогда
			СтрокаДерева.Заголовок = ПараметрЗаголовок.Значение;
		КонецЕсли;
	КонецЕсли;
	
	Если СтрокаДерева.ЕстьСтруктура Тогда
		Для Каждого ВложенныйЭлемент Из ЭлементСтруктурыСКД.Структура Цикл
			ДеревоВариантаЗарегистрироватьУзел(ВложенныйЭлемент, СтрокаДерева.Строки);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Функция ДеревоВариантаНайти(НастройкиКД, ДеревоВарианта, СвойстваНастройки)
	ИдентификаторСКД = СвойстваНастройки.ИдентификаторСКД;
	
	Если СвойстваНастройки.Тип = "Отбор" Тогда
		
		//Настройки, свойство Отбор
		//Группировка, свойство Отбор
		//ГруппировкаТаблицы, свойство Отбор
		//ГруппировкаДиаграммы, свойство Отбор
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьСтруктура", Истина), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если СтрокаДерева.Настройка.Отбор.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.Отбор;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементОтбора"
		Или СвойстваНастройки.Тип = "ГруппаЭлементовОтбора" Тогда
		
		//Настройки, свойство Отбор.Элементы
		//Группировка, свойство Отбор.Элементы
		//ГруппировкаТаблицы, свойство Отбор.Элементы
		//ГруппировкаДиаграммы, свойство Отбор.Элементы
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьСтруктура", Истина), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			НайденныйЭлемент = НайтиПоИдентификатору(СтрокаДерева.Настройка.Отбор.Элементы, ИдентификаторСКД);
			Если НайденныйЭлемент <> Неопределено Тогда
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = НайденныйЭлемент;
				Если СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
					СвойстваНастройки.ДоступнаяСКД = СтрокаДерева.Настройка.Отбор.ДоступныеПоляОтбора.НайтиПоле(НайденныйЭлемент.ЛевоеЗначение);
				КонецЕсли;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли СвойстваНастройки.Тип = "Порядок" Тогда
		
		//Настройки, свойство Порядок
		//Группировка, свойство Порядок
		//ГруппировкаТаблицы, свойство Порядок
		//ГруппировкаДиаграммы, свойство Порядок
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьСтруктура", Истина), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если СтрокаДерева.Настройка.Порядок.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.Порядок;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли СвойстваНастройки.Тип = "ВыбранныеПоля" Тогда
		
		//Настройки, свойство Выбор
		//Таблица, свойство Выбор
		//Диаграмма, свойство Выбор
		//Группировка, свойство Выбор
		//ГруппировкаДиаграммы, свойство Выбор
		//ГруппировкаТаблицы, свойство Выбор
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьПоляИОформление", Истина), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если СтрокаДерева.Настройка.Выбор.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.Выбор;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли СвойстваНастройки.Тип = "УсловноеОформление" Тогда
		
		//Настройки, свойство УсловноеОформление
		//Таблица, свойство УсловноеОформление
		//Диаграмма, свойство УсловноеОформление
		//Группировка, свойство УсловноеОформление
		//ГруппировкаДиаграммы, свойство УсловноеОформление
		//ГруппировкаТаблицы, свойство УсловноеОформление
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьПоляИОформление", Истина), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если СтрокаДерева.Настройка.УсловноеОформление.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.УсловноеОформление;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементУсловногоОформления" Тогда
		
		//Настройки, свойство УсловноеОформление.Элементы
		//Таблица, свойство УсловноеОформление.Элементы
		//Диаграмма, свойство УсловноеОформление.Элементы
		//Группировка, свойство УсловноеОформление.Элементы
		//ГруппировкаДиаграммы, свойство УсловноеОформление.Элементы
		//ГруппировкаТаблицы, свойство УсловноеОформление.Элементы
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьПоляИОформление", Истина), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Для Каждого ЭлементУО Из СтрокаДерева.Настройка.УсловноеОформление.Элементы Цикл
				Если ЭлементУО.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
					СвойстваНастройки.СтрокаДерева = СтрокаДерева;
					СвойстваНастройки.ВариантаСКД  = ЭлементУО;
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	ИначеЕсли СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
		
		ПараметрКД = СвойстваНастройки.ПользовательскаяСКД.Параметр;
		
		//ПараметрКомпоновкиДанных содержится в КоллекцияЗначенийПараметровКомпоновкиДанных, которая в свою очередь:
		//	ЗначенияПараметровДанныхКомпоновкиДанных, свойство Элементы / метод НайтиЗначениеПараметра
		//		Настройки, свойство ПараметрыДанных
		//	ЗначенияПараметровВыводаКомпоновкиДанных, свойство Элементы / метод НайтиЗначениеПараметра
		//		Настройки, свойство ПараметрыВывода
		//	ЗначенияПараметровВыводаГруппировкиКомпоновкиДанных, свойство Элементы / метод НайтиЗначениеПараметра
		//		Группировка, свойство ПараметрыВывода
		//	ЗначенияПараметровВыводаГруппировкиТаблицыКомпоновкиДанных, свойство Элементы / метод НайтиЗначениеПараметра
		//		ГруппировкаТаблицы, свойство ПараметрыВывода
		//	ЗначенияПараметровВыводаТаблицыКомпоновкиДанных, свойство Элементы / метод НайтиЗначениеПараметра
		//		Таблица, свойство ПараметрыВывода
		//	ЗначенияПараметровВыводаДиаграммыКомпоновкиДанных, свойство Элементы / метод НайтиЗначениеПараметра
		//		Диаграмма, свойство ПараметрыВывода
		//	ЗначенияПараметровВыводаГруппировкиДиаграммыКомпоновкиДанных, свойство Элементы / метод НайтиЗначениеПараметра
		//		ГруппировкаДиаграммы, свойство ПараметрыВывода
		//	ОформлениеКомпоновкиДанных, свойство Элементы / метод НайтиЗначениеПараметра
		//		ЭлементОбластиМакетаОформленияКомпоновкиДанных, свойство Оформление
		//		ЭлементУсловногоОформленияКомпоновкиДанных, свойство Оформление
		//		ПолеНабораДанныхСхемыКомпоновкиДанных, свойство Оформление
		//		ВычисляемоеПолеСхемыКомпоновкиДанных, свойство Оформление
		//	ЗначениеПараметраКомпоновкиДанных, свойство ЗначенияВложенныхПараметров
		
		//Настройки, свойство ПараметрыДанных, метод НайтиЗначениеПараметра()
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ТипНастройки", "Настройки"), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			ЗначениеПараметра = СтрокаДерева.Настройка.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКД);
			Если ЗначениеПараметра <> Неопределено
				И ЗначениеПараметра.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = ЗначениеПараметра;
				ДоступныеПараметры = СтрокаДерева.Настройка.ПараметрыДанных.ДоступныеПараметры;
				Если ДоступныеПараметры <> Неопределено Тогда
					СвойстваНастройки.ДоступнаяСКД = ДоступныеПараметры.НайтиПараметр(ПараметрКД);
				КонецЕсли;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
		//Настройки, свойство ПараметрыВывода, метод НайтиЗначениеПараметра()
		//Группировка, свойство ПараметрыВывода, метод НайтиЗначениеПараметра()
		//Таблица, свойство ПараметрыВывода, метод НайтиЗначениеПараметра()
		//Диаграмма, свойство ПараметрыВывода, метод НайтиЗначениеПараметра()
		//ГруппировкаТаблицы, свойство ПараметрыВывода, метод НайтиЗначениеПараметра()
		//ГруппировкаДиаграммы, свойство ПараметрыВывода, метод НайтиЗначениеПараметра()
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьПоляИОформление", Истина), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			ЗначениеПараметра = СтрокаДерева.Настройка.ПараметрыВывода.НайтиЗначениеПараметра(ПараметрКД);
			Если ЗначениеПараметра <> Неопределено
				И ЗначениеПараметра.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.Тип          = "ЗначениеПараметраВывода"; // Подмена типа...
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = ЗначениеПараметра;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли СвойстваНастройки.Тип = "Группировка"
		Или СвойстваНастройки.Тип = "ГруппировкаТаблицы"
		Или СвойстваНастройки.Тип = "ГруппировкаДиаграммы"
		Или СвойстваНастройки.Тип = "Таблица"
		Или СвойстваНастройки.Тип = "Диаграмма"
		Или СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта" Тогда
		
		// Прямо в структуре.
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ТипНастройки", СвойстваНастройки.Тип), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если СтрокаДерева.Настройка.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка;
				Если СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта" Тогда
					СвойстваНастройки.ДоступнаяСКД = НастройкиКД.ДоступныеОбъекты.Элементы.Найти(СтрокаДерева.Настройка.ИдентификаторОбъекта);
				КонецЕсли;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
		Если СвойстваНастройки.Тип = "Группировка" Тогда
			Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ТипНастройки", "ГруппировкаТаблицы"), Истина);
			Для Каждого СтрокаДерева Из Найденные Цикл
				Если СтрокаДерева.Настройка.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
					СвойстваНастройки.Подтип       = "ГруппировкаТаблицы";
					СвойстваНастройки.СтрокаДерева = СтрокаДерева;
					СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка;
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
			
			Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ТипНастройки", "ГруппировкаДиаграммы"), Истина);
			Для Каждого СтрокаДерева Из Найденные Цикл
				Если СтрокаДерева.Настройка.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
					СвойстваНастройки.Подтип       = "ГруппировкаДиаграммы";
					СвойстваНастройки.СтрокаДерева = СтрокаДерева;
					СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка;
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли СвойстваНастройки.Тип = "СтруктураНастроек" Тогда
		
		//Настройки, свойство Структура
		//Группировка, свойство Структура
		//ГруппировкаТаблицы, свойство Структура
		//ГруппировкаДиаграммы, свойство Структура
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ЕстьСтруктура", Истина), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если СтрокаДерева.Настройка.Структура.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.Подтип       = "Структура";
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.Структура;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
		//Таблица, свойство Колонки
		//Таблица, свойство Строки
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ТипНастройки", "Таблица"), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если СтрокаДерева.Настройка.Строки.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.Подтип       = "ТаблицаСтроки";
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.Строки;
				Возврат Истина;
			ИначеЕсли СтрокаДерева.Настройка.Колонки.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.Подтип       = "ТаблицаКолонки";
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.Колонки;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
		//Диаграмма, свойство Серии
		//Диаграмма, свойство Точки
		Найденные = ДеревоВарианта.Строки.НайтиСтроки(Новый Структура("ТипНастройки", "Диаграмма"), Истина);
		Для Каждого СтрокаДерева Из Найденные Цикл
			Если СтрокаДерева.Настройка.Серии.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.Подтип       = "ДиаграммаСерии";
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.Серии;
				Возврат Истина;
			ИначеЕсли СтрокаДерева.Настройка.Точки.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
				СвойстваНастройки.Подтип       = "ДиаграммаТочки";
				СвойстваНастройки.СтрокаДерева = СтрокаДерева;
				СвойстваНастройки.ВариантаСКД  = СтрокаДерева.Настройка.Точки;
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Функция НайтиПоИдентификатору(ЭлементыСКД, ИдентификаторСКД)
	Для Каждого ЭлементСКД Из ЭлементыСКД Цикл
		Если ЭлементСКД.ИдентификаторПользовательскойНастройки = ИдентификаторСКД Тогда
			Возврат ЭлементСКД;
		ИначеЕсли ТипЗнч(ЭлементСКД) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			Найденный = НайтиПоИдентификатору(ЭлементСКД.Элементы, ИдентификаторСКД);
			Если Найденный <> Неопределено Тогда
				Возврат Найденный;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

Функция ПредставлениеПользовательскойНастройки(СвойстваНастройки)
	ЭлементЗаголовок = "";
	
	Если СвойстваНастройки.Тип = "Отбор" Тогда
		ЭлементЗаголовок = НСтр("ru = 'Отбор'");
		
	ИначеЕсли СвойстваНастройки.Тип = "ГруппаЭлементовОтбора" Тогда
		ЭлементЗаголовок = Строка(СвойстваНастройки.ПользовательскаяСКД.ТипГруппы);
		
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементОтбора" Тогда
		ЭлементЗаголовок = Строка(СвойстваНастройки.ВариантаСКД.ЛевоеЗначение);
		
	ИначеЕсли СвойстваНастройки.Тип = "Порядок" Тогда
		ЭлементЗаголовок = НСтр("ru = 'Сортировка'");
		
	ИначеЕсли СвойстваНастройки.Тип = "ВыбранныеПоля" Тогда
		ЭлементЗаголовок = НСтр("ru = 'Поля'");
		
	ИначеЕсли СвойстваНастройки.Тип = "УсловноеОформление" Тогда
		ЭлементЗаголовок = НСтр("ru = 'Оформление'");
		
	ИначеЕсли СвойстваНастройки.Тип = "ЭлементУсловногоОформления" Тогда
		ПредставлениеЗначения = Строка(СвойстваНастройки.ПользовательскаяСКД.Оформление);
		ПредставлениеТипа = НСтр("ru = 'Элемент оформления'");
		Если ПустаяСтрока(ПредставлениеЗначения) Тогда
			ЭлементЗаголовок = ПредставлениеТипа;
		Иначе
			ЭлементЗаголовок = ПредставлениеЗначения +" ("+ НРег(ПредставлениеТипа) +")";
		КонецЕсли;
	ИначеЕсли СвойстваНастройки.Тип = "ЗначениеПараметраНастроек" Тогда
		ЭлементЗаголовок = Строка(СвойстваНастройки.ВариантаСКД.Параметр);
		
	ИначеЕсли СвойстваНастройки.Тип = "Группировка" Тогда
		Суффикс = СокрЛП(Строка(СвойстваНастройки.ВариантаСКД.ПоляГруппировки));
		ЭлементЗаголовок = НСтр("ru = 'Группировка'") + ?(Суффикс = "", "", " '" + Суффикс + "'");
		
	ИначеЕсли СвойстваНастройки.Тип = "ГруппировкаТаблицы" Тогда
		ЭлементЗаголовок = НСтр("ru = 'Группировка таблицы'");
		
	ИначеЕсли СвойстваНастройки.Тип = "ГруппировкаДиаграммы" Тогда
		Суффикс = СокрЛП(Строка(СвойстваНастройки.ВариантаСКД.ПоляГруппировки));
		ЭлементЗаголовок = НСтр("ru = 'Группировка диаграммы'") + ?(Суффикс = "", "", " '" + Суффикс + "'");
		
	ИначеЕсли СвойстваНастройки.Тип = "Таблица" Тогда
		ЭлементЗаголовок = НСтр("ru = 'Таблица'");
		
	ИначеЕсли СвойстваНастройки.Тип = "Диаграмма" Тогда
		ЭлементЗаголовок = НСтр("ru = 'Диаграмма'");
		
	ИначеЕсли СвойстваНастройки.Тип = "НастройкиВложенногоОбъекта" Тогда
		ЭлементЗаголовок = СтрЗаменить(НСтр("ru = 'Вложенная группировка ''%1'''"), "%1", Строка(СвойстваНастройки.ПользовательскаяСКД));
		
	ИначеЕсли СвойстваНастройки.Тип = "СтруктураНастроек" Тогда
		
		Если СвойстваНастройки.Подтип = "ТаблицаСтроки" Тогда
			ЭлементЗаголовок = НСтр("ru = 'Строки'");
			
		ИначеЕсли СвойстваНастройки.Подтип = "ТаблицаКолонки" Тогда
			ЭлементЗаголовок = НСтр("ru = 'Колонки'");
			
		ИначеЕсли СвойстваНастройки.Подтип = "ДиаграммаСерии" Тогда
			ЭлементЗаголовок = НСтр("ru = 'Серии'");
			
		ИначеЕсли СвойстваНастройки.Подтип = "ДиаграммаТочки" Тогда
			ЭлементЗаголовок = НСтр("ru = 'Точки'");
			
		Иначе
			ЭлементЗаголовок = НСтр("ru = 'Структура'");
			
		КонецЕсли;
		
	Иначе
		ЭлементЗаголовок = Строка(СвойстваНастройки.Тип);
		
	КонецЕсли;
	
	Возврат СокрЛП(ЭлементЗаголовок);
КонецФункции

Функция АнализТипов(ИсходноеОписаниеТипов)
	ИнформацияОТипах = Новый Структура;
	ИнформацияОТипах.Вставить("СодержитТипТип",        Ложь);
	ИнформацияОТипах.Вставить("СодержитТипДата",       Ложь);
	ИнформацияОТипах.Вставить("СодержитТипБулево",     Ложь);
	ИнформацияОТипах.Вставить("СодержитТипЧисло",      Ложь);
	ИнформацияОТипах.Вставить("СодержитТипПериод",     Ложь);
	ИнформацияОТипах.Вставить("СодержитОбъектныеТипы",    Ложь);
	
	ИнформацияОТипах.Вставить("КоличествоТипов",                0);
	ИнформацияОТипах.Вставить("КоличествоПримитивныхТипов",     0);
	
	ИнформацияОТипах.Вставить("ОбъектныеТипы", Новый Массив);
	
	ДобавляемыеТипы = Новый Массив;
	ВычитаемыеТипы = Новый Массив;
	
	ДоступнаяНастройкаТипы = ИсходноеОписаниеТипов.Типы();
	Для Каждого Тип Из ДоступнаяНастройкаТипы Цикл
		Если Тип = Тип("ПолеКомпоновкиДанных") Тогда
			ВычитаемыеТипы.Добавить(Тип);
			Продолжить;
		КонецЕсли;
		ИнформацияОТипах.КоличествоТипов = ИнформацияОТипах.КоличествоТипов + 1;
		
		Если Тип = Тип("Тип") Тогда
			ИнформацияОТипах.СодержитТипТип = Истина;
			ДобавляемыеТипы.Добавить(Тип("ОписаниеТипов"));
			ВычитаемыеТипы.Добавить(Тип("Тип"));
		ИначеЕсли Тип = Тип("Дата") Тогда
			ИнформацияОТипах.СодержитТипДата = Истина;
			ИнформацияОТипах.КоличествоПримитивныхТипов = ИнформацияОТипах.КоличествоПримитивныхТипов + 1;
		ИначеЕсли Тип = Тип("Булево") Тогда
			ИнформацияОТипах.СодержитТипБулево = Истина;
			ИнформацияОТипах.КоличествоПримитивныхТипов = ИнформацияОТипах.КоличествоПримитивныхТипов + 1;
		ИначеЕсли Тип = Тип("Число") Тогда
			ИнформацияОТипах.СодержитТипЧисло = Истина;
			ИнформацияОТипах.КоличествоПримитивныхТипов = ИнформацияОТипах.КоличествоПримитивныхТипов + 1;
		ИначеЕсли Тип = Тип("СтандартныйПериод") Тогда
			ИнформацияОТипах.СодержитТипПериод = Истина;
		Иначе
			ИнформацияОТипах.СодержитОбъектныеТипы = Истина;
			ИнформацияОТипах.ОбъектныеТипы.Добавить(Тип);
		КонецЕсли;
		
	КонецЦикла;
	
	ОписаниеТиповДляФормы = Новый ОписаниеТипов(ИсходноеОписаниеТипов, ДобавляемыеТипы, ВычитаемыеТипы);
	
	ИнформацияОТипах.Вставить("ОписаниеТиповИсходное", ИсходноеОписаниеТипов);
	ИнформацияОТипах.Вставить("ОписаниеТиповДляФормы", ОписаниеТиповДляФормы);
	
	Возврат ИнформацияОТипах;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Поиск.

// Можно ли индексировать содержимое схемы отчета.
Функция РазрешеноИндексированиеНеразделенныхДанных() Экспорт
	Возврат Не ОбщегоНазначенияПовтИсп.РазделениеВключено();
КонецФункции

// Находит ссылки по параметрам поиска.
//   Подсвечивает найденные места.
//
// Параметры:
//   ПараметрыПоиска - Структура - Условия поиска.
//       * СтрокаПоиска - Строка - Необязательный.
//       * Автор - СправочникСсылка.Пользователи - Необязательный.
//       * Подсистемы - Массив из СправочникСсылка.ИдентификаторыОбъектовМетаданных - Необязательный.
//
// Возвращаемое значение: 
//   Структура - Когда выполняется поиск по строке.
//       * Ссылки - Массив из СправочникСсылка.ВариантыОтчетов -
//           Заполняется вариантами отчетов, в данных которых найдены все искомые слова.
//       * ПодсветкаВариантов - Соответствие - Подсветка найденных слов (если задана СтрокаПоиска).
//           ** Ключ - СправочникСсылка.ВариантыОтчетов
//           ** Значение - Структура
//               *** Ссылка - СправочникСсылка.ВариантыОтчетов
//               *** НаименованияПолей                    - Строка
//               *** НаименованияПараметровИОтборов       - Строка
//               *** Описание                             - Строка
//               *** НаименованияПользовательскихНастроек - Строка
//               *** ГдеНайдены                           - Структура
//                   **** НаименованияПолей                    - Число
//                   **** НаименованияПараметровИОтборов       - Число
//                   **** Описание                             - Число
//                   **** НаименованияПользовательскихНастроек - Число
//       * Подсистемы - Массив из СправочникСсылка.ИдентификаторыОбъектовМетаданных - 
//           Заполняется подсистемами, в наименованиях которых найдены все искомые слова.
//           Для таких подсистем должны выводиться все вложенные варианты отчетов.
//       * ПодсветкаПодсистем - Соответствие - Подсветка найденных слов (если задана СтрокаПоиска).
//           ** Ключ - СправочникСсылка.ВариантыОтчетов
//           ** Значение - Структура
//               *** Ссылка - СправочникСсылка.ИдентификаторыОбъектовМетаданных
//               *** НаименованиеПодсистемы - Строка
//               *** ВсеСловаНайдены - Булево
//               *** НайденныеСлова - Массив
//       * ВариантыСвязанныеСПодсистемами - Соответствие - Варианты отчетов и их подсистемы.
//           Заполняется когда часть слов найдена в данных варианта, а другая часть - в наименованиях его подсистем.
//           В этом случае вариант должен выводиться только в найденных подсистемах (а в других - не должен выводиться).
//           Применяется в панели отчетов.
//           ** Ключ - СправочникСсылка.ВариантыОтчетов - Вариант.
//           ** Значение - Массив Из СправочникСсылка.ИдентификаторыОбъектовМетаданных - Подсистемы.
//
Функция НайтиСсылки(ПараметрыПоиска) Экспорт
	ПараметрыПоиска.Вставить("ОтчетыПользователя", ОтчетыТекущегоПользователя());
	ПараметрыПоиска.Вставить("ОтключенныеВариантыПрограммы", ВариантыОтчетовПовтИсп.ОтключенныеВариантыПрограммы());
	
	Если ПараметрыПоиска.Свойство("СтрокаПоиска") И ЗначениеЗаполнено(ПараметрыПоиска.СтрокаПоиска) Тогда
		ЕстьСтрокаПоиска = Истина;
	Иначе
		ЕстьСтрокаПоиска = Ложь;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("Подсистемы") И ЗначениеЗаполнено(ПараметрыПоиска.Подсистемы) Тогда
		ЕстьОтборПоПодсистемам = Истина;
	Иначе
		ЕстьОтборПоПодсистемам = Ложь;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ТипыОтчетов") И ЗначениеЗаполнено(ПараметрыПоиска.ТипыОтчетов) Тогда
		ЕстьОтборПоТипамОтчетов = Истина;
	Иначе
		ЕстьОтборПоТипамОтчетов = Ложь;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ТолькоВидимыеВПанелиОтчетов") И ПараметрыПоиска.ТолькоВидимыеВПанелиОтчетов = Истина Тогда
		ЕстьОтборПоВидимости = ЕстьОтборПоПодсистемам; // Поддерживается только когда задан отбор по подсистемам.
	Иначе
		ЕстьОтборПоВидимости = Ложь;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ПолучатьИтоговуюТаблицу") И ПараметрыПоиска.ПолучатьИтоговуюТаблицу = Истина Тогда
		ПолучатьИтоговуюТаблицу = Истина;
	Иначе
		ПолучатьИтоговуюТаблицу = Ложь;
	КонецЕсли;
	
	Если ПараметрыПоиска.Свойство("ПометкаУдаления") Тогда
		ЕстьОтборПоПометкеУдаления = Истина;
	Иначе
		ЕстьОтборПоПометкеУдаления = Ложь;
	КонецЕсли;
	
	Если Не ЕстьОтборПоПодсистемам И Не ЕстьСтрокаПоиска И Не ЕстьОтборПоТипамОтчетов Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ТекущийПользователь = Пользователи.АвторизованныйПользователь();
	
	Запрос.УстановитьПараметр("ТекущийПользователь",          ТекущийПользователь);
	Запрос.УстановитьПараметр("ОтчетыПользователя",           ПараметрыПоиска.ОтчетыПользователя);
	Запрос.УстановитьПараметр("ОтключенныеВариантыПрограммы", ПараметрыПоиска.ОтключенныеВариантыПрограммы);
	
	Если ЕстьОтборПоПодсистемам Или ЕстьСтрокаПоиска Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВариантыОтчетов.Ссылка,
		|	ВариантыОтчетов.Родитель КАК Родитель,
		|	ВариантыОтчетов.Наименование КАК НаименованиеВарианта,
		|	ВариантыОтчетов.Автор КАК Автор,
		|	ВариантыОтчетов.ТолькоДляАвтора КАК ТолькоДляАвтора,
		|	ВЫРАЗИТЬ(ВариантыОтчетов.Автор.Наименование КАК СТРОКА(1000)) КАК АвторПредставление,
		|	ВариантыОтчетов.Отчет КАК Отчет,
		|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
		|	ВариантыОтчетов.ТипОтчета КАК ТипОтчета,
		|	ВариантыОтчетов.Пользовательский КАК Пользовательский,
		|	ВариантыОтчетов.ПредопределенныйВариант КАК ПредопределенныйВариант,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.НаименованияПараметровИОтборов, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(Предопределенные.НаименованияПараметровИОтборов КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПараметровИОтборов КАК СТРОКА(1000))
		|	КОНЕЦ КАК НаименованияПараметровИОтборов,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.НаименованияПолей, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(Предопределенные.НаименованияПолей КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПолей КАК СТРОКА(1000))
		|	КОНЕЦ КАК НаименованияПолей,
		|	ВЫБОР
		|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
		|			ТОГДА ВЫРАЗИТЬ(Предопределенные.Описание КАК СТРОКА(1000))
		|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
		|	КОНЕЦ КАК Описание,
		|	ВариантыОтчетов.ВидимостьПоУмолчанию КАК ВидимостьПоУмолчанию
		|ПОМЕСТИТЬ Варианты
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов КАК Предопределенные
		|		ПО ВариантыОтчетов.ПредопределенныйВариант = Предопределенные.Ссылка
		|ГДЕ
		|	ВариантыОтчетов.ТипОтчета В(&ТипыОтчетов)
		|	И ВариантыОтчетов.Отчет В(&ОтчетыПользователя)
		|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
		|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ
		|	И (Предопределенные.ПометкаУдаления ЕСТЬ NULL 
		|			ИЛИ Предопределенные.ПометкаУдаления = ЛОЖЬ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(РазмещениеВариантов.Ссылка, РазмещениеПредопределенных.Ссылка) КАК Ссылка,
		|	ЕСТЬNULL(РазмещениеВариантов.Подсистема, РазмещениеПредопределенных.Подсистема) КАК Подсистема,
		|	ЕСТЬNULL(РазмещениеВариантов.НаименованиеПодсистемы, РазмещениеПредопределенных.НаименованиеПодсистемы) КАК НаименованиеПодсистемы,
		|	ЕСТЬNULL(РазмещениеВариантов.Использование, ИСТИНА) КАК Использование,
		|	ВЫБОР
		|		КОГДА РазмещениеВариантов.Ссылка ЕСТЬ NULL 
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоНастройкаРазработчика
		|ПОМЕСТИТЬ РазмещениеВсех
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВариантыОтчетов.Ссылка КАК Ссылка,
		|		ВариантыРазмещение.Использование КАК Использование,
		|		ВариантыРазмещение.Подсистема КАК Подсистема,
		|		ВариантыРазмещение.Подсистема.Синоним КАК НаименованиеПодсистемы
		|	ИЗ
		|		Варианты КАК ВариантыОтчетов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВариантыОтчетов.Размещение КАК ВариантыРазмещение
		|			ПО ВариантыОтчетов.Ссылка = ВариантыРазмещение.Ссылка
		|				И (ВариантыРазмещение.Подсистема В (&МассивПодсистем))) КАК РазмещениеВариантов
		|		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ВариантыОтчетов.Ссылка КАК Ссылка,
		|			ПредопределенныеРазмещение.Подсистема КАК Подсистема,
		|			ПредопределенныеРазмещение.Подсистема.Синоним КАК НаименованиеПодсистемы
		|		ИЗ
		|			Варианты КАК ВариантыОтчетов
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПредопределенныеВариантыОтчетов.Размещение КАК ПредопределенныеРазмещение
		|				ПО (ВариантыОтчетов.Пользовательский = ЛОЖЬ)
		|					И ВариантыОтчетов.ПредопределенныйВариант = ПредопределенныеРазмещение.Ссылка
		|					И (ПредопределенныеРазмещение.Подсистема В (&МассивПодсистем))) КАК РазмещениеПредопределенных
		|		ПО РазмещениеВариантов.Ссылка = РазмещениеПредопределенных.Ссылка
		|			И РазмещениеВариантов.Подсистема = РазмещениеПредопределенных.Подсистема
		|ГДЕ
		|	ЕСТЬNULL(РазмещениеВариантов.Использование, ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	РазмещениеВсех.Ссылка КАК Ссылка,
		|	РазмещениеВсех.Подсистема КАК Подсистема,
		|	РазмещениеВсех.НаименованиеПодсистемы КАК НаименованиеПодсистемы
		|ПОМЕСТИТЬ РазмещениеВидимых
		|ИЗ
		|	РазмещениеВсех КАК РазмещениеВсех
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиВариантовОтчетов КАК ЛичныеНастройки
		|		ПО РазмещениеВсех.Подсистема = ЛичныеНастройки.Подсистема
		|			И РазмещениеВсех.Ссылка = ЛичныеНастройки.Вариант
		|			И (ЛичныеНастройки.Пользователь = &ТекущийПользователь)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Варианты КАК Варианты
		|		ПО РазмещениеВсех.Ссылка = Варианты.Ссылка
		|ГДЕ
		|	ЕСТЬNULL(ЛичныеНастройки.Видимость, Варианты.ВидимостьПоУмолчанию)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВариантыОтчетов.Ссылка КАК Ссылка,
		|	ВариантыОтчетов.Родитель КАК Родитель,
		|	ВариантыОтчетов.НаименованиеВарианта КАК НаименованиеВарианта,
		|	ВариантыОтчетов.Автор КАК Автор,
		|	ВариантыОтчетов.АвторПредставление КАК АвторПредставление,
		|	ВариантыОтчетов.Отчет КАК Отчет,
		|	ВариантыОтчетов.Отчет.Имя КАК ИмяОтчета,
		|	ВариантыОтчетов.КлючВарианта КАК КлючВарианта,
		|	ВариантыОтчетов.ТипОтчета КАК ТипОтчета,
		|	ВариантыОтчетов.Пользовательский КАК Пользовательский,
		|	ВариантыОтчетов.ПредопределенныйВариант КАК ПредопределенныйВариант,
		|	ВариантыОтчетов.НаименованияПараметровИОтборов КАК НаименованияПараметровИОтборов,
		|	ВариантыОтчетов.НаименованияПолей КАК НаименованияПолей,
		|	ВариантыОтчетов.Описание КАК Описание,
		|	Размещение.Подсистема КАК Подсистема,
		|	Размещение.НаименованиеПодсистемы КАК НаименованиеПодсистемы,
		|	НЕОПРЕДЕЛЕНО КАК КлючПользовательскойНастройки,
		|	НЕОПРЕДЕЛЕНО КАК ПредставлениеПользовательскойНастройки
		|ИЗ
		|	Варианты КАК ВариантыОтчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазмещениеВидимых КАК Размещение
		|		ПО ВариантыОтчетов.Ссылка = Размещение.Ссылка
		|ГДЕ
		|	&ПоискВариантовИПодсистем
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Пользовательские.Вариант,
		|	Варианты.Родитель,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	НЕОПРЕДЕЛЕНО,
		|	Пользовательские.КлючПользовательскойНастройки,
		|	Пользовательские.Наименование
		|ИЗ
		|	Варианты КАК Варианты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПользовательскиеНастройкиОтчетов КАК Пользовательские
		|		ПО Варианты.Ссылка = Пользовательские.Вариант
		|ГДЕ
		|	Пользовательские.Пользователь = &ТекущийПользователь
		|	И &ПоискПользовательскихНастроек
		|	И Пользовательские.ПометкаУдаления = ЛОЖЬ";
		
		Если ЕстьОтборПоВидимости Тогда
			// Действие не требуется.
		Иначе
			// Удаление временной таблицы для фильтра по видимости.
			УдалитьВременнуюТаблицу(ТекстЗапроса, "РазмещениеВидимых");
			// Подмена имени временной таблицы, из которой нужно делать выборку.
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РазмещениеВидимых", "РазмещениеВсех");
		КонецЕсли;
		
		Если ЕстьОтборПоТипамОтчетов Тогда
			Запрос.УстановитьПараметр("ТипыОтчетов", ПараметрыПоиска.ТипыОтчетов);
		Иначе
			// Удаление отбора по типу отчета.
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ВариантыОтчетов.ТипОтчета В(&ТипыОтчетов)
				|	И ",
				"");
		КонецЕсли;
		
		Если ЕстьОтборПоПодсистемам Тогда
			Если ТипЗнч(ПараметрыПоиска.Подсистемы) = Тип("Массив") Тогда
				Запрос.УстановитьПараметр("МассивПодсистем", ПараметрыПоиска.Подсистемы);
			Иначе
				МассивПодсистем = Новый Массив;
				МассивПодсистем.Добавить(ПараметрыПоиска.Подсистемы);
				Запрос.УстановитьПараметр("МассивПодсистем", МассивПодсистем);
			КонецЕсли;
		Иначе
			// Удаление отбора по подсистемам.
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (ВариантыРазмещение.Подсистема В (&МассивПодсистем))", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (ПредопределенныеРазмещение.Подсистема В (&МассивПодсистем))", "");
		КонецЕсли;
		
		Если ЕстьСтрокаПоиска И Не ЕстьОтборПоПодсистемам Тогда
			// Для поиска информация о размещении является дополнительной, а не ключевой.
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазмещениеВидимых КАК Размещение",
				"ЛЕВОЕ СОЕДИНЕНИЕ РазмещениеВидимых КАК Размещение");
		КонецЕсли;
		
		Если ЕстьОтборПоПометкеУдаления Тогда
			Если ПараметрыПоиска.ПометкаУдаления <> Ложь Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ".ПометкаУдаления = ЛОЖЬ", ".ПометкаУдаления = ИСТИНА");
			КонецЕсли;
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (Предопределенные.ПометкаУдаления ЕСТЬ NULL 
			|			ИЛИ Предопределенные.ПометкаУдаления = ЛОЖЬ)", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Пользовательские.ПометкаУдаления = ЛОЖЬ", "");
		КонецЕсли;
		
		Если ЕстьСтрокаПоиска Тогда
			СтрокаПоиска = ВРег(СокрЛП(ПараметрыПоиска.СтрокаПоиска));
			ШаблонПоиска = "";
			МассивСлов = ВариантыОтчетовКлиентСервер.РазложитьСтрокуПоискаВМассивСлов(СтрокаПоиска);
			Для НомерСлова = 1 По МассивСлов.Количество() Цикл
				Слово = МассивСлов[НомерСлова-1];
				ИмяСлова = "Слово" + Формат(НомерСлова, "ЧГ=");
				Запрос.УстановитьПараметр(ИмяСлова, "%" + Слово + "%");
				Шаблон = "<ИмяТаблицы.ИмяПоля> ПОДОБНО &" + ИмяСлова;
				Если НомерСлова = 1 Тогда
					ШаблонПоиска = Шаблон;
				Иначе
					ШаблонПоиска = ШаблонПоиска + Символы.ПС + "				Или " + Шаблон;
				КонецЕсли;
			КонецЦикла;
			
			// Условие для поиска вариантов.
			ПоискВариантовИПодсистем = "("
				+ СтрЗаменить(ШаблонПоиска, "<ИмяТаблицы.ИмяПоля>", "ВариантыОтчетов.НаименованиеВарианта")
				+ "
				|				Или "
				+ СтрЗаменить(ШаблонПоиска, "<ИмяТаблицы.ИмяПоля>", "Размещение.НаименованиеПодсистемы")
				+ "
				|				Или "
				+ СтрЗаменить(ШаблонПоиска, "<ИмяТаблицы.ИмяПоля>", "ВариантыОтчетов.НаименованияПолей")
				+ "
				|				Или "
				+ СтрЗаменить(ШаблонПоиска, "<ИмяТаблицы.ИмяПоля>", "ВариантыОтчетов.НаименованияПараметровИОтборов")
				+ "
				|				Или "
				+ СтрЗаменить(ШаблонПоиска, "<ИмяТаблицы.ИмяПоля>", "ВариантыОтчетов.Описание")
				+ "
				|				Или "
				+ СтрЗаменить(ШаблонПоиска, "<ИмяТаблицы.ИмяПоля>", "ВариантыОтчетов.АвторПредставление")
				+ ")";
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоискВариантовИПодсистем", ПоискВариантовИПодсистем);
			
			ПоискПользовательскихНастроек = (
				"("
				+ СтрЗаменить(ШаблонПоиска, "<ИмяТаблицы.ИмяПоля>", "Пользовательские.Наименование")
				+ ")"
			);
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоискПользовательскихНастроек", ПоискПользовательскихНастроек);
		Иначе
			// Удаление отбора для поиска в данных вариантов и подсистем.
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ГДЕ
				|	&ПоискВариантовИПодсистем",
				"");
			// Удаление таблицы для поиска среди пользовательских настроек.
			НачалоВыборкиИзТаблицы = (
				"ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	Пользовательские.Вариант,");
			ТекстЗапроса = СокрП(Лев(ТекстЗапроса, Найти(ТекстЗапроса, НачалоВыборкиИзТаблицы) - 1));
		КонецЕсли;
		
		// Удаление лишних полей когда они не нужны ни для поиска ни для итоговой таблицы.
		Если Не ЕстьСтрокаПоиска И Не ПолучатьИтоговуюТаблицу Тогда
			// НаименованиеВарианта
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВариантыОтчетов.Наименование КАК", "НЕОПРЕДЕЛЕНО КАК");
			// НаименованияПолей
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ВЫБОР
				|		КОГДА ПОДСТРОКА(ВариантыОтчетов.НаименованияПолей, 1, 1) = """"
				|			ТОГДА ВЫРАЗИТЬ(Предопределенные.НаименованияПолей КАК СТРОКА(1000))
				|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПолей КАК СТРОКА(1000))
				|	КОНЕЦ КАК",
				"НЕОПРЕДЕЛЕНО КАК");
			// НаименованияПараметровИОтборов
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ВЫБОР
				|		КОГДА ПОДСТРОКА(ВариантыОтчетов.НаименованияПараметровИОтборов, 1, 1) = """"
				|			ТОГДА ВЫРАЗИТЬ(Предопределенные.НаименованияПараметровИОтборов КАК СТРОКА(1000))
				|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.НаименованияПараметровИОтборов КАК СТРОКА(1000))
				|	КОНЕЦ КАК",
				"НЕОПРЕДЕЛЕНО КАК");
			// Описание
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ВЫБОР
				|		КОГДА ПОДСТРОКА(ВариантыОтчетов.Описание, 1, 1) = """"
				|			ТОГДА ВЫРАЗИТЬ(Предопределенные.Описание КАК СТРОКА(1000))
				|		ИНАЧЕ ВЫРАЗИТЬ(ВариантыОтчетов.Описание КАК СТРОКА(1000))
				|	КОНЕЦ КАК",
				"НЕОПРЕДЕЛЕНО КАК");
			// НаименованиеПодсистемы
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ВариантыРазмещение.Подсистема.Синоним КАК",
				"НЕОПРЕДЕЛЕНО КАК");
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ПредопределенныеРазмещение.Подсистема.Синоним КАК",
				"НЕОПРЕДЕЛЕНО КАК");
		КонецЕсли;
		
		// Удаление лишних полей когда они не нужны для итоговой таблицы.
		Если Не ПолучатьИтоговуюТаблицу Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВариантыОтчетов.Автор КАК", "НЕОПРЕДЕЛЕНО КАК");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВариантыОтчетов.Отчет КАК", "НЕОПРЕДЕЛЕНО КАК");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВариантыОтчетов.Отчет.Имя КАК", "НЕОПРЕДЕЛЕНО КАК");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВариантыОтчетов.КлючВарианта КАК", "НЕОПРЕДЕЛЕНО КАК");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВариантыОтчетов.ТипОтчета КАК", "НЕОПРЕДЕЛЕНО КАК");
		КонецЕсли;
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВариантыОтчетов.Ссылка КАК Ссылка,
		|	ВариантыОтчетов.Родитель
		|ИЗ
		|	Справочник.ВариантыОтчетов КАК ВариантыОтчетов
		|ГДЕ
		|	ВариантыОтчетов.ТипОтчета В(&ТипыОтчетов)
		|	И ВариантыОтчетов.Отчет В(&ОтчетыПользователя)
		|	И НЕ ВариантыОтчетов.ПредопределенныйВариант В (&ОтключенныеВариантыПрограммы)
		|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ";
		
		Если ЕстьОтборПоТипамОтчетов Тогда
			Запрос.УстановитьПараметр("ТипыОтчетов", ПараметрыПоиска.ТипыОтчетов);
		Иначе
			ТекстЗапроса = СтрЗаменить(
				ТекстЗапроса,
				"ВариантыОтчетов.ТипОтчета В(&ТипыОтчетов)
				|	И ",
				"");
		КонецЕсли;
		
		Если ЕстьОтборПоПометкеУдаления Тогда
			Если ПараметрыПоиска.ПометкаУдаления <> Ложь Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ".ПометкаУдаления = ЛОЖЬ", ".ПометкаУдаления = ИСТИНА");
			КонецЕсли;
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "
			|	И ВариантыОтчетов.ПометкаУдаления = ЛОЖЬ", "");
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Новый Структура;
	Результат.Вставить("Ссылки", Новый Массив);
	Результат.Вставить("ПодсветкаВариантов", Новый Соответствие);
	Результат.Вставить("Подсистемы", Новый Массив);
	Результат.Вставить("ПодсветкаПодсистем", Новый Соответствие);
	Результат.Вставить("ВариантыСвязанныеСПодсистемами", Новый Соответствие);
	
	ТаблицаЗначений = Запрос.Выполнить().Выгрузить();
	Если ПолучатьИтоговуюТаблицу Тогда
		Результат.Вставить("ТаблицаЗначений", ТаблицаЗначений);
	КонецЕсли;
	
	Если ТаблицаЗначений.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ЕстьСтрокаПоиска Тогда
		ТаблицаВариантов = ТаблицаЗначений.Скопировать(, "Ссылка, Родитель");
		ТаблицаВариантов.Свернуть("Ссылка, Родитель");
		Для Каждого СтрокаТаблицы Из ТаблицаВариантов Цикл
			Если ЗначениеЗаполнено(СтрокаТаблицы.Ссылка) И Результат.Ссылки.Найти(СтрокаТаблицы.Ссылка) = Неопределено Тогда
				Результат.Ссылки.Добавить(СтрокаТаблицы.Ссылка);
				Если ЗначениеЗаполнено(СтрокаТаблицы.Родитель) И Результат.Ссылки.Найти(СтрокаТаблицы.Родитель) = Неопределено Тогда
					Результат.Ссылки.Добавить(СтрокаТаблицы.Родитель);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Возврат Результат;
	КонецЕсли;
	
	ТаблицаЗначений.Сортировать("Ссылка");
	
	ШаблонОбластиПоиска = Новый ФиксированнаяСтруктура("Значение, КоличествоНайденныхСлов, ПодсветкаСлов", "", 0, Новый СписокЗначений);
	
	СтрокаТаблицы = ТаблицаЗначений[0];
	Вариант = Новый Структура;
	Вариант.Вставить("Ссылка", СтрокаТаблицы.Ссылка);
	Вариант.Вставить("Родитель", СтрокаТаблицы.Родитель);
	Вариант.Вставить("НаименованиеВарианта",                 Новый Структура(ШаблонОбластиПоиска));
	Вариант.Вставить("Описание",                             Новый Структура(ШаблонОбластиПоиска));
	Вариант.Вставить("НаименованияПолей",                    Новый Структура(ШаблонОбластиПоиска));
	Вариант.Вставить("НаименованияПараметровИОтборов",       Новый Структура(ШаблонОбластиПоиска));
	Вариант.Вставить("НаименованияПользовательскихНастроек", Новый Структура(ШаблонОбластиПоиска));
	Вариант.Вставить("НаименованияПодсистем",                Новый Структура(ШаблонОбластиПоиска));
	Вариант.Вставить("Подсистемы",                           Новый Массив);
	Вариант.Вставить("ПредставлениеАвтора",                  Новый Структура(ШаблонОбластиПоиска));
	
	РазделительХранения = ВариантыОтчетовКлиентСервер.РазделительХранения();
	РазделительПредставления = ВариантыОтчетовКлиентСервер.РазделительПредставления();
	
	Количество = ТаблицаЗначений.Количество();
	Для Индекс = 1 По Количество Цикл
		// Заполнение переменных.
		Если Не ЗначениеЗаполнено(Вариант.НаименованиеВарианта.Значение) И ЗначениеЗаполнено(СтрокаТаблицы.НаименованиеВарианта) Тогда
			Вариант.НаименованиеВарианта.Значение = СтрокаТаблицы.НаименованиеВарианта;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Вариант.Описание.Значение) И ЗначениеЗаполнено(СтрокаТаблицы.Описание) Тогда
			Вариант.Описание.Значение = СтрокаТаблицы.Описание;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Вариант.НаименованияПолей.Значение) И ЗначениеЗаполнено(СтрокаТаблицы.НаименованияПолей) Тогда
			Вариант.НаименованияПолей.Значение = СтрокаТаблицы.НаименованияПолей;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Вариант.НаименованияПараметровИОтборов.Значение) И ЗначениеЗаполнено(СтрокаТаблицы.НаименованияПараметровИОтборов) Тогда
			Вариант.НаименованияПараметровИОтборов.Значение = СтрокаТаблицы.НаименованияПараметровИОтборов;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Вариант.ПредставлениеАвтора.Значение) И ЗначениеЗаполнено(СтрокаТаблицы.АвторПредставление) Тогда
			Вариант.ПредставлениеАвтора.Значение = СтрокаТаблицы.АвторПредставление;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаТаблицы.ПредставлениеПользовательскойНастройки) Тогда
			Если Вариант.НаименованияПользовательскихНастроек.Значение = "" Тогда
				Вариант.НаименованияПользовательскихНастроек.Значение = СтрокаТаблицы.ПредставлениеПользовательскойНастройки;
			Иначе
				Вариант.НаименованияПользовательскихНастроек.Значение = Вариант.НаименованияПользовательскихНастроек.Значение
					+ РазделительПредставления
					+ СтрокаТаблицы.ПредставлениеПользовательскойНастройки;
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаТаблицы.НаименованиеПодсистемы)
			И Вариант.Подсистемы.Найти(СтрокаТаблицы.Подсистема) = Неопределено Тогда
			Вариант.Подсистемы.Добавить(СтрокаТаблицы.Подсистема);
			Подсистема = Результат.ПодсветкаПодсистем.Получить(СтрокаТаблицы.Подсистема);
			Если Подсистема = Неопределено Тогда
				Подсистема = Новый Структура;
				Подсистема.Вставить("Ссылка", СтрокаТаблицы.Подсистема);
				Подсистема.Вставить("НаименованиеПодсистемы", Новый Структура(ШаблонОбластиПоиска));
				Подсистема.НаименованиеПодсистемы.Значение = СтрокаТаблицы.НаименованиеПодсистемы;
				Подсистема.Вставить("ВсеСловаНайдены", Истина);
				Подсистема.Вставить("НайденныеСлова", Новый Массив);
				Для Каждого Слово Из МассивСлов Цикл
					Если ОтметитьСлово(Подсистема.НаименованиеПодсистемы, Слово) Тогда
						Подсистема.НайденныеСлова.Добавить(Слово);
					Иначе
						Подсистема.ВсеСловаНайдены = Ложь;
					КонецЕсли;
				КонецЦикла;
				Если Подсистема.ВсеСловаНайдены Тогда
					Результат.Подсистемы.Добавить(Подсистема.Ссылка);
				КонецЕсли;
				Результат.ПодсветкаПодсистем.Вставить(Подсистема.Ссылка, Подсистема);
			КонецЕсли;
			Если Вариант.НаименованияПодсистем.Значение = "" Тогда
				Вариант.НаименованияПодсистем.Значение = СтрокаТаблицы.НаименованиеПодсистемы;
			Иначе
				Вариант.НаименованияПодсистем.Значение = Вариант.НаименованияПодсистем.Значение
					+ РазделительПредставления
					+ СтрокаТаблицы.НаименованиеПодсистемы;
			КонецЕсли;
		КонецЕсли;
		
		Если Индекс < Количество Тогда
			СтрокаТаблицы = ТаблицаЗначений[Индекс];
		КонецЕсли;
		
		Если Индекс = Количество Или СтрокаТаблицы.Ссылка <> Вариант.Ссылка Тогда
			// Анализ собранной информации о варианте.
			ВсеСловаНайдены = Истина;
			СвязанныеПодсистемы = Новый Массив;
			Для Каждого Слово Из МассивСлов Цикл
				СловоНайдено = Ложь;
				
				Если ОтметитьСлово(Вариант.НаименованиеВарианта, Слово) Тогда
					СловоНайдено = Истина;
				КонецЕсли;
				
				Если ОтметитьСлово(Вариант.Описание, Слово) Тогда
					СловоНайдено = Истина;
				КонецЕсли;
				
				Если ОтметитьСлово(Вариант.НаименованияПолей, Слово, Истина) Тогда
					СловоНайдено = Истина;
				КонецЕсли;
				
				Если ОтметитьСлово(Вариант.ПредставлениеАвтора, Слово, Истина) Тогда
					СловоНайдено = Истина;
				КонецЕсли;
				
				Если ОтметитьСлово(Вариант.НаименованияПараметровИОтборов, Слово, Истина) Тогда
					СловоНайдено = Истина;
				КонецЕсли;
				
				Если ОтметитьСлово(Вариант.НаименованияПользовательскихНастроек, Слово, Истина) Тогда
					СловоНайдено = Истина;
				КонецЕсли;
				
				Если Не СловоНайдено Тогда
					Для Каждого ПодсистемаСсылка Из Вариант.Подсистемы Цикл
						Подсистема = Результат.ПодсветкаПодсистем.Получить(ПодсистемаСсылка);
						Если Подсистема.НайденныеСлова.Найти(Слово) <> Неопределено Тогда
							СловоНайдено = Истина;
							СвязанныеПодсистемы.Добавить(ПодсистемаСсылка);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				
				Если Не СловоНайдено Тогда
					ВсеСловаНайдены = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ВсеСловаНайдены Тогда // Регистрация результата.
				Результат.Ссылки.Добавить(Вариант.Ссылка);
				Результат.ПодсветкаВариантов.Вставить(Вариант.Ссылка, Вариант);
				Если СвязанныеПодсистемы.Количество() > 0 Тогда
					Результат.ВариантыСвязанныеСПодсистемами.Вставить(Вариант.Ссылка, СвязанныеПодсистемы);
				КонецЕсли;
				Если ЗначениеЗаполнено(Вариант.Родитель) И Результат.Ссылки.Найти(Вариант.Родитель) = Неопределено Тогда
					Результат.Ссылки.Добавить(Вариант.Родитель);
				КонецЕсли;
			КонецЕсли;
			
			Если Индекс = Количество Тогда
				Прервать;
			КонецЕсли;
			
			// Обнуление переменных.
			Вариант = Новый Структура;
			Вариант.Вставить("Ссылка", СтрокаТаблицы.Ссылка);
			Вариант.Вставить("Родитель", СтрокаТаблицы.Родитель);
			Вариант.Вставить("НаименованиеВарианта",                 Новый Структура(ШаблонОбластиПоиска));
			Вариант.Вставить("Описание",                             Новый Структура(ШаблонОбластиПоиска));
			Вариант.Вставить("НаименованияПолей",                    Новый Структура(ШаблонОбластиПоиска));
			Вариант.Вставить("НаименованияПараметровИОтборов",       Новый Структура(ШаблонОбластиПоиска));
			Вариант.Вставить("НаименованияПользовательскихНастроек", Новый Структура(ШаблонОбластиПоиска));
			Вариант.Вставить("НаименованияПодсистем",                Новый Структура(ШаблонОбластиПоиска));
			Вариант.Вставить("Подсистемы",                           Новый Массив);
			Вариант.Вставить("ПредставлениеАвтора",                  Новый Структура(ШаблонОбластиПоиска));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Находит слово и отмечает место, где оно найдено. Возвращает Истина если слово найдено.
Функция ОтметитьСлово(СтруктураГде, Слово, ИспользоватьРазделитель = Ложь) Экспорт
	Если Лев(СтруктураГде.Значение, 1) = "#" Тогда
		СтруктураГде.Значение = Сред(СтруктураГде.Значение, 2);
	КонецЕсли;
	Результат = "";
	ОстатокВРег = ВРег(СтруктураГде.Значение);
	Позиция = Найти(ОстатокВРег, Слово);
	Если Позиция = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	Если СтруктураГде.КоличествоНайденныхСлов = 0 Тогда
		// Инициализация переменной, содержащей директивы для подсветки слов.
		СтруктураГде.ПодсветкаСлов = Новый СписокЗначений;
		// Прокрутка фокуса до значащего слова (обнаруженной информации).
		Если ИспользоватьРазделитель Тогда
			РазделительХранения = ВариантыОтчетовКлиентСервер.РазделительХранения();
			РазделительПредставления = ВариантыОтчетовКлиентСервер.РазделительПредставления();
			ДлинаРазделителя = СтрДлина(РазделительХранения);
			Пока Позиция > 10 Цикл
				ПозицияРазделителя = Найти(ОстатокВРег, РазделительХранения);
				Если ПозицияРазделителя = 0 Тогда
					Прервать;
				КонецЕсли;
				Если ПозицияРазделителя < Позиция Тогда
					// Перенос фрагмента до разделителя в конец области.
					СтруктураГде.Значение = (
						Сред(СтруктураГде.Значение, ПозицияРазделителя + ДлинаРазделителя)
						+ РазделительХранения
						+ Лев(СтруктураГде.Значение, ПозицияРазделителя - 1)
					);
					ОстатокВРег = (
						Сред(ОстатокВРег, ПозицияРазделителя + ДлинаРазделителя)
						+ РазделительХранения
						+ Лев(ОстатокВРег, ПозицияРазделителя - 1)
					);
					// Обновление информации о расположении слова.
					Позиция = Позиция - ПозицияРазделителя - ДлинаРазделителя + 1;
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
			СтруктураГде.Значение = СтрЗаменить(СтруктураГде.Значение, РазделительХранения, РазделительПредставления);
			ОстатокВРег = СтрЗаменить(ОстатокВРег, РазделительХранения, РазделительПредставления);
			Позиция = Найти(ОстатокВРег, Слово);
		КонецЕсли;
	КонецЕсли;
	// Регистрация найденного слова.
	СтруктураГде.КоличествоНайденныхСлов = СтруктураГде.КоличествоНайденныхСлов + 1;
	// Отметка слов.
	ДлинаЛевойЧасти = 0;
	ДлинаСлова = СтрДлина(Слово);
	Пока Позиция > 0 Цикл
		СтруктураГде.ПодсветкаСлов.Добавить(ДлинаЛевойЧасти + Позиция, "+");
		СтруктураГде.ПодсветкаСлов.Добавить(ДлинаЛевойЧасти + Позиция + ДлинаСлова, "-");
		ОстатокВРег = Сред(ОстатокВРег, Позиция + ДлинаСлова);
		ДлинаЛевойЧасти = ДлинаЛевойЧасти + Позиция + ДлинаСлова - 1;
		Позиция = Найти(ОстатокВРег, Слово);
	КонецЦикла;
	Возврат Истина;
КонецФункции

// Удаляет временную таблицу из текста запроса.
Процедура УдалитьВременнуюТаблицу(ТекстЗапроса, ИмяВременнойТаблицы)
	ПозицияВременнойТаблицы = Найти(ТекстЗапроса, "ПОМЕСТИТЬ " + ИмяВременнойТаблицы);
	ЛеваяЧасть = "";
	ПраваяЧасть = ТекстЗапроса;
	Пока Истина Цикл
		ПозицияТочкиСЗапятой = Найти(ПраваяЧасть, Символы.ПС + ";");
		Если ПозицияТочкиСЗапятой = 0 Тогда
			Прервать;
		ИначеЕсли ПозицияТочкиСЗапятой > ПозицияВременнойТаблицы Тогда
			ПраваяЧасть = Сред(ПраваяЧасть, ПозицияТочкиСЗапятой + 2);
			Прервать;
		Иначе
			ЛеваяЧасть = ЛеваяЧасть + Лев(ПраваяЧасть, ПозицияТочкиСЗапятой + 1);
			ПраваяЧасть = Сред(ПраваяЧасть, ПозицияТочкиСЗапятой + 2);
			ПозицияВременнойТаблицы = ПозицияВременнойТаблицы - ПозицияТочкиСЗапятой - 1;
		КонецЕсли;
	КонецЦикла;
	ТекстЗапроса = ЛеваяЧасть + ПраваяЧасть;
КонецПроцедуры

#КонецОбласти

