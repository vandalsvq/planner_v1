////////////////////////////////////////////////////////////////////////////////
// Подсистема "Контроль динамического обновления конфигурации"
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Выполняется при интерактивном начале работы пользователя с областью данных или в локальном режиме.
// Вызывается после завершения действий ПриНачалеРаботыСистемы.
// Используется для подключения обработчиков ожидания, которые не должны вызываться
// в случае интерактивных действий перед и при начале работы системы.
//
Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияПроверкиДинамическогоИзмененияИБ", 20 * 60); // раз в 20 минут
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик ожидания проверяет, что информационная база была обновлена динамически, и 
// сообщает об этом пользователю.
//
Процедура ОбработчикОжиданияПроверкиДинамическогоОбновления() Экспорт
	
	Если Не КонтрольДинамическогоОбновленияКонфигурацииВызовСервера.КонфигурацияБДБылаИзмененаДинамически() Тогда
		Возврат;
	КонецЕсли;
		
	ОтключитьОбработчикОжидания("ОбработчикОжиданияПроверкиДинамическогоИзмененияИБ");
	
	ТекстСообщения = НСтр("ru = 'Версия программы обновлена (внесены изменения в конфигурацию информационной базы).
								|Для дальнейшей работы рекомендуется перезапустить программу.
								|Перезапустить?'");
								
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОжиданияПроверкиДинамическогоОбновленияЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстСообщения, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

// Обработчик оповещения, вызываемый из процедуры ОбработчикОжиданияПроверкиДинамическогоИзмененияИБ
//
Процедура ОбработчикОжиданияПроверкиДинамическогоОбновленияЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СтандартныеПодсистемыКлиент.ПропуститьПредупреждениеПередЗавершениемРаботыСистемы();
		ЗавершитьРаботуСистемы(Истина, Истина);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("ОбработчикОжиданияПроверкиДинамическогоИзмененияИБ", 20 * 60); // раз в 20 минут
	
КонецПроцедуры

#КонецОбласти
