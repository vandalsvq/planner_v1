#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда 
		Возврат;
	КонецЕсли;
	Если Объект.Ссылка.Пустая() Тогда
		ОшибкаОткрытия = НСтр("ru = 'Новый вариант отчета можно создать только из формы отчета'");
		Возврат;
	КонецЕсли;
	Параметры.Свойство("ОткрытьФормуДополнительногоОтчетаПриОткрытии", ОткрытьФормуДополнительногоОтчетаПриОткрытии);
	Если ОткрытьФормуДополнительногоОтчетаПриОткрытии Тогда
		Возврат;
	КонецЕсли;
	
	Доступен = ?(Объект.ТолькоДляАвтора, "1", "2");
	
	// Чтение свойств предопределенного;
	// Заполнение реквизитов, связанных с предопределенным объектом при открытии.
	ПрочитатьСвойстваПредопределенного(Истина);
	
	ПолныеПраваНаВарианты = ВариантыОтчетов.ПолныеПраваНаВарианты();
	
	Если Объект.ПометкаУдаления Тогда
		Элементы.ДеревоПодсистем.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Не Объект.Пользовательский Тогда
		Элементы.Наименование.ТолькоПросмотр = Истина;
		Элементы.Доступен.ТолькоПросмотр = Истина;
		Элементы.Автор.ТолькоПросмотр = Истина;
		Элементы.Автор.АвтоОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
	ЭтоВнешний = (Объект.ТипОтчета = Перечисления.ТипыОтчетов.Внешний);
	Если ЭтоВнешний Тогда
		Элементы.ДеревоПодсистем.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Элементы.Доступен.ТолькоПросмотр = Не ПолныеПраваНаВарианты;
	Элементы.Автор.ТолькоПросмотр = Не ПолныеПраваНаВарианты;
	Элементы.ТехническаяИнформация.Видимость = ПолныеПраваНаВарианты;
	
	// Заполнение имени отчета для команды "Просмотр"
	Если Объект.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний Тогда
		ИмяОтчета = Объект.Отчет.Имя;
	ИначеЕсли Объект.ТипОтчета = Перечисления.ТипыОтчетов.Дополнительный Тогда
		ИмяОтчета = Объект.Отчет.ИмяОбъекта;
	Иначе
		ИмяОтчета = Объект.Отчет;
	КонецЕсли;
	
	ПерезаполнитьДерево(Ложь);
	
	ВариантыОтчетов.ДеревоПодсистемДобавитьУсловноеОформление(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект) // Перезаполнение реквизитов, автоматически очищаемых после записи объекта.
	Если Параметры.Свойство("АвтоТест") Тогда 
		Возврат;
	КонецЕсли;
	Если ОткрытьФормуДополнительногоОтчетаПриОткрытии Тогда 
		Возврат;
	КонецЕсли;
	
	ПрочитатьСвойстваПредопределенного(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Не ПустаяСтрока(ОшибкаОткрытия) Тогда
		Если Объект.Ссылка.Пустая() Тогда
			Отказ = Истина;
		Иначе
			ТолькоПросмотр = Истина;
		КонецЕсли;
		
		ПоказатьПредупреждение(, ОшибкаОткрытия);
	ИначеЕсли ОткрытьФормуДополнительногоОтчетаПриОткрытии Тогда
		Отказ = Истина;
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Вариант",      Объект.Ссылка);
		ПараметрыОткрытия.Вставить("Отчет",        Объект.Отчет);
		ПараметрыОткрытия.Вставить("КлючВарианта", Объект.КлючВарианта);
		ВариантыОтчетовКлиент.ОткрытьВариантДополнительногоОтчета(ПараметрыОткрытия);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Источник <> ЭтотОбъект И ИмяСобытия = ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта() Тогда
		ПерезаполнитьДерево(Истина);
		Элементы.ДеревоПодсистем.Развернуть(ДеревоПодсистем.ПолучитьЭлементы()[0].ПолучитьИдентификатор(), Истина);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// Запись свойств, связанных с предопределенным вариантом отчета.
	Если ТипЗнч(СвойстваПредопределенного) = Тип("ФиксированнаяСтруктура") Тогда
		Если Не ПустаяСтрока(Объект.Описание) И НРег(СокрЛП(Объект.Описание)) = НРег(СокрЛП(СвойстваПредопределенного.Описание)) Тогда
			Объект.Описание = "";
		КонецЕсли;
	КонецЕсли;
	
	// Запись дерева подсистем.
	ВариантыОтчетов.ДеревоПодсистемЗаписать(ЭтотОбъект, ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ПерезаполнитьДерево(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПараметрОповещения = Новый Структура("Ссылка, Наименование, Автор, Описание");//, ВидимостьПоУмолчанию");
	ЗаполнитьЗначенияСвойств(ПараметрОповещения, Объект);
	Оповестить(ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта(), ПараметрОповещения, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВариантыОтчетовКлиент.РедактироватьМногострочныйТекст(ЭтотОбъект, Элемент.ТекстРедактирования, Объект, "Описание", НСтр("ru = 'Описание'"));
КонецПроцедуры

&НаКлиенте
Процедура ДоступенПриИзменении(Элемент)
	Объект.ТолькоДляАвтора = (ЭтотОбъект.Доступен = "1");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоПодсистем

&НаКлиенте
Процедура ДеревоПодсистемИспользованиеПриИзменении(Элемент)
	ВариантыОтчетовКлиент.ДеревоПодсистемИспользованиеПриИзменении(ЭтотОбъект, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПодсистемВажностьПриИзменении(Элемент)
	ВариантыОтчетовКлиент.ДеревоПодсистемВажностьПриИзменении(ЭтотОбъект, Элемент);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПерезаполнитьДерево(Прочитать)
	Если Прочитать Тогда
		ЭтотОбъект.Прочитать();
	КонецЕсли;
	ДеревоПриемник = ВариантыОтчетов.ДеревоПодсистемСформировать(ЭтотОбъект, Объект);
	ЗначениеВРеквизитФормы(ДеревоПриемник, "ДеревоПодсистем");
	Возврат Истина;
КонецФункции

&НаСервере
Процедура ПрочитатьСвойстваПредопределенного(ПервоеЧтение)
	Если ПервоеЧтение Тогда
		Если Объект.ТипОтчета = Перечисления.ТипыОтчетов.Внутренний
			И Не Объект.Пользовательский
			И ЗначениеЗаполнено(Объект.ПредопределенныйВариант) Тогда // Чтение настроек предопределенного.
			Запрос = Новый Запрос("ВЫБРАТЬ ВидимостьПоУмолчанию, Описание ИЗ Справочник.ПредопределенныеВариантыОтчетов ГДЕ Ссылка = &Ссылка");
			Запрос.УстановитьПараметр("Ссылка", Объект.ПредопределенныйВариант);
			РезультатЗапроса = Запрос.Выполнить().Выгрузить()[0];
			СтруктураСвойствПредопределенного = Новый Структура("ВидимостьПоУмолчанию, Описание");
			ЗаполнитьЗначенияСвойств(СтруктураСвойствПредопределенного, РезультатЗапроса);
			СвойстваПредопределенного = Новый ФиксированнаяСтруктура(СтруктураСвойствПредопределенного);
		Иначе
			Возврат; // Не предопределенный.
		КонецЕсли;
	Иначе
		Если ТипЗнч(СвойстваПредопределенного) <> Тип("ФиксированнаяСтруктура") Тогда
			Возврат; // Не предопределенный.
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.Описание) Тогда
		Объект.Описание = СвойстваПредопределенного.Описание;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
