&НаКлиенте
Перем КлиентскийКэш;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// Проверка возможности загрузки новых обработок в информационную базу
	ЭтоНовый = Объект.Ссылка.Пустая();
	Если ЭтоНовый И НЕ ДополнительныеОтчетыИОбработки.ПравоДобавления() Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав доступа для добавления дополнительных отчетов или обработок.'");
	КонецЕсли;
	
	// Ограничение возможности выбора вида публикации в зависимости от настроек информационной базы
	Элементы.Публикация.СписокВыбора.Очистить();
	ДоступныеВидыПубликации = ДополнительныеОтчетыИОбработкиПовтИсп.ДоступныеВидыПубликации();
	Для Каждого ВидПубликации Из ДоступныеВидыПубликации Цикл
		Элементы.Публикация.СписокВыбора.Добавить(ВидПубликации);
	КонецЦикла;
	
	// Ограничение отображения расширенной информации
	ОтображениеРасширеннойИнформации = ДополнительныеОтчетыИОбработки.ОтображатьРасширеннуюИнформацию(Объект.Ссылка);
	Элементы.СтраницаДополнительнаяИнформация.Видимость = ОтображениеРасширеннойИнформации;
	
	// Ограничение возможности загрузки обработки из файла / сохранения в файл
	Элементы.ЗагрузитьИзФайла.Видимость = 
		ДополнительныеОтчетыИОбработки.ВозможнаЗагрузкаОбработкиИзФайла(Объект.Ссылка);
	Элементы.ВыгрузитьВФайл.Видимость =
		ДополнительныеОтчетыИОбработки.ВозможнаВыгрузкаОбработкиВФайл(Объект.Ссылка);
	
	ВидДополнительнаяОбработка = Перечисления.ВидыДополнительныхОтчетовИОбработок.ДополнительнаяОбработка;
	ВидДополнительныйОтчет     = Перечисления.ВидыДополнительныхОтчетовИОбработок.ДополнительныйОтчет;
	ВидОтчет                   = Перечисления.ВидыДополнительныхОтчетовИОбработок.Отчет;
	
	Параметры.Свойство("ПоказатьДиалогЗагрузкиИзФайлаПриОткрытии", ПоказатьДиалогЗагрузкиИзФайлаПриОткрытии);
	
	Если ЭтоНовый Тогда
		Объект.ИспользоватьДляФормыОбъекта = Истина;
		Объект.ИспользоватьДляФормыСписка  = Истина;
		ПоказатьДиалогЗагрузкиИзФайлаПриОткрытии = Истина;
	КонецЕсли;
	
	Если ПоказатьДиалогЗагрузкиИзФайлаПриОткрытии И Не Элементы.ЗагрузитьИзФайла.Видимость Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно прав для загрузки дополнительных отчетов и обработок'");
	КонецЕсли;
	
	ЗаполнитьКоманды();
	
	АдресРазрешений = ПоместитьВоВременноеХранилище(
		РеквизитФормыВЗначение("Объект").Разрешения.Выгрузить(),
		ЭтотОбъект.УникальныйИдентификатор);
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	КлиентскийКэш = Новый Структура;
	
	Если ПоказатьДиалогЗагрузкиИзФайлаПриОткрытии Тогда
		ПодключитьОбработчикОжидания("ОбновлениеИзФайлаЗапуск", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.ДополнительныеОтчетыИОбработки.Форма.ПодборРазделов") Тогда
		
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("СписокЗначений") Тогда
			Возврат;
		КонецЕсли;
		
		Объект.Разделы.Очистить();
		Для Каждого ЭлементСписка Из ВыбранноеЗначение Цикл
			НоваяСтрока = Объект.Разделы.Добавить();
			НоваяСтрока.Раздел = ЭлементСписка.Значение;
		КонецЦикла;
		
		Модифицированность = Истина;
		УстановитьВидимостьДоступность();
		
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.ДополнительныеОтчетыИОбработки.Форма.БыстрыйДоступКДополнительнымОтчетамИОбработкам") Тогда
		
		Если ТипЗнч(ВыбранноеЗначение) <> Тип("СписокЗначений") Тогда
			Возврат;
		КонецЕсли;
		
		ЭлементКоманда = Объект.Команды.НайтиПоИдентификатору(КлиентскийКэш.ИдентификаторСтрокиКоманды);
		Если ЭлементКоманда = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Найденные = БыстрыйДоступ.НайтиСтроки(Новый Структура("ИдентификаторКоманды", ЭлементКоманда.Идентификатор));
		Для Каждого СтрокаТаблицы Из Найденные Цикл
			БыстрыйДоступ.Удалить(СтрокаТаблицы);
		КонецЦикла;
		
		Для Каждого ЭлементСписка Из ВыбранноеЗначение Цикл
			СтрокаТаблицы = БыстрыйДоступ.Добавить();
			СтрокаТаблицы.ИдентификаторКоманды = ЭлементКоманда.Идентификатор;
			СтрокаТаблицы.Пользователь = ЭлементСписка.Значение;
		КонецЦикла;
		
		ЭлементКоманда.БыстрыйДоступПредставление = ПредставлениеБыстрогоДоступаПользователей(ВыбранноеЗначение.Количество());
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыборОбъектовМетаданных" Тогда
		
		ЗагрузитьВыбранныеОбъектыМетаданных(Параметр);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ДополнительныеОтчетыИОбработки.ВозможнаВыгрузкаОбработкиВФайл(Объект.Ссылка) Тогда
		
		АдресДанныхОбработки = ПоместитьВоВременноеХранилище(
			ТекущийОбъект.ХранилищеОбработки.Получить(),
			УникальныйИдентификатор);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ТекущийОбъект.Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеРегистра.ИдентификаторКоманды,
	|	ДанныеРегистра.Пользователь
	|ИЗ
	|	РегистрСведений.ПользовательскиеНастройкиДоступаКОбработкам КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ДополнительныйОтчетИлиОбработка = &Ссылка
	|	И ДанныеРегистра.Доступно = ИСТИНА";
	БыстрыйДоступ.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если РегистрацияОбработки И ДополнительныеОтчетыИОбработки.ВозможнаЗагрузкаОбработкиИзФайла(Объект.Ссылка) Тогда
		ДвоичныеДанныеОбработки = ПолучитьИзВременногоХранилища(АдресДанныхОбработки);
		ТекущийОбъект.ХранилищеОбработки = Новый ХранилищеЗначения(ДвоичныеДанныеОбработки, Новый СжатиеДанных(9));
	КонецЕсли;
	
	Если Объект.Вид = ВидДополнительнаяОбработка ИЛИ Объект.Вид = ВидДополнительныйОтчет Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("АктуальныеКоманды", Объект.Команды.Выгрузить());
	Иначе
		БыстрыйДоступ.Очистить();
	КонецЕсли;
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("БыстрыйДоступ", БыстрыйДоступ.Выгрузить());
	
	ТекущийОбъект.Разрешения.Загрузить(ПолучитьИзВременногоХранилища(АдресРазрешений));
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ДополнительныеСвойства.Свойство("ОшибкаПодключения") Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекущийОбъект.ДополнительныеСвойства.ОшибкаПодключения;
		Сообщение.Сообщить();
	КонецЕсли;
	ЭтоНовый = Ложь;
	Если РегистрацияОбработки Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		РегистрацияОбработки = Ложь;
	КонецЕсли;
	ЗаполнитьКоманды();
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДополнительныеОтчетыИОбработкиКлиент.РедактироватьМногострочныйТекст(
		ЭтотОбъект,
		Элемент.ТекстРедактирования,
		Объект,
		"Комментарий"
	);
КонецПроцедуры

&НаКлиенте
Процедура ВариантыДополнительногоОтчетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВариантыДополнительногоОтчетаПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	ОткрытьВариант();
КонецПроцедуры

&НаКлиенте
Процедура ВариантыДополнительногоОтчетаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	Вариант = Элементы.ВариантыДополнительногоОтчета.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Вариант.Пользовательский Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Пометка на удаление предопределенного варианта отчета запрещена.'"));
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Пометить ""%1"" на удаление?'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, Вариант.Наименование);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Вариант", Вариант);
	Обработчик = Новый ОписаниеОповещения("ВариантыДополнительногоОтчетаПередУдалениемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДляФормыСпискаПриИзменении(Элемент)
	Если НЕ Объект.ИспользоватьДляФормыОбъекта И НЕ Объект.ИспользоватьДляФормыСписка Тогда
		Объект.ИспользоватьДляФормыОбъекта = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьДляФормыОбъектаПриИзменении(Элемент)
	Если НЕ Объект.ИспользоватьДляФормыОбъекта И НЕ Объект.ИспользоватьДляФормыСписка Тогда
		Объект.ИспользоватьДляФормыСписка = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВключениеПрофилейБезопасностиНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	Если НавигационнаяСсылка = "int://sp-on" Тогда
		
		ОткрытьФорму(
			"Обработка.НастройкаРазрешенийНаИспользованиеВнешнихРесурсов.Форма.НастройкиИспользованияПрофилейБезопасности",
			,
			,
			,
			,
			,
			,
			РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектКоманды

&НаКлиенте
Процедура ОбъектКомандыБыстрыйДоступПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ИзменитьБыстрыйДоступ();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКомандыБыстрыйДоступПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКомандыРегламентноеЗаданиеИспользованиеПриИзменении(Элемент)
	ИзменитьРегламентноеЗадание(Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКомандыРегламентноеЗаданиеПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ИзменитьРегламентноеЗадание(Истина, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКомандыРегламентноеЗаданиеПредставлениеОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКомандыНастроитьБыстрыйДоступ(Команда)
	ИзменитьБыстрыйДоступ();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКомандыНастроитьРасписание(Команда)
	ИзменитьРегламентноеЗадание(Истина, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКомандыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ОбъектКомандыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	ЗаписатьНаКлиенте(Истина);
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	ЗаписатьНаКлиенте(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	ОбновлениеИзФайлаЗапуск();
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	ПараметрыВыгрузки = Новый Структура;
	ПараметрыВыгрузки.Вставить("ЭтоОтчет", Объект.Вид = ВидОтчет Или Объект.Вид = ВидДополнительныйОтчет);
	ПараметрыВыгрузки.Вставить("ИмяФайла", Объект.ИмяФайла);
	ПараметрыВыгрузки.Вставить("АдресДанныхОбработки", АдресДанныхОбработки);
	ДополнительныеОтчетыИОбработкиКлиент.ВыгрузитьВФайл(ПараметрыВыгрузки);
КонецПроцедуры

&НаКлиенте
Процедура ВариантыДополнительногоОтчетаОткрыть(Команда)
	Вариант = ЭтотОбъект.Элементы.ВариантыДополнительногоОтчета.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите вариант отчета.'"));
		Возврат;
	КонецЕсли;
	
	ДополнительныеОтчетыИОбработкиКлиент.ОткрытьВариантДополнительногоОтчета(Объект.Ссылка, Вариант.КлючВарианта);
КонецПроцедуры

&НаКлиенте
Процедура РазместитьВРазделах(Команда)
	МассивВариантов = Новый Массив;
	Для Каждого ИдентификаторСтроки Из Элементы.ВариантыДополнительногоОтчета.ВыделенныеСтроки Цикл
		Вариант = ВариантыДополнительногоОтчета.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ЗначениеЗаполнено(Вариант.Ссылка) Тогда
			МассивВариантов.Добавить(Вариант.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	// Открывает диалог размещения нескольких вариантов отчетов в разделах командного интерфейса.
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		МодульВариантыОтчетовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ВариантыОтчетовКлиент");
		МодульВариантыОтчетовКлиент.ОткрытьДиалогРазмещенияВариантовВРазделах(МассивВариантов);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НазначениеКомандЗаполнениеФормыНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Объект.Вид = ВидДополнительныйОтчет ИЛИ Объект.Вид = ВидДополнительнаяОбработка Тогда
		// Выбор разделов
		ДоступныеРазделы = СписокРазделов(Объект.Разделы);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Разделы",      ДоступныеРазделы);
		ПараметрыФормы.Вставить("ВидОбработки", Объект.Вид);
		
		ОткрытьФорму("Справочник.ДополнительныеОтчетыИОбработки.Форма.ПодборРазделов", ПараметрыФормы, ЭтотОбъект);
	Иначе
		// Выбор объектов метаданных
		ПараметрыФормы = ПодготовитьПараметрыФормыВыборОбъектовМетаданных();
		ОткрытьФорму("ОбщаяФорма.ВыборОбъектовМетаданных", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектКомандыРегламентноеЗаданиеИспользование.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбъектКомандыРегламентноеЗаданиеПредставление.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Команды.РегламентноеЗаданиеРазрешено");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Клиент

&НаКлиенте
Процедура ЗаписатьНаКлиенте(ЗакрытьПослеЗаписи)
	Запросы = ЗапросыОбновленияРазрешений();
	РаботаВБезопасномРежимеКлиент.ПрименитьЗапросыНаИспользованиеВнешнихРесурсов(Запросы, ЭтотОбъект, Новый ОписаниеОповещения("ПродолжитьЗаписьНаКлиенте", ЭтотОбъект, ЗакрытьПослеЗаписи));
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗаписьНаКлиенте(Результат, ЗакрытьПослеЗаписи)  Экспорт
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("РегистрацияОбработки", РегистрацияОбработки);
	ПараметрыЗаписи.Вставить("ЗакрытьПослеЗаписи", ЗакрытьПослеЗаписи);
	
	Успех = Записать(ПараметрыЗаписи);
	Если Не Успех Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыЗаписи.РегистрацияОбработки Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		Обработчик = Новый ОписаниеОповещения("ЗаписатьНаКлиентеЗавершение", ЭтотОбъект, ПараметрыЗаписи);
		ТекстПредупреждения = НСтр("ru = 'Для применения изменений в открытых окнах
		|необходимо их закрыть и открыть заново.'");
		ПоказатьПредупреждение(Обработчик, ТекстПредупреждения);
	Иначе
		ЗаписатьНаКлиентеЗавершение(ПараметрыЗаписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗапросыОбновленияРазрешений()
	
	Возврат ДополнительныеОтчетыИОбработкиВБезопасномРежимеСлужебный.ЗапросыРазрешенийДополнительнойОбработки(
		Объект, ПолучитьТаблицуРазрешений());
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьНаКлиентеЗавершение(ПараметрыЗаписи) Экспорт
	Если ПараметрыЗаписи.ЗакрытьПослеЗаписи И Открыта() Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИзФайлаЗапуск()
	ПараметрыРегистрации = Новый Структура;
	ПараметрыРегистрации.Вставить("Успех", Ложь);
	ПараметрыРегистрации.Вставить("АдресДанныхОбработки", АдресДанныхОбработки);
	ПараметрыРегистрации.Вставить("ОбработчикРезультата", Новый ОписаниеОповещения("ОбновлениеИзФайлаЗавершение", ЭтотОбъект));
	Обработчик = Новый ОписаниеОповещения("ОбновлениеИзФайлаПослеВыбораФайла", ЭтотОбъект, ПараметрыРегистрации);
	
	ПараметрыДиалога = Новый Структура("Режим, Фильтр, ИндексФильтра, Заголовок");
	ПараметрыДиалога.Режим  = РежимДиалогаВыбораФайла.Открытие;
	ПараметрыДиалога.Фильтр = ДополнительныеОтчетыИОбработкиКлиентСервер.ФильтрДиалоговВыбораИСохранения();
	Если Объект.Ссылка.Пустая() Тогда
		ПараметрыДиалога.ИндексФильтра = 0;
		ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите файл внешнего отчета или обработки'");
	ИначеЕсли Объект.Вид = ВидДополнительныйОтчет Или Объект.Вид = ВидОтчет Тогда
		ПараметрыДиалога.ИндексФильтра = 1;
		ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите файл внешнего отчета'");
	Иначе
		ПараметрыДиалога.ИндексФильтра = 2;
		ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите файл внешней обработки'");
	КонецЕсли;
	
	ДополнительныеОтчетыИОбработкиКлиент.ПоказатьПомещениеФайла(Обработчик, УникальныйИдентификатор, Объект.ИмяФайла, ПараметрыДиалога);
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИзФайлаПослеВыбораФайла(ПомещенныеФайлы, ПараметрыРегистрации) Экспорт
	Если ПомещенныеФайлы = Неопределено Или ПомещенныеФайлы.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(ПараметрыРегистрации.ОбработчикРезультата, ПараметрыРегистрации);
		Возврат;
	КонецЕсли;
	
	ОписаниеФайла = ПомещенныеФайлы[0];
	
	Ключи = Новый Структура("ИмяФайла, ЭтоОтчет, ОтключатьПубликацию, ОтключатьКонфликтующие, Конфликтующие");
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыРегистрации, Ключи, Ложь);
	
	ПараметрыРегистрации.ОтключатьПубликацию = Ложь;
	ПараметрыРегистрации.ОтключатьКонфликтующие = Ложь;
	ПараметрыРегистрации.Конфликтующие = Новый СписокЗначений;
	
	МассивПодстрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ОписаниеФайла.Имя, "\");
	ПараметрыРегистрации.ИмяФайла = МассивПодстрок.Получить(МассивПодстрок.ВГраница());
	РасширениеФайла = ВРег(Прав(ПараметрыРегистрации.ИмяФайла, 3));
	
	Если РасширениеФайла = "ERF" Тогда
		ПараметрыРегистрации.ЭтоОтчет = Истина;
	ИначеЕсли РасширениеФайла = "EPF" Тогда
		ПараметрыРегистрации.ЭтоОтчет = Ложь;
	Иначе
		ПараметрыРегистрации.Успех = Ложь;
		ДополнительныеОтчетыИОбработкиКлиент.ВернутьРезультатПослеПоказаПредупреждения(
			НСтр("ru = 'Расширение файла не соответствует расширению внешнего отчета (ERF) или обработки (EPF).'"),
			ПараметрыРегистрации.ОбработчикРезультата,
			ПараметрыРегистрации);
		Возврат;
	КонецЕсли;
	
	ПараметрыРегистрации.АдресДанныхОбработки = ОписаниеФайла.Хранение;
	
	ОбновлениеИзФайлаМеханикаНаКлиенте(ПараметрыРегистрации);
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИзФайлаМеханикаНаКлиенте(ПараметрыРегистрации)
	// Подготовка к вызову сервера.
	ОбработчикРезультата = ПараметрыРегистрации.ОбработчикРезультата;
	ПараметрыРегистрации.Удалить("ОбработчикРезультата");
	// Вызов сервера.
	ОбновлениеИзФайлаМеханикаНаСервере(ПараметрыРегистрации);
	// Отмена изменений.
	ПараметрыРегистрации.Вставить("ОбработчикРезультата", ОбработчикРезультата);
	
	Если ПараметрыРегистрации.ОтключатьКонфликтующие Тогда
		// Отключается несколько объектов, потому динамические списки надо обновить
		ОповеститьОбИзменении(Тип("СправочникСсылка.ДополнительныеОтчетыИОбработки"));
	КонецЕсли;
	
	// Обработка результата работы сервера
	Если ПараметрыРегистрации.Успех Тогда
		ОповещениеЗаголовок = ?(ПараметрыРегистрации.ЭтоОтчет, НСтр("ru = 'Файл внешнего отчета загружен'"), НСтр("ru = 'Файл внешней обработки загружен'"));
		ОповещениеСсылка    = ?(ЭтоНовый, "", ПолучитьНавигационнуюСсылку(Объект.Ссылка));
		ОповещениеТекст     = ПараметрыРегистрации.ИмяФайла;
		ПоказатьОповещениеПользователя(ОповещениеЗаголовок, ОповещениеСсылка, ОповещениеТекст);
		ВыполнитьОбработкуОповещения(ПараметрыРегистрации.ОбработчикРезультата, ПараметрыРегистрации);
	Иначе
		// Разбор причины отказа загрузки обработки и отображение информации пользователю
		Если ПараметрыРегистрации.ИмяОбъектаЗанято Тогда
			ОбновлениеИзФайлаПоказатьКонфликты(ПараметрыРегистрации);
		Иначе
			ДополнительныеОтчетыИОбработкиКлиент.ВернутьРезультатПослеПоказаПредупреждения(
			    ПараметрыРегистрации.КраткоеПредставлениеОшибки,
				ПараметрыРегистрации.ОбработчикРезультата,
				ПараметрыРегистрации);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИзФайлаПоказатьКонфликты(ПараметрыРегистрации)
	Если ПараметрыРегистрации.КоличествоКонфликтующих > 1 Тогда
		Если ПараметрыРегистрации.ЭтоОтчет Тогда
			ЗаголовокВопроса = НСтр("ru = 'Конфликты при загрузке внешнего отчета'");
			ТекстВопроса = НСтр("ru = 'Внутреннее имя отчета ""[Name]"" уже занято другими дополнительными отчетами ([Count]): 
			|[List].
			|
			|Выберите:
			|1. ""[Continue]"" - выполнить загрузку, отключив публикацию этого отчета.
			|2. ""[Disable]"" - выполнить загрузку, отключив публикацию всех других конфликтующих отчетов.
			|3. ""[Open]"" - отменить загрузку и открыть список конфликтующих отчетов.'");
		Иначе
			ЗаголовокВопроса = НСтр("ru = 'Конфликты при загрузке внешней обработки'");
			ТекстВопроса = НСтр("ru = 'Внутреннее имя обработки ""[Name]"" уже занято другими дополнительными обработками ([Count]): 
			|[List].
			|
			|Выберите:
			|1. ""[Continue]"" - выполнить загрузку, отключив публикацию этой обработки.
			|2. ""[Disable]"" - выполнить загрузку, отключив публикацию всех других конфликтующих обработок.
			|3. ""[Open]"" - отменить загрузку и открыть список конфликтующих обработок.'");
		КонецЕсли;
		ПредставлениеКнопкиОтключить = НСтр("ru = 'Отключить конфликтующие'");
		ПредставлениеКнопкиОткрыть = НСтр("ru = 'Отменить и показать список'");
	Иначе
		Если ПараметрыРегистрации.ЭтоОтчет Тогда
			ЗаголовокВопроса = НСтр("ru = 'Конфликт при загрузке внешнего отчета'");
			ТекстВопроса = НСтр("ru = 'Внутреннее имя отчета ""[Name]"" уже занято другим дополнительным отчетом: 
			|[List].
			|
			|Выберите:
			|1. ""[Continue]"" - выполнить загрузку, отключив публикацию этого отчета.
			|2. ""[Disable]"" - выполнить загрузку, отключив публикацию другого отчета.
			|3. ""[Open]"" - отменить загрузку и открыть карточку другого отчета.'");
			ПредставлениеКнопкиОтключить = НСтр("ru = 'Отключить другой отчет'");
		Иначе
			ЗаголовокВопроса = НСтр("ru = 'Конфликт при загрузке внешней обработки'");
			ТекстВопроса = НСтр("ru = 'Внутреннее имя обработки ""[Name]"" уже занято другой дополнительной обработкой: 
			|[List].
			|
			|Выберите:
			|1. ""[Continue]"" - выполнить загрузку, отключив публикацию этой обработки.
			|2. ""[Disable]"" - выполнить загрузку, отключив публикацию другой обработки.
			|3. ""[Open]"" - отменить загрузку и открыть карточку другой обработку.'");
			ПредставлениеКнопкиОтключить = НСтр("ru = 'Отключить другую обработку'");
		КонецЕсли;
		ПредставлениеКнопкиОткрыть = НСтр("ru = 'Отменить и открыть'");
	КонецЕсли;
	ПредставлениеКнопкиПродолжить = НСтр("ru = 'В режиме отладки'");
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "[Name]",  ПараметрыРегистрации.ИмяОбъекта);
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "[Count]", ПараметрыРегистрации.КоличествоКонфликтующих);
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "[List]",  ПараметрыРегистрации.ПредставлениеЗанявших);
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "[Disable]",  ПредставлениеКнопкиОтключить);
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "[Open]",     ПредставлениеКнопкиОткрыть);
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "[Continue]", ПредставлениеКнопкиПродолжить);
	
	КнопкиВопроса = Новый СписокЗначений;
	КнопкиВопроса.Добавить("ПродолжитьБезПубликации", ПредставлениеКнопкиПродолжить);
	КнопкиВопроса.Добавить("ОтключитьКонфликтующие",  ПредставлениеКнопкиОтключить);
	КнопкиВопроса.Добавить("ОтменитьИОткрыть",        ПредставлениеКнопкиОткрыть);
	КнопкиВопроса.Добавить(КодВозвратаДиалога.Отмена);
	
	Обработчик = Новый ОписаниеОповещения("ОбновлениеИзФайлаРазрешениеКонфликтов", ЭтотОбъект, ПараметрыРегистрации);
	
	ПоказатьВопрос(Обработчик, ТекстВопроса, КнопкиВопроса, , "ПродолжитьБезПубликации", ЗаголовокВопроса);
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИзФайлаРазрешениеКонфликтов(Ответ, ПараметрыРегистрации) Экспорт
	Если Ответ = "ПродолжитьБезПубликации" Тогда
		// Повторный вызов сервера (публикация в режиме отладки) и обработка результата.
		ПараметрыРегистрации.ОтключатьПубликацию = Истина;
		ОбновлениеИзФайлаМеханикаНаКлиенте(ПараметрыРегистрации);
	ИначеЕсли Ответ = "ОтключитьКонфликтующие" Тогда
		// Повторный вызов сервера (с переводом конфликтующих в режим отладки) и обработка результата.
		ПараметрыРегистрации.ОтключатьКонфликтующие = Истина;
		ОбновлениеИзФайлаМеханикаНаКлиенте(ПараметрыРегистрации);
	ИначеЕсли Ответ = "ОтменитьИОткрыть" Тогда
		// Отмена и показ конфликтующих.
		// Список показывается когда конфликтует более одного элемента.
		ПоказатьСписок = (ПараметрыРегистрации.КоличествоКонфликтующих > 1);
		Если ПараметрыРегистрации.СтароеИмяОбъекта = ПараметрыРегистрации.ИмяОбъекта И Не ЭтоНовый Тогда
			// Или текущий элемент уже записан с конфликтующим именем.
			// В списке будут два элемента - текущий и конфликтующий.
			// Это позволит принять решение о том, какой из них отключать.
			ПоказатьСписок = Истина;
		КонецЕсли;
		Если ПоказатьСписок Тогда // Форма списка с отбором по конфликтующим
			ФормаИмя = "Справочник.ДополнительныеОтчетыИОбработки.ФормаСписка";
			ФормаЗаголовок = НСтр("ru = 'Дополнительные отчеты и обработки с внутреннем именем ""%1""'");
			ФормаЗаголовок = СтрЗаменить(ФормаЗаголовок, "%1", ПараметрыРегистрации.ИмяОбъекта);
			ФормаПараметры = Новый Структура;
			ФормаПараметры.Вставить("Отбор", Новый Структура);
			ФормаПараметры.Отбор.Вставить("ИмяОбъекта", ПараметрыРегистрации.ИмяОбъекта);
			ФормаПараметры.Отбор.Вставить("ЭтоГруппа", Ложь);
			ФормаПараметры.Вставить("Заголовок", ФормаЗаголовок);
			ФормаПараметры.Вставить("Отображение", "Список");
		Иначе // Форма элемента
			ФормаИмя = "Справочник.ДополнительныеОтчетыИОбработки.ФормаОбъекта";
			ФормаПараметры = Новый Структура;
			ФормаПараметры.Вставить("Ключ", ПараметрыРегистрации.Конфликтующие[0].Значение);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ПараметрыРегистрации.ОбработчикРезультата, ПараметрыРегистрации);
		ОткрытьФорму(ФормаИмя, ФормаПараметры, Неопределено, Истина);
	Иначе // Отмена.
		ВыполнитьОбработкуОповещения(ПараметрыРегистрации.ОбработчикРезультата, ПараметрыРегистрации);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеИзФайлаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат.Успех = Ложь Тогда
		Если ПоказатьДиалогЗагрузкиИзФайлаПриОткрытии И Открыта() Тогда
			Закрыть();
		КонецЕсли;
	ИначеЕсли Результат.Успех = Истина Тогда
		Если Не Открыта() Тогда
			Открыть();
		КонецЕсли;
		Модифицированность = Истина;
		РегистрацияОбработки = Истина;
		АдресДанныхОбработки = Результат.АдресДанныхОбработки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВариант()
	Вариант = Элементы.ВариантыДополнительногоОтчета.ТекущиеДанные;
	Если Вариант = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Вариант.Ссылка) Тогда
		ТекстОшибки = НСтр("ru = 'Вариант отчета ""%1"" не зарегистрирован.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Вариант.Наименование);
		ПоказатьПредупреждение(, ТекстОшибки);
	Иначе
		ПоказатьЗначение(, Вариант.Ссылка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРегламентноеЗадание(РежимВыбора = Ложь, ИзмененФлажок = Ложь)
	
	ЭлементКоманда = Элементы.ОбъектКоманды.ТекущиеДанные;
	Если ЭлементКоманда = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭлементКоманда.ВариантЗапуска <> ПредопределенноеЗначение("Перечисление.СпособыВызоваДополнительныхОбработок.ВызовСерверногоМетода")
		И ЭлементКоманда.ВариантЗапуска <> ПредопределенноеЗначение("Перечисление.СпособыВызоваДополнительныхОбработок.СценарийВБезопасномРежиме") Тогда
		ТекстОшибки = НСтр("ru = 'Команда с вариантом запуска ""%1""
		|не может использоваться в регламентных заданиях.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Строка(ЭлементКоманда.ВариантЗапуска));
		ПоказатьПредупреждение(, ТекстОшибки);
		Если ИзмененФлажок тогда
			ЭлементКоманда.РегламентноеЗаданиеИспользование = НЕ ЭлементКоманда.РегламентноеЗаданиеИспользование;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ИзмененФлажок Тогда
		Если НЕ ЭлементКоманда.РегламентноеЗаданиеИспользование Тогда
			ЭлементКоманда.РегламентноеЗаданиеПредставление = ПредставлениеОтключенногоРасписания();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭлементКоманда.РегламентноеЗаданиеРасписание.Количество() > 0 Тогда
		РасписаниеКоманды = ЭлементКоманда.РегламентноеЗаданиеРасписание.Получить(0).Значение;
	Иначе
		РасписаниеКоманды = Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(РасписаниеКоманды) <> Тип("РасписаниеРегламентногоЗадания") Тогда
		РасписаниеКоманды = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЭлементКоманда", ЭлементКоманда);
	Обработчик = Новый ОписаниеОповещения("ИзменитьРегламентноеЗаданиеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	РедактированиеРасписания = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеКоманды);
	РедактированиеРасписания.Показать(Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьРегламентноеЗаданиеЗавершение(Расписание, ДополнительныеПараметры) Экспорт
	Если Расписание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементКоманда = ДополнительныеПараметры.ЭлементКоманда;
	ЭлементКоманда.РегламентноеЗаданиеРасписание.Очистить();
	ЭлементКоманда.РегламентноеЗаданиеРасписание.Добавить(Расписание);
	ЭлементКоманда.РегламентноеЗаданиеПредставление = Строка(Расписание);
	
	Если ЭлементКоманда.РегламентноеЗаданиеПредставление = ПредставлениеПустогоРасписания() Тогда
		ЭлементКоманда.РегламентноеЗаданиеПредставление = ПредставлениеОтключенногоРасписания();
		ЭлементКоманда.РегламентноеЗаданиеИспользование = Ложь;
		Модифицированность = Истина;
	Иначе
		ЭлементКоманда.РегламентноеЗаданиеИспользование = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьБыстрыйДоступ()
	ЭлементКоманда = Элементы.ОбъектКоманды.ТекущиеДанные;
	Если ЭлементКоманда = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Найденные = БыстрыйДоступ.НайтиСтроки(Новый Структура("ИдентификаторКоманды", ЭлементКоманда.Идентификатор));
	ПользователиСБыстрымДоступом = Новый СписокЗначений;
	Для Каждого СтрокаТаблицы Из Найденные Цикл
		ПользователиСБыстрымДоступом.Добавить(СтрокаТаблицы.Пользователь);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПользователиСБыстрымДоступом", ПользователиСБыстрымДоступом);
	ПараметрыФормы.Вставить("ПредставлениеКоманды",         ЭлементКоманда.Представление);
	
	КлиентскийКэш.Вставить("ИдентификаторСтрокиКоманды", ЭлементКоманда.ПолучитьИдентификатор());
	ОткрытьФорму("Справочник.ДополнительныеОтчетыИОбработки.Форма.БыстрыйДоступКДополнительнымОтчетамИОбработкам", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешенияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Переход = ДанныеСобытия.Href;
	Если Не ПустаяСтрока(Переход) Тогда
		ПодключитьОбработчикОжидания("РазрешенияПриНажатии_Подключаемый", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешенияПриНажатии_Подключаемый()
	
	КлючВнутреннейОбработки = "internal:";
	
	Если Переход = КлючВнутреннейОбработки + "home" Тогда
		
		СформироватьСписокРазрешений();
		
	ИначеЕсли Лев(Переход, СтрДлина(КлючВнутреннейОбработки)) = КлючВнутреннейОбработки Тогда
		
		СформироватьПредставленияРазрешений(Прав(Переход, СтрДлина(Переход) - СтрДлина(КлючВнутреннейОбработки)));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантыДополнительногоОтчетаПередУдалениемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Вариант = ДополнительныеПараметры.Вариант;
		УдалитьВариантДополнительногоОтчета("ВнешнийОтчет." + Объект.ИмяОбъекта, Вариант.КлючВарианта);
		ВариантыДополнительногоОтчета.Удалить(Вариант);
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Клиент, Сервер

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеПустогоРасписания()
	Возврат Строка(Новый РасписаниеРегламентногоЗадания);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеОтключенногоРасписания()
	Возврат НСтр("ru = 'Расписание не задано'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеБыстрогоДоступаПользователей(КоличествоПользователей)
	Если КоличествоПользователей = 0 Тогда
		Возврат НСтр("ru = 'Нет'");
	КонецЕсли;
	
	ПоследняяЦифра = КоличествоПользователей - 10 * Цел(КоличествоПользователей/10);
	
	Если ПоследняяЦифра = 1 Тогда
		БыстрыйДоступПредставление = НСтр("ru = '%1 пользователь'");
	ИначеЕсли ПоследняяЦифра = 2 Или ПоследняяЦифра = 3 Или ПоследняяЦифра = 4 Тогда
		БыстрыйДоступПредставление = НСтр("ru = '%1 пользователя'");
	Иначе
		БыстрыйДоступПредставление = НСтр("ru = '%1 пользователей'");
	КонецЕсли;
	
	БыстрыйДоступПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		БыстрыйДоступПредставление, 
		Формат(КоличествоПользователей, "ЧГ=0"));
	
	Возврат БыстрыйДоступПредставление;
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Вызов сервера, Сервер

&НаСервере
Процедура ОбновлениеИзФайлаМеханикаНаСервере(ПараметрыРегистрации)
	ОбъектСправочника = РеквизитФормыВЗначение("Объект");
	КомандыСохраненные = ОбъектСправочника.Команды.Выгрузить();
	
	РезультатРегистрации = ДополнительныеОтчетыИОбработки.ЗарегистрироватьОбработку(ОбъектСправочника, ПараметрыРегистрации);
	
	АдресРазрешений = ПоместитьВоВременноеХранилище(ОбъектСправочника.Разрешения.Выгрузить(), ЭтотОбъект.УникальныйИдентификатор);
	ЗначениеВРеквизитФормы(ОбъектСправочника, "Объект");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыРегистрации, РезультатРегистрации, Истина);
	
	Если ПараметрыРегистрации.Успех Тогда
		ЗаполнитьКоманды(КомандыСохраненные);
	ИначеЕсли ПараметрыРегистрации.ИмяОбъектаЗанято Тогда
		// Представление занявших объектов.
		ПредставлениеЗанявших = "";
		Для Каждого ЭлементСписка Из ПараметрыРегистрации.Конфликтующие Цикл
			ПредставлениеЗанявших = ПредставлениеЗанявших 
			+ ?(ПредставлениеЗанявших = "", "", ", ")
			+ СокрЛП(ЭлементСписка.Представление);
			Если СтрДлина(ПредставлениеЗанявших) > 80 Тогда
				ПредставлениеЗанявших = Лев(ПредставлениеЗанявших, 70) + "... ";
				Прервать;
			КонецЕсли;
		КонецЦикла;
		ПараметрыРегистрации.Вставить("ПредставлениеЗанявших", ПредставлениеЗанявших);
		// Количество занявших объектов.
		ПараметрыРегистрации.Вставить("КоличествоКонфликтующих", ПараметрыРегистрации.Конфликтующие.Количество());
	КонецЕсли;
	
	УстановитьВидимостьДоступность(Истина);
КонецПроцедуры

&НаСервере
Функция ПодготовитьПараметрыФормыВыборОбъектовМетаданных()
	ФильтрПоОбъектамМетаданных = Новый СписокЗначений;
	Если Объект.Вид = Перечисления.ВидыДополнительныхОтчетовИОбработок.ЗаполнениеОбъекта Тогда
		ОбщаяКоманда = Метаданные.ОбщиеКоманды.ЗаполнениеОбъекта;
	ИначеЕсли Объект.Вид = Перечисления.ВидыДополнительныхОтчетовИОбработок.Отчет Тогда
		ОбщаяКоманда = Метаданные.ОбщиеКоманды.ОтчетыОбъекта;
	ИначеЕсли Объект.Вид = Перечисления.ВидыДополнительныхОтчетовИОбработок.ПечатнаяФорма Тогда
		ОбщаяКоманда = Метаданные.ОбщиеКоманды.ДополнительныеПечатныеФормыОбъекта;
	ИначеЕсли Объект.Вид = Перечисления.ВидыДополнительныхОтчетовИОбработок.СозданиеСвязанныхОбъектов Тогда
		ОбщаяКоманда = Метаданные.ОбщиеКоманды.СозданиеСвязанныхОбъектов;
	КонецЕсли;
	Для Каждого ТипПараметраКоманды Из ОбщаяКоманда.ТипПараметраКоманды.Типы() Цикл
		ФильтрПоОбъектамМетаданных.Добавить(Метаданные.НайтиПоТипу(ТипПараметраКоманды).ПолноеИмя());
	КонецЦикла;
	
	ВыбранныеОбъектыМетаданных = Новый СписокЗначений;
	Для Каждого ЭлементНазначение ИЗ Объект.Назначение Цикл
		Если НЕ ЭлементНазначение.ОбъектНазначения.ПометкаУдаления Тогда
			ВыбранныеОбъектыМетаданных.Добавить(ЭлементНазначение.ОбъектНазначения.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФильтрПоОбъектамМетаданных", ФильтрПоОбъектамМетаданных);
	ПараметрыФормы.Вставить("ВыбранныеОбъектыМетаданных", ВыбранныеОбъектыМетаданных);
	ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Назначение дополнительной обработки'"));
	
	Возврат ПараметрыФормы;
КонецФункции

&НаСервере
Процедура ЗагрузитьВыбранныеОбъектыМетаданных(Параметр)
	Объект.Назначение.Очистить();
	
	Для Каждого ЭлементПараметр Из Параметр Цикл
		ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ЭлементПараметр.Значение);
		Если ОбъектМетаданных = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтрокаНазначение = Объект.Назначение.Добавить();
		СтрокаНазначение.ОбъектНазначения = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ОбъектМетаданных);
	КонецЦикла;
	
	Модифицированность = Истина;
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьВариантДополнительногоОтчета(КлючОбъекта, КлючВарианта)
	ХранилищаНастроек["ХранилищеВариантовОтчетов"].Удалить(КлючОбъекта, КлючВарианта, Неопределено);
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность(Регистрация = Ложь)
	
	Если НЕ Регистрация И НЕ ЭтоНовый И Объект.Вид = ВидДополнительныйОтчет Тогда
		ВариантыДополнительногоОтчетаЗаполнить();
	Иначе
		ВариантыДополнительногоОтчета.Очистить();
	КонецЕсли;
	
	ЭтоГлобальнаяОбработка = (Объект.Вид = ВидДополнительнаяОбработка ИЛИ Объект.Вид = ВидДополнительныйОтчет);
	ЭтоОтчет = (Объект.Вид = ВидДополнительныйОтчет ИЛИ Объект.Вид = ВидОтчет);
	
	КоличествоВариантов = ВариантыДополнительногоОтчета.Количество();
	КоличествоКоманд = Объект.Команды.Количество();
	КоличествоВидимыхЗакладок = 1;
	
	Если Объект.Вид = ВидДополнительныйОтчет И Объект.ИспользуетХранилищеВариантов Тогда
		
		КоличествоВидимыхЗакладок = КоличествоВидимыхЗакладок + 1;
		
		Элементы.СтраницыВарианты.Видимость = Истина;
		
		Если Регистрация ИЛИ КоличествоВариантов = 0 Тогда
			Элементы.СтраницыВарианты.ТекущаяСтраница = Элементы.ВариантыСкрытьДоЗаписи;
			Элементы.СтраницаВарианты.Заголовок = НСтр("ru = 'Варианты отчета'");
		Иначе
			Элементы.СтраницыВарианты.ТекущаяСтраница = Элементы.ВариантыПоказать;
			Элементы.СтраницаВарианты.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Варианты отчета (%1)'"),
				Формат(КоличествоВариантов, "ЧГ="));
		КонецЕсли;
	Иначе
		Элементы.СтраницыВарианты.Видимость = Ложь;
	КонецЕсли;
	
	Если КоличествоКоманд = 0 Тогда
		
		Элементы.СтраницаКоманды.Видимость = Ложь;
		Элементы.СтраницаКоманды.Заголовок = НСтр("ru = 'Команды'");
		
	Иначе
		
		КоличествоВидимыхЗакладок = КоличествоВидимыхЗакладок + 1;
		
		Элементы.СтраницаКоманды.Видимость = Истина;
		Элементы.СтраницаКоманды.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Команды (%1)'"),
			Формат(КоличествоКоманд, "ЧГ="));
		
	КонецЕсли;
	
	КоличествоРазрешений = ПолучитьТаблицуРазрешений().Количество();
	РежимСовместимостиРазрешений = Объект.РежимСовместимостиРазрешений;
	
	Если РежимСовместимостиРазрешений = Перечисления.РежимыСовместимостиРазрешенийДополнительныхОтчетовИОбработок.Версия_2_1_3 Тогда
		БезопасныйРежим = Объект.БезопасныйРежим;
		Если БезопасныйРежим И КоличествоРазрешений > 0 И ПолучитьФункциональнуюОпцию("ИспользуютсяПрофилиБезопасности") Тогда
			Если ЭтоНовый Тогда
				БезопасныйРежим = "";
			Иначе
				БезопасныйРежим = РаботаВБезопасномРежимеСлужебный.РежимПодключенияВнешнегоМодуля(Объект.Ссылка);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если КоличествоРазрешений = 0 Тогда
			БезопасныйРежим = Истина;
		Иначе
			Если ПолучитьФункциональнуюОпцию("ИспользуютсяПрофилиБезопасности") Тогда
				Если ЭтоНовый Тогда
					БезопасныйРежим = "";
				Иначе
					БезопасныйРежим = РаботаВБезопасномРежимеСлужебный.РежимПодключенияВнешнегоМодуля(Объект.Ссылка);
				КонецЕсли;
			Иначе
				БезопасныйРежим = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если КоличествоРазрешений = 0 Тогда
		
		Элементы.СтраницаРазрешения.Видимость = Ложь;
		Элементы.ГруппаБезопасныйРежимГлобальный.Видимость = Истина;
		
		Если БезопасныйРежим = Истина Тогда
			Элементы.СтраницыБезопасныйРежимГлобальный.ТекущаяСтраница = Элементы.СтраницаБезопасныйРежимИстина;
		ИначеЕсли БезопасныйРежим = Ложь Тогда
			Элементы.СтраницыБезопасныйРежимГлобальный.ТекущаяСтраница = Элементы.СтраницаБезопасныйРежимЛожь;
		Иначе
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 не является корректным режимом подключения для дополнительных отчетов и обработок, не требующих разрешений на использование профилей безопасности!'"),
				БезопасныйРежим);
		КонецЕсли;
		
		Элементы.ГруппаВключениеПрофилейБезопасности.Видимость = Ложь;
		
	Иначе
		
		КоличествоВидимыхЗакладок = КоличествоВидимыхЗакладок + 1;
		
		Элементы.СтраницыВариантыКомандыРазрешения.ТекущаяСтраница = Элементы.СтраницаРазрешения;
		Элементы.СтраницаРазрешения.Видимость = Истина;
		Элементы.СтраницаРазрешения.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Разрешения (%1)'"),
			Формат(КоличествоРазрешений, "ЧГ="));
		
		Элементы.ГруппаБезопасныйРежимГлобальный.Видимость = Ложь;
		
		Если РежимСовместимостиРазрешений = Перечисления.РежимыСовместимостиРазрешенийДополнительныхОтчетовИОбработок.Версия_2_1_3 Тогда
			Элементы.ГруппаСтраницыРежимыСовместимостиРазрешений.ТекущаяСтраница = Элементы.СтраницаРазрешенияВерсия_2_1_3;
		Иначе
			Элементы.ГруппаСтраницыРежимыСовместимостиРазрешений.ТекущаяСтраница = Элементы.СтраницаРазрешенияВерсия_2_2_2;
		КонецЕсли;
		
		Если БезопасныйРежим = Истина Тогда
			Элементы.СтраницыБезопасныйРежимСРазрешениями.ТекущаяСтраница = Элементы.СтраницаБезопасныйРежимСРазрешениями;
		ИначеЕсли БезопасныйРежим = Ложь Тогда
			Элементы.СтраницыБезопасныйРежимСРазрешениями.ТекущаяСтраница = Элементы.СтраницаНебезопасныйРежимСРазрешениями;
		ИначеЕсли ТипЗнч(БезопасныйРежим) = Тип("Строка") Тогда
			Элементы.СтраницыБезопасныйРежимСРазрешениями.ТекущаяСтраница = Элементы.СтраницаПерсональныйПрофильБезопасности;
			Элементы.ДекорацияПерсональныйПрофильБезопасностиНадпись.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Дополнительный отчет или обработка будет подключаться к программе с использованием ""персонального""
                      |профиля безопасности %1, в котором будут разрешены только следующие операции:'"), БезопасныйРежим);
		Иначе
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 не является корректным режимом подключения для дополнительных отчетов и обработок, требующих разрешений на использование профилей безопасности!'"),
				БезопасныйРежим);
		КонецЕсли;
		
		Если БезопасныйРежим = Ложь И Не ПолучитьФункциональнуюОпцию("ИспользуютсяПрофилиБезопасности") И РаботаВБезопасномРежимеСлужебный.ДоступнаНастройкаПрофилейБезопасности() Тогда
			Элементы.ГруппаВключениеПрофилейБезопасности.Видимость = Истина;
		Иначе
			Элементы.ГруппаВключениеПрофилейБезопасности.Видимость = Ложь;
		КонецЕсли;
		
		СформироватьСписокРазрешений();
		
	КонецЕсли;
	
	Элементы.СтраницыВариантыКомандыРазрешения.ОтображениеСтраниц = ОтображениеСтраницФормы[?(КоличествоВидимыхЗакладок > 1, "ЗакладкиСверху", "Нет")];
	
	Если ЭтоГлобальнаяОбработка Тогда
		Если Объект.Разделы.Количество() = 0 Тогда
			ПредставлениеНазначения = НСтр("ru = 'Не определено'");
		Иначе
			ПредставлениеНазначения = "";
			Для Каждого СтрокаРаздел Из Объект.Разделы Цикл
				Если ПредставлениеНазначения <> "" Тогда
					ПредставлениеНазначения = ПредставлениеНазначения + ", ";
				КонецЕсли;
				ПредставлениеНазначения = ПредставлениеНазначения + ДополнительныеОтчетыИОбработки.ПредставлениеРаздела(СтрокаРаздел.Раздел);
			КонецЦикла;
		КонецЕсли;
	Иначе
		Если Объект.Назначение.Количество() = 0 Тогда
			ПредставлениеНазначения = НСтр("ru = 'Не определено'");
		Иначе
			ПредставлениеНазначения = "";
			Для Каждого СтрокаНазначение Из Объект.Назначение Цикл
				Если ПредставлениеНазначения <> "" Тогда
					ПредставлениеНазначения = ПредставлениеНазначения + ", ";
				КонецЕсли;
				ПредставлениеНазначения = ПредставлениеНазначения + Строка(СтрокаНазначение.ОбъектНазначения);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ОбъектКомандыБыстрыйДоступПредставление.Видимость       = ЭтоГлобальнаяОбработка;
	Элементы.ОбъектКомандыНастроитьБыстрыйДоступ.Видимость           = ЭтоГлобальнаяОбработка;
	Элементы.ОбъектКомандыРегламентноеЗаданиеПредставление.Видимость = ЭтоГлобальнаяОбработка;
	Элементы.ОбъектКомандыРегламентноеЗаданиеИспользование.Видимость = ЭтоГлобальнаяОбработка;
	Элементы.ОбъектКомандыНастроитьРасписание.Видимость              = ЭтоГлобальнаяОбработка;
	Элементы.РазмещениеКоманд.Видимость                              = ЭтоГлобальнаяОбработка;
	Элементы.НазначениеКоманд.Видимость                              = НЕ ЭтоГлобальнаяОбработка;
	Элементы.ТипыФорм.Видимость                                      = НЕ ЭтоГлобальнаяОбработка;
	
	Если ЭтоНовый Тогда
		Заголовок = ?(ЭтоОтчет, НСтр("ru = 'Дополнительный отчет (создание)'"), НСтр("ru = 'Дополнительная обработка (создание)'"));
	Иначе
		Заголовок = Объект.Наименование + " " + ?(ЭтоОтчет, НСтр("ru = '(Дополнительный отчет)'"), НСтр("ru = '(Дополнительная обработка)'"));
	КонецЕсли;
	
	Если КоличествоВариантов > 0 Тогда
		
		ВыводитьЗаголовокТаблицы = КоличествоВидимыхЗакладок <= 1 И Объект.Вид = ВидДополнительныйОтчет И Объект.ИспользуетХранилищеВариантов;
		
		Элементы.ВариантыДополнительногоОтчета.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы[?(ВыводитьЗаголовокТаблицы, "Верх", "Нет")];
		Элементы.ВариантыДополнительногоОтчета.Шапка               = НЕ ВыводитьЗаголовокТаблицы;
		Элементы.ВариантыДополнительногоОтчета.ГоризонтальныеЛинии = НЕ ВыводитьЗаголовокТаблицы;
		
	КонецЕсли;
	
	Если КоличествоКоманд > 0 Тогда
		
		ВыводитьЗаголовокТаблицы = КоличествоВидимыхЗакладок <= 1 И НЕ ЭтоГлобальнаяОбработка;
		
		Элементы.ОбъектКоманды.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы[?(ВыводитьЗаголовокТаблицы, "Верх", "Нет")];
		Элементы.ОбъектКоманды.Шапка               = НЕ ВыводитьЗаголовокТаблицы;
		Элементы.ОбъектКоманды.ГоризонтальныеЛинии = НЕ ВыводитьЗаголовокТаблицы;
		
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = ДополнительныеОтчетыИОбработки.ВидВСтроку(Объект.Вид);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставленияРазрешений(Знач ВидРазрешения)
	
	ТаблицаРазрешений = ПолучитьТаблицуРазрешений();
	СтрокаРазрешения = ТаблицаРазрешений.Найти(ВидРазрешения, "ВидРазрешения");
	Если СтрокаРазрешения <> Неопределено Тогда
		ПараметрыРазрешения = СтрокаРазрешения.Параметры.Получить();
		ПредставлениеРазрешений_2_1_3 = ДополнительныеОтчетыИОбработкиВБезопасномРежимеСлужебный.СформироватьПодробноеОписаниеРазрешения(
			ВидРазрешения, ПараметрыРазрешения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокРазрешений()
	
	Если Объект.РежимСовместимостиРазрешений = Перечисления.РежимыСовместимостиРазрешенийДополнительныхОтчетовИОбработок.Версия_2_1_3 Тогда
		ПредставлениеРазрешений_2_1_3 = ДополнительныеОтчетыИОбработкиВБезопасномРежимеСлужебный.СформироватьПредставлениеРазрешений(
			ПолучитьТаблицуРазрешений());
	ИначеЕсли Объект.РежимСовместимостиРазрешений = Перечисления.РежимыСовместимостиРазрешенийДополнительныхОтчетовИОбработок.Версия_2_2_2 Тогда
		ПредставлениеРазрешений_2_2_2 = РаботаВБезопасномРежимеСлужебный.ПредставлениеРазрешенийНаИспользованиеВнешнихРесурсов(
			ПолучитьТаблицыРазрешений());
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокРазделов(Знач Разделы)
	
	ДоступныеРазделы = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	                |	Разделы.Раздел
	                |ПОМЕСТИТЬ Разделы
	                |ИЗ
	                |	&Разделы КАК Разделы
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ
	                |	ЕСТЬNULL(ИдентификаторыОбъектовМетаданных.Ссылка, ЗНАЧЕНИЕ(Справочник.ИдентификаторыОбъектовМетаданных.ПустаяСсылка)) КАК Ссылка,
	                |	ИдентификаторыОбъектовМетаданных.ПометкаУдаления
	                |ИЗ
	                |	Разделы КАК Разделы
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	                |		ПО Разделы.Раздел = ИдентификаторыОбъектовМетаданных.Ссылка
	                |ГДЕ
	                |	ИдентификаторыОбъектовМетаданных.ПометкаУдаления = ЛОЖЬ";

	Запрос.УстановитьПараметр("Разделы", Разделы.Выгрузить(, "Раздел"));
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока РезультатЗапроса.Следующий() Цикл
		ДоступныеРазделы.Добавить(РезультатЗапроса.Ссылка);
	КонецЦикла;
	
	Возврат ДоступныеРазделы;
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Сервер

&НаСервере
Процедура ЗаполнитьКоманды(КомандыСохраненные = Неопределено)
	
	Объект.Команды.Сортировать("Представление");
	
	Для Каждого ЭлементКоманда Из Объект.Команды Цикл
		Если Объект.Вид = ВидДополнительнаяОбработка ИЛИ Объект.Вид = ВидДополнительныйОтчет Тогда
			Найденные = БыстрыйДоступ.НайтиСтроки(Новый Структура("ИдентификаторКоманды", ЭлементКоманда.Идентификатор));
			ЭлементКоманда.БыстрыйДоступПредставление = ПредставлениеБыстрогоДоступаПользователей(
				Найденные.Количество());
		КонецЕсли;
		
		ЭлементКоманда.РегламентноеЗаданиеИспользование = Ложь;
		ЭлементКоманда.РегламентноеЗаданиеРазрешено = Ложь;
		
		Если Объект.Вид = ВидДополнительнаяОбработка
			И (ЭлементКоманда.ВариантЗапуска = Перечисления.СпособыВызоваДополнительныхОбработок.ВызовСерверногоМетода
			ИЛИ ЭлементКоманда.ВариантЗапуска = Перечисления.СпособыВызоваДополнительныхОбработок.СценарийВБезопасномРежиме) Тогда
			
			ЭлементКоманда.РегламентноеЗаданиеРазрешено = Истина;
			
			РегламентноеЗаданиеGUID = ЭлементКоманда.РегламентноеЗаданиеGUID;
			Если КомандыСохраненные <> Неопределено Тогда
				НайденнаяСтрока = КомандыСохраненные.Найти(ЭлементКоманда.Идентификатор, "Идентификатор");
				Если НайденнаяСтрока <> Неопределено Тогда
					РегламентноеЗаданиеGUID = НайденнаяСтрока.РегламентноеЗаданиеGUID;
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(РегламентноеЗаданиеGUID) Тогда
				РегламентноеЗадание = ДополнительныеОтчетыИОбработкиРегламентныеЗадания.НайтиЗадание(РегламентноеЗаданиеGUID);
				
				Если РегламентноеЗадание <> Неопределено Тогда
					
					ПараметрыЗадания = ДополнительныеОтчетыИОбработкиРегламентныеЗадания.ПолучитьПараметрыЗадания(РегламентноеЗадание);
					
					ЭлементКоманда.РегламентноеЗаданиеGUID = РегламентноеЗаданиеGUID;
					ЭлементКоманда.РегламентноеЗаданиеПредставление = Строка(ПараметрыЗадания.Расписание);
					ЭлементКоманда.РегламентноеЗаданиеИспользование = ПараметрыЗадания.Использование;
					ЭлементКоманда.РегламентноеЗаданиеРасписание.Вставить(0, ПараметрыЗадания.Расписание);
					
					Если ЭлементКоманда.РегламентноеЗаданиеПредставление = ПредставлениеПустогоРасписания() Тогда
						ЭлементКоманда.РегламентноеЗаданиеИспользование = Ложь;
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			Если НЕ ЭлементКоманда.РегламентноеЗаданиеИспользование Тогда
				ЭлементКоманда.РегламентноеЗаданиеПредставление = ПредставлениеОтключенногоРасписания();
			КонецЕсли;
		Иначе
			ЭлементКоманда.РегламентноеЗаданиеПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не применимо для команд с вариантом запуска ""%1""'"),
				Строка(ЭлементКоманда.ВариантЗапуска));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВариантыДополнительногоОтчетаЗаполнить()
	ВариантыДополнительногоОтчета.Очистить();
	
	Попытка
		ВнешнийОбъект = ДополнительныеОтчетыИОбработки.ПолучитьОбъектВнешнейОбработки(Объект.Ссылка);
	Исключение
		ТекстОшибки = НСтр("ru = 'Не удалось получить список вариантов отчета из-за ошибки, возникшей при подключении этого отчета:'");
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстОшибки + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
		
		ОтчетМетаданные = ВнешнийОбъект.Метаданные();
		СхемаКДМетаданные = ОтчетМетаданные.ОсновнаяСхемаКомпоновкиДанных;
		Если СхемаКДМетаданные <> Неопределено Тогда
			СхемаКД = ВнешнийОбъект.ПолучитьМакет(СхемаКДМетаданные.Имя);
			Для Каждого ВариантНастроекКД Из СхемаКД.ВариантыНастроек Цикл
				КлючВарианта = ВариантНастроекКД.Имя;
				ВариантСсылка = МодульВариантыОтчетов.ПолучитьСсылку(Объект.Ссылка, КлючВарианта);
				Если ВариантСсылка <> Неопределено Тогда
					Вариант = ВариантыДополнительногоОтчета.Добавить();
					Вариант.КлючВарианта = КлючВарианта;
					Вариант.Наименование = ВариантНастроекКД.Представление;
					Вариант.Пользовательский = Ложь;
					Вариант.ИндексКартинки = 5;
					Вариант.Ссылка = ВариантСсылка;
				КонецЕсли;
			КонецЦикла;
		Иначе
			КлючВарианта = "";
			ВариантСсылка = МодульВариантыОтчетов.ПолучитьСсылку(Объект.Ссылка, КлючВарианта);
			Если ВариантСсылка <> Неопределено Тогда
				Вариант = ВариантыДополнительногоОтчета.Добавить();
				Вариант.КлючВарианта = КлючВарианта;
				Вариант.Наименование = ОтчетМетаданные.Представление();
				Вариант.Пользовательский = Ложь;
				Вариант.ИндексКартинки = 5;
				Вариант.Ссылка = ВариантСсылка;
			КонецЕсли;
		КонецЕсли;
	Иначе
		МодульВариантыОтчетов = Неопределено;
	КонецЕсли;
	
	Если Объект.ИспользуетХранилищеВариантов Тогда
		Хранилище = ХранилищаНастроек["ХранилищеВариантовОтчетов"];
		КлючОбъекта = Объект.Ссылка;
	Иначе
		Хранилище = ХранилищеВариантовОтчетов;
		КлючОбъекта = "ВнешнийОтчет." + Объект.ИмяОбъекта;
	КонецЕсли;
	
	СписокНастроек = Хранилище.ПолучитьСписок(КлючОбъекта);
	
	Для Каждого ЭлементСписка Из СписокНастроек Цикл
		Вариант = ВариантыДополнительногоОтчета.Добавить();
		Вариант.КлючВарианта = ЭлементСписка.Значение;
		Вариант.Наименование = ЭлементСписка.Представление;
		Вариант.Пользовательский = Истина;
		Вариант.ИндексКартинки = 3;
		Если МодульВариантыОтчетов <> Неопределено Тогда
			Вариант.Ссылка = МодульВариантыОтчетов.ПолучитьСсылку(Объект.Ссылка, Вариант.КлючВарианта);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ПолучитьТаблицуРазрешений()
	
	Возврат ПолучитьИзВременногоХранилища(АдресРазрешений);
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицыРазрешений()
	
	Менеджеры = Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсов.МенеджерыРегистровРазрешений();
	Результат = Обработки.НастройкаРазрешенийНаИспользованиеВнешнихРесурсов.ТаблицыРазрешений();
	
	ИсходнаяТаблица = ПолучитьТаблицуРазрешений();
	Для Каждого СтрокаИсходнойТаблицы Из ИсходнаяТаблица Цикл
		
		Набор = Неопределено;
		Для Каждого Менеджер Из Менеджеры Цикл
			Если Менеджер.ТипXDTOПредставленияРазрешений().Имя = СтрокаИсходнойТаблицы.ВидРазрешения Тогда
				XDTOПредставления = Новый Массив();
				XDTOПредставления.Добавить(СтрокаИсходнойТаблицы.Параметры.Получить());
				Набор = Менеджер.НаборЗаписейИзXDTOПредставления(XDTOПредставления, Истина, Объект.Ссылка, Ложь);
			КонецЕсли;
		КонецЦикла;
		
		Если Набор = Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Неизвестный вид разрешения!'");
		КонецЕсли;
		
		ТаблицаРезультата = Результат[СтрокаИсходнойТаблицы.ВидРазрешения];
		
		Для Каждого СтрокаНабора Из Набор Цикл
			
			РазрешениеРезультата = ТаблицаРезультата.Добавить();
			ЗаполнитьЗначенияСвойств(РазрешениеРезультата, СтрокаНабора);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
