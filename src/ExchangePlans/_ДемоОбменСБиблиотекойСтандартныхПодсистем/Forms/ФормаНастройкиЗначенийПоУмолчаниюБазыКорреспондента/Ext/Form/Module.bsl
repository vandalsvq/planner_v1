
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДополнительныеДанные = Неопределено;
	
	ОбменДаннымиСервер.ФормаНастройкиЗначенийПоУмолчаниюБазыКорреспондентаПриСозданииНаСервере(
		ЭтотОбъект,
		Метаданные.ПланыОбмена._ДемоОбменСБиблиотекойСтандартныхПодсистем.Имя,
		ДополнительныеДанные);
	
	ИспользоватьУчетНДС = Ложь;
	
	Если ПараметрыВнешнегоСоединения.ТипСоединения = "ВнешнееСоединение" Тогда
		
		Подключение = ОбменДаннымиПовтИсп.УстановитьВнешнееСоединениеСБазой(ПараметрыВнешнегоСоединения);
		СтрокаСообщенияОбОшибке = Подключение.ПодробноеОписаниеОшибки;
		ВнешнееСоединение       = Подключение.Соединение;
		
		Если ВнешнееСоединение = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщенияОбОшибке,,,, Отказ);
			Возврат;
		КонецЕсли;
		
		ИспользоватьУчетНДС = ВнешнееСоединение.ПолучитьФункциональнуюОпцию("_ДемоУчитыватьНДС");
		
	ИначеЕсли ПараметрыВнешнегоСоединения.ТипСоединения = "ВебСервис" Тогда
		
		СтрокаСообщенияОбОшибке = "";
		WSПрокси = ОбменДаннымиСервер.ПолучитьWSПрокси(ПараметрыВнешнегоСоединения, СтрокаСообщенияОбОшибке);
		
		Если WSПрокси = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщенияОбОшибке,,,, Отказ);
			Возврат;
		КонецЕсли;
		
		ИспользоватьУчетНДС = WSПрокси.GetFunctionalOption("_ДемоУчитыватьНДС");
		
	ИначеЕсли ПараметрыВнешнегоСоединения.ТипСоединения = "ВременноеХранилище" Тогда
		
		ИспользоватьУчетНДС = ДополнительныеДанные.ИспользоватьУчетНДС;
		
	КонецЕсли;
	
	Элементы.СтавкаНДСПоУмолчанию.Видимость = ИспользоватьУчетНДС;
	Элементы.СтавкаНДС.Видимость = ИспользоватьУчетНДС;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ОбменДаннымиСервер.ОпределитьПроверяемыеРеквизитыСУчетомНастроекВидимостиПолейФормы(ПроверяемыеРеквизиты, Элементы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбменДаннымиКлиент.ОбработчикВыбораЭлементовБазыКорреспондентаОбработкаВыбора(ЭтотОбъект, ВыбранноеЗначение);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ОчиститьСообщения();
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(НоменклатураПоУмолчанию) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо выбрать номенклатуру.'"),,, 
			"НоменклатураПоУмолчанию", Отказ);
	КонецЕсли;
	Если ИспользоватьУчетНДС И Не ЗначениеЗаполнено(СтавкаНДСПоУмолчанию) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Необходимо задать значение ставки НДС.'"),,, 
			"СтавкаНДСПоУмолчанию", Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	ОбменДаннымиКлиент.ФормаНастройкиЗначенийПоУмолчаниюКомандаЗакрытьФорму(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыВыбора = Новый Структура("ВыборГруппИЭлементов", Элемент.ВыборГруппИЭлементов);
	
	ОбменДаннымиКлиент.ОбработчикВыбораЭлементовБазыКорреспондентаНачалоВыбора("НоменклатураПоУмолчанию", "Справочник._ДемоНоменклатура", 
		ЭтотОбъект, СтандартнаяОбработка, ПараметрыВнешнегоСоединения, ПараметрыВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНДСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыВыбора = Новый Структура("ВыборГруппИЭлементов", Элемент.ВыборГруппИЭлементов);
	
	ОбменДаннымиКлиент.ОбработчикВыбораЭлементовБазыКорреспондентаНачалоВыбора("СтавкаНДСПоУмолчанию", "Справочник._ДемоСтавкиНДС", 
		ЭтотОбъект, СтандартнаяОбработка, ПараметрыВнешнегоСоединения, ПараметрыВыбора);
	
КонецПроцедуры

#КонецОбласти
