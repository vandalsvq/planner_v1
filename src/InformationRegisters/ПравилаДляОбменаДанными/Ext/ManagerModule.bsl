#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Загружает правила в регистр
//
// Параметры:
//	Отказ - Булево - Отказ от записи в регистр
//	Запись - РегистрСведенийЗапись.ПравилаДляОбменаДанными - запись регистра, в которую будут помещены данные
//	АдресВременногоХранилища - Строка - Адрес временного хранилища, из которого будут загружены XML-правила 
//	ИмяФайлаПравил - Строка - Имя файла из которого были загружены файлы(оно также фиксируется в регистре)	
//	ДвоичныеДанные - ДвоичныеДанные - данные, в которые сохраняется XML-файл (в том числе и распакованный из ZIP-архива)
//	ЭтоАрхив - Булево - Признак того, что правила загружаются из ZIP-архива, а не из XML-файла
//
Процедура ЗагрузитьПравила(Отказ, Запись, АдресВременногоХранилища = "", ИмяФайлаПравил = "", ЭтоАрхив = Ложь) Экспорт
	
	// проверка заполнения обязательныех полей записи
	ВыполнитьПроверкуЗаполненияПолей(Отказ, Запись);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПравилаКонвертации = (Запись.ВидПравил = Перечисления.ВидыПравилДляОбменаДанными.ПравилаКонвертацииОбъектов);
	
	// Получаем двоичные данные правил из файла или макета конфигурации
	Если Запись.ИсточникПравил = Перечисления.ИсточникиПравилДляОбменаДанными.МакетКонфигурации Тогда
		
		ДвоичныеДанные = ПолучитьДвоичныеДанныеИзМакетаКонфигурации(Отказ, Запись.ИмяПланаОбмена, Запись.ИмяМакетаПравил);
		
		Если ЭтоПравилаКонвертации Тогда
			
			Если ПустаяСтрока(Запись.ИмяМакетаПравилКорреспондента) Тогда
				Запись.ИмяМакетаПравилКорреспондента = Запись.ИмяМакетаПравил + "Корреспондента";
			КонецЕсли;
			ДвоичныеДанныеКорреспондента = ПолучитьДвоичныеДанныеИзМакетаКонфигурации(Отказ, Запись.ИмяПланаОбмена, Запись.ИмяМакетаПравилКорреспондента);
			
		КонецЕсли;
		
	Иначе
		
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		
	КонецЕсли;
	
	// Если это архив, тогда разархивируем его и заново заложим в двоичные данные для дальнейшей работы.
	Если ЭтоАрхив Тогда
		
		// Получаем файл архива из двоичных данных
		ИмяВременногоАрхива = ПолучитьИмяВременногоФайла("zip");
		ДвоичныеДанные.Записать(ИмяВременногоАрхива);
		
		// Распаковываем архив
		ИмяВременнойПапки = ПолучитьИмяВременногоФайла("");
		Если ОбменДаннымиСервер.РаспаковатьZipФайл(ИмяВременногоАрхива, ИмяВременнойПапки) Тогда
			
			СписокРаспакованныхФайлов = НайтиФайлы(ИмяВременнойПапки, "*.*", Истина);
			
			// В архиве не было ни одного файла - отказываемся от загрузки
			Если СписокРаспакованныхФайлов.Количество() = 0 Тогда
				НСтрока = НСтр("ru = 'При распаковке архива не найден файл с правилами.'");
				ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
			КонецЕсли;
			
			Если ЭтоПравилаКонвертации Тогда
				
				// Закладываем полученный файл правил обратно в двоичные данные
				Если СписокРаспакованныхФайлов.Количество() = 2 Тогда
					
					Если СписокРаспакованныхФайлов[0].Имя = "ExchangeRules.xml" 
						И СписокРаспакованныхФайлов[1].Имя ="CorrespondentExchangeRules.xml" Тогда
						
						ДвоичныеДанные = Новый ДвоичныеДанные(СписокРаспакованныхФайлов[0].ПолноеИмя);
						ДвоичныеДанныеКорреспондента = Новый ДвоичныеДанные(СписокРаспакованныхФайлов[1].ПолноеИмя);
						
					ИначеЕсли СписокРаспакованныхФайлов[1].Имя = "ExchangeRules.xml" 
						И СписокРаспакованныхФайлов[0].Имя ="CorrespondentExchangeRules.xml" Тогда
						
						ДвоичныеДанные = Новый ДвоичныеДанные(СписокРаспакованныхФайлов[1].ПолноеИмя);
						ДвоичныеДанныеКорреспондента = Новый ДвоичныеДанные(СписокРаспакованныхФайлов[0].ПолноеИмя);
						
					Иначе
						
						НСтрока = НСтр("ru = 'Имена файлов в архиве не соответствуют ожидаемым. Ожидаются файлы:
							|ExchangeRules.xml - правила конвертации для текущей программы;
							|CorrespondentExchangeRules.xml - правила конвертации для программы-корреспондента.'");
						ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
						
					КонецЕсли;
					
				// Старый формат
				ИначеЕсли СписокРаспакованныхФайлов.Количество() = 1 Тогда
					НСтрока = НСтр("ru = 'В архиве найден один файл правил конвертации. Ожидаемое количество файлов в архиве - два. Ожидаются файлы:
						|ExchangeRules.xml - правила конвертации для текущей программы;
						|CorrespondentExchangeRules.xml - правила конвертации для программы-корреспондента.'");
					ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
				// В архиве оказалось несколько файлов, хотя должен быть один - отказываеся от загрузки
				ИначеЕсли СписокРаспакованныхФайлов.Количество() > 1 Тогда
					НСтрока = НСтр("ru = 'При распаковке архива найдено несколько файлов. Должен быть только один файл с правилами.'");
					ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
				КонецЕсли;
				
			Иначе
				
				// Закладываем полученный файл правил обратно в двоичные данные
				Если СписокРаспакованныхФайлов.Количество() = 1 Тогда
					ДвоичныеДанные = Новый ДвоичныеДанные(СписокРаспакованныхФайлов[0].ПолноеИмя);
					
				// В архиве оказалось несколько файлов, хотя должен быть один - отказываеся от загрузки
				ИначеЕсли СписокРаспакованныхФайлов.Количество() > 1 Тогда
					НСтрока = НСтр("ru = 'При распаковке архива найдено несколько файлов. Должен быть только один файл с правилами.'");
					ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе // Если не удалось распаковать файл - отказываемся от загрузки
			НСтрока = НСтр("ru = 'Не удалось распаковать архив с правилами.'");
			ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
		КонецЕсли;
		
		// Удаляем временный архив и временную папку, в которую был распакован архив
		УдалитьВременныйФайл(ИмяВременнойПапки);
		УдалитьВременныйФайл(ИмяВременногоАрхива);
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// получаем имя временного файла в локальной ФС на сервере
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	
	// получаем файл правил для зачитки
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	Если ЭтоПравилаКонвертации Тогда
		
		// зачитываем правила конвертации
		КонвертацияОбъектовИнформационныхБаз = Обработки.КонвертацияОбъектовИнформационныхБаз.Создать();
		
		// свойства обработки
		КонвертацияОбъектовИнформационныхБаз.РежимОбмена = "Выгрузка";
		КонвертацияОбъектовИнформационныхБаз.ИмяПланаОбменаВРО = Запись.ИмяПланаОбмена;
		КонвертацияОбъектовИнформационныхБаз.КлючСообщенияЖурналаРегистрации = ОбменДаннымиСервер.СобытиеЖурналаРегистрацииЗагрузкаПравилДляОбменаДанными();
		
		ОбменДаннымиСервер.УстановитьНастройкиОтладкиВыгрузкиДляПравилОбмена(КонвертацияОбъектовИнформационныхБаз, Запись.ИмяПланаОбмена, Запись.РежимОтладки);
		
		// методы обработки
		ПравилаЗачитанные = КонвертацияОбъектовИнформационныхБаз.ПолучитьСтруктуруПравилОбмена(ИмяВременногоФайла);
		
		РежимСовместимости = Ложь;
		ИнформацияОПравилах = КонвертацияОбъектовИнформационныхБаз.ПолучитьИнформациюОПравилах(Ложь, РежимСовместимости);
		
		Если КонвертацияОбъектовИнформационныхБаз.ФлагОшибки() Тогда
			Отказ = Истина;
		КонецЕсли;
		
		// получаем имя временного файла в локальной ФС на сервере
		ИмяВременногоФайлаКорреспондента = ПолучитьИмяВременногоФайла("xml");
		// получаем файл правил для зачитки
		ДвоичныеДанныеКорреспондента.Записать(ИмяВременногоФайлаКорреспондента);
		
		// зачитываем правила конвертации
		КонвертацияОбъектовИнформационныхБаз = Обработки.КонвертацияОбъектовИнформационныхБаз.Создать();
		
		// свойства обработки
		КонвертацияОбъектовИнформационныхБаз.РежимОбмена = "Загрузка";
		КонвертацияОбъектовИнформационныхБаз.ИмяПланаОбменаВРО = Запись.ИмяПланаОбмена;
		КонвертацияОбъектовИнформационныхБаз.КлючСообщенияЖурналаРегистрации = ОбменДаннымиСервер.СобытиеЖурналаРегистрацииЗагрузкаПравилДляОбменаДанными();
		
		// методы обработки
		ПравилаЗачитанныеКорреспондента = КонвертацияОбъектовИнформационныхБаз.ПолучитьСтруктуруПравилОбмена(ИмяВременногоФайлаКорреспондента);
		
		РежимСовместимостиКорреспондента = Ложь;
		ИнформацияОПравилахКорреспондента = КонвертацияОбъектовИнформационныхБаз.ПолучитьИнформациюОПравилах(
			Истина, РежимСовместимостиКорреспондента);
		
		Если КонвертацияОбъектовИнформационныхБаз.ФлагОшибки() Тогда
			Отказ = Истина;
		КонецЕсли;
		
 		ИнформацияОПравилах = ИнформацияОПравилах + Символы.ПС + Символы.ПС + ИнформацияОПравилахКорреспондента;
		
	Иначе // ПравилаРегистрацииОбъектов
		
		// зачитываем правила регистрации
		ЗагрузкаПравилРегистрации = Обработки.ЗагрузкаПравилРегистрацииОбъектов.Создать();
		
		// свойства обработки
		ЗагрузкаПравилРегистрации.ИмяПланаОбменаДляЗагрузки = Запись.ИмяПланаОбмена;
		
		// методы обработки
		ЗагрузкаПравилРегистрации.ЗагрузитьПравила(ИмяВременногоФайла);
		
		ПравилаЗачитанные = ЗагрузкаПравилРегистрации.ПравилаРегистрацииОбъектов;
		
		ИнформацияОПравилах = ЗагрузкаПравилРегистрации.ПолучитьИнформациюОПравилах();
		
		Если ЗагрузкаПравилРегистрации.ФлагОшибки Тогда
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	// удаляем временный файл правил
	УдалитьВременныйФайл(ИмяВременногоФайла);
	
	Если Не Отказ Тогда
		
		Запись.ПравилаXML          = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());
		Запись.ПравилаЗачитанные   = Новый ХранилищеЗначения(ПравилаЗачитанные);
		
		Если ЭтоПравилаКонвертации Тогда
			
			Запись.ПравилаXMLКорреспондента = Новый ХранилищеЗначения(ДвоичныеДанныеКорреспондента, Новый СжатиеДанных());
			Запись.ПравилаЗачитанныеКорреспондента = Новый ХранилищеЗначения(ПравилаЗачитанныеКорреспондента);
			Если РежимСовместимости <> РежимСовместимостиКорреспондента Тогда
				ВызватьИсключение Нстр("ru = 'Резличаются режимы совместимости правил обмена текущей конфигурации и конфигурации-корреспондента.'");
			Иначе
				Запись.РежимСовместимости = РежимСовместимости;
			КонецЕсли;
			
		КонецЕсли;
		
		Запись.ИнформацияОПравилах = ИнформацияОПравилах;
		Запись.ИмяФайлаПравил = ИмяФайлаПравил;
		Запись.ПравилаЗагружены = Истина;
		Запись.ИмяПланаОбменаИзПравил = Запись.ИмяПланаОбмена;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗагрузитьКомплектПравил(Отказ, ДанныеДляЗаписи, ОписаниеОшибки, АдресВременногоХранилища = "", ИмяФайлаПравил = "") Экспорт
	
	ЗаписьПравилКонвертации = ДанныеДляЗаписи.ЗаписьПравилКонвертации;
	ЗаписьПравилРегистрации = ДанныеДляЗаписи.ЗаписьПравилРегистрации;
	
	// Получаем двоичные данные правил из файла или макета конфигурации
	Если ЗаписьПравилКонвертации.ИсточникПравил = Перечисления.ИсточникиПравилДляОбменаДанными.МакетКонфигурации Тогда
		
		ДвоичныеДанные               = ПолучитьДвоичныеДанныеИзМакетаКонфигурации(Отказ, ЗаписьПравилКонвертации.ИмяПланаОбмена, ЗаписьПравилКонвертации.ИмяМакетаПравил);
		ДвоичныеДанныеКорреспондента = ПолучитьДвоичныеДанныеИзМакетаКонфигурации(Отказ, ЗаписьПравилКонвертации.ИмяПланаОбмена, ЗаписьПравилКонвертации.ИмяМакетаПравилКорреспондента);
		ДвоичныеДанныеРегистрации    = ПолучитьДвоичныеДанныеИзМакетаКонфигурации(Отказ, ЗаписьПравилРегистрации.ИмяПланаОбмена, ЗаписьПравилРегистрации.ИмяМакетаПравил);
		
	Иначе
		
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
		
	КонецЕсли;
	
	Если ЗаписьПравилКонвертации.ИсточникПравил = Перечисления.ИсточникиПравилДляОбменаДанными.Файл Тогда
		
		// Получаем файл архива из двоичных данных
		ИмяВременногоАрхива = ПолучитьИмяВременногоФайла("zip");
		ДвоичныеДанные.Записать(ИмяВременногоАрхива);
		
		// Распаковываем архив
		ИмяВременнойПапки = ПолучитьИмяВременногоФайла("");
		Если ОбменДаннымиСервер.РаспаковатьZipФайл(ИмяВременногоАрхива, ИмяВременнойПапки) Тогда
			
			СписокРаспакованныхФайлов = НайтиФайлы(ИмяВременнойПапки, "*.*", Истина);
			
			// В архиве не было ни одного файла - отказываемся от загрузки
			Если СписокРаспакованныхФайлов.Количество() = 0 Тогда
				НСтрока = НСтр("ru = 'При распаковке архива не найден файл с правилами.'");
				ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
			КонецЕсли;
			
			// В архиве количество файлов не соответсвует ожидаемому - отказываемся от загрузки
			Если СписокРаспакованныхФайлов.Количество() <> 3 Тогда
				НСтрока = НСтр("ru = 'Не верный формат комплекта правил. Ожидаемое количество файлов в архиве - три. Ожидаются файлы:
					|ExchangeRules.xml - правила конвертации для текущей программы;
					|CorrespondentExchangeRules.xml - правила конвертации для программы-корреспондента;
					|RegistrationRules.xml - правила регистрации для текущей программы.'");
				ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
			КонецЕсли;
				
			// Закладываем полученный файл правил обратно в двоичные данные
			Для Каждого ПолученныйФайл Из СписокРаспакованныхФайлов Цикл
				
				Если ПолученныйФайл.Имя = "ExchangeRules.xml" Тогда
					ДвоичныеДанные = Новый ДвоичныеДанные(ПолученныйФайл.ПолноеИмя);
				ИначеЕсли ПолученныйФайл.Имя ="CorrespondentExchangeRules.xml" Тогда
					ДвоичныеДанныеКорреспондента = Новый ДвоичныеДанные(ПолученныйФайл.ПолноеИмя);
				ИначеЕсли ПолученныйФайл.Имя ="RegistrationRules.xml" Тогда
					ДвоичныеДанныеРегистрации = Новый ДвоичныеДанные(ПолученныйФайл.ПолноеИмя);
				Иначе
					НСтрока = НСтр("ru = 'Имена файлов в архиве не соответствуют ожидаемым. Ожидаются файлы:
						|ExchangeRules.xml - правила конвертации для текущей программы;
					|CorrespondentExchangeRules.xml - правила конвертации для программы-корреспондента;
					|RegistrationRules.xml - правила регистрации для текущей программы.'");
					ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		Иначе 
			// Если не удалось распаковать файл - отказываемся от загрузки
			НСтрока = НСтр("ru = 'Не удалось распаковать архив с правилами.'");
			ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
		КонецЕсли;
		
		// Удаляем временный архив и временную папку, в которую был распакован архив
		УдалитьВременныйФайл(ИмяВременнойПапки);
		УдалитьВременныйФайл(ИмяВременногоАрхива);
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияОПравилахКонвертации = "[ИнформацияОПравилахИсточника]
		|
		|[ИнформацияОПравилахКорреспондента]";
		
	// получаем имя временного файла конвертации в локальной ФС на сервере
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	
	// получаем файл правил для зачитки
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	// зачитываем правила конвертации
	КонвертацияОбъектовИнформационныхБаз = Обработки.КонвертацияОбъектовИнформационныхБаз.Создать();
	
	// свойства обработки
	КонвертацияОбъектовИнформационныхБаз.РежимОбмена = "Выгрузка";
	КонвертацияОбъектовИнформационныхБаз.ИмяПланаОбменаВРО = ЗаписьПравилКонвертации.ИмяПланаОбмена;
	КонвертацияОбъектовИнформационныхБаз.КлючСообщенияЖурналаРегистрации = ОбменДаннымиСервер.СобытиеЖурналаРегистрацииЗагрузкаПравилДляОбменаДанными();
	
	ОбменДаннымиСервер.УстановитьНастройкиОтладкиВыгрузкиДляПравилОбмена(КонвертацияОбъектовИнформационныхБаз, ЗаписьПравилКонвертации.ИмяПланаОбмена, ЗаписьПравилКонвертации.РежимОтладки);
	
	// методы обработки
	Если ЗаписьПравилКонвертации.ИсточникПравил = Перечисления.ИсточникиПравилДляОбменаДанными.Файл И ОписаниеОшибки = Неопределено
		И Не ПравилаКонвертацииСовместимыСТекущейВерсией(ЗаписьПравилКонвертации.ИмяПланаОбмена, ОписаниеОшибки, ИнформацияОПравилахИзФайла(ИмяВременногоФайла)) Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	ПравилаЗачитанные = КонвертацияОбъектовИнформационныхБаз.ПолучитьСтруктуруПравилОбмена(ИмяВременногоФайла);
	
	РежимСовместимости = Ложь;
	ИнформацияОПравилахИсточника = КонвертацияОбъектовИнформационныхБаз.ПолучитьИнформациюОПравилах(Ложь, РежимСовместимости);
	
	Если КонвертацияОбъектовИнформационныхБаз.ФлагОшибки() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// получаем имя временного файла конвертации корреспондента в локальной ФС на сервере
	ИмяВременногоФайлаКорреспондента = ПолучитьИмяВременногоФайла("xml");
	// получаем файл правил для зачитки
	ДвоичныеДанныеКорреспондента.Записать(ИмяВременногоФайлаКорреспондента);
	
	// зачитываем правила конвертации
	КонвертацияОбъектовИнформационныхБаз = Обработки.КонвертацияОбъектовИнформационныхБаз.Создать();
	
	// свойства обработки
	КонвертацияОбъектовИнформационныхБаз.РежимОбмена = "Загрузка";
	КонвертацияОбъектовИнформационныхБаз.ИмяПланаОбменаВРО = ЗаписьПравилКонвертации.ИмяПланаОбмена;
	КонвертацияОбъектовИнформационныхБаз.КлючСообщенияЖурналаРегистрации = ОбменДаннымиСервер.СобытиеЖурналаРегистрацииЗагрузкаПравилДляОбменаДанными();
	
	// методы обработки
	ПравилаЗачитанныеКорреспондента = КонвертацияОбъектовИнформационныхБаз.ПолучитьСтруктуруПравилОбмена(ИмяВременногоФайлаКорреспондента);
	
	РежимСовместимостиКорреспондента = Ложь;
	ИнформацияОПравилахКорреспондента = КонвертацияОбъектовИнформационныхБаз.ПолучитьИнформациюОПравилах(
		Истина, РежимСовместимостиКорреспондента);
	
	Если КонвертацияОбъектовИнформационныхБаз.ФлагОшибки() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ИнформацияОПравилахКонвертации = СтрЗаменить(ИнформацияОПравилахКонвертации, "[ИнформацияОПравилахИсточника]", ИнформацияОПравилахИсточника);
	ИнформацияОПравилахКонвертации = СтрЗаменить(ИнформацияОПравилахКонвертации, "[ИнформацияОПравилахКорреспондента]", ИнформацияОПравилахКорреспондента);
	
	// получаем имя временного файла регистрации в локальной ФС на сервере
	ИмяВременногоФайлаРегистрации = ПолучитьИмяВременногоФайла("xml");
	// получаем файл правил для зачитки
	ДвоичныеДанныеРегистрации.Записать(ИмяВременногоФайлаРегистрации);

	
	// зачитываем правила регистрации
	ЗагрузкаПравилРегистрации = Обработки.ЗагрузкаПравилРегистрацииОбъектов.Создать();
	
	// свойства обработки
	ЗагрузкаПравилРегистрации.ИмяПланаОбменаДляЗагрузки = ЗаписьПравилРегистрации.ИмяПланаОбмена;
	
	// методы обработки
	ЗагрузкаПравилРегистрации.ЗагрузитьПравила(ИмяВременногоФайлаРегистрации);
	ПравилаРегистрацииЗачитанные   = ЗагрузкаПравилРегистрации.ПравилаРегистрацииОбъектов;
	ИнформацияОПравилахРегистрации = ЗагрузкаПравилРегистрации.ПолучитьИнформациюОПравилах();
	
	Если ЗагрузкаПравилРегистрации.ФлагОшибки Тогда
		Отказ = Истина;
	КонецЕсли;
	
	// удаляем временные файлы правил
	УдалитьВременныйФайл(ИмяВременногоФайла);
	УдалитьВременныйФайл(ИмяВременногоФайлаКорреспондента);
	УдалитьВременныйФайл(ИмяВременногоФайлаРегистрации);
	
	Если Не Отказ Тогда
		
		//Сделаем запись правил конвертации 
		ЗаписьПравилКонвертации.ПравилаXML                      = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());
		ЗаписьПравилКонвертации.ПравилаЗачитанные               = Новый ХранилищеЗначения(ПравилаЗачитанные);
		ЗаписьПравилКонвертации.ПравилаXMLКорреспондента        = Новый ХранилищеЗначения(ДвоичныеДанныеКорреспондента, Новый СжатиеДанных());
		ЗаписьПравилКонвертации.ПравилаЗачитанныеКорреспондента = Новый ХранилищеЗначения(ПравилаЗачитанныеКорреспондента);
		ЗаписьПравилКонвертации.ИнформацияОПравилах             = ИнформацияОПравилахКонвертации;
		ЗаписьПравилКонвертации.ИмяФайлаПравил                  = ИмяФайлаПравил;
		ЗаписьПравилКонвертации.ПравилаЗагружены                = Истина;
		ЗаписьПравилКонвертации.ИмяПланаОбменаИзПравил          = ЗаписьПравилКонвертации.ИмяПланаОбмена;
		
		Если РежимСовместимости <> РежимСовместимостиКорреспондента Тогда
			ВызватьИсключение Нстр("ru = 'Резличаются режимы совместимости правил обмена текущей конфигурации и конфигурации-корреспондента.'");
		Иначе
			ЗаписьПравилКонвертации.РежимСовместимости = РежимСовместимости;
		КонецЕсли;
		
		//Сделаем запись правил регистрации
		ЗаписьПравилРегистрации.ПравилаXML             = Новый ХранилищеЗначения(ДвоичныеДанныеРегистрации, Новый СжатиеДанных());
		ЗаписьПравилРегистрации.ПравилаЗачитанные      = Новый ХранилищеЗначения(ПравилаРегистрацииЗачитанные);
		ЗаписьПравилРегистрации.ИнформацияОПравилах    = ИнформацияОПравилахРегистрации;
		ЗаписьПравилРегистрации.ИмяФайлаПравил         = ИмяФайлаПравил;
		ЗаписьПравилРегистрации.ПравилаЗагружены       = Истина;
		ЗаписьПравилРегистрации.ИмяПланаОбменаИзПравил = ЗаписьПравилРегистрации.ИмяПланаОбмена;
		
	КонецЕсли;
	
КонецПроцедуры

// Получает зачитанные правила конвертации объектов из ИБ для плана обмена
//
// Параметры:
//  ИмяПланаОбмена - Строка - имя плана обмена как объекта метаданных
// 
// Возвращаемое значение:
//  ПравилаЗачитанные - ХранилищеЗначения - зачитанные правила конвертации объектов
//  Неопределено - Если правила конвертации не были загружены в базу для плана обмена
//
Функция ПолучитьЗачитанныеПравилаКонвертацииОбъектов(Знач ИмяПланаОбмена, ПолучатьПравилаКорреспондента = Ложь) Экспорт
	
	// возвращаемое значение функции
	ПравилаЗачитанные = Неопределено;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПравилаДляОбменаДанными.%1 КАК ПравилаЗачитанные
	|ИЗ
	|	РегистрСведений.ПравилаДляОбменаДанными КАК ПравилаДляОбменаДанными
	|ГДЕ
	|	  ПравилаДляОбменаДанными.ИмяПланаОбмена = &ИмяПланаОбмена
	|	И ПравилаДляОбменаДанными.ВидПравил      = ЗНАЧЕНИЕ(Перечисление.ВидыПравилДляОбменаДанными.ПравилаКонвертацииОбъектов)
	|	И ПравилаДляОбменаДанными.ПравилаЗагружены
	|";
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗапроса,
		?(ПолучатьПравилаКорреспондента, "ПравилаЗачитанныеКорреспондента", "ПравилаЗачитанные"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ИмяПланаОбмена", ИмяПланаОбмена);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ПравилаЗачитанные = Выборка.ПравилаЗачитанные;
		
	КонецЕсли;
	
	Возврат ПравилаЗачитанные;
	
КонецФункции

Функция ИспользуютсяПравилаИзФайла(ИмяПланаОбмена, ПодробныйРезультат = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПравилаДляОбменаДанными.ИмяПланаОбмена КАК ИмяПланаОбмена,
	|	ПравилаДляОбменаДанными.ВидПравил КАК ВидПравил
	|ИЗ
	|	РегистрСведений.ПравилаДляОбменаДанными КАК ПравилаДляОбменаДанными
	|ГДЕ
	|	ПравилаДляОбменаДанными.ИсточникПравил = ЗНАЧЕНИЕ(Перечисление.ИсточникиПравилДляОбменаДанными.Файл)
	|	И ПравилаДляОбменаДанными.ПравилаЗагружены
	|	И ПравилаДляОбменаДанными.ИмяПланаОбмена = &ИмяПланаОбмена";
	
	Запрос.УстановитьПараметр("ИмяПланаОбмена", ИмяПланаОбмена);
	Результат = Запрос.Выполнить();
	
	Если ПодробныйРезультат Тогда
		
		ПравилаИзФайла = Новый Структура("ПравилаРегистрации, ПравилаКонвертации", Ложь, Ложь);
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.ВидПравил = Перечисления.ВидыПравилДляОбменаДанными.ПравилаКонвертацииОбъектов Тогда
				ПравилаИзФайла.ПравилаКонвертации = Истина;
			ИначеЕсли Выборка.ВидПравил = Перечисления.ВидыПравилДляОбменаДанными.ПравилаРегистрацииОбъектов Тогда
				ПравилаИзФайла.ПравилаРегистрации = Истина;
			КонецЕсли;
		КонецЦикла;
		
		Возврат ПравилаИзФайла;
		
	Иначе
		Возврат Не Результат.Пустой();
	КонецЕсли;
	
КонецФункции

Процедура ЗагрузитьИнформациюОПравилах(Отказ, АдресВременногоХранилища, СтрокаИнформацииОПравилах) Экспорт
	
	Перем ВидПравил;
	
	СтрокаИнформацииОПравилах = "";
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	// получаем имя временного файла в локальной ФС на сервере
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	
	// получаем файл правил для зачитки
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ОпределитьВидПравилДляОбменаДанными(ВидПравил, ИмяВременногоФайла, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидПравил = Перечисления.ВидыПравилДляОбменаДанными.ПравилаКонвертацииОбъектов Тогда
		
		// зачитываем правила конвертации
		КонвертацияОбъектовИнформационныхБаз = Обработки.КонвертацияОбъектовИнформационныхБаз.Создать();
		
		КонвертацияОбъектовИнформационныхБаз.ЗагрузитьПравилаОбмена(ИмяВременногоФайла, "XMLФайл",, Истина);
		
		Если КонвертацияОбъектовИнформационныхБаз.ФлагОшибки() Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если Не Отказ Тогда
			СтрокаИнформацииОПравилах = КонвертацияОбъектовИнформационныхБаз.ПолучитьИнформациюОПравилах();
		КонецЕсли;
		
	Иначе // ПравилаРегистрацииОбъектов
		
		// зачитываем правила регистрации
		ЗагрузкаПравилРегистрации = Обработки.ЗагрузкаПравилРегистрацииОбъектов.Создать();
		
		ЗагрузкаПравилРегистрации.ЗагрузитьПравила(ИмяВременногоФайла, Истина);
		
		Если ЗагрузкаПравилРегистрации.ФлагОшибки Тогда
			Отказ = Истина;
		КонецЕсли;
		
		Если Не Отказ Тогда
			СтрокаИнформацииОПравилах = ЗагрузкаПравилРегистрации.ПолучитьИнформациюОПравилах();
		КонецЕсли;
		
	КонецЕсли;
	
	// удаляем временный файл правил
	УдалитьВременныйФайл(ИмяВременногоФайла);
	
КонецПроцедуры

Функция ПолучитьДвоичныеДанныеИзМакетаКонфигурации(Отказ, ИмяПланаОбмена, ИмяМакета)
	
	// получаем имя временного файла в локальной ФС на сервере
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	
	ПланОбменаМенеджер = ОбменДаннымиПовтИсп.ПолучитьМенеджерПланаОбменаПоИмени(ИмяПланаОбмена);
	
	// получаем макет типовых правил
	Попытка
		МакетПравил = ПланОбменаМенеджер.ПолучитьМакет(ИмяМакета);
	Исключение
		
		СтрокаСообщения = НСтр("ru = 'Ошибка получения макета конфигурации %1 для плана обмена %2'");
		СтрокаСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаСообщения, ИмяМакета, ИмяПланаОбмена);
		ОбменДаннымиСервер.СообщитьОбОшибке(СтрокаСообщения, Отказ);
		Возврат Неопределено;
		
	КонецПопытки;
	
	МакетПравил.Записать(ИмяВременногоФайла);
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
	
	// удаляем временный файл правил
	УдалитьВременныйФайл(ИмяВременногоФайла);
	
	Возврат ДвоичныеДанные;
КонецФункции

Процедура УдалитьВременныйФайл(ИмяВременногоФайла)
	
	Попытка
		Если Не ПустаяСтрока(ИмяВременногоФайла) Тогда
			УдалитьФайлы(ИмяВременногоФайла);
		КонецЕсли;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

Процедура ВыполнитьПроверкуЗаполненияПолей(Отказ, Запись)
	
	Если ПустаяСтрока(Запись.ИмяПланаОбмена) Тогда
		
		НСтрока = НСтр("ru = 'Укажите план обмена.'");
		
		ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
		
	ИначеЕсли Запись.ИсточникПравил = Перечисления.ИсточникиПравилДляОбменаДанными.МакетКонфигурации
		    И ПустаяСтрока(Запись.ИмяМакетаПравил) Тогда
		
		НСтрока = НСтр("ru = 'Укажите типовые правила.'");
		
		ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьВидПравилДляОбменаДанными(ВидПравил, ИмяФайла, Отказ)
	
	// открываем файл для чтения
	Попытка
		Правила = Новый ЧтениеXML();
		Правила.ОткрытьФайл(ИмяФайла);
		Правила.Прочитать();
	Исключение
		Правила = Неопределено;
		
		НСтрока = НСтр("ru = 'Не удалось определить вид правил из-за ошибки в разбора XML-файла [ИмяФайла]. 
		|Возможно выбран не тот файл, либо XML-файл имеет некорректную структуру. Выберите корректный файл.'");
		НСтрока = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(НСтрока, Новый Структура("ИмяФайла", ИмяФайла));
		ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
		Возврат;
	КонецПопытки;
	
	Если Правила.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
		
		Если Правила.ЛокальноеИмя = "ПравилаОбмена" Тогда
			
			ВидПравил = Перечисления.ВидыПравилДляОбменаДанными.ПравилаКонвертацииОбъектов;
			
		ИначеЕсли Правила.ЛокальноеИмя = "ПравилаРегистрации" Тогда
			
			ВидПравил = Перечисления.ВидыПравилДляОбменаДанными.ПравилаРегистрацииОбъектов;
			
		Иначе
			
			НСтрока = НСтр("ru = 'Не удалось определить вид правил из-за ошибки в формате правил XML-файла [ИмяФайла].
			|Возможно выбран не тот файл, либо XML-файл имеет некорректную структуру. Выберите корректный файл.'");
			НСтрока = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(НСтрока, Новый Структура("ИмяФайла", ИмяФайла));
			ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
			
		КонецЕсли;
		
	Иначе
		
		НСтрока = НСтр("ru = 'Не удалось определить вид правил из-за ошибки в формате правил XML-файла [ИмяФайла].
		|Возможно выбран не тот файл, либо XML-файл имеет некорректную структуру. Выберите корректный файл.'");
		НСтрока = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(НСтрока, Новый Структура("ИмяФайла", ИмяФайла));
		ОбменДаннымиСервер.СообщитьОбОшибке(НСтрока, Отказ);
		
	КонецЕсли;
	
	Правила.Закрыть();
	Правила = Неопределено;
	
КонецПроцедуры

// Процедура добавляет запись в регистр по переданным значениям структуры
Процедура ДобавитьЗапись(СтруктураЗаписи) Экспорт
	
	ОбменДаннымиСервер.ДобавитьЗаписьВРегистрСведений(СтруктураЗаписи, "ПравилаДляОбменаДанными");
	
КонецПроцедуры

Функция ИнформацияОПравилахИзФайла(ИмяФайлаПравил)
	
	ПравилаОбмена = Новый ЧтениеXML();
	ПравилаОбмена.ОткрытьФайл(ИмяФайлаПравил);
	ПравилаОбмена.Прочитать();
	
	Если Не ((ПравилаОбмена.ЛокальноеИмя = "ПравилаОбмена") И (ПравилаОбмена.ТипУзла = ТипУзлаXML.НачалоЭлемента)) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка формата правил обмена'");
	КонецЕсли;
	
	Пока ПравилаОбмена.Прочитать() Цикл
		
		ИмяУзла = ПравилаОбмена.ЛокальноеИмя;
		
		Если ИмяУзла = "Источник" И ПравилаОбмена.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			ИнформацияОПравилах = Новый Структура;
			ИнформацияОПравилах.Вставить("ВерсияКонфигурации", ПравилаОбмена.ПолучитьАтрибут("ВерсияКонфигурации"));
			ИнформацияОПравилах.Вставить("СинонимКонфигурацииВПравилах", ПравилаОбмена.ПолучитьАтрибут("СинонимКонфигурации"));
			ПравилаОбмена.Прочитать();
			ИнформацияОПравилах.Вставить("ИмяКонфигурации", ПравилаОбмена.Значение);
			
		ИначеЕсли (ИмяУзла = "Источник") И (ПравилаОбмена.ТипУзла = ТипУзлаXML.КонецЭлемента) Тогда
			
			ПравилаОбмена.Закрыть();
			Возврат ИнформацияОПравилах;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВызватьИсключение НСтр("ru = 'Ошибка формата правил обмена'");
	
КонецФункции

Функция ПравилаКонвертацииСовместимыСТекущейВерсией(ИмяПланаОбмена, ОписаниеОшибки, ДанныеОПравилах)
	
	Если Не ОбменДаннымиСервер.ЗначениеНастройкиПланаОбмена(ИмяПланаОбмена, "ПредупреждатьОНесоответствииВерсийПравилОбмена") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ДанныеОПравилах.ИмяКонфигурации <> Метаданные.Имя Тогда
		
		ОписаниеОшибки = Новый Структура;
		ОписаниеОшибки.Вставить("ТекстОшибки", Нстр("ru = 'Правила не могут быть загружены, т.к. они предназначены для программы ""%1"". Следует использовать правила из конфигурации или загрузить корректный комплект правил из файла.'"));
		ОписаниеОшибки.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ОписаниеОшибки.ТекстОшибки,
		ДанныеОПравилах.СинонимКонфигурацииВПравилах);
		ОписаниеОшибки.Вставить("ВидОшибки", "НекорректнаяКонфигурация");
		ОписаниеОшибки.Вставить("Картинка", БиблиотекаКартинок.Ошибка32);
		Возврат Ложь;
		
	КонецЕсли;
	
	ВерсияВПравилахБезСборки = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(ДанныеОПравилах.ВерсияКонфигурации);
	ВерсияКонфигурацииБезСборки = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(Метаданные.Версия);
	РезультатСравнения = ОбменДаннымиСервер.СравнитьВерсииБезНомераСБорки(ВерсияВПравилахБезСборки, ВерсияКонфигурацииБезСборки);
	
	Если РезультатСравнения <> 0 Тогда
		
		Если РезультатСравнения < 0 Тогда
			
			ТекстОшибки = Нстр("ru = 'Синхронизация данных может работать некорректно, так как загружаемые правила предназначены для предыдущей версии программы ""%1"" (%2). Рекомендуется использовать правила из конфигурации или загрузить комплект правил, предназначенный для текущей версии программы (%3).'");
			ВидОшибки = "УстаревшаяВерсияКонфигурации";
			
		Иначе
			
			ТекстОшибки = Нстр("ru = 'Синхронизация данных может работать некорректно, так как загружаемые правила предназначены для более новой версии программы ""%1"" (%2). Рекомендуется обновить версию программы или использовать комплект правил, предназначенный для текущей версии программы (%3).'");
			ВидОшибки = "УстаревшиеПравила";
			
		КонецЕсли;
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Метаданные.Синоним,
			ВерсияВПравилахБезСборки, ВерсияКонфигурацииБезСборки);
		
		ОписаниеОшибки = Новый Структура;
		ОписаниеОшибки.Вставить("ТекстОшибки", ТекстОшибки);
		ОписаниеОшибки.Вставить("ВидОшибки", ВидОшибки);
		ОписаниеОшибки.Вставить("Картинка", БиблиотекаКартинок.Предупреждение32);
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Профили безопасности

Процедура ПриЗаполненииРазрешенийНаДоступКВнешнимРесурсам(ЗапросыРазрешений) Экспорт
	
	ЗагруженныеПравила = ЗагруженныеПравила();
	
	Пока ЗагруженныеПравила.Следующий() Цикл
		
		ЗапросНаИспользованиеВнешнихРесурсов(ЗапросыРазрешений, ЗагруженныеПравила);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПравилаРегистрацииИзФайла(ИмяПланаОбмена) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1 1
	|ИЗ
	|	РегистрСведений.ПравилаДляОбменаДанными КАК ПравилаДляОбменаДанными
	|ГДЕ
	|	ПравилаДляОбменаДанными.ПравилаЗагружены = ИСТИНА
	|	И ПравилаДляОбменаДанными.ВидПравил = ЗНАЧЕНИЕ(Перечисление.ВидыПравилДляОбменаДанными.ПравилаРегистрацииОбъектов)
	|	И ПравилаДляОбменаДанными.ИсточникПравил = ЗНАЧЕНИЕ(Перечисление.ИсточникиПравилДляОбменаДанными.Файл)
	|	И ПравилаДляОбменаДанными.ИмяПланаОбмена = &ИмяПланаОбмена";
	
	Запрос.УстановитьПараметр("ИмяПланаОбмена", ИмяПланаОбмена);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ПравилаКонвертацииИзФайла(ИмяПланаОбмена) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПравилаДляОбменаДанными.ИмяПланаОбмена,
	|	ПравилаДляОбменаДанными.РежимСовместимости,
	|	ПравилаДляОбменаДанными.РежимОтладки,
	|	ПравилаДляОбменаДанными.РежимОтладкиВыгрузки,
	|	ПравилаДляОбменаДанными.РежимОтладкиЗагрузки,
	|	ПравилаДляОбменаДанными.РежимПротоколированияОбменаДанными,
	|	ПравилаДляОбменаДанными.ИмяФайлаОбработкиДляОтладкиВыгрузки,
	|	ПравилаДляОбменаДанными.ИмяФайлаОбработкиДляОтладкиЗагрузки,
	|	ПравилаДляОбменаДанными.ИмяФайлаПротоколаОбмена
	|ИЗ
	|	РегистрСведений.ПравилаДляОбменаДанными КАК ПравилаДляОбменаДанными
	|ГДЕ
	|	ПравилаДляОбменаДанными.ПравилаЗагружены = ИСТИНА
	|	И ПравилаДляОбменаДанными.ВидПравил = ЗНАЧЕНИЕ(Перечисление.ВидыПравилДляОбменаДанными.ПравилаКонвертацииОбъектов)
	|	И ПравилаДляОбменаДанными.ИмяПланаОбмена = &ИмяПланаОбмена";
	
	Запрос.УстановитьПараметр("ИмяПланаОбмена", ИмяПланаОбмена);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка;
		
	КонецЕсли;
	
КонецФункции

Функция ЗагруженныеПравила()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПравилаДляОбменаДанными.ИмяПланаОбмена,
	|	ПравилаДляОбменаДанными.РежимСовместимости,
	|	ПравилаДляОбменаДанными.РежимОтладки,
	|	ПравилаДляОбменаДанными.РежимОтладкиВыгрузки,
	|	ПравилаДляОбменаДанными.РежимОтладкиЗагрузки,
	|	ПравилаДляОбменаДанными.РежимПротоколированияОбменаДанными,
	|	ПравилаДляОбменаДанными.ИмяФайлаОбработкиДляОтладкиВыгрузки,
	|	ПравилаДляОбменаДанными.ИмяФайлаОбработкиДляОтладкиЗагрузки,
	|	ПравилаДляОбменаДанными.ИмяФайлаПротоколаОбмена,
	|	ИСТИНА КАК ЕстьПравилаКонвертации
	|ПОМЕСТИТЬ ПравилаКонвертации
	|ИЗ
	|	РегистрСведений.ПравилаДляОбменаДанными КАК ПравилаДляОбменаДанными
	|ГДЕ
	|	ПравилаДляОбменаДанными.ВидПравил = ЗНАЧЕНИЕ(Перечисление.ВидыПравилДляОбменаДанными.ПравилаКонвертацииОбъектов)
	|	И ПравилаДляОбменаДанными.ПравилаЗагружены = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПравилаДляОбменаДанными.ИмяПланаОбмена,
	|	ИСТИНА КАК ПравилаРегистрацииИзФайла
	|ПОМЕСТИТЬ ПравилаРегистрации
	|ИЗ
	|	РегистрСведений.ПравилаДляОбменаДанными КАК ПравилаДляОбменаДанными
	|ГДЕ
	|	ПравилаДляОбменаДанными.ПравилаЗагружены = ИСТИНА
	|	И ПравилаДляОбменаДанными.ВидПравил = ЗНАЧЕНИЕ(Перечисление.ВидыПравилДляОбменаДанными.ПравилаРегистрацииОбъектов)
	|	И ПравилаДляОбменаДанными.ИсточникПравил = ЗНАЧЕНИЕ(Перечисление.ИсточникиПравилДляОбменаДанными.Файл)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПравилаРегистрации.ПравилаРегистрацииИзФайла
	|			ТОГДА ПравилаРегистрации.ИмяПланаОбмена
	|		ИНАЧЕ ПравилаКонвертации.ИмяПланаОбмена
	|	КОНЕЦ КАК ИмяПланаОбмена,
	|	ПравилаКонвертации.РежимСовместимости,
	|	ПравилаКонвертации.РежимОтладки,
	|	ПравилаКонвертации.РежимОтладкиВыгрузки,
	|	ПравилаКонвертации.РежимОтладкиЗагрузки,
	|	ПравилаКонвертации.РежимПротоколированияОбменаДанными,
	|	ПравилаКонвертации.ИмяФайлаОбработкиДляОтладкиВыгрузки,
	|	ПравилаКонвертации.ИмяФайлаОбработкиДляОтладкиЗагрузки,
	|	ПравилаКонвертации.ИмяФайлаПротоколаОбмена,
	|	ЕСТЬNULL(ПравилаРегистрации.ПравилаРегистрацииИзФайла, ЛОЖЬ) КАК ПравилаРегистрацииИзФайла,
	|	ЕСТЬNULL(ПравилаКонвертации.ЕстьПравилаКонвертации, ЛОЖЬ) КАК ЕстьПравилаКонвертации
	|ИЗ
	|	ПравилаКонвертации КАК ПравилаКонвертации
	|		ПОЛНОЕ СОЕДИНЕНИЕ ПравилаРегистрации КАК ПравилаРегистрации
	|		ПО ПравилаКонвертации.ИмяПланаОбмена = ПравилаРегистрации.ИмяПланаОбмена";
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции

Процедура ЗапросНаИспользованиеВнешнихРесурсов(ЗапросыРазрешений, Запись, ЕстьПравилаКонвертации = Неопределено, ПравилаРегистрацииИзФайла = Неопределено) Экспорт
	
	Разрешения = Новый Массив;
	
	Если ПравилаРегистрацииИзФайла = Неопределено Тогда
		ПравилаРегистрацииИзФайла = Запись.ПравилаРегистрацииИзФайла;
	КонецЕсли;
	
	Если ЕстьПравилаКонвертации = Неопределено Тогда
		ЕстьПравилаКонвертации = Запись.ЕстьПравилаКонвертации;
	КонецЕсли;
	
	Если ПравилаРегистрацииИзФайла Тогда
		Разрешения.Добавить(РаботаВБезопасномРежиме.РазрешениеНаИспользованиеПривилегированногоРежима());
	КонецЕсли;
	
	Если ЕстьПравилаКонвертации Тогда
		
		Если (Не Запись.РежимСовместимости Или ОбщегоНазначенияПовтИсп.РазделениеВключено()) И Не Запись.РежимОтладки Тогда
			// Запрос на персональный профиль не требуется
		Иначе
			
			Если Не ПравилаРегистрацииИзФайла Тогда
				Разрешения.Добавить(РаботаВБезопасномРежиме.РазрешениеНаИспользованиеПривилегированногоРежима());
			КонецЕсли;
			
			Если Запись.РежимОтладки Тогда
				
				Если Запись.РежимОтладкиВыгрузки Тогда
					
					СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(Запись.ИмяФайлаОбработкиДляОтладкиВыгрузки);
					Разрешения.Добавить(РаботаВБезопасномРежиме.РазрешениеНаИспользованиеКаталогаФайловойСистемы(
					СтруктураИмениФайла.Путь, Истина, Ложь));
					
				КонецЕсли;
				
				Если Запись.РежимОтладкиЗагрузки Тогда
					
					СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(Запись.ИмяФайлаОбработкиДляОтладкиВыгрузки);
					Разрешения.Добавить(РаботаВБезопасномРежиме.РазрешениеНаИспользованиеКаталогаФайловойСистемы(
					СтруктураИмениФайла.Путь, Истина, Ложь));
					
				КонецЕсли;
				
				Если Запись.РежимПротоколированияОбменаДанными Тогда
					
					СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(Запись.ИмяФайлаОбработкиДляОтладкиВыгрузки);
					Разрешения.Добавить(РаботаВБезопасномРежиме.РазрешениеНаИспользованиеКаталогаФайловойСистемы(
					СтруктураИмениФайла.Путь, Истина, Истина));
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИдентификаторПланаОбмена = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.ПланыОбмена[Запись.ИмяПланаОбмена]);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ЗапросыРазрешений,
		РаботаВБезопасномРежимеСлужебный.ЗапросыНаИспользованиеВнешнихРесурсовДляВнешнегоМодуля(ИдентификаторПланаОбмена, Разрешения));
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаЗаписи" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если Параметры.Ключ.ВидПравил = Перечисления.ВидыПравилДляОбменаДанными.ПравилаКонвертацииОбъектов Тогда
			
			ВыбраннаяФорма = "РегистрСведений.ПравилаДляОбменаДанными.Форма.ПравилаКонвертацииОбъектов";
			
		ИначеЕсли Параметры.Ключ.ВидПравил = Перечисления.ВидыПравилДляОбменаДанными.ПравилаРегистрацииОбъектов Тогда
			
			ВыбраннаяФорма = "РегистрСведений.ПравилаДляОбменаДанными.Форма.ПравилаРегистрацииОбъектов";
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
