#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ПредставлениеПоля", ПредставлениеПоля);
	Заголовок = НСтр("ru = 'Список выбранных'") + " (" + ПредставлениеПоля + ")";
	
	Список.ТипЗначения = Параметры.ОписаниеТипов;
	
	Для Каждого Значение Из Параметры.ЗначенияДляВыбора Цикл
		ДобавитьУникальноеЗначениеВСписок(Список, Значение, Неопределено, Ложь);
	КонецЦикла;
	
	Для Каждого ЭлементПрототип Из Параметры.Отмеченные Цикл
		ЗначениеВСКД = ЭлементПрототип.Значение;
		Если ТипЗнч(ЗначениеВСКД) = Тип("Тип") Тогда
			МассивТипов = Новый Массив;
			МассивТипов.Добавить(ЗначениеВСКД);
			ЗначениеВФорме = Новый ОписаниеТипов(МассивТипов);
		Иначе
			ЗначениеВФорме = ЗначениеВСКД;
		КонецЕсли;
		ДобавитьУникальноеЗначениеВСписок(Список, ЗначениеВФорме, Неопределено, Истина);
	КонецЦикла;
	
	Параметры.Свойство("ОграничиватьВыборУказаннымиЗначениями", ОграничиватьВыборУказаннымиЗначениями);
	
	Если ОграничиватьВыборУказаннымиЗначениями Тогда
		Элементы.Список.ИзменятьСоставСтрок = Ложь;
		Элементы.СписокПодбор.Видимость = Ложь;
		Элементы.СписокДобавить.Видимость = Ложь;
		Элементы.СписокВставитьИзБуфераОбмена.Видимость = Ложь;
		Элементы.СписокКонтекстноеМенюДобавить.Видимость = Ложь;
		Элементы.СписокЗначение.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрыВыбора") И Параметры.ПараметрыВыбора <> Неопределено Тогда
		Элементы.СписокЗначение.ПараметрыВыбора = Новый ФиксированныйМассив(Параметры.ПараметрыВыбора);
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЗагрузкаДанныхИзФайла") Тогда
		Элементы.СписокВставитьИзБуфераОбмена.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если ОграничиватьВыборУказаннымиЗначениями Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	Если ОграничиватьВыборУказаннымиЗначениями Тогда
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаВыбора(Элемент, РезультатВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	// Добавление выбранных элементов с контролем уникальности.
	Если ТипЗнч(РезультатВыбора) = Тип("Массив") Тогда
		Для Каждого Значение Из РезультатВыбора Цикл
			ДобавитьУникальноеЗначениеВСписок(Список, Значение, Неопределено, Истина);
		КонецЦикла;
	Иначе
		ДобавитьУникальноеЗначениеВСписок(Список, РезультатВыбора, Неопределено, Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	Если МодальныйРежим
		Или РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс
		Или ВладелецФормы = Неопределено Тогда
		Закрыть(Список);
	Иначе
		ОповеститьОВыборе(Список);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВставитьИзБуфераОбмена(Команда)
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("ОписаниеТипов", Список.ТипЗначения);
	ПараметрыПоиска.Вставить("ПараметрыВыбора", Элементы.СписокЗначение.ПараметрыВыбора);
	ПараметрыПоиска.Вставить("ПредставлениеПоля", ПредставлениеПоля);
	ПараметрыПоиска.Вставить("Сценарий", "ПоискСсылок");
	
	ПараметрыВыполнения = Новый Структура;
	Обработчик = Новый ОписаниеОповещения("ВставитьИзБуфераОбменаЗавершение", ЭтотОбъект, ПараметрыВыполнения);
	
	МодульЗагрузкаДанныхИзФайлаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЗагрузкаДанныхИзФайлаКлиент");
	МодульЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗаполненияСсылок(ПараметрыПоиска, Обработчик);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВставитьИзБуфераОбменаЗавершение(НайденныеОбъекты, ПараметрыВыполнения) Экспорт
	Если НайденныеОбъекты = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Значение Из НайденныеОбъекты Цикл
		ДобавитьУникальноеЗначениеВСписок(Список, Значение, Неопределено, Истина);
	КонецЦикла;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДобавитьУникальноеЗначениеВСписок(СписокЗначений, Значение, Представление, Использование)
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ЭлементСписка = СписокЗначений.НайтиПоЗначению(Значение);
	Если ЭлементСписка = Неопределено Тогда
		ЭлементСписка = СписокЗначений.Добавить();
		ЭлементСписка.Значение = Значение;
	КонецЕсли;
	Если ЗначениеЗаполнено(Представление) Тогда
		ЭлементСписка.Представление = Представление;
	ИначеЕсли Не ЗначениеЗаполнено(ЭлементСписка.Представление) Тогда
		ЭлементСписка.Представление = Строка(Значение);
	КонецЕсли;
	Если Использование И Не ЭлементСписка.Пометка Тогда
		ЭлементСписка.Пометка = Истина;
	КонецЕсли;
	Возврат ЭлементСписка;
КонецФункции

#КонецОбласти
