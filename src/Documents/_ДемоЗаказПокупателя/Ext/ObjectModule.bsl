#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// См. описание в комментарии к одноименной процедуре в модуле УправлениеДоступом.
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	// Логика ограничения:
	// Чтения:                Организация И Партнер
	// Изменения:             Организация И Партнер
	
	// Чтение, Изменение: набор №0.
	Строка = Таблица.Добавить();
	Строка.ЗначениеДоступа = Организация;
	
	Строка = Таблица.Добавить();
	Строка.ЗначениеДоступа = Партнер;
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ДанныеЗаполнения = Неопределено Тогда // Ввод нового.
		_ДемоСтандартныеПодсистемы.ПриВводеНовогоЗаполнитьОрганизацию(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Демонстрация программного интерфейса для обработки локальных объектов
	ПередЗаписьюЛокальнойКонтактнойИнформации();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.СостоянияПредметовВзаимодействий.УстановитьПризнакАктивен(Ссылка,НЕ УдалитьЗаказЗакрыт);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает вспомогательные реквизиты по данным контакной информации шапки объекта
//
Процедура ПередЗаписьюЛокальнойКонтактнойИнформации()
	
	// 1. Адрес
	СтруктураСтраныДоставки = УправлениеКонтактнойИнформацией.СтранаАдресаКонтактнойИнформации(АдресДоставки);
	Если ЗначениеЗаполнено(СтруктураСтраныДоставки.Ссылка) Тогда
		// Нашли как используемую страну
		СтранаДоставки = СтруктураСтраныДоставки.Ссылка
	Иначе
		// Или данные классификатора
		СтранаДоставки = СтруктураСтраныДоставки.Наименование;
	КонецЕсли;
	РегионДоставки = УправлениеКонтактнойИнформацией.РегионАдресаКонтактнойИнформации(АдресДоставки);
	ГородДоставки  = УправлениеКонтактнойИнформацией.ГородАдресаКонтактнойИнформации(АдресДоставки);
	
	// 2. Электронная почта
	ДоменноеИмяСервера = УправлениеКонтактнойИнформацией.ДоменАдресаКонтактнойИнформации(ЭлектроннаяПочта);
КонецПроцедуры

#КонецОбласти

#КонецЕсли
