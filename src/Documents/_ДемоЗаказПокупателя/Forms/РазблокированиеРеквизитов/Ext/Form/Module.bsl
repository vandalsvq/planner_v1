
#Область ОбработчикиКомандФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.ЗаблокированныеРеквизиты = Неопределено Тогда
		БлокируемыеРеквизиты = Документы._ДемоЗаказПокупателя.ПолучитьБлокируемыеРеквизитыОбъекта();
		Параметры.ЗаблокированныеРеквизиты = Новый ФиксированныйМассив(БлокируемыеРеквизиты);
	КонецЕсли;
	
	Для Каждого Реквизит Из Параметры.ЗаблокированныеРеквизиты Цикл
		Элементы[Реквизит].Видимость = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактирование(Команда)
	
	РазблокируемыеРеквизиты = Новый Массив;
	
	Для Каждого Реквизит Из Параметры.ЗаблокированныеРеквизиты Цикл
		Если Элементы[Реквизит].Видимость И ЭтотОбъект[Реквизит] Тогда
			РазблокируемыеРеквизиты.Добавить(Реквизит);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть();
	
	ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(
		ВладелецФормы, РазблокируемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти
